      SUBROUTINE T1W2AB(Z,W,TA,TB,FA,FB,POP1,POP2,VRT1,VRT2,
     &                  DISSYZ,DISSYWA,DISSYWB,NUMSYZ,NUMSYWA,
     &                  NUMSYWB,NTAA,NTBB,NFAA,NFBB,LISTWA,
     &                  LISTWB,LISTZ,IRREP,IUHF,TMP)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DISSYZ,DISSYWA,DISSYWB,DIRPRD,POP1,POP2,VRT1,VRT2
      DIMENSION Z(DISSYZ,NUMSYZ),W(DISSYWA,1),TA(NTAA),TB(NTBB),
     &          FA(NFAA),FB(NFBB)
      DIMENSION POP1(8),POP2(8),VRT1(8),VRT2(8) 
      DIMENSION TMP(1)
C
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &                DIRPRD(8,8)
C
      DATA AZERO,ONE /0.0D0,1.0D0/
C
      CALL ZERO(Z,DISSYZ*NUMSYZ)
C
C GET INTEGRALS <Mn|Ie> FROM (LISTWA1,LISTWA2)
C
      IF(MIN(NUMSYWA,DISSYWA).NE.0) THEN
C
       CALL GETLST(W,1,NUMSYWA,2,IRREP,LISTWA)
C
C      PERFORM MULTIPLICATION
C
       JOFFW=1
       JOFFZ=1
       IOFF=1
C
       DO 90 IRREPJ=1,NIRREP
C
        NOCCJ=POP2(IRREPJ)
        NVRTJ=VRT2(IRREPJ)
C
        IRREPI=DIRPRD(IRREPJ,IRREP)
C
        NOCCI=POP1(IRREPI)
C
        IF(NVRTJ.EQ.0.OR.NOCCJ.EQ.0.OR.NOCCI.EQ.0) GO TO 80
C
         CALL XGEMM('N','N',DISSYWA*NOCCI,NOCCJ,NVRTJ,ONE,W(1,JOFFW),
     &               DISSYWA*NOCCI,TB(IOFF),NVRTJ,AZERO,Z(1,JOFFZ),
     &               DISSYZ*NOCCI)
80      CONTINUE
        JOFFW=JOFFW+NVRTJ*NOCCI
        JOFFZ=JOFFZ+NOCCJ*NOCCI
        IOFF=IOFF+NOCCJ*NVRTJ
90     CONTINUE
       CALL MTRAC2(FA,Z,DISSYZ,POP1,POP2,IRREP,IUHF)
      ENDIF
C
      IF(MIN(NUMSYWA,DISSYWA).NE.0) THEN
C
C  IN RHF, THE SECOND TERM IS THE TRANSPOSED OF THE FIRST
C
       CALL GETLST(W,1,NUMSYZ,1,IRREP,LISTZ)
       IF(IUHF.EQ.0) THEN
        CALL SYMRHF(IRREP,POP1,POP1,DISSYZ,Z,TMP,TMP(1+DISSYZ),
     &              TMP(1+2*DISSYZ))
       ENDIF
       CALL VADD(W,W,Z,NUMSYZ*DISSYZ,ONE)
       CALL PUTLST(W,1,NUMSYZ,1,IRREP,LISTZ)
      ENDIF
      IF(IUHF.EQ.1) THEN
C
C  UHF, THE SECOND TERM MUST BE CALCULATED EXPLICITELY
C
       IF(MIN(NUMSYWB,DISSYWB).NE.0) THEN
C
       CALL ZERO(Z,DISSYZ*NUMSYZ)
C
C  GET INTEGRALS <Mn|Aj> FROM (LISTW1B,LISTW2B)
C
       CALL GETLST(W,1,NUMSYWB,2,IRREP,LISTWB)
       CALL SYMTR1(IRREP,VRT1,POP2,DISSYWB,W,TMP,TMP(1+DISSYWB),
     &             TMP(1+2*DISSYWB))
C
       IOFF=1
       JOFFW=1
       JOFFZ=1
       DO 190 IRREPI=1,NIRREP
C
        NOCCI=POP1(IRREPI)
        NVRTI=VRT1(IRREPI)
C
        IRREPJ=DIRPRD(IRREP,IRREPI)
C 
        NOCCJ=POP2(IRREPJ)
C
        IF(NOCCI.EQ.0.OR.NOCCJ.EQ.0.OR.NVRTI.EQ.0) GO TO 180
C
         CALL XGEMM('N','N',DISSYWB*NOCCJ,NOCCI,NVRTI,ONE,W(1,JOFFW),
     &              DISSYWB*NOCCJ,TA(IOFF),NVRTI,ONE,Z(1,JOFFZ),
     &              DISSYZ*NOCCJ)
C
180     CONTINUE
        JOFFW=JOFFW+NOCCJ*NVRTI
        JOFFZ=JOFFZ+NOCCJ*NOCCI
        IOFF=IOFF+NOCCI*NVRTI
190    CONTINUE
       CALL SYMTR1(IRREP,POP2,POP1,DISSYZ,Z,TMP,TMP(1+DISSYZ),
     &             TMP(1+2*DISSYZ))
       CALL MTRAC3(FB,Z,DISSYZ,POP2,POP1,IRREP)
       CALL GETLST(W,1,NUMSYZ,1,IRREP,LISTZ)
       CALL VADD(W,W,Z,NUMSYZ*DISSYZ,ONE)
       CALL PUTLST(W,1,NUMSYZ,1,IRREP,LISTZ)
       ENDIF
      ENDIF  
      RETURN
      END
