      SUBROUTINE EUCC(ICORE,MAXCOR,IUHF,ELAST)
C
C   THIS ROUTINE CALCULATE THE UCC ENERGY CONTRIBUTION. 
C   FIRST CALCULATES THE X(IJ,AB) CONTRIBUTION AS IN DENS.
C   !!!! NOTE THAT THIS ROUTINE REWRITES WMBEJ LISTS 54-59 !!!!
C
C    UCC(4) :   X(IJ,AB) = 1/4 SUM M,N SUM E,F
C
C                T(MN,EF) T(IJ,EF) T(MN,AB)
C
C              - 2 P(AB) T(MN,EF) T(MN,BE) T(IJ,AF)
C
C              - 2 P(IJ) T(MN,EF) T(IM,EF) T(JN,AB)
C
C             + 2 P(IJ)P(AB) T(MN,EF) T(JN,BF) T(IN,AF)
C
CEND
C
C  CODED   JUL/91  PS
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD,DSZAB,DSZAA,POP1,POP2,VRT1,VRT2
      LOGICAL MBPT3,MBPT4,CC,TRPEND,SNGEND,GRAD,MBPTT,SING1,QCISD,UCC
      DIMENSION ICORE(MAXCOR),ECRR(3)
      COMMON /FILES/ LUOUT,MOINTS
      COMMON /FLAGS/ IFLAGS(100)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SWITCH/ MBPT3,MBPT4,CC,TRPEND,SNGEND,GRAD,MBPTT,SING1,
     &                QCISD,UCC
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &                DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /SYM/ POP1(8),POP2(8),VRT1(8),VRT2(8),NTAA,NTBB,NF1AA,
     &             NF1BB,NF2AA,NF2BB
C
      IF(IFLAGS(1).GE.10)THEN
       WRITE(LUOUT,100)
100    FORMAT(T3,'@EUCC-I, Calculating UCC energy')
       CALL TIMER(1)
      ENDIF
C
C   CALCULATE TRIPLES CONTRIBUTION FOR UCC(4)
C
      IF(TRPEND.AND.GRAD) THEN
C
C   SET BACK IFLAGS(3) IF GRADIENTS ARE REQUIRED
C
         IF(IFLAGS(3).EQ.0) IFLAGS(3)=1
         CALL INIT3(IUHF)
         CALL TRPS(ICORE,MAXCOR,IUHF,EDUMMY)
      ENDIF
C
C    G1 AND G2 INTERMIDIATES 
C     
      FACT=1.D0
      CALL FORMG1(ICORE,MAXCOR,IUHF,FACT)
      CALL FORMG2(ICORE,MAXCOR,IUHF,FACT)
C
C    CALCULATE V1 INTERMEDIATE T(IJ,EF) T(MN,EF)
C
      CALL FORMV1(ICORE,MAXCOR,IUHF)
C
C    CALCULATE THIRD THE INTERMEDIATE T(IM,AE) T(JN,BF)
C
      CALL FORMH4(ICORE,MAXCOR,IUHF)
C
C    CONTRACT THE INTERMDIATES TO GIVE THE X-CONTRIBUTION
C
      CALL V1INX2(ICORE,MAXCOR,IUHF)
C
C This extra arugument to the following two calls were added during
C The modifications for pCC gradients. All it does is to control the
C reading the appropriate list. In this case it is 191(1,2),192(1,2).
C Ajith Perera, 01/2016.

      CALL G1INX2(ICORE,MAXCOR,IUHF,0)
      CALL G2INX2(ICORE,MAXCOR,IUHF,0)
      CALL H4INX2(ICORE,MAXCOR,IUHF)
C
C FORM X2(AA) AND DUMP THEM.
C
      IF(IUHF.EQ.0)THEN
       DO 200 IRREP=1,NIRREP
        NUMAB=IRPDPD(IRREP,ISYTYP(2,46))
        DSZAB=IRPDPD(IRREP,ISYTYP(1,46))
        NUMAA=IRPDPD(IRREP,ISYTYP(2,44))
        DSZAA=IRPDPD(IRREP,ISYTYP(1,44))
        NSIZ1=DSZAB*NUMAA
        NSIZ2=DSZAB*NUMAB
        I000=1
        I010=I000+IINTFP*NSIZ1
        I020=I010+IINTFP*NSIZ2
        CALL GETLST(ICORE(I010),1,NUMAB,1,IRREP,116)
        CALL ASSYM(IRREP,POP1,DSZAB,DSZAB,ICORE(I000),ICORE(I010))
        CALL SQSYM(IRREP,VRT1,DSZAA,DSZAB,NUMAA,ICORE(I010),ICORE(I000))
        CALL PUTLST(ICORE(I010),1,NUMAA,1,IRREP,114)
200    CONTINUE 
      ENDIF
C
C    AND NOW CONTRACT X WITH THE INTEGRALS TO GET ENERGY
C
      CALL CMPENG(ICORE,MAXCOR,113,0,ECRR,ETOT,ETOTT2,IUHF,1)
c      CALL GETREC(20,'JOBARC','SCFENEG ',IINTFP,ESCF)
      ELAST=ELAST-0.5d0*ETOT

      IF(IFLAGS(1).GE.10)THEN
       print '(T3,A,f20.12,A)',' The UCC total energy is ',ELAST,' a.u.'
       CALL TIMER(1)
c       WRITE(LUOUT,102)TIMENEW
102    FORMAT(T3,'@EUCC-I, UCC energy required ',F9.3,' seconds.')
      ENDIF
      RETURN
      END
