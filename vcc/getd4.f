      SUBROUTINE GETD4(ICORE,MAXCOR,E4D,IUHF)
C
C ROUTINE COMPUTES THE FOURTH-ORDER DOUBLES ENERGY FROM THE RELATION
C
C            E4D = |T(2)**2|*D(ijab)
C
C HERE, THE T2 INCREMENTS *BEFORE DENOMINATOR WEIGHTING* ARE PICKED UP
C  AND USED, SO THAT THE ACTUAL WORKING EQUATION IS 
C
C            E4D = |TINC(2)**2|/D(ijab).
C
C  WHEN THINGS ARE DONE THIS WAY, THEN THE RECIPROCAL DENOMINATORS
C   STORED ON LISTS 48-50 CAN BE USED DIRECTLY WITH VECPRD.
C
CEND
      IMPLICIT INTEGER (A-Z)
      DIMENSION ICORE(MAXCOR)
      DOUBLE PRECISION E4D,FACTOR,EAA,E4X,SDOT
      COMMON /INFO/ NOCCO(2),NVRTO(2)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &                DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      EAA=0.0
      E4D=0.0
      FACTOR=1.0
      NLIST0=60
      IF(IUHF.EQ.0)FACTOR=2.0
C
C COMPUTE AB CONTRIBUTION TO E4D.
C
      NLIST=NLIST0+3
      DO 10 IRREP=1,NIRREP
       NDSSYM=IRPDPD(IRREP,ISYTYP(2,NLIST))
       DISSYM=IRPDPD(IRREP,ISYTYP(1,NLIST))
       NSIZE=DISSYM*NDSSYM
       I000=1
       I010=I000+IINTFP*NSIZE
       I020=I010+IINTFP*NSIZE
       CALL GETLST(ICORE(I000),1,NDSSYM,1,IRREP,NLIST)
       CALL VECPRD(ICORE(I000),ICORE(I000),ICORE(I000),NSIZE)
       CALL GETLST(ICORE(I010),1,NDSSYM,1,IRREP,50)
       CALL INVERS(ICORE(I010),NSIZE)
       E4X=SDOT(NSIZE,ICORE(I000),1,ICORE(I010),1)
       E4D=E4D+E4X
10    CONTINUE
C
C COMPUTE AA AND BB (BB FOR UHF ONLY) CONTRIBUTIONS TO E4D.
C
      DO 100 ISPIN=1,1+IUHF
       NLIST=NLIST0+ISPIN
       DO 20 IRREP=1,NIRREP
        NDSSYM=IRPDPD(IRREP,ISYTYP(2,NLIST))
        DISSYM=IRPDPD(IRREP,ISYTYP(1,NLIST))
        NSIZE=DISSYM*NDSSYM
        I000=1
        I010=I000+IINTFP*NSIZE
        I020=I010+IINTFP*NSIZE
        CALL GETLST(ICORE(I000),1,NDSSYM,1,IRREP,NLIST)
        CALL VECPRD(ICORE(I000),ICORE(I000),ICORE(I000),NSIZE)
        CALL GETLST(ICORE(I010),1,NDSSYM,1,IRREP,47+ISPIN)
        CALL INVERS(ICORE(I010),NSIZE)
        E4X=SDOT(NSIZE,ICORE(I000),1,ICORE(I010),1)
        EAA=EAA+E4X
20     CONTINUE
100   CONTINUE
      E4D=E4D+FACTOR*EAA
      RETURN
      END
