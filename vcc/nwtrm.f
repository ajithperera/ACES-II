       SUBROUTINE NWTRM(ICORE,MAXCOR,IUHF,FACT)
C
C      ARGUMENTS IN SUBROUTINE CALL : ICORE,MAXCOR,IUHF
C
C THIS ROUTINE COMPUTES :
C
C       T(AB,IJ) = 1/4 SUM M,N SUM E,F <AB||MN> T(EF,MN)T(EF,IJ)
C
C THE MULTIPLICATION IS CARRIED OUT USING SYMMETRY
C AND THE RESULTING PRODUCT ARRAY IS STORED IN A 
C SYMMETRY ADAPTED WAY
C
C NOTE  ISPIN = 1   .... T(AB,IJ) (UHF ONLY)
C       ISPIN = 2   .... T(ab,ij) (UHF ONLY)
C       ISPIN = 3   .... T(Ab,Ij) (RHF AND UHF)
C

CEND
C
C CODED PS JUL/91
C 
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DISSYW,DISSYT,DISSYI,DIRPRD
      DIMENSION ICORE(MAXCOR)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /FILES/ LUOUT,MOINTS
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
      DATA ZILCH,ONE/0.D0,1.D0/
C
      MXCOR=MAXCOR
C
      IBOT=3
      IF(IUHF.EQ.1) IBOT=1
C
C   LOOP OVER SPIN CASES
C
      DO 1000 ISPIN=IBOT,3
C 
         LISTT=43+ISPIN
         LISTI=60+ISPIN
         LISTW=13+ISPIN
C
C   LOOP OVER IRREPS
C
         DO 100 IRREP=1,NIRREP
            DISSYT=IRPDPD(IRREP,ISYTYP(1,LISTT))
            NUMSYT=IRPDPD(IRREP,ISYTYP(2,LISTT))
            IF(MIN(NUMSYT,DISSYT).GT.0) THEN
C
C    1: Z  2: T  
C
               I001=1
               I002=I001+IINTFP*NUMSYT*NUMSYT
               I003=I002+IINTFP*NUMSYT*DISSYT
               IF(I003.LT.MXCOR) THEN
C
C     INCORE VERSION
C
                  CALL GETLST(ICORE(I002),1,NUMSYT,1,IRREP,LISTT)
                  CALL XGEMM('T','N',NUMSYT,NUMSYT,DISSYT,FACT,
     &                        ICORE(I002),DISSYT,ICORE(I002),DISSYT,
     &                        ZILCH,ICORE(I001),NUMSYT)
               ELSE
C
C      NO OUT OF CORE VERSION YET
C
                  CALL INSMEM('NWTRM',I003,MXCOR)
               ENDIF
C
C   1: Z  2: W  3: T
C
               DISSYI=IRPDPD(IRREP,ISYTYP(1,LISTI))
               NUMSYI=IRPDPD(IRREP,ISYTYP(2,LISTI))
               DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
               NUMSYW=IRPDPD(IRREP,ISYTYP(2,LISTW))
               I003=I002+IINTFP*DISSYW*NUMSYW
               I004=I003+IINTFP*NUMSYI*DISSYI
               IF(I004.LT.MXCOR) THEN
                  CALL GETLST(ICORE(I002),1,NUMSYW,2,IRREP,LISTW)
                  CALL XGEMM('N','N',DISSYI,NUMSYI,NUMSYT,ONE,
     &                        ICORE(I002),DISSYW,ICORE(I001),NUMSYT,
     &                        ZILCH,ICORE(I003),DISSYI)
                  NSIZE=NUMSYI*DISSYI
                  CALL SUMSYM2(ICORE(I003),ICORE(I001),NSIZE,1,IRREP,
     &                        LISTI)
               ELSE
                  CALL INSMEM('NWTRM',I005,MXCORE)
               ENDIF
            ENDIF
 100     CONTINUE
 1000 CONTINUE
      RETURN
      END
