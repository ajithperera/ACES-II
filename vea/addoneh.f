      SUBROUTINE ADDONEH(W,SCR,ISIZE,FVV,FOO,DISSZ1,NUMDS1,
     $   IPOPSPIN, IVRTSPIN)
C
C THIS SUBROUTINE ACCEPTS THE QUANTITIES F(AB) [FVV], F(IJ) [FOO]
C AND W(AB,IJ) AND RETURNS
C
C   Z(AB,IJ) = W(AB,IJ) + F(AB) DELTA(IJ) - F(IJ) DELTA (AB)
C
C WHERE DELTA IS THE KRONECKER DELTA FUNCTION.
C
CEND
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,DISSZ1,POP,VRT
      DIMENSION W(ISIZE),SCR(ISIZE),FOO(*),FVV(*)
C
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMINF/NSTART,NIRREP,IRREP0(255,2),DIRPRD(8,8)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/INFO/NOCCO(2),NVRTO(2)
C 
      DATA ONE,ONEM /1.0D0,-1.0D0/
      DATA ZILCH,HALF,HALFM,TWO /0.0D0,0.5D0,-0.5D0,2.0D0/
C
      IOFFH0=1
      DO 20 IRREP=1,NIRREP
       NUMO=POP(IRREP,IPOPSPIN)
       IOFFH1=IOFFH0
       DO 21 INDXJ=1,POP(IRREP,IPOPSPIN)
        CALL SAXPY(NFEA(IVRTSPIN),ONE,FVV,1,W(IOFFH1),1)
        IOFFH1=IOFFH1+DISSZ1*(NUMO+1)
21     CONTINUE
       IOFFH0=IOFFH0+NUMO*NUMO*DISSZ1
20    CONTINUE
      CALL TRANSP(W,SCR,NUMDS1,DISSZ1)
      IOFFH0=1
      DO 22 IRREP=1,NIRREP
       NUMV=VRT(IRREP,IVRTSPIN)
       IOFFH1=IOFFH0
       DO 23 INDXB=1,VRT(IRREP,IVRTSPIN)
        CALL SAXPY(NFMI(IPOPSPIN),ONEM,FOO,1,SCR(IOFFH1),1)
        IOFFH1=IOFFH1+NUMDS1*(NUMV+1)
23     CONTINUE
       IOFFH0=IOFFH0+NUMV*NUMV*NUMDS1
22    CONTINUE
      CALL TRANSP(SCR,W,DISSZ1,NUMDS1)
      RETURN
      END
