C
C  UPDATES THE ACTUAL LENGHTS OF AN S2 LIST.
C  INPUT IS POP1, NS AND SIRREP, WHICH ALLOW DETERMINATION OF NUMDSS(XIRREP)
C  AND SYTYPL WHICH ALLOWS DETERMINATION OF DISTRIBUTION SIZE
C
      SUBROUTINE UPDLSTS2(LISTS2, POP1, NS, SYTYPL, SIRREP)
      IMPLICIT INTEGER (A-Z)
      DIMENSION NUMDSS(8), POP1(8)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /IOPOS/ ICRSIZ,ICHCSZ,IOFF(2),LENREC
      COMMON /LISTS/ MOIO(10,500),MOIOWD(10,500),MOIOSZ(10,500),
     &   MOIODS(10,500),MOIOFL(10,500)
      COMMON /AUXCACHE/ QUIKGET(10,500)
#ifdef _ASSERT
      if ((SYTYPL.lt.1).or.(22.lt.SYTYPL)) then
         print *, '@UPDLSTS2: Assertion failed.'
         print *, '   SYTYPL = ',SYTYPL
         print *, '   LISTS2 = ',LISTS2
         call errex
      end if
#endif /* _ASSERT */
C
C FIRST DETERMINE ROW-SIZE FROM NS AND SIRREP
C
      DO 5 XIRREP = 1, NIRREP
         MIRREP = DIRPRD(XIRREP, SIRREP)
         NUMDSS(XIRREP) = POP1(MIRREP) * NS
 5    CONTINUE
      IWORD2=QUIKGET(1,LISTS2)
      IWORD=MOIOWD(1,LISTS2)
      IREC =MOIO(1,LISTS2)
      DO 10 XIRREP = 1,NIRREP
         NUMDIS=NUMDSS(XIRREP)
         DISSIZ=IRPDPD(XIRREP,SYTYPL)
         MOIODS(XIRREP,LISTS2)=NUMDIS
         MOIOSZ(XIRREP,LISTS2)=DISSIZ
         MOIOWD(XIRREP,LISTS2)=IWORD
         MOIO  (XIRREP,LISTS2)=IREC
         IF(QUIKGET(XIRREP,LISTS2).NE.0) QUIKGET(XIRREP, LISTS2)=IWORD2
         IWORD=IWORD+NUMDIS*DISSIZ*IINTFP
         IWORD2=IWORD2+NUMDIS*DISSIZ*IINTFP
         IF(IWORD.GT.LENREC)THEN
            INC=(IWORD-1)/LENREC
            IWORD=IWORD-INC*LENREC
            IREC=IREC+INC
         ENDIF
 10   CONTINUE
      ISYTYP(1, LISTS2) = SYTYPL
C
C  THE RIGHT HAND TYPE [I,S] DOES NOT HAVE A LABEL
C
      RETURN
      END
