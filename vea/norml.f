      SUBROUTINE NORML(SCR, MAXCOR, IUHF, ISPIN)
C
C  THE LEFTHAND EIGENVECTOR RESIDING ON LISTH0 (COLUMN 3) IS NORMALIZED.
C  NEXT THE RIGHT HAND EIGENVECTOR, ALSO RESIDING ON
C  LISTH0 (COLUMN 2) IS NORMALIZED WITH RESPECT TO LEFT HAND VECTOR.
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER SIRREP
      DIMENSION SCR(MAXCOR)
C
      COMMON/LISTDAV/LISTC, LISTHC, LISTH0
      COMMON/EXTRAP/MAXEXP,NREDUCE,NTOL,NSIZEC
      COMMON /SINFO/ NS(8), SIRREP
      COMMON/SLISTS/LS1IN, LS1OUT, LS2IN(2,2), LS2OUT(2,2)
C
      I000 = 1
      I010 = I000 + NSIZEC
      I020 = I010 + NSIZEC
      I030 = I020 + NSIZEC
C
C CALCULATE OVERLAP BETWEEN RIGHT AND LEFT HAND
C
      CALL GETLST(SCR(I000),3,1,1,1,LISTH0)
      CALL GETLST(SCR(I010),2,1,1,1,LISTH0)
      IF (IUHF .EQ. 0) THEN
         CALL SPNTSING(NSIZEC, SCR(I010),SCR(I020), MAXCOR-I020+1,
     $      SIRREP, 2)
      ENDIF
      OVRLAPLR = SDOT(NSIZEC, SCR(I000),1, SCR(I010),1)
      WRITE(6,'(1X,A,F12.6)') ' OVERLAP RIGHT AND LEFT HAND STATE',
     $   OVRLAPLR
C
C NORMALIZE LEFT HAND VECTOR, BY CALCULATING THE EXPANSION COEFFICIENTS IN TERMS OF
C DETERMINANTS (P AND 2PH CONFIGURATIONS IN CASE OF LEFT HAND VECTOR)
C
      CALL PUTS(SCR(I000), NSIZEC, ISPIN, IUHF, LS1IN, LS2IN)
      CALL LEFTS1(SCR(I010), MAXCOR, IUHF, ISPIN)
      CALL LOADS(SCR(I010),NSIZEC,ISPIN, IUHF, LS1OUT, LS2OUT,.FALSE.,
     $   SCR(I010),MAXCOR-I010+1)
      CALL SCOPY(NSIZEC,SCR(I010),1,SCR(I020),1)
      IF (IUHF .EQ. 0) THEN
         CALL SPNTSING(NSIZEC, SCR(I020), SCR(I030), MAXCOR-I030+1,
     $      SIRREP, 2)
      ENDIF
      XNORML = 1.D0/(SQRT(SDOT(NSIZEC, SCR(I010),1, SCR(I020),1)))
      WRITE(6,'(1X,A,6x,F12.6)') ' NORMALIZING  LEFTHAND STATE', XNORML
C
      CALL SSCAL(NSIZEC,XNORML, SCR(I000),1)
      CALL PUTLST(SCR(I000), 3, 1, 1, 1, LISTH0)
C
      XNORMR = 1.D0/(OVRLAPLR * XNORML)
      WRITE(6,'(1X,A,6x,F12.6)') ' NORMALIZING RIGHTHAND STATE', XNORMR
      CALL GETLST(SCR(I000),2,1,1,1,LISTH0)
      CALL SSCAL(NSIZEC,XNORMR, SCR(I000),1)
      CALL PUTLST(SCR(I000), 2, 1, 1, 1, LISTH0)
C
      RETURN
      END
