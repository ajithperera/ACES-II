 
      SUBROUTINE INIHF2(SCR,NBAS,IUHF)
C
C WRITES THE PREAMBLE TO THE HF2 FILE.
C
CEND
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      CHARACTER*8 DUMMY,LABEL
      CHARACTER*80 FNAME
C
      DIMENSION SCR(NBAS*NBAS) 
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/HF2FIL/LUHF2,LUHFA,LUHFB
C
      DATA DUMMY /'****    '/
      DATA LABEL /'HF2E    '/
C
      ID=-6
      IONE=1
      NSIZ=(NBAS*(NBAS+1))/2
      IF(IUHF.EQ.0) THEN
       CALL GFNAME('HF2     ',FNAME,ILENGTH)
      ELSE
       CALL GFNAME('HF2AB   ',FNAME,ILENGTH)
      ENDIF
      OPEN(UNIT=LUHF2,FILE=FNAME(1:ILENGTH),
     &     FORM='UNFORMATTED',ACCESS='SEQUENTIAL',
     &     STATUS='UNKNOWN')
      IF(IUHF.NE.0) THEN
       CALL GFNAME('HF2AA   ',FNAME,ILENGTH)
       OPEN(UNIT=LUHFA,FILE=FNAME(1:ILENGTH),
     &      FORM='UNFORMATTED',ACCESS='SEQUENTIAL',
     &      STATUS='UNKNOWN')
       CALL GFNAME('HF2BB   ',FNAME,ILENGTH)
       OPEN(UNIT=LUHFB,FILE=FNAME(1:ILENGTH),
     &      FORM='UNFORMATTED',ACCESS='SEQUENTIAL',
     &     STATUS='UNKNOWN')
       REWIND(LUHFA)
       REWIND(LUHFB)
      ENDIF
      REWIND(LUHF2)
      CALL GETREC(20,'JOBARC','NBASCOMP',IONE,NCOMP)
      CALL GETREC(20,'JOBARC','SCFEVECA',NCOMP*NBAS*IINTFP,
     &            SCR)
      WRITE(LUHF2)ID,NBAS*NCOMP,SCR
      IF(IUHF.EQ.1)THEN
       CALL GETREC(20,'JOBARC','SCFEVECB',NCOMP*NBAS*IINTFP,
     &            SCR)
       WRITE(LUHF2)ID,NBAS*NBAS,SCR
      ENDIF
      CALL ZERO(SCR,NBAS*NBAS)
      WRITE(LUHF2)ID,NSIZ,(SCR(J),J=1,NSIZ)
      IF(IUHF.EQ.1)WRITE(LUHF2)ID,NSIZ,(SCR(J),J=1,NSIZ)
      WRITE(LUHF2)ID,NBAS,(SCR(J),J=1,NBAS)
      IF(IUHF.EQ.1)WRITE(LUHF2)ID,NBAS,(SCR(J),J=1,NBAS)
      WRITE(LUHF2)DUMMY,DUMMY,LABEL,DUMMY
      RETURN
      END
