      SUBROUTINE DS16AA(ICORE,MAXCOR,ITYPE,IUHF,ISPIN,
     &                  ICANT,NMO,IRRVEC)
C
C THIS ROUTINE DRIVES THE SORTING OF PPPP OR HHHH INTEGRALS
C  FOR THE A-A SPIN CASE.
C
      IMPLICIT INTEGER(A-Z)
      LOGICAL REDUCE
      CHARACTER*4 INTYPE(6)
      CHARACTER*2 SPCASE(3)
      DIMENSION IPW(8),IPDIS(8),IPDSZ(8)
      DIMENSION ICORE(MAXCOR),IRRVEC(NMO,2)
      COMMON /FLAGS/ IFLAGS(100)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /FILES/ LUOUT,MOINTS
      COMMON /ABCD/ ABCDTYPE
      COMMON /BUFLEN/ ILNBUF
      COMMON /INFO/ NOCCO(2),NVRTO(2)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPY(255,2),DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
      COMMON /MINTPC/ REDUCE
C
      DATA INTYPE /'HHHH','PHHH','PPHH','PHPH','PPPH','PPPP'/
      DATA SPCASE /'AA','BB','AB'/
C
      IF(ITYPE.EQ.1)NORB=NOCCO(ISPIN)
      IF(ITYPE.EQ.6)NORB=NVRTO(ISPIN)
      NWDIS=(NORB*(NORB+1))/2
      IF(ITYPE.EQ.1) THEN
       ISW=ISPIN+6
      ELSE
       IF(ABCDTYPE.EQ.0)THEN
        ISW=ISPIN+4
       ELSE
        ISW=ISPIN
        NWDIS=(NORB*(NORB-1))/2
       ENDIF
      ENDIF
      CALL SETUP3(ICORE,MAXCOR,IR1,IR2,IR3,ISW,ISW,
     &            NWDIS,NWDIS)
C
C    CALCULATE OFFSETS FOR W-ARRAY
C
      IPW(1)=0
      IPDSZ(1)=0
      IPDIS(1)=0
      NSIZE2=0
      DO 2 IRREP=1,NIRREP-1
      IPW(IRREP+1)=IPW(IRREP)+ICORE(IR1+IRREP-1)
     &             *ICORE(IR1+NIRREP+IRREP-1)
      IPDSZ(IRREP+1)=IPDSZ(IRREP)+ICORE(IR1+IRREP+NIRREP-1)
      IPDIS(IRREP+1)=IPDIS(IRREP)+ICORE(IR1+IRREP-1)
2     CONTINUE
      if(abcdtype.eq.0.or.itype.eq.1)then
       NSIZE=IPW(IRREP)+ICORE(IR1+NIRREP-1)*ICORE(IR1+2*NIRREP-1)
       DO 3 IRREP=1,NIRREP
       NSIZE2=MAX(NSIZE2,ICORE(IR1+IRREP-1)*ICORE(IR1+IRREP-1))
3      CONTINUE
       IR5=IR3+NSIZE*IINTFP
       IR6=IR5+NSIZE2*IINTFP
       IR7=IR6+ILNBUF*IINTFP
       IR8=IR7+ILNBUF
       IF(IR8.LT.MAXCOR) THEN
C
C DO IT IN CORE IF POSSIBLE.
C
        IF(IFLAGS(1).GT.1)
     &  WRITE(LUOUT,200)INTYPE(ITYPE),SPCASE(ISPIN),IR7,MAXCOR
200     FORMAT(T3,'@DS16AA-I, Sort of ',A4,A2,' will be done in core.',
     &        /,T3,' Words required:',I8,' Words available:',I8,'.')
        CALL ST16AA(ICORE(IR3),ICORE(IR5),ICORE(IR6),ICORE(IR7),
     &             ICORE(IR1),ICORE(IR2),IPW,IPDIS,IPDSZ,
     &             NSIZE,ITYPE,ISPIN,NMO,IRRVEC)
       ELSE
        IF(ITYPE.EQ.1)THEN
         WRITE(6,3333)
3333     FORMAT(T3,'@DS16AA-F, Insufficient core to sort ijkl ',
     &             'MO integrals!')
C 08/96 bug fix
         CALL INSMEM('DS16AA', IR8, MAXCOR)
        ENDIF
C
C  ONE PROBLEM IN THE OUT CORE VERSION, WE NEED A DIFFERENT
C  SYMMETRY VECTOR WHICH DOES NOT EXIST
C
C
C STARTING POSITION OF THIS SYMMETRY VECTOR
C
        NWDIS=(NORB*(NORB-1))/2
        ISW=ISPIN
        CALL SETUP3(ICORE,MAXCOR,IR1,IR2,IR3,ISW,ISW,
     &               NWDIS,NWDIS)
        IR2A=IR2+NWDIS
        IR1A=IR1+NIRREP
        NOFF=NOCCO(ISPIN)
        DO 5 IRREP=1,NIRREP
         ICORE(IR1A+IRREP-1)=0
5       CONTINUE
        IND=0
        DO 6 IA=1,NORB
        DO 6 IB=1,NORB
         IND=IND+1
         IRREPAB=DIRPRD(IRRVEC(IA+NOFF,ISPIN),IRRVEC(IB+NOFF,ISPIN))
         ICORE(IR1A+IRREPAB-1)=ICORE(IR1A+IRREPAB-1)+1
         ICORE(IR2A+IND-1)=ICORE(IR1A+IRREPAB-1)
6       CONTINUE
        NWDSZ=0 
        DO 7 IRREP=1,NIRREP
         NWDSZ=NWDSZ+ICORE(IR1A+IRREP-1)
7       CONTINUE
        IR3=IR2+NWDIS+NWDSZ
        IF(MOD(IR3,2).EQ.0) IR3=IR3+1
        IPW(1)=0
        IPDIS(1)=0
        DO 8 IRREP=1,NIRREP-1
         IPW(IRREP+1)=IPW(IRREP)+ICORE(IR1+IRREP-1)*
     &                     ICORE(IR1+IRREP+NIRREP-1)
         IPDIS(IRREP+1)=IPDIS(IRREP)+ICORE(IR1+IRREP-1)
8       CONTINUE
        MEMLFT=MAXCOR-IR3
        IF(REDUCE) MEMLFT=MEMLFT/2
        CALL DABCD1(ICORE(IR3),MEMLFT,ICORE(IR1),ICORE(IR2),IPW,
     &              IPDIS,NWDIS,ISPIN,NMO,IRRVEC)
       ENDIF
      ELSE
       bufsiz=600
       IR4=IR3+IINTFP*BUFSIZ
       IR5=IR4+BUFSIZ
       MXCOR=MAXCOR-IR5+1
       CALL ST16AA2(ICORE(IR5),ICORE(IR3),ICORE(IR4),ICORE(IR1),
     &              ICORE(IR2),IPW,IPDIS,IPDSZ,MXCOR,IRRVEC(1,ISPIN),
     &              BUFSIZ,ISPIN)
      ENDIF
      RETURN
      END
