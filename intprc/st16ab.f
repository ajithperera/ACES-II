      SUBROUTINE ST16AB(W,BUF,IBUF,NUMIRW,ISYM,IPW,IPDIS,IPDSZ,
     &                  NSIZE,NORBA,NOFFA,NOFFB,NWDIS,ITYPE,IUHF,
     &                  GRAD,IRREPA,IRREPB)
C
C THIS ROUTINE DOES AN IN-CORE SORT AND SYMMETRY PACKED
C  LIST GENERATION FOR <ij|kl> OR <ab|cd> INTEGRALS (AB SPIN CASE).
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
cjp
cjp some explicit array bounds checkings added to prevent mysterious crashes
cjp
      LOGICAL GRAD
      INTEGER DIRPRD,AND
      CHARACTER*4 SPCASE(3)
      CHARACTER*4 INTYPE(6)
      CHARACTER*8 INAME
      CHARACTER*80 FNAME
      DIMENSION W(NSIZE),NUMIRW(1),IPW(8),IPDIS(8),IPDSZ(8)
      DIMENSION ISYM(1),BUF(ILNBUF),IBUF(ILNBUF)
      DIMENSION IRREPA(*),IRREPB(*)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /FILES/ LUOUT,MOINTS
      COMMON /BUFLEN/ ILNBUF
      COMMON /SYMINF/ NSTART,NIRREP,IRREPY(255,2),DIRPRD(8,8)
      COMMON /FLAGS/ IFLAGS(100)
      COMMON /SHIFT/ ISHIFT,NDRGEO
      DATA INTYPE /'HHHH','PHHH','PPHH','PHPH','PPPH','PPPP'/
      DATA SPCASE /'AA  ','BB  ','AB  '/
      IUPKI(INTX)=AND(INTX,IALONE)
      IUPKJ(INTX)=AND(ISHFT(INTX,-IBITWD),IALONE)
      IUPKK(INTX)=AND(ISHFT(INTX,-2*IBITWD),IALONE)
      IUPKL(INTX)=AND(ISHFT(INTX,-3*IBITWD),IALONE)
      INDX(I,J,N)=I+(J-1)*N
      INDXA(I,J)=I+(J-1)*J/2
      IF(IFLAGS(1).GT.1)THEN
       WRITE(LUOUT,2000)INTYPE(ITYPE)
2000   FORMAT(T3,'@ST16AB-I, Processing integral type ',
     & A4,' spin case AB.')
      ENDIF
      CALL ZERO(W,NSIZE)
      INAME=INTYPE(ITYPE)//'AB  '
      IF(IUHF.EQ.0)INAME=INTYPE(ITYPE)//'AA  '
      CALL GFNAME(INAME,FNAME,ILENGTH)
      OPEN(UNIT=15,FILE=FNAME(1:ILENGTH),
     &FORM='UNFORMATTED',ACCESS='SEQUENTIAL',STATUS='OLD')
C
C PICK UP INTEGRALS ONE BY ONE AND PLACE THEM IN A TRIANGULAR ARRAY.
C  <ij|kl> (OR <ab|cd>) ARE STORED i<=k;j<=l (a<=c;b<=d) IN W.
C
      IF(IUHF.EQ.1)THEN
1      READ(15)BUF,IBUF,NUT
CDIR$ IVDEP
       DO 100 INTGRL=1,NUT
        I=IUPKI(IBUF(INTGRL))
        J=IUPKJ(IBUF(INTGRL))
        K=IUPKK(IBUF(INTGRL))
        L=IUPKL(IBUF(INTGRL))
C
C TERM 1 AND 2
        I1=INDX(I,J,NORBA)
        I2=INDX(K,L,NORBA)
        IRREPIJ=DIRPRD(IRREPA(I+NOFFA),IRREPB(J+NOFFB))
        IOFF=IPW(IRREPIJ)
        NN=NUMIRW(IRREPIJ)
        IADR1=IOFF+ISYM(I1)-IPDSZ(IRREPIJ)
     &          +NN*(ISYM(I2)-1-IPDIS(IRREPIJ))
cjp the iadr1 becomes out of bounds and another array is overwritten
        W(IADR1)=BUF(INTGRL)
cjp
        if(iadr1.gt.nsize) stop 'iadr1 out of range in st16ab'
        IADR2=IOFF+ISYM(I2)-IPDSZ(IRREPIJ)
     &          +NN*(ISYM(I1)-1-IPDIS(IRREPIJ))
        W(IADR2)=BUF(INTGRL)
cjp
        if(iadr2.gt.nsize) stop 'iadr2 out of range in st16ab'
C
C TERM 3 AND 4   
C
        I3=INDX(I,L,NORBA)
        I4=INDX(K,J,NORBA)
        IRREPIL=DIRPRD(IRREPA(I+NOFFA),IRREPB(L+NOFFB))
        IOFF=IPW(IRREPIL)
        NN=NUMIRW(IRREPIL)
        IADR3=IOFF+ISYM(I3)-IPDSZ(IRREPIL)
     &          +NN*(ISYM(I4)-1-IPDIS(IRREPIL))
        W(IADR3)=BUF(INTGRL)
cjp
        if(iadr3.gt.nsize) stop 'iadr3 out of range in st16ab'
        IADR4=IOFF+ISYM(I4)-IPDSZ(IRREPIL)
     &          +NN*(ISYM(I3)-1-IPDIS(IRREPIL))
        W(IADR4)=BUF(INTGRL)
cjp
        if(iadr4.gt.nsize) stop 'iadr4 out of range in st16ab'
100    CONTINUE
       IF(NUT.EQ.ILNBUF)GOTO 1
      ELSEIF(IUHF.EQ.0)THEN
       IF(ITYPE.EQ.1.OR.GRAD) THEN
2      READ(15)BUF,IBUF,NUT
CDIR$ IVDEP
*VOCL LOOP,NOVREC
       DO 200 INTGRL=1,NUT
        I=IUPKI(IBUF(INTGRL))
        J=IUPKJ(IBUF(INTGRL))
        K=IUPKK(IBUF(INTGRL))
        L=IUPKL(IBUF(INTGRL))
C
C TERM 1, 2, 5 AND 6
        I1=INDX(I,J,NORBA)
        I2=INDX(K,L,NORBA)
        I5=INDX(J,I,NORBA)
        I6=INDX(L,K,NORBA)
        IRREPIJ=DIRPRD(IRREPA(I+NOFFA),IRREPB(J+NOFFB))
        IOFF=IPW(IRREPIJ)
        NN=NUMIRW(IRREPIJ)
        IADR1=IOFF+ISYM(I1)-IPDSZ(IRREPIJ)
     &          +NN*(ISYM(I2)-1-IPDIS(IRREPIJ))
        W(IADR1)=BUF(INTGRL)
        IADR2=IOFF+ISYM(I2)-IPDSZ(IRREPIJ)
     &          +NN*(ISYM(I1)-1-IPDIS(IRREPIJ))
        W(IADR2)=BUF(INTGRL)
        IADR5=IOFF+ISYM(I5)-IPDSZ(IRREPIJ)
     &          +NN*(ISYM(I6)-1-IPDIS(IRREPIJ))
        W(IADR5)=BUF(INTGRL)
        IADR6=IOFF+ISYM(I6)-IPDSZ(IRREPIJ)
     &          +NN*(ISYM(I5)-1-IPDIS(IRREPIJ))
        W(IADR6)=BUF(INTGRL)
C
C TERM 3, 4, 7 AND 8    
C
        I3=INDX(I,L,NORBA)
        I4=INDX(K,J,NORBA)
        I7=INDX(L,I,NORBA)
        I8=INDX(J,K,NORBA)
        IRREPIL=DIRPRD(IRREPA(I+NOFFB),IRREPB(L+NOFFA))
        IOFF=IPW(IRREPIL)
        NN=NUMIRW(IRREPIL)
        IADR3=IOFF+ISYM(I3)-IPDSZ(IRREPIL)
     &          +NN*(ISYM(I4)-1-IPDIS(IRREPIL))
        W(IADR3)=BUF(INTGRL)
        IADR4=IOFF+ISYM(I4)-IPDSZ(IRREPIL)
     &          +NN*(ISYM(I3)-1-IPDIS(IRREPIL))
        W(IADR4)=BUF(INTGRL)
        IADR7=IOFF+ISYM(I7)-IPDSZ(IRREPIL)
     &          +NN*(ISYM(I8)-1-IPDIS(IRREPIL))
        W(IADR7)=BUF(INTGRL)
        IADR8=IOFF+ISYM(I8)-IPDSZ(IRREPIL)
     &          +NN*(ISYM(I7)-1-IPDIS(IRREPIL))
        W(IADR8)=BUF(INTGRL)
200    CONTINUE
       IF(NUT.EQ.ILNBUF)GOTO 2
       ELSE IF(ITYPE.EQ.6.AND..NOT.GRAD) THEN
3      READ(15)BUF,IBUF,NUT
CCDIR$ IVDEP
       DO 300 INTGRL=1,NUT
        I=IUPKI(IBUF(INTGRL))
        J=IUPKJ(IBUF(INTGRL))
        K=IUPKK(IBUF(INTGRL))
        L=IUPKL(IBUF(INTGRL))
C
C TERM 1, 2, 5 AND 6
        I1=INDX(I,J,NORBA)
        I2=INDX(K,L,NORBA)
        I5=INDX(J,I,NORBA)
        I6=INDX(L,K,NORBA)
        I1A=INDXA(I,J)
        I2A=INDXA(K,L)
        I5A=INDXA(J,I)
        I6A=INDXA(L,K)
        IRREPIJ=DIRPRD(IRREPA(I+NOFFA),IRREPB(J+NOFFB))
        IOFF=IPW(IRREPIJ)
        NN=NUMIRW(IRREPIJ+NIRREP)
        IF(I.LE.J) THEN
        IADR1=IOFF+ISYM(I1A+NWDIS)-IPDSZ(IRREPIJ)
     &          +NN*(ISYM(I2)-1-IPDIS(IRREPIJ))
        W(IADR1)=BUF(INTGRL)
        ENDIF
        IF(J.LE.I) THEN
        IADR5=IOFF+ISYM(I5A+NWDIS)-IPDSZ(IRREPIJ)
     &          +NN*(ISYM(I6)-1-IPDIS(IRREPIJ))
        W(IADR5)=BUF(INTGRL)
        ENDIF
        IF(K.LE.L) THEN
        IADR2=IOFF+ISYM(I2A+NWDIS)-IPDSZ(IRREPIJ)
     &          +NN*(ISYM(I1)-1-IPDIS(IRREPIJ))
        W(IADR2)=BUF(INTGRL)
        ENDIF
        IF(L.LE.K) THEN
        IADR6=IOFF+ISYM(I6A+NWDIS)-IPDSZ(IRREPIJ)
     &          +NN*(ISYM(I5)-1-IPDIS(IRREPIJ))
        W(IADR6)=BUF(INTGRL)
        ENDIF
C
C TERM 3, 4, 7 AND 8    
C
        I3=INDX(I,L,NORBA)
        I4=INDX(K,J,NORBA)
        I7=INDX(L,I,NORBA)
        I8=INDX(J,K,NORBA)
        I3A=INDXA(I,L)
        I4A=INDXA(K,J)
        I7A=INDXA(L,I)
        I8A=INDXA(J,K)
        IRREPIL=DIRPRD(IRREPA(I+NOFFB),IRREPB(L+NOFFA))
        IOFF=IPW(IRREPIL)
        NN=NUMIRW(IRREPIL+NIRREP)
        IF(I.LE.L) THEN
        IADR3=IOFF+ISYM(I3A+NWDIS)-IPDSZ(IRREPIL)
     &          +NN*(ISYM(I4)-1-IPDIS(IRREPIL))
        W(IADR3)=BUF(INTGRL)
        ENDIF
        IF(L.LE.I) THEN
        IADR7=IOFF+ISYM(I7A+NWDIS)-IPDSZ(IRREPIL)
     &          +NN*(ISYM(I8)-1-IPDIS(IRREPIL))
        W(IADR7)=BUF(INTGRL)
        ENDIF
        IF(K.LE.J) THEN
        IADR4=IOFF+ISYM(I4A+NWDIS)-IPDSZ(IRREPIL)
     &          +NN*(ISYM(I3)-1-IPDIS(IRREPIL))
        W(IADR4)=BUF(INTGRL)
        ENDIF
        IF(J.LE.K) THEN
        IADR8=IOFF+ISYM(I8A+NWDIS)-IPDSZ(IRREPIL)
     &          +NN*(ISYM(I7)-1-IPDIS(IRREPIL))
        W(IADR8)=BUF(INTGRL)
        ENDIF
300    CONTINUE
       IF(NUT.EQ.ILNBUF)GOTO 3
      ENDIF
      ENDIF
C
C FORM SYMMETRY-BLOCKED INTEGRALS AND WRITE THESE
C  TO DISK ONE BUFFER AT A TIME.
C
      IF(ITYPE.EQ.1)NLIST=13 + ISHIFT 
      IF(ITYPE.EQ.6)NLIST=233
      CALL PUTALL(W,NSIZE,1,NLIST)
      CLOSE(UNIT=15,STATUS='DELETE')
      RETURN
      END
