 
      SUBROUTINE CABCI2(W,BUF,IBUF,ICHAIN,
     &                  DISSIZ,NDBCK,NBKINT,NBUCK,
     &                  IRECL,NUMIRW,ISYM,IPW,IPDIS,
     &                  IPDSZ,NWDIS,IRREPA,IRREPB)
C
C THIS ROUTINE CHAINS IN OFF OF THE PPPH SORT FILE AND
C  WRITES THEM TO MOINTS.
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DISSIZ,DIRPRD,A,B,C
      INTEGER AND
      CHARACTER*80 FNAME
      PARAMETER (LUINT=10)
      PARAMETER (LUSRT=15)
      DIMENSION W(DISSIZ*NDBCK),BUF(NBKINT),IBUF(NBKINT)
      DIMENSION ICHAIN(NBUCK),ISYM(1),NUMIRW(2*NIRREP),
     &          IPW(8),IPDIS(8),IPDSZ(8)
      DIMENSION IRREPA(*),IRREPB(*)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /INFO/ NOCCO(2),NVRTO(2)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPY(255,2),DIRPRD(8,8)
      COMMON /SHIFT/ ISHIFT,NDRGEO
C
C STATEMENT FUNCTIONS
C
      IUPKI(IX)=AND(IX,IALONE)
      IUPKJ(IX)=AND(ISHFT(IX,-IBITWD),IALONE)
      IUPKK(IX)=AND(ISHFT(IX,-2*IBITWD),IALONE)
      IUPKL(IX)=AND(ISHFT(IX,-3*IBITWD),IALONE)
      INDX(I,J,N)=I+(J-1)*N
      IDIS=0
      IRREPW=1
      NOCA=NOCCO(1)
      NOCB=NOCCO(2)
      NVRTA=NVRTO(1)
      NVRTB=NVRTO(2)
      CALL GFNAME('SRTFIL  ',FNAME,ILENGTH)
      OPEN(UNIT=LUSRT,FILE=FNAME(1:ILENGTH),FORM='UNFORMATTED',
     &     ACCESS='DIRECT',RECL=IRECL)
      IPOS=0
      DO 10 IBK=1,NBUCK
       ISTART=(IBK-1)*NDBCK+1
       DO 1 IRREP=1,NIRREP
       IRREPST=IRREP
       IF(IPDIS(IRREP).GE.ISTART) THEN
        GO TO 2
       ENDIF
1      CONTINUE
       IRREPST=IRREPST+1
2      CONTINUE
       IRREPST=IRREPST-1
       IOFFST1=IPW(IRREPST)
       NNST=NUMIRW(IRREPST+NIRREP)
       IOFFST=IOFFST1+NNST*(ISTART-1-IPDIS(IRREPST))
       CALL ZERO(W,DISSIZ*NDBCK)
5      READ(LUSRT,REC=ICHAIN(IBK))BUF,IBUF,NUT,ICHAN
       ICHAIN(IBK)=ICHAN
       DO 20 INT=1,NUT
        A=IUPKI(IBUF(INT))
        B=IUPKJ(IBUF(INT))
        I=IUPKK(IBUF(INT))
        C=IUPKL(IBUF(INT))
        IND1=INDX(A,B,NVRTA)
        IND2=INDX(I,C,NOCA)
        IRREPAB=DIRPRD(IRREPA(A+NOCA),IRREPB(B+NOCB))
        IOFF=IPW(IRREPAB)
        NN=NUMIRW(IRREPAB+NIRREP)
        IADR=IOFF+ISYM(IND1+NWDIS)-IPDSZ(IRREPAB)
     &       +NN*(ISYM(IND2)-1-IPDIS(IRREPAB))-IOFFST 
        W(IADR)=BUF(INT)
20     CONTINUE
       IF(ICHAIN(IBK).NE.0)GOTO 5
C
C IF WE REACH THIS POINT, THE ENTIRE BUCKET IS IN CORE.  WRITE THIS
C  TO A DIRECT ACCESS FILE *ONE DISTRIBUTION AT A TIME*.
C
       IOFFW=1
       DO 30 ID=1,NDBCK
29      IDIS=IDIS+1
        IF(IDIS.GT.NUMIRW(IRREPW)) THEN
        IRREPW=IRREPW+1
        IF(IRREPW.GT.NIRREP) GO TO 35
        IDIS=0
        GO TO 29
        ENDIF
        CALL PUTLST(W(IOFFW),IDIS,1,2,IRREPW,29+ISHIFT) 
        IOFFW=IOFFW+NUMIRW(IRREPW+NIRREP)
30     CONTINUE
35     CONTINUE
C
C FINISHED PROCESSING THIS BUCKET.  GO BACK AND PICK UP ANOTHER ONE.
C
10    CONTINUE
C
C ALL DONE.  KILL SORT FILE AND GO HOME.
C
      CLOSE(UNIT=LUSRT,STATUS='DELETE')
c      WRITE(6,100)NDBCK+NOFF
c100   FORMAT(T3,'@CABCI2-I, Wrote ',I6,' logical records of '
c     &          'ordered PPPH integrals.')
      RETURN
      END
