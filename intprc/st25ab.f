      SUBROUTINE ST25AB(W,BUF,IBUF,NUMIRW,ISYM,IPW,IPDIS,IPDSZ,
     &                  NSIZE,NWDIS,NOCCA,
     &                  NOCCB,NVRTA,NVRTB,ITYPE,ISPIN,IUHF,IPART,
     &                  IRREPA,IRREPB)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C THIS ROUTINE PERFORMS AN IN-CORE SORT AND FORMS THE
C SYMMETRY PACKED LIST FOR THE <IJ//KA> AND <AB//CI>
C INTEGRALS (ABAB SPIN CASE ONLY)         
C
CEND
C
C CODED JULY/90  JG
C
      INTEGER DIRPRD,A,B,C,AND
      CHARACTER*1 MODX(2)
      CHARACTER*3 UNIQUE
      CHARACTER*4 SPCASE(4)
      CHARACTER*4 INTYPE(6)
      CHARACTER*8 INAME
      CHARACTER*80 FNAME
      DIMENSION W(NSIZE),BUF(ILNBUF),IBUF(ILNBUF)
      DIMENSION NUMIRW(1),IPW(8),IPDIS(8),IPDSZ(8),ISYM(1)
      DIMENSION IRREPA(*),IRREPB(*)
      COMMON /FLAGS/IFLAGS(100)
      COMMON/MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/FILES/LUOUT,MOINTS
      COMMON /BUFLEN/ ILNBUF
      COMMON/INFO/NOCCO(2),NVRTO(2)
      COMMON/SYMINF/NSTART,NIRREP,IRREPY(255,2),DIRPRD(8,8)
      COMMON /SHIFT/ ISHIFT,NDRGEO
      DATA INTYPE /'HHHH','PHHH','PPHH','PHPH','PPPH','PPPP'/
      DATA SPCASE /'AA  ','BB  ','AB  ','BA  '/
      DATA MODX/'1','2'/
C
      INDX(I,J,N)=I+(J-1)*N
      IUPKI(INT)=AND(INT,IALONE)
      IUPKJ(INT)=AND(ISHFT(INT,-IBITWD),IALONE)
      IUPKK(INT)=AND(ISHFT(INT,-2*IBITWD),IALONE)
      IUPKL(INT)=AND(ISHFT(INT,-3*IBITWD),IALONE)
      IF(IFLAGS(1).GT.1)THEN
       WRITE(LUOUT,2009)INTYPE(ITYPE),SPCASE(ISPIN)
2009   FORMAT(T3,'@ST25AB-I, Processing integral type ',
     &        A4,' spin case ',A2,'.')
      ENDIF
C
C  ZERO THE OUTPUT ARRAY
C
      CALL ZERO(W,NSIZE)
C
C OPEN INTEGRAL FILE
C
      IF(ITYPE.EQ.2) UNIQUE='P  '
      IF(ITYPE.EQ.5) UNIQUE='H  '
      INAME=INTYPE(ITYPE)//MODX(IPART)//UNIQUE
      IF(IUHF.EQ.1) THEN
       CALL GFNAME(INAME,FNAME,ILENGTH)
       OPEN(UNIT=15,FILE=FNAME(1:ILENGTH),
     &      FORM='UNFORMATTED',ACCESS='SEQUENTIAL',STATUS='OLD')
      ELSE
       INAME=INTYPE(ITYPE)//'AA  '
       CALL GFNAME(INAME,FNAME,ILENGTH)
       OPEN(UNIT=15,FILE=FNAME(1:ILENGTH),
     &      FORM='UNFORMATTED',ACCESS='SEQUENTIAL',STATUS='OLD')
      ENDIF
C
C GET INTEGRALS ONE BY ONE AND PLACE THEM IN THE OUTPUT ARRAY
C NOTE THAT THE INTEGRALS <IJ/KA> AND <AB/CI> ARE SAVED WITH
C I<=K;J,A AND A<=B;CI RESPECTIVELY
C
      IF(ITYPE.EQ.2) THEN
C
C PROCESS <IJ/KA> INTEGRALS
C
       IF(IUHF.EQ.0) THEN
C
C CLOSED SHELL CASE
C
1000   READ(15) BUF,IBUF,NUT 
C
CDIR$ IVDEP
C
*VOCL LOOP,NOVREC
       DO 1001 INT=1,NUT
        I=IUPKI(IBUF(INT))
        J=IUPKJ(IBUF(INT))
        K=IUPKK(IBUF(INT))
        A=IUPKL(IBUF(INT))
C
C CONTRIBUTIONS OF THIS INTEGRAL TO <IJ/KA> AND <KJ/IA>
C
        I1=INDX(I,J,NOCCA)
        I2=INDX(K,A,NOCCA)
        IRREPIJ=DIRPRD(IRREPA(I),IRREPB(J))
        IOFF1=IPW(IRREPIJ)
        NN=NUMIRW(IRREPIJ+NIRREP)
        IADR1=IOFF1+ISYM(I1+NWDIS)-IPDSZ(IRREPIJ)
     &          +NN*(ISYM(I2)-1-IPDIS(IRREPIJ)) 
        W(IADR1)=BUF(INT)
        I3=INDX(K,J,NOCCA)
        I4=INDX(I,A,NOCCA)
        IRREPKJ=DIRPRD(IRREPA(K),IRREPB(J))
        IOFF2=IPW(IRREPKJ)
        NN=NUMIRW(IRREPKJ+NIRREP)
        IADR2=IOFF2+ISYM(I3+NWDIS)-IPDSZ(IRREPKJ)+
     &             NN*(ISYM(I4)-1-IPDIS(IRREPKJ)) 
        W(IADR2)=BUF(INT)
1001   CONTINUE
       IF(NUT.EQ.ILNBUF) GOTO 1000 
       CALL PUTALL(W,NSIZE,1,10+ISHIFT) 
1002   CONTINUE
       ELSE IF(IPART.EQ.1.AND.IUHF.EQ.1) THEN
1       READ(15)BUF,IBUF,NUT
C
CDIR$ IVDEP
C
*VOCL LOOP,NOVREC
        DO 100 INT=1,NUT
         I=IUPKI(IBUF(INT))
         J=IUPKJ(IBUF(INT))
         A=IUPKK(IBUF(INT))
         K=IUPKL(IBUF(INT))
         I1=INDX(I,J,NOCCA)
         I2=INDX(A,K,NVRTA)
         IRREPIJ=DIRPRD(IRREPA(I),IRREPB(J))
         IOFF1=IPW(IRREPIJ)
         NN=NUMIRW(IRREPIJ+NIRREP)
         IADR1=IOFF1+ISYM(I1+NWDIS)-IPDSZ(IRREPIJ)
     &               +NN*(ISYM(I2)-1-IPDIS(IRREPIJ))
         W(IADR1)=BUF(INT)
         I3=INDX(I,K,NOCCA)
         I4=INDX(A,J,NVRTA)
         IRREPIK=DIRPRD(IRREPA(I),IRREPB(K))
         IOFF2=IPW(IRREPIK)
         NN=NUMIRW(IRREPIK+NIRREP)
         IADR2=IOFF2+ISYM(I3+NWDIS)-IPDSZ(IRREPIK)
     &              +NN*(ISYM(I4)-1-IPDIS(IRREPIK))
         W(IADR2)=BUF(INT)
100     CONTINUE
        IF(NUT.EQ.ILNBUF) GO TO 1
        CALL PUTALL(W,NSIZE,1,9 + ISHIFT) 
       ELSE IF(IPART.EQ.2) THEN
2       READ(15)BUF,IBUF,NUT
C
CDIR$ IVDEP
C
*VOCL LOOP,NOVREC
        DO 101 INT=1,NUT
         I=IUPKI(IBUF(INT))
         J=IUPKJ(IBUF(INT))
         K=IUPKK(IBUF(INT))
         A=IUPKL(IBUF(INT))
         I1=INDX(I,J,NOCCA)
         I2=INDX(K,A,NOCCA)
         IRREPIJ=DIRPRD(IRREPA(I),IRREPB(J))
         IOFF1=IPW(IRREPIJ)
         NN=NUMIRW(IRREPIJ+NIRREP)
         IADR1=IOFF1+ISYM(I1+NWDIS)-IPDSZ(IRREPIJ)
     &               +NN*(ISYM(I2)-1-IPDIS(IRREPIJ))
         W(IADR1)=BUF(INT)
         I3=INDX(K,J,NOCCA)
         I4=INDX(I,A,NOCCA)
         IRREPKJ=DIRPRD(IRREPA(K),IRREPB(J))
         IOFF2=IPW(IRREPKJ)
         NN=NUMIRW(IRREPKJ+NIRREP)
         IADR2=IOFF2+ISYM(I3+NWDIS)-IPDSZ(IRREPKJ)
     &                +NN*(ISYM(I4)-1-IPDIS(IRREPKJ))
         W(IADR2)=BUF(INT)
101     CONTINUE
        IF(NUT.EQ.ILNBUF) GO TO 2
        CALL PUTALL(W,NSIZE,1,10 + ISHIFT) 
       ENDIf
C
       ELSE IF (ITYPE.EQ.5) THEN   
C
C PROCESS <AB//CI> INTEGRALS
C
       IF(IUHF.EQ.0) THEN
C
C CLOSED SHELL CASE
C
2000   READ(15) BUF,IBUF,NUT 
C
CDIR$ IVDEP
C
*VOCL LOOP,NOVREC
       DO 2001 INT=1,NUT
        I=IUPKI(IBUF(INT))
        A=IUPKJ(IBUF(INT))
        B=IUPKK(IBUF(INT))
        C=IUPKL(IBUF(INT))
C
C CONTRIBUTIONS OF THIS INTEGRAL TO <AB/CI> AND <CB/AI>
C
        I1=INDX(A,B,NVRTA)
        I2=INDX(C,I,NVRTA)
        IRREPAB=DIRPRD(IRREPA(A+NOCCA),IRREPB(B+NOCCB))
        IOFF1=IPW(IRREPAB)
        NN=NUMIRW(IRREPAB+NIRREP)
        IADR1=IOFF1+ISYM(I1+NWDIS)-IPDSZ(IRREPAB)
     &          +NN*(ISYM(I2)-1-IPDIS(IRREPAB)) 
        W(IADR1)=BUF(INT)
        I3=INDX(C,B,NVRTA)
        I4=INDX(A,I,NVRTA)
        IRREPCB=DIRPRD(IRREPA(C+NOCCA),IRREPB(B+NOCCB))
        IOFF2=IPW(IRREPCB)
        NN=NUMIRW(IRREPCB+NIRREP)
        IADR2=IOFF2+ISYM(I3+NWDIS)-IPDSZ(IRREPCB)
     &          +NN*(ISYM(I4)-1-IPDIS(IRREPCB)) 
        W(IADR2)=BUF(INT)
2001   CONTINUE
       IF(NUT.EQ.ILNBUF) GOTO 2000 
       CALL PUTALL(W,NSIZE,1,30 + ISHIFT) 
       ELSE IF(IPART.EQ.1.AND.IUHF.EQ.1) THEN
3       READ(15)BUF,IBUF,NUT
C
CDIR$ IVDEP
C
*VOCL LOOP,NOVREC
        DO 200 INT=1,NUT
         I=IUPKI(IBUF(INT))
         B=IUPKJ(IBUF(INT))
         A=IUPKK(IBUF(INT))
         C=IUPKL(IBUF(INT))
         I1=INDX(A,B,NVRTA)
         I2=INDX(I,C,NOCCA)
         IRREPAB=DIRPRD(IRREPA(A+NOCCA),IRREPB(B+NOCCB))
         IOFF1=IPW(IRREPAB)
         NN=NUMIRW(IRREPAB+NIRREP)
         IADR1=IOFF1+ISYM(I1+NWDIS)-IPDSZ(IRREPAB)
     &          +NN*(ISYM(I2)-1-IPDIS(IRREPAB))
         W(IADR1)=BUF(INT)
         I3=INDX(A,C,NVRTA)
         I4=INDX(I,B,NOCCA)
         IRREPAC=DIRPRD(IRREPA(A+NOCCA),IRREPB(C+NOCCB))
         IOFF2=IPW(IRREPAC)
         NN=NUMIRW(IRREPAC+NIRREP)
         IADR2=IOFF2+ISYM(I3+NWDIS)-IPDSZ(IRREPAC)
     &            +NN*(ISYM(I4)-1-IPDIS(IRREPAC))
         W(IADR2)=BUF(INT)
200     CONTINUE
        IF(NUT.EQ.ILNBUF) GO TO 3
        CALL PUTALL(W,NSIZE,1,29 + ISHIFT)
       ELSE IF(IPART.EQ.2) THEN
4       READ(15)BUF,IBUF,NUT
C
CDIR$ IVDEP
C
*VOCL LOOP,NOVREC
        DO 201 INT=1,NUT
         A=IUPKI(IBUF(INT))
         I=IUPKJ(IBUF(INT))
         C=IUPKK(IBUF(INT))
         B=IUPKL(IBUF(INT))
         I1=INDX(A,B,NVRTA)
         I2=INDX(C,I,NVRTA)
         IRREPAB=DIRPRD(IRREPA(A+NOCCA),IRREPB(B+NOCCB))
         IOFF1=IPW(IRREPAB)
         NN=NUMIRW(IRREPAB+NIRREP)
         IADR1=IOFF1+ISYM(I1+NWDIS)-IPDSZ(IRREPAB)
     &          +NN*(ISYM(I2)-1-IPDIS(IRREPAB))
         W(IADR1)=BUF(INT)
         I3=INDX(C,B,NVRTA)
         I4=INDX(A,I,NVRTA)
         IRREPCB=DIRPRD(IRREPA(C+NOCCA),IRREPB(B+NOCCB))
         IOFF2=IPW(IRREPCB)
         NN=NUMIRW(IRREPCB+NIRREP)
         IADR2=IOFF2+ISYM(I3+NWDIS)-IPDSZ(IRREPCB)
     &            +NN*(ISYM(I4)-1-IPDIS(IRREPCB))
         W(IADR2)=BUF(INT)
201     CONTINUE
        IF(NUT.EQ.ILNBUF) GO TO 4
        CALL PUTALL(W,NSIZE,1,30 + ISHIFT) 
       ENDIF
      ENDIF
      CLOSE(UNIT=15,STATUS='DELETE')
      RETURN
      END  
