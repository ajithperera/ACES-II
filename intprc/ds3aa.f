      SUBROUTINE DS3AA(ICORE,MAXCOR,ITYPE,IUHF,ISPIN,NMO,IRRVEC)
C
C DRIVER FOR THE PROCESSING OF <AB||IJ> INTEGRALS, SPIN CASES
C   AAAA AND BBBB.
C
CEND
      IMPLICIT INTEGER(A-Z)
      CHARACTER*4 INTYPE(6)
      CHARACTER*2 SPCASE(3)
C
      DIMENSION ICORE(MAXCOR),IRRVEC(NMO,2)
C
      COMMON /FLAGS/ IFLAGS(100)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /FILES/ LUOUT,MOINTS
      COMMON /BUFLEN/ ILNBUF
      COMMON /INFO/ NOCCO(2),NVRTO(2)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPY(255,2),DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
C
      DATA INTYPE /'HHHH','PHHH','PPHH','PHPH','PPPH','PPPP'/
      DATA SPCASE /'AA','BB','AB'/
C
C ALLOCATE SUFFICIENT CORE FOR THE FULL EXPANDED <AB|IJ> ARRAY
C  AND THE BUFFERS.
C
      ISYNM1=18+ISPIN
      ISYNM2=20+ISPIN
      LENW=ISYMSZ(ISYNM1,ISYNM2)
      I000=1
      I010=I000+IINTFP*LENW
      I020=I010+IINTFP*ILNBUF*IINTFP
      I030=I020+ILNBUF
C
C NOW PICK UP THE REQUIRED SYMMETRY VECTORS AND SYMMETRY POPULATION
C   VECTORS.
C 
      LEN1=NVRTO(ISPIN)*NVRTO(ISPIN)
      LEN2=NOCCO(ISPIN)*NOCCO(ISPIN)
      I040=I030+LEN1
      I050=I040+LEN2
      I060=I050+NIRREP
      I070=I060+NIRREP
C
C For large systems we need to check whether we have enough 
C memory before we proceed. Ajith Perera 11/2001. 
C
      IF (I070 .GE. MAXCOR) CALL INSMEM("@DS3AA",I070,MAXCOR)
      IF(ISPIN.EQ.1)THEN
       CALL GETREC(20,'JOBARC','SVAVA2I ',LEN1,ICORE(I030))
       CALL GETREC(20,'JOBARC','SOAOA2I ',LEN2,ICORE(I040))
       CALL GETREC(20,'JOBARC','SVAVA2X ',NIRREP,ICORE(I050))
       CALL GETREC(20,'JOBARC','SOAOA2X ',NIRREP,ICORE(I060))
      ELSE
       CALL GETREC(20,'JOBARC','SVBVB2I ',LEN1,ICORE(I030))
       CALL GETREC(20,'JOBARC','SOBOB2I ',LEN2,ICORE(I040))
       CALL GETREC(20,'JOBARC','SVBVB2X ',NIRREP,ICORE(I050))
       CALL GETREC(20,'JOBARC','SOBOB2X ',NIRREP,ICORE(I060))
      ENDIF
C
C NOW COMPUTE OFFSET VECTORS NEEDED FOR ROUTINE
C
      I080=I070+NIRREP
      I090=I080+NIRREP
      I100=I090+NIRREP
      I100=I100+MOD(I100+1,2)
      LENWF=LENW
      I110=I100+IINTFP*LENWF
      IOFF1=0
      IOFF2=0
      IOFF3=0
      DO 10 IRREP=1,NIRREP
       ICORE(I070+IRREP-1)=IOFF1
       ICORE(I080+IRREP-1)=IOFF2
       ICORE(I090+IRREP-1)=IOFF3
       INC1=ICORE(I050+IRREP-1)
       INC2=ICORE(I060+IRREP-1)
       IOFF1=IOFF1+INC1
       IOFF2=IOFF2+INC2
       IOFF3=IOFF3+INC1*INC2
10    CONTINUE
      IF(I110.GT.MAXCOR)THEN
       CALL INSMEM('DS3AA',I110,MAXCOR)
      ENDIF
      IF(IFLAGS(1).GT.10)THEN
       WRITE(LUOUT,1002)INTYPE(ITYPE),SPCASE(ISPIN)
1002   FORMAT(T3,'@DS3AA-I, ',A4,A2,' integrals will be sorted ',
     &        'in core.')
       WRITE(LUOUT,1001)I110,MAXCOR
1001   FORMAT(T3,'   Words required: ',I8,'  Words available: ',I8)
      ENDIF
C
      CALL STT3AA(ICORE(I000),ICORE(I010),ICORE(I020),ICORE(I030),
     &            ICORE(I040),ICORE(I050),ICORE(I060),ICORE(I070),
     &            ICORE(I080),ICORE(I090),ICORE(I100),
     &            LENW,LENWF,ISPIN,IUHF,NMO,IRRVEC)
      CALL SCOPY(LENWF,ICORE(I100),1,ICORE(I000),1)
C
      RETURN
      END
