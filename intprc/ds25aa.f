      SUBROUTINE DS25AA(ICORE,MAXCOR,ITYPE,IUHF,ISPIN,IOUT,NMO,IRRVEC)
      IMPLICIT INTEGER(A-Z)
      INTEGER DIRPRD
      LOGICAL REDUCE
      CHARACTER*4 INTYPE(6)
      CHARACTER*2 SPCASE(3)
      CHARACTER*8 POPVEC
C
      DIMENSION ICORE(MAXCOR),IRRVEC(NMO,2)
      DIMENSION IPW(8),IPDIS(8),IPDSZ(8)
C
      COMMON /FLAGS/ IFLAGS(100)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /FILES/ LUOUT,MOINTS
      COMMON /BUFLEN/ ILNBUF
      COMMON /INFO/ NOCCO(2),NVRTO(2)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPY(255,2),DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
      COMMON /MINTPC/REDUCE
      common /shift/ ishift,NDRGEO
C
      DATA INTYPE /'HHHH','PHHH','PPHH','PHPH','PPPH','PPPP'/
      DATA SPCASE /'AA','BB','AB'/
C
      NOCC=NOCCO(ISPIN)
      NVRT=NVRTO(ISPIN)
      IOUT=0
      NWDIS=NOCC*NVRT
      IF(ITYPE.EQ.2)THEN
       NWDSZ=(NOCC*(NOCC+1))/2
       ISW1=ISPIN+6 
       ISW2=ISPIN+15 
       IF(ISPIN.EQ.1) POPVEC='SOAOA0X '
       IF(ISPIN.EQ.2) POPVEC='SOBOB0X '
      ELSE
       NWDSZ=(NVRT*(NVRT+1))/2
       ISW1=ISPIN+4 
       ISW2=ISPIN+8 
       IF(ISPIN.EQ.1)POPVEC='SVAVA0X '
       IF(ISPIN.EQ.2)POPVEC='SVBVB0X '
      ENDIF
C
      CALL SETUP3(ICORE,MAXOR,IR1,IR2,IR3,ISW1,ISW2,
     &            NWDIS,NWDSZ)
      CALL GETREC(20,'JOBARC',POPVEC,NIRREP,ICORE(IR3))
C
C    CALCULATE OFFSETS FOR W-ARRAY 
C
      IPW(1)=0
      IPDSZ(1)=0
      IPDIS(1)=0
      NSIZE2=0
      DO 2 IRREP=1,NIRREP-1
      IPW(IRREP+1)=IPW(IRREP)+ICORE(IR1+IRREP-1)*
     &             ICORE(IR1+NIRREP+IRREP-1)
      IPDSZ(IRREP+1)=IPDSZ(IRREP)+ICORE(IR1+IRREP+NIRREP-1)
      IPDIS(IRREP+1)=IPDIS(IRREP)+ICORE(IR1+IRREP-1)
2     CONTINUE
      NSIZE=IPW(NIRREP)+ICORE(IR1+NIRREP-1)*ICORE(IR1+2*NIRREP-1)
      DO 3 IRREP=1,NIRREP
      NSIZE2=MAX(NSIZE2,ICORE(IR1+IRREP-1)*ICORE(IR3+IRREP-1))
3     CONTINUE
      IR4=IR3+NIRREP*IINTFP
      IR5=IR4+NSIZE*IINTFP
      IR6=IR5+NSIZE2*IINTFP
      IR7=IR6+ILNBUF*IINTFP
      IR8=IR7+ILNBUF
      IF(IUHF.EQ.0.AND.ITYPE.EQ.5)THEN
C
C SEE IF AB HAS TO BE DONE OUT OF CORE FOR RHF.  IF IT DOES,
C THEN DO THE AA OUT OF CORE SO THAT WE GET AA AND AB FROM ONE SORT.
C
       NSIZE3=0
       NDIS=0
       NDSZ=0
       DO 4 IRREP=1,NIRREP
        NSIZE3=NSIZE3+IRPDPD(IRREP,ISYTYP(1,30+ishift))*
     &                IRPDPD(IRREP,ISYTYP(2,30+ishift))
        NDIS=NDIS+IRPDPD(IRREP,ISYTYP(2,30+ishift))
        NDSZ=NDSZ+IRPDPD(IRREP,ISYTYP(1,30+ishift))
4      CONTINUE
       ITEST=MAX(IR8,(IINTFP*NSIZE3+2*NIRREP+NDIS+
     &      NDSZ+ILNBUF*(IINTFP+1)))
      ELSE
       ITEST=IR8
      ENDIF
      IF(ITEST.GT.MAXCOR)THEN
       IF(ITYPE.EQ.2)THEN
        WRITE(6,3333)
3333    FORMAT(T3,'@DS25AA-F, Insufficient core to sort ijka ',
     &            'MO integrals!')
        CALL INSMEM('DS25AA',MAXCOR,ITEST)
       ENDIF
       IF(IFLAGS(1).GE.1)THEN
        WRITE(LUOUT,1000)INTYPE(ITYPE),SPCASE(ISPIN)
       ENDIF
C
C  ONE PROBLEM HERE, WE NEED A DIFFERENT SYMMETRy VECTOR FOR
C  THE OUT CORE VERSION (I,J FULL ARRAY)
C
       NWDSZ=NVRT*NVRT
C
C  STARTING POSITION OF THIS SYMMETRY VECTOR 
C
       IR2A=IR2+NWDIS
       IR1A=IR1+NIRREP
       DO 5 IRREP=1,NIRREP
       ICORE(IR1A+IRREP-1)=0
5      CONTINUE
       IND=0
       DO 6 IA=1,NVRT
       DO 6 IB=1,NVRT
       IND=IND+1 
       IRREPAB=DIRPRD(IRRVEC(IA+NOCC,ISPIN),IRRVEC(IB+NOCC,ISPIN))
       ICORE(IR1A+IRREPAB-1)=ICORE(IR1A+IRREPAB-1)+1
       ICORE(IR2A+IND-1)=ICORE(IR1A+IRREPAB-1)
6      CONTINUE
       NWDSZ=0
       DO 7 IRREP=1,NIRREP 
       NWDSZ=NWDSZ+ICORE(IR1A+IRREP-1)
7      CONTINUE
       IR3=IR2+NWDIS+NWDSZ
       IF(MOD(IR3,2).EQ.0) IR3=IR3+1
       IPW(1)=0
       DO 8 IRREP=1,NIRREP-1
       IPW(IRREP+1)=IPW(IRREP)+ICORE(IR1+IRREP-1)*
     &              ICORE(IR1+IRREP+NIRREP-1)
8      CONTINUE 
C
       MEMLFT=MAXCOR-IR3
       IF(REDUCE) MEMLFT=MEMLFT/2
       CALL DABCI0(ICORE(IR3),MEMLFT,ICORE(IR1),ICORE(IR2),
     &             IPW,IPDIS,NWDIS,ISPIN,NMO,IRRVEC)
       IOUT=1
      ELSE
       IF(IFLAGS(1).GE.1)THEN
        WRITE(LUOUT,1002)INTYPE(ITYPE),SPCASE(ISPIN)
        WRITE(LUOUT,1001)IR5,MAXCOR
       ENDIF
       CALL ST25AA(ICORE(IR4),ICORE(IR5),ICORE(IR6),ICORE(IR7),
     &             ICORE(IR1),ICORE(IR2),IPW,IPDIS,
     &             IPDSZ,NSIZE,NWDIS,NOCC,NVRT,ITYPE,ISPIN,IUHF,
     &             NMO,IRRVEC)
      ENDIF
1000  FORMAT(T3,'@DS25AA-I, Out-of-core sort used for ',A4,A2,
     &           ' integrals.')
1001  FORMAT(T3,'   Words required: ',I8,'  Words available: ',I8)
1002  FORMAT(T3,'@DS25AA-I, ',A4,A2,' integrals will be sorted in ',
     & 'core.')
      RETURN
      END
