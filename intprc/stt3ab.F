      SUBROUTINE STT3AB(W,BUF,IBUF,ISYMVV,ISYMOO,ISIZVV,ISIZOO,
     &                  IOFFVV,IOFFOO,IOFFW,LENW,IUHF,
     &                  NMO,IRREPS)
C
C THIS SUBROUTINE PROCESSES THE <Ab|Ij> INTEGRALS OF SYMMETRY
C  TYPE ABAB, PUTS THEM INTO A SYMMETRY PACKED LIST AND WRITES
C  THEM TO DISK.
C
CEND
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER A,B,DIRPRD,POP,VRT,AND
      CHARACTER*80 FNAME
      DIMENSION BUF(ILNBUF),IBUF(ILNBUF),W(LENW)
      DIMENSION ISYMOO(1),ISYMVV(1)
      DIMENSION ISIZVV(NIRREP),ISIZOO(NIRREP),IOFFVV(NIRREP)
      DIMENSION IOFFOO(NIRREP),IOFFW(NIRREP)
      DIMENSION IRREPS(NMO,2)
C
      COMMON /FLAGS/ IFLAGS(100)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPY(255,2),DIRPRD(8,8)
      COMMON /FILES/ LUOUT,MOINTS
      COMMON /BUFLEN/ ILNBUF
      COMMON /INFO/ NOCCO(2),NVRTO(2)
      COMMON /SHIFT/ ISHIFT,NDRGEO
C
      IUPKI(INT)=AND(INT,IALONE)
      IUPKJ(INT)=AND(ISHFT(INT,-IBITWD),IALONE)
      IUPKK(INT)=AND(ISHFT(INT,-2*IBITWD),IALONE)
      IUPKL(INT)=AND(ISHFT(INT,-3*IBITWD),IALONE)
      INDXF(I,J,N)=I+(J-1)*N
C
      NOCA=NOCCO(1)
      NOCB=NOCCO(2)
      NVRTA=NVRTO(1)
      NVRTB=NVRTO(2)
C
      IF(IFLAGS(1).GE.1)WRITE(LUOUT,2000)
2000  FORMAT(T3,'@STT3-I, Processing integral type PPPP, ',
     &       'spin case ABAB.')
C
      CALL GFNAME('PPHHAB  ',FNAME,ILENGTH)
      OPEN(UNIT=15,FILE=FNAME(1:ILENGTH),
     &FORM='UNFORMATTED',ACCESS='SEQUENTIAL',STATUS='OLD')
      CALL ZERO(W,LENW)
C
C PICK UP A BUFFER OF INTEGRALS
C
1     READ(15)BUF,IBUF,NUT
      DO 10 INT=1,NUT
       I=IUPKI(IBUF(INT))
       J=IUPKJ(IBUF(INT))
       A=IUPKK(IBUF(INT))
       B=IUPKL(IBUF(INT))
       X=BUF(INT)
       IRRI=IRREPS(I,1)
       IRRJ=IRREPS(J,2)
       IRDPD=DIRPRD(IRRI,IRRJ)
C
C GET IJ AND AB ABSOLUTE OFFSETS
C
       IOFFIJ=INDXF(I,J,NOCA)
       IOFFAB=INDXF(A,B,NVRTA)
C
C NOW GET RELATIVE IJ SYMMETRY OFFSET WITHIN THE DPD BLOCK AND DO SAME
C  FOR AB
C
       IOFIJS=ISYMOO(IOFFIJ)-IOFFOO(IRDPD)
       IOFABS=ISYMVV(IOFFAB)-IOFFVV(IRDPD)
C
C NOW CALCULATE ABSOLUTE POSITION IN W VECTOR WHERE THIS ELEMENT
C  BELONGS
C
       IABSOF=IOFFW(IRDPD)
       IRELOF=INDXF(IOFABS,IOFIJS,ISIZVV(IRDPD))
       IOFF=IRELOF+IABSOF
       W(IOFF)=X
10    CONTINUE
      IF(NUT.EQ.ILNBUF)GOTO 1
C
C NOW DUMP THESE TO DISK
C
      CALL PUTALL(W,LENW,1,16 + ISHIFT) 
#ifdef _DEBUG_LVLM
      Write(6,*) "<AB|IJ> ABAB integrals"
      Write(6,"(6(1X,F15.10))") (W(i),i=1,Lenw)
#endif 
      CLOSE(UNIT=15,STATUS='DELETE')
      RETURN
      END
