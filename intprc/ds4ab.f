      SUBROUTINE DS4AB(ICORE,MAXCOR,ITYPE,IUHF,ISPIN,NMO,IRRVEC)
C
C DRIVER FOR THE PROCESSING OF <Ai|Bj> AND <aI|bJ> INTEGRALS.
C
CEND
      IMPLICIT INTEGER(A-Z)
      CHARACTER*4 INTYPE(6)
      CHARACTER*2 SPCASE(3)
C
      DIMENSION ICORE(MAXCOR),IRRVEC(NMO,2)
C
      COMMON /FLAGS/ IFLAGS(100)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /FILES/ LUOUT,MOINTS
      COMMON /BUFLEN/ ILNBUF
      COMMON /INFO/ NOCCO(2),NVRTO(2)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPY(255,2),DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
C
      DATA INTYPE /'HHHH','PHHH','PPHH','PHPH','PPPH','PPPP'/
      DATA SPCASE /'AA','BB','AB'/
C
C LOOP OVER CASES.  CASE 1 IS <Ai|Bj> -> W(Aj,Bi), CASE 2 
C  IS <aI|bJ> -> W(aJ,bI).
C
      ISYNM=11
      ISPINA=1
      ISPINJ=2
      DO 10 ICASE=1,2
C       
C ALLOCATE CORE FOR THE W(Aj,Bi) OR W(aJ,bI) ARRAY AND THE INTEGRAL
C  BUFFERS
C
      LENW=ISYMSZ(ISYNM,ISYNM)
      I000=1
      I010=I000+IINTFP*LENW
      I020=I010+ILNBUF*IINTFP
      I030=I020+ILNBUF
C
C NOW PICK UP THE REQUIRED SYMMETRY VECTORS AND SYMMETRY POPULATION
C   VECTORS.
C 
      LEN=NVRTO(ISPINA)*NOCCO(ISPINJ)
      I040=I030+LEN
      I050=I040+NIRREP
      IF(ICASE.EQ.1)THEN
       CALL GETREC(20,'JOBARC','SOBVA2I ',LEN,ICORE(I030))
       CALL GETREC(20,'JOBARC','SOBVA2X ',NIRREP,ICORE(I040))
      ELSE
       CALL GETREC(20,'JOBARC','SVBOA2I ',LEN,ICORE(I030))
       CALL GETREC(20,'JOBARC','SVBOA2X ',NIRREP,ICORE(I040))
      ENDIF
C
C NOW COMPUTE OFFSET VECTORS NEEDED FOR ROUTINE
C
      I060=I050+NIRREP
      I070=I060+NIRREP
      IOFF1=0
      IOFF2=0
      DO 20 IRREP=1,NIRREP
       ICORE(I050+IRREP-1)=IOFF1
       ICORE(I060+IRREP-1)=IOFF2
       INC=ICORE(I040+IRREP-1)
       IOFF1=IOFF1+INC
       IOFF2=IOFF2+INC*INC
20    CONTINUE
C
C ALLOCATE TEMPORARY INDEX ARRAYS
C
      I080=I070+ILNBUF
      I090=I080+ILNBUF
      I100=I090+ILNBUF
      I110=I100+ILNBUF
      IF(I110.GT.MAXCOR)THEN
       CALL INSMEM('DS4AA',I110,MAXCOR)
      ENDIF
      IF(IFLAGS(1).GT.10)THEN
       WRITE(LUOUT,1002)INTYPE(ITYPE),SPCASE(ISPIN)
1002   FORMAT(T3,'@DS4AB-I, ',A4,A2,' integrals will be sorted ',
     &        'in core.')
       WRITE(LUOUT,1001)I110,MAXCOR
1001   FORMAT(T3,'   Words required: ',I8,'  Words available: ',I8)
      ENDIF
C
      CALL STT4AB(ICORE(I000),ICORE(I010),ICORE(I020),ICORE(I030),
     &            ICORE(I040),ICORE(I050),ICORE(I060),ICORE(I070),
     &            ICORE(I080),ICORE(I090),ICORE(I100),
     &            LENW,ICASE,IUHF,NMO,IRRVEC)
      ISYNM=12
      ISPINA=2
      ISPINJ=1
10    CONTINUE
C
      RETURN
      END
