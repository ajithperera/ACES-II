      SUBROUTINE DS3AB(ICORE,MAXCOR,ITYPE,IUHF,ISPIN,NMO,IRRVEC)
C
C DRIVER FOR THE PROCESSING OF <Ab||Ij> INTEGRALS, SPIN CASE
C   ABAB.
C
CEND
      IMPLICIT INTEGER(A-Z)
      CHARACTER*4 INTYPE(6)
      CHARACTER*2 SPCASE(3)
C
      DIMENSION ICORE(MAXCOR),IRRVEC(NMO,2)
C
      COMMON /FLAGS/ IFLAGS(100)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /FILES/ LUOUT,MOINTS
      COMMON /BUFLEN/ ILNBUF
      COMMON /INFO/ NOCCO(2),NVRTO(2)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPY(255,2),DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      common /shift/ ishift,NDRGEO
C
      DATA INTYPE /'HHHH','PHHH','PPHH','PHPH','PPPH','PPPP'/
      DATA SPCASE /'AA','BB','AB'/
C
C ALLOCATE SUFFICIENT CORE FOR THE FULL <Ab|Ij> ARRAY
C  AND THE BUFFERS.
C
      ISYNM1=ISYTYP(1,16+ishift)
      ISYNM2=ISYTYP(2,16+ishift)
      LENW=ISYMSZ(ISYNM1,ISYNM2)
      I000=1
      I010=I000+IINTFP*LENW
      I020=I010+IINTFP*ILNBUF*IINTFP
      I030=I020+ILNBUF
C
C NOW PICK UP THE REQUIRED SYMMETRY VECTORS AND SYMMETRY POPULATION
C   VECTORS.
C 
      LEN1=NVRTO(1)*NVRTO(2)
      LEN2=NOCCO(1)*NOCCO(2)
      I040=I030+LEN1
      I050=I040+LEN2
      I060=I050+NIRREP
      I070=I060+NIRREP
      CALL GETREC(20,'JOBARC','SVAVB2I ',LEN1,ICORE(I030))
      CALL GETREC(20,'JOBARC','SOAOB2I ',LEN2,ICORE(I040))
      CALL GETREC(20,'JOBARC','SVAVB2X ',NIRREP,ICORE(I050))
      CALL GETREC(20,'JOBARC','SOAOB2X ',NIRREP,ICORE(I060))
C
C NOW COMPUTE OFFSET VECTORS NEEDED FOR ROUTINE
C
      I080=I070+NIRREP
      I090=I080+NIRREP
      I100=I090+NIRREP
      IOFF1=0
      IOFF2=0
      IOFF3=0
      DO 10 IRREP=1,NIRREP
       ICORE(I070+IRREP-1)=IOFF1
       ICORE(I080+IRREP-1)=IOFF2
       ICORE(I090+IRREP-1)=IOFF3
       INC1=ICORE(I050+IRREP-1)
       INC2=ICORE(I060+IRREP-1)
       IOFF1=IOFF1+INC1
       IOFF2=IOFF2+INC2
       IOFF3=IOFF3+INC1*INC2
10    CONTINUE
      LENW=IOFF3
      IF(I100.GT.MAXCOR)THEN
       CALL INSMEM('DS3AB',I100,MAXCOR)
      ENDIF
      IF(IFLAGS(1).GT.10)THEN
       WRITE(LUOUT,1002)INTYPE(ITYPE),SPCASE(ISPIN)
1002   FORMAT(T3,'@DS3AB-I, ',A4,A2,' integrals will be sorted ',
     &        'in core.')
       WRITE(LUOUT,1001)I100,MAXCOR
1001   FORMAT(T3,'   Words required: ',I8,'  Words available: ',I8)
      ENDIF
c
C
      CALL STT3AB(ICORE(I000),ICORE(I010),ICORE(I020),ICORE(I030),
     &            ICORE(I040),ICORE(I050),ICORE(I060),ICORE(I070),
     &            ICORE(I080),ICORE(I090),LENW,IUHF,NMO,IRRVEC)
C
      RETURN
      END
