      SUBROUTINE FLOPUSE(IUHF)
      IMPLICIT INTEGER(A-Z)
      CHARACTER*8 PTGRP
      COMMON /INFO/ NOCCA,NOCCB,NVRTA,NVRTB
      COMMON /SYMINF/ NSTART,NIRREP,IRREPY(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NTAA,NTBB,NF1AA,NF1BB,
     &             NF2AA,NF2BB
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
      COMMON /FILES/ LUOUT,MOINTS 
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      NNM1O2(I)=I*(I-1)/2
C
      CALL GETCREC(20,'JOBARC','COMPPTGP',8,PTGRP)
C
C     INTERMEDIATES
C
C     W(MNIJ) INTEREMDIATES
C
      FW2AB=NOCCA*NOCCB*NOCCA*NOCCB*NVRTA*NVRTB
      IF(IUHF.EQ.1) THEN
       FW2AA=NNM1O2(NOCCA)*NNM1O2(NOCCA)*NNM1O2(NVRTA)
       FW2BB=NNM1O2(NOCCB)*NNM1O2(NOCCB)*NNM1O2(NVRTB)
      ENDIF
C
C ADDITIONAL TERMS CCSD ONLY
C
      FW2ABCC=NOCCA*NOCCB*NOCCA*NOCCB*NVRTA
      IF(IUHF.EQ.1) THEN
       FW2ABCC=FW2ABCC+NOCCA*NOCCB*NOCCA*NOCCB*NVRTB
       FW2AACC=NNM1O2(NOCCA)*NOCCA*NVRTA*NOCCA
       FW2BBCC=NNM1O2(NOCCB)*NOCCB*NVRTB*NOCCB
      ENDIF
C
C ADDITIONAL TERMS W(ABEF) INTERMEDIATES CCSD ONLY
C
      FW1ABCC=NOCCA*NOCCB*NVRTA*NVRTB*NOCCA*NVRTB
     &        +NOCCA*NOCCB*NVRTB*NOCCA*NVRTA
      IF(IUHF.EQ.1) THEN
       FW1ABCC=FW1ABCC+NOCCA*NOCCB*NVRTA*NVRTB*NOCCB*NVRTA
     &         +NOCCA*NOCCB*NVRTA*NVRTB*NOCCB
       FW1AACC=NNM1O2(NOCCA)*NOCCA*NVRTA*NNM1O2(NVRTA)
     &         +NNM1O2(NOCCA)*NVRTA*NVRTA*NOCCA
       FW1BBCC=NNM1O2(NOCCB)*NOCCB*NVRTB*NNM1O2(NVRTB)
     &         +NNM1O2(NOCCB)*NVRTB*NVRTB*NOCCB
      ENDIF
C
C W(MBEJ) INTERMEDIATES
C
      FW3AAAA=NOCCA*NOCCA*NVRTA*NVRTA*(NOCCA*NVRTA+NOCCB*NVRTB)
      FW3ABAB=NOCCA*NVRTB*NVRTA*NOCCB*(NOCCA*NVRTA+NOCCB*NVRTB)
      FW3BAAB=NOCCB*NVRTA*NVRTA*NOCCB*NOCCA*NVRTB 
      IF(IUHF.EQ.1) THEN
       FW3BBBB=NOCCB*NOCCB*NVRTB*NVRTB*(NOCCA*NVRTA+NOCCB*NVRTB)
       FW3BABA=NOCCB*NVRTA*NVRTB*NOCCA*(NOCCA*NVRTA+NOCCB*NVRTB)
       FW3ABBA=NOCCA*NVRTB*NVRTB*NOCCA*NOCCB*NVRTA
      ENDIF
C
C ADDITIONAL TERMS CCSD ONLY
C
      FW35CC=NOCCA*NVRTA*NVRTA*NVRTA*NOCCA
      FW36CC=NOCCA*NOCCA*NVRTA*NVRTA*NOCCA
      FW31CC=NOCCA*NVRTB*NVRTA*NVRTB*NOCCB
      FW32CC=NOCCA*NOCCB*NVRTB*NVRTA*NOCCB
      FW33CC=NOCCB*NVRTA*NVRTA*NVRTB*NOCCB
      FW34CC=NOCCB*NOCCA*NVRTA*NVRTA*NOCCB
      IF(IUHF.EQ.1) THEN
       FW37CC=NOCCB*NVRTB*NVRTB*NVRTB*NOCCB
       FW38CC=NOCCB*NOCCB*NVRTB*NVRTB*NOCCB
       FW39CC=NOCCB*NVRTA*NVRTB*NVRTA*NOCCA
       FW310C=NOCCB*NOCCA*NVRTA*NVRTB*NOCCA
       FW311C=NOCCA*NVRTB*NVRTB*NVRTA*NOCCA
       FW312C=NOCCA*NOCCB*NVRTB*NVRTB*NOCCA
      ENDIF 
C
C   F(MI) INTERMEDIATES
C
      FFMIAA=NVRTA*NVRTB*NOCCB*NOCCA*NOCCA
      IF(IUHF.EQ.1) THEN
       FFMIAA=FFMIAA+NNM1O2(NVRTA)*NOCCA*NOCCA*NOCCA
       FFMIBB=NVRTB*NVRTA*NOCCA*NOCCB*NOCCB
     &       +NNM1O2(NVRTB)*NOCCB*NOCCB*NOCCB
      ENDIF
C
C   NO ADDITIONAL WORK FOR CCSD WITH MORE THAN N**4 OPERATIONS REQUIRED
C
C
C   F(EA) INTERMEDIATES
C
      FFEAAA=NOCCA*NOCCB*NVRTB*NVRTA*NVRTA
      IF(IUHF.EQ.1) THEN
       FFEAAA=FFEAAA+NNM1O2(NOCCA)*NVRTA*NVRTA*NVRTA
       FFEABB=NOCCB*NOCCA*NVRTA*NVRTB*NVRTB
     &       +NNM1O2(NOCCB)*NVRTB*NVRTB*NVRTB
      ENDIF
C
C   NO ADDITIONAL WORK FOR CCSD WITRH MORE THAN N**4 OPERATIONS REQUIRED
C
C 
C   F(ME) INTERMEDIATES
C
C   ONLY N**4 OPERATIONS REQUIRED, NOT CONSIDERED
C
C   T1 INTO T1
C
C   ONLY N**4 OIPERATIONS REQUIRED, NOT CONSIDERED 
C
C   T2 INTO T1
C
C  HERE SPIN ADAPTED CODE POSSIBLE !
C
      FT2T1AA=NVRTA*NVRTB*NOCCB*NVRTA*NOCCA
     &        +NNM1O2(NVRTA)*NOCCA*NOCCA*NVRTA
     &        +NOCCA*NOCCB*NVRTB*NOCCA*NVRTA
     &        +NNM1O2(NOCCA)*NVRTA*NVRTA*NOCCA
      IF(IUHF.EQ.1) THEN
       FT2T1BB=NVRTB*NVRTA*NOCCA*NOCCB*NVRTB
     &         +NNM1O2(NVRTB)*NOCCB*NVRTB*NOCCB
     &         +NOCCB*NOCCA*NVRTA*NOCCB*NVRTB
     &         +NNM1O2(NOCCB)*NVRTB*NOCCB*NVRTB
      ENDIF
C
C   CONTRACTION of F(ME) WITH T2
C
C    NOT CONSIDERED SINCE N**4 TERM   
C
C   CONTRACTION OF F(EA) WITH T2 TO T2
C
      FFEACAB=NOCCA*NOCCB*NVRTA*NVRTB*NVRTB
      IF(IUHF.EQ.1) THEN
       FFEACAB=FFEACAB+NOCCA*NOCCB*NVRTB*NVRTA*NVRTA
       FFEACAA=NNM1O2(NOCCA)*NVRTA*NVRTA*NVRTA
       FFEACBB=NNM1O2(NOCCB)*NVRTB*NVRTB*NVRTB
      ENDIF
C    
C    CONTRACTION OF F(MI) WITH T2 TO T2
C
      FFMICAB=NVRTA*NVRTB*NOCCA*NOCCB*NOCCB
      IF(IUHF.EQ.1) THEN
       FFMICAB=FFMICAB+NVRTA*NVRTB*NOCCB*NOCCA*NOCCA  
       FFMICAA=NNM1O2(NVRTA)*NOCCA*NOCCA*NOCCA
       FFMICBB=NNM1O2(NVRTB)*NOCCB*NOCCB*NOCCB
      ENDIF
C
C    PARTICLE LADDER
C
C  SPIN ADAPTED CODE POSSIBLE
C
      FLPPAB=NOCCA*NOCCB*NVRTA*NVRTB*NVRTA*NVRTB
      IF(IUHF.EQ.1) THEN
       FLPPAA=NNM1O2(NVRTA)*NNM1O2(NOCCA)*NNM1O2(NVRTA)
       FLPPBB=NNM1O2(NVRTB)*NNM1O2(NOCCB)*NNM1O2(NVRTB)
      ENDIF
C
C     HOLE LADDER
C
C SPIN ADAPTED CODE POSSIBLE
C
      FLHHAB=NOCCA*NOCCB*NOCCA*NOCCB*NVRTA*NVRTB
      IF(IUHF.EQ.1) THEN
       FLHHAA=NNM1O2(NOCCA)*NNM1O2(NOCCA)*NNM1O2(NVRTA)
       FLHHBB=NNM1O2(NOCCB)*NNM1o2(NOCCB)*NNM1O2(NVRTB)
      ENDIF
C
C     RINGS
C
C  SPIN ADAPTED CODE POSSIBLE
C
      FRABAB=NOCCA*NVRTA*NVRTB*NOCCB*(NOCCA*NVRTA+NOCCB*NVRTB)
      FRBAAB=NOCCB*NVRTA*NVRTB*NOCCA*NOCCA*NVRTB 
      IF(IUHF.EQ.1) THEN
       FRAAAA=NOCCA*NOCCA*NVRTA*NVRTA*(NOCCA*NVRTA+NOCCB*NVRTB)
       FRBBBB=NOCCB*NOCCB*NVRTB*NVRTB*(NOCCA*NVRTA+NOCCB*NVRTB)
       FRBABA=NOCCB*NVRTB*NVRTA*NOCCA*(NOCCA*NVRTA+NOCCB*NVRTB)
       FRABBA=NOCCA*NVRTB*NVRTA*NOCCB*NOCCB*NVRTA
      ENDIF
C
C     T1**2 TO T2 CONTRIBUTION
C
      FTABAB=NOCCA*NVRTB*NVRTA*NOCCA*NOCCB
     &       +NOCCA*NVRTA*NVRTB*NOCCA*NOCCB
      FTBAAB=NOCCB*NVRTA*NVRTA*NOCCA*NOCCB
     &       +NOCCB*NVRTB*NVRTA*NOCCA*NOCCB
      IF(IUHF.EQ.1) THEN
       FTAAAA=NOCCA*NOCCA*NOCCA*NVRTA*NVRTA
     &        +NOCCA*NOCCA*NVRTA*NVRTA*NOCCA
       FTBBBB=NOCCB*NOCCB*NOCCB*NVRTB*NVRTB
     &        +NOCCB*NOCCB*NOCCB*NVRTB*NVRTB
       FTBABA=NOCCB*NVRTA*NVRTB*NOCCB*NOCCA
     &        +NOCCB*NVRTB*NVRTA*NOCCB*NOCCA
       FTABBA=NOCCA*NVRTB*NVRTB*NOCCB*NOCCA
     &        +NOCCA*NVRTA*NVRTB*NOCCB*NOCCA
      ENDIF
C
C     T1 TO T2 CONTRIBUTIONS
C
      FT1T2AB1=NOCCA*NOCCB*NVRTA*NVRTB*NVRTA
      FT1T2AB2=NOCCA*NOCCB*NOCCA*NVRTA*NVRTB
      IF(IUHF.EQ.1) THEN
       FT1T2AB1=FT1T2AB1+NOCCA*NOCCB*NVRTA*NVRTB*NVRTB
       FT1T2AB2=FT1T2AB2+NOCCA*NOCCB*NVRTA*NVRTB*NOCCB 
       FT1T2AA1=NOCCA*NOCCA*NVRTA*NNM1O2(NVRTA)
       FT1T2AA2=NVRTA*NVRTA*NOCCA*NNM1O2(NOCCA)
       FT1T2BB1=NOCCB*NOCCB*NVRTB*NNM1o2(NVRTB)
       FT1T2BB2=NVRTB*NVRTB*NOCCB*NNM1O2(NOCCB)
      ENDIF
C
C   ALL FLOPS GREATER THAN N**4 HAVe BEEN CALCULATED 
C   FOR THE C1 CASE
C   
C   NOW CALCULATE FOR THE ACTUAL POINT GROUP THE NUMBER 
C   OF FLOPS

      SW2AB=0
      SW2AA=0
      SW2BB=0
      SW2ABCC=0
      SW2AACC=0
      SW2BBCC=0
      SW1ABCC=0
      SW1AACC=0
      SW1BBCC=0
      SW3ABAB=0
      SW3BAAB=0
      SW3AAAA=0
      SW3BBBB=0
      SW3BABA=0
      SW3ABBA=0
      SW31CC=0
      SW32CC=0
      SW33CC=0
      SW34CC=0
      SW35CC=0
      SW35CC=0
      SW36CC=0
      SW37CC=0
      SW38CC=0
      SW39CC=0
      SW310C=0
      SW311C=0
      SW312C=0
      SFMIAA=0
      SFMIBB=0
      SFEAAA=0
      SFEABB=0
      SFEBAA=0
      ST2T1AA=0
      ST2T1BB=0
      SFEACAB=0
      SFEACAA=0
      SFEACBB=0
      SFMICAB=0
      SFMICAA=0
      SFMICBB=0
      SLPPAB=0
      SLPPAA=0
      SLPPBB=0
      SLHHAB=0
      SLHHAA=0
      SLHHBB=0
      SRABAB=0
      SRBAAB=0
      SRAAAA=0
      SRBBBB=0
      SRBABA=0
      SRABBA=0
      STABAB=0
      STBAAB=0
      STAAAA=0
      STBBBB=0
      STBABA=0
      STABBA=0
      ST1T2AB1=0
      ST1T2AB2=0
      ST1T2AA1=0
      ST1T2AA2=0
      ST1T2BB1=0
      ST1T2BB2=0
C
C   LOOP OVER ALL IRREPS
C
      DO 1000 IRREP=1,NIRREP  
C
C   FIRST THE EASY PART, NO FURTHER LOOPING REQUIRED FOR THESE TERMS
C
      LENABAA=IRPDPD(IRREP,1)
      LENABBB=IRPDPD(IRREP,2)
      LENIJAA=IRPDPD(IRREP,3)
      LENIJBB=IRPDPD(IRREP,4)
      LENAIAA=IRPDPD(IRREP,9)
      LENAIBB=IRPDPD(IRREP,10)
      LENAIAB=IRPDPD(IRREP,11)
      LENAIBA=IRPDPD(IRREP,12)
      LENABAB=IRPDPD(IRREP,13)
      LENIJAB=IRPDPD(IRREP,14)
      LENIAAA=IRPDPD(IRREP,16)
      LENIABB=IRPDPD(IRREP,17)
      LENIAAB=IRPDPD(IRREP,18)
C
      SW2AB=SW2AB+LENABAB*LENIJAB*LENIJAB
      IF(IUHF.EQ.1) THEN
       SW2AA=SW2AA+LENABAA*LENIJAA*LENIJAA
       SW2BB=SW2BB+LENABBB*LENIJBB*LENIJBB
      ENDIF
      SLPPAB=SLPPAB+LENABAB*LENABAB*LENIJAB
      IF(IUHF.EQ.1) THEN
       SLPPAA=SLPPAA+LENIJAA*LENABAA*LENABAA
       SLPPBB=SLPPBB+LENIJBB*LENABBB*LENABBB
      ENDIF
      SLHHAB=SLHHAB+LENIJAB*LENIJAB*LENABAB
      IF(IUHF.EQ.1) THEN
       SLHHAA=SLHHAA+LENIJAA*LENIJAA*LENABAA
       SLHHBB=SLHHBB+LENIJBB*LENIJBB*LENABBB
      ENDIF 
      SW3AAAA=SW3AAAA+LENAIAA*LENAIAA*LENAIAA
     $                +LENAIAA*LENAIBB*LENAIBB
      SW3ABAB=SW3ABAB+LENAIAA*LENAIAA*LENAIBB
     $               +LENAIAA*LENAIBB*LENAIBB
      SW3BAAB=SW3BAAB+LENAIAB*LENAIBA*LENAIBA
      IF(IUHF.EQ.1) THEN
       SW3BBBB=SW3BBBB+LENAIBB*LENAIBB*LENAIBB
     $                +LENAIBB*LENAIAA*LENAIAA
       SW3BABA=SW3BABA+LENAIBB*LENAIBB*LENAIAA
     $                +LENAIBB*LENAIAA*LENAIAA
       SW3ABBA=SW3ABBA+LENAIBA*LENAIAB*LENAIAB
      ENDIF
      SRABAB=SRABAB+LENAIAA*LENAIAA*LENAIBB
     $             +LENAIAA*LENAIBB*LENAIBB
      SRBAAB=SRBAAB+LENAIAB*LENAIBA*LENAIBA
      IF(IUHF.EQ.1) THEN
       SRAAAA=SRAAAA+LENAIAA*LENAIAA*LENAIAA
     $              +LENAIAA*LENAIBB*LENAIBB
       SRBBBB=SRBBBB+LENAIBB*LENAIBB*LENAIBB
     $              +LENAIBB*LENAIAA*LENAIAA
       SRBABA=SRBABA+LENAIBB*LENAIBB*LENAIAA
     $              +LENAIBB*LENAIAA*LENAIAA
       SRABBA=SRABBA+LENAIBA*LENAIAB*LENAIAB
      ENDIF
C
C     MORE DIFFICULT TERMS
C
C     LOOP OVER IRREPJ
C
      DO 100 IRREPJ=1,NIRREP
      NOCCJA=POP(IRREPJ,1)
      NOCCJB=POP(IRREPJ,2) 
      NVRTJA=VRT(IRREPJ,1)
      NVRTJB=VRT(IRREPJ,2)
      IRREPI=DIRPRD(IRREP,IRREPJ) 
      NOCCIA=POP(IRREPI,1)
      NOCCIB=POP(IRREPI,2)
      NVRTIA=VRT(IRREPI,1)
      NVRTIB=VRT(IRREPI,2)
C     
      SFMIAA=SFMIAA+LENABAB*NOCCIB*NOCCJA*NOCCJA
      IF(IUHF.EQ.1) THEN
      SFMIAA=SFMIAA+LENABAA*NOCCIA*NOCCJA*NOCCJA
      SFMIBB=SFMIBB+LENABAB*NOCCIA*NOCCJB*NOCCJB
     $             +LENABBB*NOCCIB*NOCCJB*NOCCJB
      ENDIF
      SFEAAA=SFEAAA+LENIJAB*NVRTIB*NVRTJA*NVRTJA
      IF(IUHF.EQ.1) THEN
      SFEAAA=SFEAAA+LENIJAA*NVRTIA*NVRTJA*NVRTJA
      SFEABB=SFEABB+LENIJAB*NVRTIA*NVRTJB*NVRTJB
     $             +LENIJBB*NVRTIB*NVRTJB*NVRTJB
      ENDIF
      ST2T1AA=ST2T1AA+LENABAB*NVRTJA*NOCCJA*NOCCIB
     $               +LENABAA*NVRTJA*NOCCJA*NOCCIA
     $               +LENIJAB*NOCCJA*NVRTJA*NVRTIB
     $               +LENIJAA*NOCCJA*NVRTJA*NVRTIA
      IF(IUHF.EQ.1) THEN
      ST2T1BB=ST2T1BB+LENABAB*NVRTJB*NOCCJB*NOCCIA
     $               +LENABBB*NVRTJB*NOCCJB*NOCCIB
     $               +LENIJAB*NOCCJB*NVRTJB*NVRTIA
     $               +LENIJBB*NOCCJB*NVRTJB*NVRTIB
      ENDIF
      SFMICAB=SFMICAB+LENABAB*NOCCIA*NOCCJB*NOCCJB
      IF(IUHF.EQ.1) THEN
       SFMICAB=SFMICAB+LENABAB*NOCCIB*NOCCJA*NOCCJA
       SFMICAA=SFMICAA+LENABAA*NOCCIA*NOCCJA*NOCCJA
       SFMICBB=SFMICBB+LENABBB*NOCCIB*NOCCJB*NOCCJB
      ENDIF
      SFEACAB=SFEACAB+LENIJAB*NVRTIA*NVRTJB*NVRTJB
      IF(IUHF.EQ.1) THEN
       SFEACAB=SFEACAB+LENIJAB*NVRTIB*NVRTJA*NVRTJA
       SFEACAA=SFEACAA+LENIJAA*NVRTIA*NVRTJA*NVRTJA
       SFEACBB=SFEACBB+LENIJBB*NVRTIB*NVRTJB*NVRTJB
      ENDIF
      ST1T2AB1=ST1T2AB1+LENABAB*NOCCIA*NVRTJB*NOCCJB
      ST1T2AB2=ST1T2AB2+LENIJAB*NVRTIA*NOCCJB*NVRTJB
      IF(IUHF.EQ.1) THEN
       ST1T2AB1=ST1T2AB1+LENABAB*NOCCIB*NVRTJA*NOCCJA
       ST1T2AB2=ST1T2AB2+LENIJAB*NVRTIB*NVRTJA*NOCCJA
       ST1T2AA1=ST1T2AA1+LENABAA*NOCCIA*NVRTJA*NOCCJA
       ST1T2AA2=ST1T2AA2+LENIJAA*NVRTIA*NOCCJA*NVRTJA
       ST1T2BB1=ST1T2BB1+LENABBB*NOCCIB*NVRTJB*NOCCJB
       ST1T2BB2=ST1T2BB2+LENIJBB*NVRTIB*NVRTJB*NOCCJB
      ENDIF
      SW2ABCC=SW2ABCC+LENIJAB*NOCCIA*NOCCJB*NVRTJB
      IF(IUHF.EQ.1) THEN
       SW2ABCC=SW2ABCC+LENIJAB*NOCCIB*NOCCJA*NVRTJA
       SW2AACC=SW2AACC+LENIJAA*NOCCIA*NOCCJA*NVRTJA
       SW2BBCC=SW2BBCC+LENIJBB*NOCCIB*NOCCJB*NVRTJB
      ENDIF
      SW1ABCC=SW1ABCC+LENABAB*LENIJAB*NOCCJA*NVRTIB 
     $               +LENIJAB*NOCCJA*NVRTJA*NVRTIB
      IF(IUHF.EQ.1) THEN
       SW1ABCC=SW1ABCC+LENABAB*LENIJAB*NOCCJB*NVRTIA
     $                +LENIJAB*NOCCJB*NVRTJB*NVRTIA
       SW1AACC=SW1AACC+LENABAA*LENIJAA*NOCCJA*NVRTIA
     $                +LENIJAA*NOCCJA*NVRTIA*NVRTJA
       SW1BBCC=SW1BBCC+LENABBB*LENIJBB*NOCCJB*NVRTIB
     $                +LENIJBB*NOCCJB*NVRTIB*NVRTJB
      ENDIF
      SW31CC=SW31CC+LENAIAB*NVRTIA*NVRTJB*NOCCJB
      SW32CC=SW32CC+LENAIAB*NVRTJB*NOCCJB*NOCCIA
      SW33CC=SW33CC+LENAIAB*NVRTIA*NOCCJB*NVRTJB
      SW34CC=SW34CC+LENAIAB*NOCCIB*NVRTJA*NOCCJA
      SW35CC=SW35CC+LENAIAA*NVRTIA*NVRTJA*NOCCJA
      SW36CC=SW36CC+LENAIAA*NVRTJA*NOCCJA*NOCCIA
      IF(IUHF.EQ.1) THEN
       SW37CC=SW37CC+LENAIBB*NVRTJA*NVRTIB*NOCCJB
       SW38CC=SW38CC+LENAIBB*NVRTJB*NOCCJB*NOCCIB
       SW39CC=SW39CC+LENAIAB*NVRTIB*NVRTJA*NOCCJA
       SW310C=SW310C+LENAIBA*NVRTJA*NOCCJA*NOCCJB
       SW311C=SW311C+LENAIBA*NVRTJA*NOCCJA*NVRTJA
       SW312C=SW312C+LENAIBA*NOCCIA*NVRTJB*NOCCJB
      ENDIF
      STABAB=STABAB+LENAIAB*NOCCIA*NVRTJB*NOCCJB
     $             +LENAIAB*NOCCIB*NOCCJA*NVRTJA
      STBAAB=STBAAB+LENAIAA*NOCCIB*NVRTJB*NOCCJB
     $             +LENAIAA*NOCCIB*NOCCJB*NVRTJB
      IF(IUHF.EQ.1) THEN
       STAAAA=STAAAA+LENAIAA*NOCCIA*NVRTJA*NOCCJA
     $              +LENAIAA*NOCCIA*NOCCJA*NVRTJA
       STBBBB=STBBBB+LENAIBB*NOCCIB*NVRTJB*NOCCJB
     $              +LENAIBB*NOCCIB*NOCCJB*NVRTJB
       STBABA=STBABA+LENAIBA*NOCCIB*NVRTJA*NOCCJA
     $              +LENAIBA*NOCCIA*NOCCJB*NVRTJB
       STABBA=STABBA+LENAIBB*NOCCIA*NOCCJA*NVRTJA
     $              +LENAIBB*NOCCIA*NOCCJA*NVRTJA
      ENDIF
100   CONTINUE
1000  CONTINUE
      MBPT3C1=FLPPAB+FLHHAB+FRABAB+FRBAAB
      MBPT3SY=SLPPAB+SLHHAB+SRABAB+SRBAAB
      IF(IUHF.EQ.1) THEN
       MBPT3C1=MBPT3C1+FLPPAA+FLPPBB+FLHHAA+FLHHBB
     &                +FRAAAA+FRBBBB+FRBABA+FRABBA 
       MBPT3SY=MBPT3SY+SLPPAA+SLPPBB+SLHHAA+SLHHBB
     &                +SRAAAA+SRBBBB+SRBABA+SRABBA
      ENDIF
      CCDC1=MBPT3C1+FW2AB+FW3AAAA+FW3ABAB+FW3BAAB+FFMIAA+FFEAAA
     &              +FFMICAB+FFEACAB
      CCDSY=MBPT3SY+SW2AB+SW3AAAA+SW3ABAB+SW3BAAB+SFMIAA+SFEAAA
     &              +SFMICAB+SFEACAB
      IF(IUHF.EQ.1) THEN
       CCDC1=CCDC1+FW2AA+FW2BB+FW3BBBB+FW3BABA
     &              +FW3ABBA+FFMIBB+FFEABB+FFMICAA+FFMICBB
     &              +FFEACAA+FFEACBB
       CCDSY=CCDSY+SW2AA+SW2BB+SW3BBBB+SW3BABA
     &              +SW3ABBA+SFMIBB+SFEABB+SFMICAA+SFMICBB
     &              +SFEACAA+SFEACBB
      ENDIF
      QCISDC1=CCDC1+FT2T1AA+FT1T2AB1+FT1T2AB2
      QCISDSY=CCDSY+ST2T1AA+ST1T2AB1+ST1T2AB2
      IF(IUHF.EQ.1) THEN
       QCISDC1=QCISDC1+FT2T1BB+FT1T2AA1+FT1T2AA2
     &                +FT1T2BB1+FT1T2BB2
       QCISDSY=QCISDSY+ST2T1BB+ST1T2AA1+ST1T2AA2
     &                +ST1T2BB1+ST1T2BB2
      ENDIF
      CCSDC1=QCISDC1+FW2ABCC+FW1ABCC+FW31CC+FW32CC+FW33CC
     &               +FW34CC+FTABAB+FTBAAB
      CCSDSY=QCISDSY+SW2ABCC+SW1ABCC+SW31CC+SW32CC+SW33CC
     &              +SW34CC+STABAB+STBAAB
      IF(IUHF.EQ.1) THEN
       CCSDC1=CCSDC1+FW2AACC+FW2BBCC+FW1AACC+FW1BBCC+FW35CC
     &              +FW36CC+FW37CC+FW38CC+FW39CC+FW310C+FW311C
     &              +FW312C+FTAAAA+FTBBBB+FTBABA+FTABBA
       CCSDSY=CCSDSY+SW2AACC+SW2BBCC+SW1AACC+SW1BBCC+SW35CC
     &              +SW36CC+SW37CC+SW38CC+SW39CC+SW310C+SW311C
     &              +SW312C+STAAAA+STBBBB+STBABA+STABBA
      ENDIF
      M4DQC1=CCDC1+FRABAB+FRBAAB+FLHHAB
      M4DQSY=CCDSY+SRABAB+SRBAAB+SLHHAB
      IF(IUHF.EQ.1) THEN
       M4DQC1=M4DQC1+FRAAAA+FRBBBB+FRBABA+FRABBA+FLHHBB+FLHHAA
       M4DQSY=M4DQSY+SRAAAA+SRBBBB+SRBABA+SRABBA+SLHHBB+SLHHAA
      ENDIF
      M4SDQC1=M4DQC1+FT2T1AA
      M4SDQSY=M4DQSY+ST2T1AA
      IF(IUHF.EQ.1) THEN
       M4SDQC1=M4SDQC1+FT2T1BB
       M4SDQSY=M4SDQSY+ST2T1BB
      ENDIF
8999  FORMAT(' ')
      WRITE(LUOUT,9000)
9000  FORMAT(72('-'))
      WRITE(LUOUT,9001)
9001  FORMAT(T5,' Floating point operations required for full',
     &          '  matrix algorithms',//,T3,'Term',T19,'spins',T27,
     &          'method',T53,'FLOP')
      WRITE(LUOUT,9002)PTGRP(1:3)
9002  FORMAT(T44,'C1',T63,A3)
      WRITE(LUOUT,9000)
      WRITE(LUOUT,9003)
9003  FORMAT(' a) contributions to the intermediates :')
      WRITE(LUOUT,8999)
      IF(IUHF.EQ.1)WRITE(LUOUT,9004)FW2AA,SW2AA
      IF(IUHF.EQ.1)WRITE(LUOUT,9005)FW2BB,SW2BB
      WRITE(LUOUT,9006)FW2AB,SW2AB
9004  FORMAT(' T2 --> W(MNIJ)  (AAAA)   2 - 7',T36,I13,T54,I13)
9005  FORMAT(' T2 --> W(MNIJ)  (BBBB)   2 - 7',T36,I13,T54,I13)
9006  FORMAT(' T2 --> W(MNIJ)  (ABAB)   2 - 7',T36,I13,T54,I13)
      WRITE(LUOUT,8999)
      IF(IUHF.EQ.1)WRITE(LUOUT,9007)FW2AACC,SW2AACC
      IF(IUHF.EQ.1)WRITE(LUOUT,9008)FW2BBCC,SW2BBCC
      WRITE(LUOUT,9009)  FW2ABCC,SW2ABCC
9007  FORMAT(' T1 --> W(MNIJ)  (AAAA)     7  ',T36,I13,T54,I13)
9008  FORMAT(' T1 --> W(MNIJ)  (BBBB)     7  ',T36,I13,T54,I13)
9009  FORMAT(' T1 --> W(MNIJ)  (ABAB)     7  ',T36,I13,T54,I13)
      WRITE(LUOUT,8999)
      WRITE(LUOUT,9010)FW3AAAA,SW3AAAA
      IF(IUHF.EQ.1)WRITE(LUOUT,9011)FW3BBBB,SW3BBBB
      WRITE(LUOUT,9012)FW3ABAB,SW3ABAB
      WRITE(LUOUT,9013)FW3BAAB,SW3BAAB
      IF(IUHF.EQ.1)WRITE(LUOUT,9014)FW3BABA,SW3BABA
      IF(IUHF.EQ.1)WRITE(LUOUT,9015)FW3ABBA,SW3ABBA
9010  FORMAT(' T2 --> W(MBEJ)  (AAAA)   3 - 7',T36,I13,T54,I13)
9011  FORMAT(' T2 --> W(MBEJ)  (BBBB)   3 - 7',T36,I13,T54,I13)
9012  FORMAT(' T2 --> W(MBEJ)  (ABAB)   3 - 7',T36,I13,T54,I13)
9013  FORMAT(' T2 --> W(MBEJ)  (BAAB)   3 - 7',T36,I13,T54,I13)
9014  FORMAT(' T2 --> W(MBEJ)  (BABA)   3 - 7',T36,I13,T54,I13)
9015  FORMAT(' T2 --> W(MBEJ)  (ABBA)   3 - 7',T36,I13,T54,I13)
      WRITE(LUOUT,8999)
      WRITE(LUOUT,9016)FW35CC+FW36CC,SW35CC+SW36CC
      IF(IUHF.EQ.1)WRITE(LUOUT,9017)FW37CC+FW38CC,SW37CC+SW38CC
      WRITE(LUOUT,9018)FW31CC+FW32CC,SW31CC+SW32CC
      WRITE(LUOUT,9019)FW33CC+FW34CC,SW33CC+SW34CC
      IF(IUHF.EQ.1)WRITE(LUOUT,9020)FW39CC+FW310C,SW39CC+SW310C 
      IF(IUHF.EQ.1)WRITE(LUOUT,9021)FW311C+FW312C,SW311C+SW312C
9016  FORMAT(' T1 --> W(MBEJ)  (AAAA)     7',T36,I13,T54,I13)
9017  FORMAT(' T1 --> W(MBEJ)  (BBBB)     7',T36,I13,T54,I13)
9018  FORMAT(' T1 --> W(MBEJ)  (ABAB)     7',T36,I13,T54,I13)
9019  FORMAT(' T1 --> W(MBEJ)  (BAAB)     7',T36,I13,T54,I13)
9020  FORMAT(' T1 --> W(MBEJ)  (BABA)     7',T36,I13,T54,I13)
9021  FORMAT(' T1 --> W(MBEJ)  (ABBA)     7',T36,I13,T54,I13)
      WRITE(LUOUT,8999)
      IF(IUHF.EQ.1)WRITE(LUOUT,9022)FW1AACC,SW1AACC
      IF(IUHF.EQ.1)WRITE(LUOUT,9023)FW1BBCC,SW1BBCC
      WRITE(LUOUT,9024)FW1ABCC,SW1ABCC      
9022  FORMAT(' T1 --> W(ABEF)  (AAAA)     7',T36,I13,T54,I13)
9023  FORMAT(' T1 --> W(ABEF)  (BBBB)     7',T36,I13,T54,I13)
9024  FORMAT(' T1 --> W(ABEF)  (ABAB)     7',T36,I13,T54,I13)
      WRITE(LUOUT,8999)
      WRITE(LUOUT,9025)FFMIAA,SFMIAA
      IF(IUHF.EQ.1)WRITE(LUOUT,9026)FFMIBB,SFMIBB
9025  FORMAT(' T2 --> F(MI)    (AA)     3 - 7',T36,I13,T54,I13)
9026  FORMAT(' T2 --> F(MI)    (BB)     3 - 7',T36,I13,T54,I13)  
      WRITE(LUOUT,8999) 
      WRITE(LUOUT,9027)FFEAAA,SFEAAA
      IF(IUHF.EQ.1)WRITE(LUOUT,9028)FFEABB,SFEABB
9027  FORMAT(' T2 --> F(AE)    (AA)     3 - 7',T36,I13,T54,I13)
9028  FORMAT(' T2 --> F(AE)    (BB)     3 - 7',T36,I13,T54,I13)
      WRITE(LUOUT,8999)
      WRITE(LUOUT,9029)
      WRITE(LUOUT,9000)
9029  FORMAT('b) contractions')
      WRITE(LUOUT,8999)
      IF(IUHF.EQ.1)WRITE(LUOUT,9030)FLPPAA,SLPPAA
      IF(IUHF.EQ.1)WRITE(LUOUT,9031)FLPPBB,SLPPBB
      WRITE(LUOUT,9032)FLPPAB,SLPPAB
9030  FORMAT(' W(ABEF) --> T2 (AAAA)    1 - 7',T36,I13,T54,I13)
9031  FORMAT(' W(ABEF) --> T2 (BBBB)    1 - 7',T36,I13,T54,I13)
9032  FORMAT(' W(ABEF) --> T2 (ABAB)    1 - 7',T36,I13,T54,I13)
      WRITE(LUOUT,8999)
      IF(IUHF.EQ.1)WRITE(LUOUT,9033)FLHHAA,SLHHAA
      IF(IUHF.EQ.1)WRITE(LUOUT,9034)FLHHBB,SLHHBB
      WRITE(LUOUT,9035)FLHHAB,SLHHAB
9033  FORMAT(' W(MNIJ) --> T2 (AAAA)    1 - 7',T36,I13,T54,I13)
9034  FORMAT(' W(MNIJ) --> T2 (BBBB)    1 - 7',T36,I13,T54,I13)
9035  FORMAT(' W(MNIJ) --> T2 (ABAB)    1 - 7',T36,I13,T54,I13) 
      WRITE(LUOUT,8999)
      IF(IUHF.EQ.1)WRITE(LUOUT,9036)FRAAAA,SRAAAA
      IF(IUHF.EQ.1)WRITE(LUOUT,9037)FRBBBB,SRBBBB
      WRITE(LUOUT,9038)FRABAB,SRABAB
      WRITE(LUOUT,9039)FRBAAB,SRBAAB
      IF(IUHF.EQ.1)WRITE(LUOUT,9040)FRBABA,SRBABA
      IF(IUHF.EQ.1)WRITE(LUOUT,9041)FRABBA,SRABBA
9036  FORMAT(' W(MBEJ) --> T2 (AAAA)    1 - 7',T36,I13,T54,I13)
9037  FORMAT(' W(MBEJ) --> T2 (BBBB)    1 - 7',T36,I13,T54,I13)
9038  FORMAT(' W(MBEJ) --> T2 (ABAB)    1 - 7',T36,I13,T54,I13)
9039  FORMAT(' W(MBEJ) --> T2 (BAAB)    1 - 7',T36,I13,T54,I13)
9040  FORMAT(' W(MBEJ) --> T2 (BABA)    1 - 7',T36,I13,T54,I13) 
9041  FORMAT(' W(MBEJ) --> T2 (ABBA)    1 - 7',T36,I13,T54,I13)
      WRITE(LUOUT,8999)
      IF(IUHF.EQ.1)WRITE(LUOUT,9042)FFMICAA,SFMICAA
      IF(IUHF.EQ.1)WRITE(LUOUT,9043)FFMICBB,SFMICBB
      WRITE(LUOUT,9044)FFMICAB,SFMICAB
9042  FORMAT(' F(MI) --> T2   (AAAA)    2 - 7',T36,I13,T54,I13)
9043  FORMAT(' F(MI) --> T2   (BBBB)    2 - 7',T36,I13,T54,I13)
9044  FORMAT(' F(MI) --> T2   (ABAB)    2 - 7',T36,I13,T54,I13)
      WRITE(LUOUT,8999)
      IF(IUHF.EQ.1)WRITE(LUOUT,9045)FFEACAA,SFEACAA
      IF(IUHF.EQ.1)WRITE(LUOUT,9046)FFEACBB,SFEACBB
      WRITE(LUOUT,9047)FFEACAB,SFEACAB
9045  FORMAT(' F(AE) --> T2   (AAAA)    2 - 7',T36,I13,T54,I13)
9046  FORMAT(' F(AE) --> T2   (BBBB)    2 - 7',T36,I13,T54,I13)
9047  FORMAT(' F(AE) --> T2   (ABAB)    2 - 7',T36,I13,T54,I13)
      WRITE(LUOUT,8999)
      WRITE(LUOUT,9048)
      WRITE(LUOUT,9000)
9048  FORMAT('c) direct contributions')
      WRITE(LUOUT,8999)
      WRITE(LUOUT,9049)FT2T1AA,ST2T1AA
      IF(IUHF.EQ.1)WRITE(LUOUT,9050)FT2T1BB,ST2T1BB
9049  FORMAT(' T2 --> T1       (AA)     3,4,6,7',T36,I13,T54,I13)
9050  FORMAT(' T2 --> T1       (BB)     3,4,6,7',T36,i13,T54,I13)
      WRITE(LUOUT,8999)
      IF(IUHF.EQ.1)WRITE(LUOUT,9051)FT1T2AA1+FT1T2AA2,ST1T2AA1+ST1T2AA2
      IF(IUHF.EQ.1)WRITE(LUOUT,9052)FT1T2BB1+FT1T2BB2,ST1T2BB1+ST1T2BB2
      WRITE(LUOUT,9053)FT1T2AB1+FT1T2AB2,ST1T2AB1+ST1T2AB2
9051  FORMAT(' T1 --> T2      (AAAA)     6,7',T36,I13,T54,I13)
9052  FORMAT(' T1 --> T2      (BBBB)     6,7',T36,I13,T54,I13)
9053  FORMAT(' T1 --> T2      (ABAB)     6,7',T36,I13,T54,I13)
      WRITE(LUOUT,8999)
      IF(IUHF.EQ.1)WRITE(LUOUT,9054)FTAAAA,STAAAA
      IF(IUHF.EQ.1)WRITE(LUOUT,9055)FTBBBB,STBBBB
      WRITE(LUOUT,9056)FTABAB,STABAB
      WRITE(LUOUT,9057)FTBAAB,STBAAB
      IF(IUHF.EQ.1)WRITE(LUOUT,9058)FTBABA,STBABA
      IF(IUHF.EQ.1)WRITE(LUOUT,9059)FTABBA,STABBA
9054  FORMAT(' T1**2 --> T2   (AAAA)      7',T36,I13,T54,I13)
9055  FORMAT(' T1**2 --> T2   (BBBB)      7',T36,I13,T54,I13)
9056  FORMAT(' T1**2 --> T2   (ABAB)      7',T36,I13,T54,I13)
9057  FORMAT(' T1**2 --> T2   (BAAB)      7',T36,I13,T54,I13)
9058  FORMAT(' T1**2 --> T2   (BABA)      7',T36,I13,T54,I13)
9059  FORMAT(' T1**2 --> T2   (ABBA)      7',T36,I13,T54,I13)
      WRITE(LUOUT,8999)
      WRITE(LUOUT,9060)
      WRITE(LUOUT,9000)
9060  FORMAT('d) total number of operations for various methods')
      WRITE(LUOUT,8999)
      WRITE(LUOUT,9061)MBPT3C1,MBPT3SY
      WRITE(LUOUT,9062)M4DQC1,M4DQSY
      WRITE(LUOUT,9063)M4SDQC1,M4SDQSY
C      WRITE(LUOUT,9064)M4SDTQC1,M4SDTQSY
      WRITE(LUOUT,9065)CCDC1,CCDSY
      WRITE(LUOUT,9066)QCISDC1,QCISDSY
      WRITE(LUOUT,9067)CCSDC1,CCSDSY
9061  FORMAT(' MBPT(3)                        (1)',T36,I13,T54,I13)
9062  FORMAT(' DQ-MBPT(4)                     (2)',T36,I13,T54,I13)
9063  FORMAT(' SDQ-MBPT(4)                    (3)',T36,I13,T54,I13)
9064  FORMAT(' SDTQ-MBPT(4)                   (4)',T36,I13,T54,I13)
9065  FORMAT(' CCD          per iteration     (5)',T36,I13,T54,I13)
9066  FORMAT(' QCISD        per iteration     (6)',T36,I13,T54,I13)
9067  FORMAT(' CCSD         per iteration     (7)',T36,I13,T54,I13)
      WRITE(LUOUT,8999)
      WRITE(LUOUT,9000)
      RETURN
      END
