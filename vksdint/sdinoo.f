
      SUBROUTINE SDINOO(IRREPX,IOO,DOO,ICORE,MXCOR,IUHF)
C
C THIS ROUTINE COMPUTES THE CONTRIBUTIONS:
C
C      FD(I,J) = - SUM SD(M,N) * <MI||NJ> + SD(m,n) * <Im|Jn> (ISPIN=1)
C
C      FD(i,j) = - SUM SD(m,n) * <mi||nj> + SD(M,N) * <iM|jN> (ISPIN=2)
C
CEND
C

      IMPLICIT INTEGER (A-Z)
      CHARACTER*4 TYPE
      DOUBLE PRECISION IOO,DOO
      DIMENSION DOO(1),IOO(1),ICORE(MXCOR)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      IF(IUHF.EQ.0)THEN
C
C SPIN ADAPTED RHF CODE
C
       LISTW=13
       DO 120 IRREP=1,NIRREP
        DISSIZ=IRPDPD(IRREP,ISYTYP(1,LISTW)) 
        NUMDIS=IRPDPD(IRREP,ISYTYP(2,LISTW)) 
        I000=1
        I010=I000+IINTFP*NUMDIS*DISSIZ
        I020=I010+IINTFP*DISSIZ
        I030=I020+IINTFP*DISSIZ
        CALL GETLST(ICORE(I000),1,NUMDIS,1,IRREP,LISTW)
        CALL SPINAD1(IRREP,POP(1,1),DISSIZ,ICORE(I000),
     &               ICORE(I010),ICORE(I020))
        CALL VMINUS(ICORE(I000),NUMDIS*DISSIZ)
        CALL DDOT24(IRREPX,IRREP,IOO,DOO,ICORE(I000),ICORE(I010),DISSIZ,
     &             POP(1,1),POP(1,1),POP(1,1),POP(1,1),POP(1,1),
     &             POP(1,1),'STST')
120    CONTINUE
      ELSEIF(IUHF.NE.0)THEN
       DO 10 ISPIN=1,2
        LISTW1=10+ISPIN
        LISTW2=13
        DO 20 IRREP=1,NIRREP
         NDSZ1=IRPDPD(IRREP,ISYTYP(1,LISTW1))
         NDSZF=IRPDPD(IRREP,20+ISPIN)
         NDIS1=IRPDPD(IRREP,ISYTYP(2,LISTW1))
         NDISF=IRPDPD(IRREP,20+ISPIN)
         NDSZ2=IRPDPD(IRREP,ISYTYP(1,LISTW2))
         NDIS2=IRPDPD(IRREP,ISYTYP(2,LISTW2))
         IOFFI=1+(ISPIN-1)*IRPDPD(IRREPX,21)
C
C DO FIRST PART - SD(m,n) * I(mi,nj)
C
         I000=1
         I010=I000+IINTFP*NDSZF*NDISF
         I020=I010+IINTFP*IRPDPD(IRREPX,20+ISPIN)
         IOFFD=1+(ISPIN-1)*IRPDPD(IRREPX,21)
         CALL GETLST(ICORE(I000),1,NDIS1,2,IRREP,LISTW1)
         CALL SYMEXP(IRREP,POP(1,ISPIN),NDSZ1,ICORE(I000))
         CALL SYMEXP2(IRREP,POP(1,ISPIN),NDSZF,NDSZ1,NDISF,ICORE(I000),
     &                ICORE(I000))
         CALL VMINUS(ICORE(I000),NDSZF*NDISF)
         CALL DDOT24(IRREPX,IRREP,IOO(IOFFI),DOO(IOFFD),ICORE(I000),
     &              ICORE(I010),NDSZF,POP(1,ISPIN),POP(1,ISPIN),
     &              POP(1,ISPIN),POP(1,ISPIN),POP(1,ISPIN),POP(1,ISPIN),
     &             'STST')
C
C NOW DO SECOND PART 
C
C        SD(m,n) * <Im|Jn> [ISPIN=1]
C 
C        SD(M,N) * <Mi|Nj> [ISPIN=2]
C
         I000=1
         I010=I000+IINTFP*NDSZ2*NDIS2
         I020=I010+IINTFP*IRPDPD(IRREPX,20+ISPIN)
         IOFFD=1+(2-ISPIN)*IRPDPD(IRREPX,21)
         CALL GETLST(ICORE(I000),1,NDIS2,2,IRREP,LISTW2)
         CALL VMINUS(ICORE(I000),NDSZ2*NDIS2)
         IF(ISPIN.EQ.1)THEN
          TYPE='TSTS'
         ELSE
          TYPE='STST'
         ENDIF
         CALL DDOT24(IRREPX,IRREP,IOO(IOFFI),DOO(IOFFD),ICORE(I000),
     &              ICORE(I010),NDSZ2,POP(1,ISPIN),POP(1,ISPIN),
     &              POP(1,1),POP(1,2),POP(1,1),POP(1,2),TYPE)
20      CONTINUE
10     CONTINUE
      ENDIF
      RETURN
      END
