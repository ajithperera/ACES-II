      SUBROUTINE FORMIJ(IRREPP,DAO,CMO,NBSSQ,SCR,MXSCR,IUHF,
     &                  DMOIJ,NOOSQ,FLAG)
C
C THIS SUBROUTINE TAKES ONE-ELECTRON INTEGRAL DERIVATIVES
C GIVEN IN THE AO BASIS AND CALCULATES THE OCCUPIED-OCCUPIED
C BLOCK OF THE SAME MATRIX IN THE MO REPRESENTATION.
C
C INPUT : IRREPP ..... IRREP OF PERTURBATION
C         DAO ........ INTEGRAL DERIVATIVES IN AO BASIS
C         CMO ........ MO COEFFICIENT MATRIX
C         NBSSQ ...... SIZE OF THE CMO MATRIX
C         SCR ........ SCRACTH VECTOR
C         MXSCR ...... LENGTH OF SCRACTH VECTOR IN DOUBLE PRECISION WORDS
C         IUHF ....... UHF FLAG
C         NOOSQ ...... SIZE OF THE ALPHA-ALPHA OCCUPIED-OCCUPIED BLOCK
C
C OUTPUT : DMOIJ ..... INTEGRAL DERIVATIVES IN MO BASIS
C
CEND
C
C CODED JG MARCH/91
C
       IMPLICIT DOUBLE PRECISION(A-H,O-Z)
       INTEGER POP,VRT,DIRPRD
       LOGICAL FLAG
C
       DIMENSION DAO(1),CMO(NBSSQ,1),SCR(MXSCR),DMOIJ(NOOSQ,1)
C
       COMMON/LSYM/NLENQ(8),NLENT(8)
       COMMON/OFFSET/IOFF1(8)
       COMMON/BASSYM/NBAS(8),NBASIS,NBASSQ,MBASTT
       COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
       COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
C
       DATA AZERO,ONE /0.D0,1.D0/
C
C LOOP OVER ALL POSSIBLE SPIN CASES
C
       DO 1000 ISPIN=1,IUHF+1
C
C   LOOP OVER ALL IRREPS ON THE RIGHT HAND SIDE OF DAO
C
        IOFFCR=1
        IOFFAO=1
        IF(FLAG) IOFFAO=1+(ISPIN-1)*NLENQ(IRREPP)
        IOFFMO=1
        DO 900 IRREPR=1,NIRREP
C
C   IRREP OF THE LEFT HAND SIDE OF DAO IS NOW
C
         IRREPL=DIRPRD(IRREPP,IRREPR)
C
         IOFFCL=IOFF1(IRREPL)
C
C   PERFORM MULTIPLICATION
C
         CALL XGEMM('N','N',NBAS(IRREPL),POP(IRREPR,ISPIN),
     &              NBAS(IRREPR),ONE,
     &              DAO(IOFFAO),NBAS(IRREPL),CMO(IOFFCR,ISPIN),
     &              NBAS(IRREPR),AZERO,SCR,NBAS(IRREPL))
C
         CALL XGEMM('T','N',POP(IRREPL,ISPIN),POP(IRREPR,ISPIN),
     &              NBAS(IRREPL),ONE,
     &              CMO(IOFFCL,ISPIN),NBAS(IRREPL),SCR,NBAS(IRREPL),
     &              AZERO,DMOIJ(IOFFMO,ISPIN),POP(IRREPL,ISPIN))
C
         IOFFMO=IOFFMO+POP(IRREPR,ISPIN)*POP(IRREPL,ISPIN)
         IOFFAO=IOFFAO+NBAS(IRREPR)*NBAS(IRREPL)
         IOFFCR=IOFFCR+NBAS(IRREPR)*NBAS(IRREPR)
C
900     CONTINUE 
C
c        write(*,3456)( dmoij(I,ispin),I=1,ioffmo-1)
c3456     format(8F11.8)
c        call checksum('formij',dmoij(1,ispin),ioffmo-1)
1000   CONTINUE
C
       RETURN
       END
