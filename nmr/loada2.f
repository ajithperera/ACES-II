      SUBROUTINE LOADA2(CMO,W,W2,BUF,IBUF,ISYMAO,
     &                  NBAS,NSTART,NEND,ISIZE,ISIZT,IOFFAO,
     &                  IOFFI,NSIZE,ILNBUF,NPERT,ISPIN)
C
C THIS ROUTINE LOADS GIAO AO INTEGRALS FROM
C THE FILE IIJJ.. AND TRANSFORMS THE FIRST 
C INDEX TO THE MO BASIS
C
CEND
      IMPLICIT INTEGER(A-Z)
      DOUBLE PRECISION W,W2,BUF,X,X1,X2,CMO
      DOUBLE PRECISION TCPU,TSYS,TIME, TIMEIN, 
     &                 TIMENOW, TIMETOT, TIMENEW 
      CHARACTER*8 NAMXYZ
      CHARACTER*80 FNAME
C
      DIMENSION IBUF(ILNBUF),BUF(ILNBUF),W(1),W2(1),CMO(1)
      DIMENSION NBAS(8),NSTART(8),NEND(8),ISIZE(8),IOFFAO(8),IOFFI(8)
      DIMENSION ISIZT(8,8)
      DIMENSION ISYMAO(1) 
C
      COMMON/SYMINF/NDUMMY,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/AOOFST/INDOCC(8,2)
      COMMON/PERT4/IXYZ
      COMMON/BFILE/NAMXYZ(4,3)
      COMMON/INFOL/NREAD,NPASS,TIME
      COMMON /TIMEINFO/ TIMEIN, TIMENOW, TIMETOT, TIMENEW 
C
      IUPKI(IX)=AND(IX,IALONE)
      IUPKJ(IX)=AND(ISHFT(IX,-IBITWD),IALONE)
      IUPKK(IX)=AND(ISHFT(IX,-2*IBITWD),IALONE)
      IUPKL(IX)=AND(ISHFT(IX,-3*IBITWD),IALONE)
      INDXF(I,J,N)=I+N*(J-1)
C
      CALL TIMER(1)
C
C  OPEN FILE CONTAINING 2e DERIVATIVE INTEGRALS
C
      CALL GFNAME(NAMXYZ(2,IXYZ),FNAME,ILENGTH)
      OPEN(UNIT=30,FILE=FNAME(1:ILENGTH),FORM='UNFORMATTED',
     &     ACCESS='SEQUENTIAL')
      REWIND(UNIT=30)
      NAOINT=0
C     
C READ IN INTEGRALS
C
      CALL ZERO(W,NSIZE)
1     READ(30) BUF,IBUF,NUT
      IF(NUT.EQ.-1) GO TO 11
      DO 10 INT=1,NUT
       X=BUF(INT)
       ITMP=IBUF(INT)
C
C  X VALUE OF GIAO INTEGRAL
C  I,K INDICES OF COMPLEX CONJUGATE ORBITALS
C
       IX=IUPKI(ITMP)
       JX=IUPKJ(ITMP)
       KX=IUPKK(ITMP)
       LX=IUPKL(ITMP)
C
C GET IRREP OF INTEGRAL
C
       IRREPX1=ISYMAO(IX)
       IRREPX2=ISYMAO(KX)
C
C  OFFSET WITHIN BASIS FUNCTIONS
C
       IOFF1=IOFFAO(IRREPX1)
       IOFF2=IOFFAO(IRREPX2)
C
C  INDICES WITHIN IRREP
C
       IX=IX-IOFF1
       JX=JX-IOFF1
       KX=KX-IOFF2
       LX=LX-IOFF2
C
C  OFFSET FOR CMO WITHIN IRREPX1 AND IRREPX2
C
       IOFFC1=INDOCC(IRREPX1,ISPIN)
       IOFFC2=INDOCC(IRREPX2,ISPIN)
C
C  NUMBER OF OCCUPIED ORBITALS WITHIN IRREPX1 AND IRREPX2
C
       NSTART1=NSTART(IRREPX1)
       NSTART2=NSTART(IRREPX2)
       NEND1=NEND(IRREPX1)
       NEND2=NEND(IRREPX2)
C
C  NUMBER OF BASIS FUNCTIONS WITHIN IRREPX 
C
       NBASX1=NBAS(IRREPX1)
       NBASX2=NBAS(IRREPX2)
C
C  OFFSET WITHIN W
C
       IOFFW1=IOFFI(IRREPX1)
       IOFFW2=IOFFI(IRREPX2)
C
       ISIZEX1=ISIZE(IRREPX1)
       ISIZEX2=ISIZE(IRREPX2)
C
C FIRST ELEMENT:     TRANSFORM INDEX L TO MO BASIS
C
        IND=INDXF(IX,JX,NBASX1)+ISIZT(IRREPX2,IRREPX1)
        X1=X
        DO 9001 IOCC=NSTART2,NEND2
         W2(IOCC)=CMO(IOFFC2+LX-1+(IOCC-1)*NBASX2)*X1 
9001    CONTINUE 
C
C INCREMENT L(KX,IX,JX;I)
C
        DO 9002 IOCC=NSTART2,NEND2
         IADR=IOFFW2+KX+NBASX2*(IND-1)+ISIZEX2*(IOCC-NSTART2)
         W(IADR)=W(IADR)+W2(IOCC)
9002    CONTINUE
C
C SECOND ELEMENT:     TRANSFORM INDEX K TO MO BASIS
C
        X1=-X
        IND=INDXF(JX,IX,NBASX1)+ISIZT(IRREPX2,IRREPX1) 
        DO 9003 IOCC=NSTART2,NEND2
         W2(IOCC)=CMO(IOFFC2+KX-1+(IOCC-1)*NBASX2)*X1
9003    CONTINUE 
C
C INCREMENT L(LX,JX,IX;I)
C
        DO 9004 IOCC=NSTART2,NEND2
         IADR=IOFFW2+LX+NBASX2*(IND-1)+ISIZEX2*(IOCC-NSTART2)
         W(IADR)=W(IADR)+W2(IOCC)
9004    CONTINUE
C
C THIRD ELEMENT:     TRANSFORM INDEX J TO MO BASIS
C
        IND=INDXF(KX,LX,NBASX2)+ISIZT(IRREPX1,IRREPX2)
        X2=X
        DO 9005 IOCC=NSTART1,NEND1
         W2(IOCC)=CMO(IOFFC1+JX-1+(IOCC-1)*NBASX1)*X2
9005    CONTINUE 
C
C INCREMENT L(IX,KX,LX;I)
C
        DO 9006 IOCC=NSTART1,NEND1
         IADR=IOFFW1+IX+NBASX1*(IND-1)+ISIZEX1*(IOCC-NSTART1)
         W(IADR)=W(IADR)+W2(IOCC)
9006    CONTINUE
C
C FOURTH ELEMENT:     TRANSFORM INDEX I TO MO BASIS
C
        X2=-X
        IND=INDXF(LX,KX,NBASX2)+ISIZT(IRREPX1,IRREPX2)
        DO 9007 IOCC=NSTART1,NEND1
         W2(IOCC)=CMO(IOFFC1+IX-1+(IOCC-1)*NBASX1)*X2
9007    CONTINUE 
C
C INCREMENT L(JX,LX,KX;I)
C
        DO 9008 IOCC=NSTART1,NEND1
         IADR=IOFFW1+JX+NBASX1*(IND-1)+ISIZEX1*(IOCC-NSTART1)
         W(IADR)=W(IADR)+W2(IOCC)
9008    CONTINUE
10    CONTINUE
C
      NAOINT=NAOINT+NUT
      GO TO 1
11    CLOSE(UNIT=30,STATUS='KEEP')
C
      CALL TIMER(1)
      TIME=TIME+TIMENEW
      NREAD=NAOINT
      NPASS=NPASS+1
C      
      RETURN
900   WRITE(6,1000)
1000  FORMAT(T3,'@LOADA2-F, Unexpected end-of-file on integral file.')
      CALL ERREX
      RETURN
901   WRITE(6,1000)
1001  FORMAT(T3,'@LOADA2-F, I/O error on unit ',I5,'.')
      CALL ERREX
      RETURN
      END
