      SUBROUTINE SINTRN1(EVEC,AOINT,BUF1,RHF,NSTART,NEND,NOCC,NVRT,
     &                  ISPIN,IRREPX)
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL RHF
      INTEGER DIRPRD,DISZAO,DISZMO,AOPOP
C
      DIMENSION EVEC(1),AOINT(1),BUF1(1)
      DIMENSION NSTART(8),NEND(8),NOCC(8),NVRT(8)
C
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/AOOFST/INDOCC(8,2)   
      COMMON/AOPOPS/AOPOP(8)
      COMMON/SYMINF/NDUMMY,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/INFOT/NPASS1,NPASS2,NPASS3,NPASS4,
     &             NREAD1,NREAD2,NREAD3,NREAD4,
     &             TIME1L,TIME2L,TIME3L,TIME4L,
     &             TIME1S,TIME2S,TIME3S,TIME4S
      COMMON /TIMEINFO/ TIMEIN, TIMENOW, TIMETOT, TIMENEW 
C
      DATA AZERO,ONE/0.D0,1.D0/
C
      CALL TIMER(1)
C
C LOOP OVER ALL IRREPS
C
      IOFF2=1
      IOFF3=1
      IOFF4=1
      IOFF5=1
C
      DO 1000 IRREPR=1,NIRREP
C
       IRREPL=DIRPRD(IRREPR,IRREPX)
       NAOI=AOPOP(IRREPL)
       NMOI=NVRT(IRREPL)
       NSTARTI=NSTART(IRREPR)
       NENDI=NEND(IRREPR)
       NOCCI2=NOCC(IRREPL)
       DISZAO=NAOI*(NAOI+1)/2
       DISZMO=NMOI*(NMOI+1)/2
       NUMDIS=NMOI*(NENDI-NSTARTI+1)
C
C LOOP OVER ALL OCCUPIED ORBITALS OR ALL ORBITALS IN CASE OF
C A FULL TRANSFORMATION
C
       IOFF1=INDOCC(IRREPL,ISPIN)+NOCCI2*NAOI
C
       DO 100 IFIRST=NSTARTI,NENDI
C
C  TRANSFORM SECOND INDEX  (Mu I | SIGMA RHO) --> (P I | SIGMA RHO )
C
        CALL XGEMM('T','N',NMOI,DISZAO,NAOI,ONE,EVEC(IOFF1),NAOI,
     &             AOINT(IOFF2),NAOI,AZERO,BUF1,NMOI)
C
        CALL TRANSP(BUF1,AOINT(IOFF3),DISZAO,NMOI)
C
        IOFF2=IOFF2+NAOI*DISZAO
        IOFF3=IOFF3+NMOI*DISZAO
C
100    CONTINUE
C
C  NOW WE HAVE THE (PI,SIGMA RHO) INTEGRALS ORDERED AS   X X P I 
C
C NOW TRANSFORM NEW LHS TO RS, SPIN CASE ISPIN,ISPIN
C
       IX=1+NAOI*NAOI
C
       DO 200 IDIS=1,NUMDIS
C
        CALL EXPND2(AOINT(IOFF4),BUF1,NAOI)
C     
        CALL GHTRAN(BUF1,BUF1,EVEC,IOFF1,IOFF1,1,1,BUF1(IX),
     &              0,1,NAOI,NAOI,NMOI,NMOI)
C
        CALL SQUEZ2(BUF1,AOINT(IOFF5),NMOI)
C
        IOFF4=IOFF4+DISZAO 
        IOFF5=IOFF5+DISZMO
C
200    CONTINUE
C
C DUMP INTEGRALS 
C
1000  CONTINUE
C
      IOFFBUF=1
      DO 1001 IRREP=1,NIRREP
       IOFFBUF=MAX(IOFFBUF,1+NVRT(IRREP)*NVRT(IRREP)*NVRT(IRREP))
1001  CONTINUE
C
      CALL SDUMP1(AOINT,BUF1,BUF1(IOFFBUF),NSTART,NEND,IRREPX)
C
      CALL TIMER(1)
      TIME1S=TIME1S+TIMENEW
C
      RETURN
      END
