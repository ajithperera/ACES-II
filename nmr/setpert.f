      SUBROUTINE SETPERT
C
C
C /PERT2/
C  NTP......... TOTAL NUMBER OF PERTURBATIONS
C  NTPERT...... TOTAL NUMBER OF NUCLEAR MAGNETIC MOMENT 
C               PERTURBATIONS PER IRREP
C  NFPERT...... NUMBER OF MAGNETIC FIELD PERTURBATIONS
C               PER IRREP
C  IPERT....... OFFSETS FOR PERTURBATIONS IN ONE PARTICULAR IRREP
C  
C /PERT3/
C  IXYZSYM..... ORDER OF XYZ IN CASE OF SYMMETRY BLOCKING
C
CEND
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
      LOGICAL STERM,JSO
      INTEGER DIRPRD 
      CHARACTER*8 NAMXYZ
C
      COMMON/PERT2/NTP,NFP,NTPERT(8),NFPERT(8),IPERT(8),IFPERT(8)
      COMMON/PERT3/IXYZSYM(3)
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/PERT/NT,NPERT(8),JPERT(8),IXPERT,IYPERT,IZPERT,
     &            IYZPERT,IXZPERT,IXYPERT,ITRANSX,ITRANSY,
     &            ITRANSZ,NUCIND
      COMMON/JNMR/JSO
      COMMON/BFILE/NAMXYZ(4,3) 
      COMMON/FLAGS/IFLAGS(100)
      COMMON/TREATP/IPERNUM
C
      IF(IFLAGS(90).EQ.1) THEN 
       IONE=1
       CALL GETREC(20,'JOBARC','TREATPER',IONE,IPERNUM)
      ELSE
       IPERNUM=0
      ENDIF
C
      CALL GETREC(20,'JOBARC','NSYMPERT',27,NT)
C
      CALL IZERO(NTPERT,8) 
      CALL IZERO(NFPERT,8)
C
      IF(IPERNUM.EQ.0.OR.IPERNUM.EQ.1) THEN
       IF(.NOT.JSO) write(*,1200)  iyzpert
1200   FORMAT(' Bx is a perturbation of irrep',I2,'.')
      ENDIF
C
      IF(IPERNUM.EQ.0.OR.IPERNUM.EQ.2) THEN
       IF(.NOT.JSO) write(*,1300)  ixzpert
1300   FORMAT(' By is a perturbation of irrep',I2,'.')
      ENDIF
C
      IF(IPERNUM.EQ.0.OR.IPERNUM.EQ.3) THEN
       IF(.NOT.JSO) write(*,1400)  ixypert
1400   FORMAT(' Bz is a perturbation of irrep',I2,'.')
      ENDIF
C
C FILL COMMON BLOCK /PERT2/
C
      CALL ICOPY(8,NPERT,1,NTPERT,1)
C
      NFPERT(IYZPERT)=NFPERT(IYZPERT)+1
      NFPERT(IXZPERT)=NFPERT(IXZPERT)+1
      NFPERT(IXYPERT)=NFPERT(IXYPERT)+1
C
      IPERT(1)=0 
      IFPERT(1)=0
      NTP=0
      NFP=0
      DO 200 IRREP=1,NIRREP-1
       IPERT(IRREP+1)=IPERT(IRREP)+NTPERT(IRREP)
       IFPERT(IRREP+1)=IFPERT(IRREP)+NFPERT(IRREP)
       NTP=NTP+NTPERT(IRREP)
       NFP=NFP+NFPERT(IRREP)
200   CONTINUE 
      NTP=NTP+NTPERT(NIRREP)
      NFP=NFP+NFPERT(NIRREP)
C
      IF(JSO) THEN
       CALL IZERO(NFPERT,8)
       NFP=NTP
      ENDIF
C
C FILL IXYZSYM
C
      IOFF=1
      DO 300 IRREP=1,NIRREP
C
       IF(IYZPERT.EQ.IRREP) THEN
        IXYZSYM(IOFF)=1
        IOFF=IOFF+1
       ENDIF
       IF(IXZPERT.EQ.IRREP) THEN
        IXYZSYM(IOFF)=2
        IOFF=IOFF+1
       ENDIF
       IF(IXYPERT.EQ.IRREP) THEN
        IXYZSYM(IOFF)=3
        IOFF=IOFF+1
       ENDIF
C
300   CONTINUE
C
C FILL NAMXYZ
C
      IF(IYZPERT.EQ.1) THEN
       NAMXYZ(1,1)='IIIIX   '
       NAMXYZ(2,1)='IIJJX   '
       NAMXYZ(3,1)='IJIJX   '
       NAMXYZ(4,1)='IJKLX   '
      ELSE
       NAMXYZ(1,1)='IIIJX   '
       NAMXYZ(2,1)='IIJKX   '
       NAMXYZ(3,1)='IJIKX   '
       NAMXYZ(4,1)='IJKLX   '
      ENDIF
      IF(IXZPERT.EQ.1) THEN
       NAMXYZ(1,2)='IIIIY   '
       NAMXYZ(2,2)='IIJJY   '
       NAMXYZ(3,2)='IJIJY   '
       NAMXYZ(4,2)='IJKLY   '
      ELSE
       NAMXYZ(1,2)='IIIJY   '
       NAMXYZ(2,2)='IIJKY   '
       NAMXYZ(3,2)='IJIKY   '
       NAMXYZ(4,2)='IJKLY   '
      ENDIF
      IF(IXYPERT.EQ.1) THEN
       NAMXYZ(1,3)='IIIIZ   '
       NAMXYZ(2,3)='IIJJZ   '
       NAMXYZ(3,3)='IJIJZ   '
       NAMXYZ(4,3)='IJKLZ   '
      ELSE
       NAMXYZ(1,3)='IIIJZ   '
       NAMXYZ(2,3)='IIJKZ   '
       NAMXYZ(3,3)='IJIKZ   '
       NAMXYZ(4,3)='IJKLZ   '
      ENDIF
      RETURN
      END
