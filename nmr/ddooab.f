      SUBROUTINE DDOOAB(T1,T2,DDOO,FACT,ISPIN,POP1,POP2,
     &                  VRT1,VRT2,DISSYL,NUMSYL,DISSYR,NUMSYR,
     &                  LISTT1,LISTT2,LISTT3,IRREPL,IRREPR,
     &                  TMP,IUHF,IFLAG)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DISSYL,DISSYR,DIRPRD,POP1,POP2,VRT1,VRT2
      DIMENSION T1(DISSYL,1),T2(DISSYR,1),DDOO(1),TMP(1)
      DIMENSION POP1(8),POP2(8),VRT1(8),VRT2(8) 
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,ILONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),DIRPRD(8,8)
C
      DATA ONE /1.0D0/,HALF /0.5D0/,ONEM /-1.D0/, TWO /2.D0/
C
C PICK UP FIRST THE T1  AND T2 AMPLITUDES
C
      CALL GETLST(T1,1,NUMSYL,1,IRREPL,LISTT1)
      CALL GETLST(T2,1,NUMSYR,2,IRREPR,LISTT2)
C
C SPIN ADAPTED CODE FOR RHF
C
      IF(IUHF.EQ.0) THEN
       CALL SPINAD1(IRREPR,POP1,DISSYR,T2,TMP,TMP(1+DISSYR))
      ENDIF
C
C TRANSPOSE THE LAST TWO INDICES IN THE AA CASE
C
C HOWEVER, IN RHF TAKE ADVANTAGE IF THE SPIN SYMMETRY
C
      IF(IUHF.EQ.1.AND.ISPIN.EQ.1) THEN 
       CALL SYMTR1(IRREPL,POP1,POP2,DISSYL,T1,TMP,TMP(1+DISSYL),
     &             TMP(1+2*DISSYL))
       CALL SYMTR1(IRREPR,POP1,POP2,DISSYR,T2,TMP,TMP(1+DISSYR),
     &             TMP(1+2*DISSYR))
      ENDIF
C
C  PERFORM MULTIPLICATION
C
C  JOFF1 OFFSET IN THE OCCUPIED-OCCUPIED BLOCK OF T1 
C  JOFF2 OFFSET IN THE OCCUPIED-OCCUPIED BLOCK OF T2
C  IOFF OFFSET IN DOO
C
      JOFF2=1
      IOFF=1
C
      DO 90 IRREPIR=1,NIRREP
C          
C GET OCCUPATION NUMBER FOR IRREPIR
C
       NOCCIR=POP1(IRREPIR)
C
C DETERMINE IRREPJ WHOSE DIRECT PRODUCT WITH IRREPIR GIVES IRREPR
C
       IRREPJ=DIRPRD(IRREPR,IRREPIR)
C
C GET OCCUPATION NUMBER FOR IRREPJ
C
       NOCCJ=POP2(IRREPJ)
C
       IRREPIL=DIRPRD(IRREPJ,IRREPL)
C
       NOCCIL=POP1(IRREPIL)
C
       JOFF1=1
       DO 89 IRREP=1,IRREPIL-1
        JOFF1=JOFF1+POP1(IRREP)*POP2(DIRPRD(IRREP,IRREPL))
89     CONTINUE 
C
C IF ZERO, NOTHING TO COMPUTE
C
       IF(MIN(NOCCIR,NOCCIL,NOCCJ).NE.0) THEN
C
        CALL XGEMM('T','N',NOCCIL,NOCCIR,DISSYR*NOCCJ,FACT,
     &             T1(1,JOFF1),NOCCJ*DISSYL,T2(1,JOFF2),      
     &             NOCCJ*DISSYR,ONE,DDOO(IOFF),NOCCIL)
C
       ENDIF
C
C  UPDATE OFFSETS
C
       JOFF2=JOFF2+NOCCJ*NOCCIR
       IOFF=IOFF+NOCCIL*NOCCIR
90    CONTINUE
C
      RETURN
      END
