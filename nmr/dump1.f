      SUBROUTINE DUMP1(AODINT,BUF,BUF1,BUF2,NSTART,NEND)
C
C THIS ROUTINE DUMPS TRANSFORMED INTEGRAL DERIVATIVES
C TO DISK. HERE, INTEGRALS FOR SYMMETRIC PERTURBATIONS
C OF TYPE IIII ARE HANDLED
C
CEND
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD,AOPOP,POP,VRT
C
      DIMENSION AODINT(1),BUF(1),BUF1(1),BUF2(1),
     &          NSTART(8),NEND(8)
C
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMINF/NDUMMY,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/AOPOPS/AOPOP(8)
      COMMON/INFOI/TIMEI,NWRITI
C
      INDXF(I,J,N)=I+N*(J-1)
C
C INITIALIZE OFFSETS
C
      IOFF=1
      IOFFOO=0
      IOFFVV=0
      IOFFVO=0
C
C LOOP OVER ALL IRREPS
C
      DO 1000 IRREP=1,NIRREP
C
       NBAS=AOPOP(IRREP)
       NBAS2=NBAS*NBAS
       NBAS3=NBAS*NBAS2
       IF(NBAS3.EQ.0) GO TO 1000
       NOCC=POP(IRREP,1)
       NSTARTI=NSTART(IRREP)
       NENDI=NEND(IRREP)
       NVRT=VRT(IRREP,1) 
C
       DO 100 IND=NSTARTI,NENDI
C
C REORDER ADOINT ARRAY, BUT ONLY INTEGRALS WHICH BELONG TO
C IRREP (THIS IS DONE IN EXPND5)
C
        CALL SCOPY(NBAS3,AODINT(IOFF),1,BUF,1)
        IOFF=IOFF+NBAS3
        CALL EXPND5(BUF,NBAS,NBAS,BUF1,1)
C
        LENGTH=NOCC*NOCC
        NWRITI=NWRITI+LENGTH*NOCC
        IF(LENGTH.NE.0) THEN
         DO 15 INDQ=1,NOCC
          IOFFIJKL=INDXF(1,INDQ,NBAS2)
          IDISIJKL=IOFFOO+INDXF(INDQ,IND,NOCC)
          LOFFIJKL=IOFFOO
          CALL BLKCPY2(BUF(IOFFIJKL),NBAS,NBAS,BUF1,NOCC,NOCC,1,1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISIJKL,LOFFIJKL,
     &               1,1,1,313,0)
15       CONTINUE
        ENDIF
C
        LENGTH=NOCC*NOCC
        NWRITI=NWRITI+LENGTH*NVRT
        IF(LENGTH.NE.0) THEN
         DO 35 IQ=1,NVRT
          INDQ=IQ+NOCC
          IOFFIJKA=INDXF(1,INDQ,NBAS2)
          IDISIJKA=IOFFVO+INDXF(IND,IQ,NOCC)
          LOFFIJKA=IOFFOO
          CALL BLKCPY2(BUF(IOFFIJKA),NBAS,NBAS,BUF1,NOCC,NOCC,1,1)
          CALL MTRAN2(BUF1,NOCC)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISIJKA,LOFFIJKA,
     &               1,1,1,310,0)
35       CONTINUE
        ENDIF
C
        LENGTH=NVRT*NVRT
        NWRITI=NWRITI+LENGTH*NOCC
        IF(LENGTH.NE.0) THEN
         DO 45 INDQ=1,NOCC
          IOFFABIJ=INDXF(1,INDQ,NBAS2)
          IDISABIJ=IOFFOO+INDXF(INDQ,IND,NOCC)
          LOFFABIJ=IOFFVV 
          CALL BLKCPY2(BUF(IOFFABIJ),NBAS,NBAS,BUF1,NVRT,NVRT,
     &                 NOCC+1,NOCC+1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISABIJ,LOFFABIJ,
     &               1,1,1,316,0)
45       CONTINUE
        ENDIF
C
        LENGTH=NVRT*NVRT
        NWRITI=NWRITI+LENGTH*NVRT
        IF(LENGTH.NE.0) THEN
         DO 65 IQ=1,NVRT
          INDQ=IQ+NOCC
          IOFFABCI=INDXF(1,INDQ,NBAS2)
          IDISABCI=IOFFVO+INDXF(IQ,IND,NVRT)
          LOFFABCI=IOFFVV
          CALL BLKCPY2(BUF(IOFFABCI),NBAS,NBAS,BUF1,NVRT,NVRT,
     &                 NOCC+1,NOCC+1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISABCI,LOFFABCI,
     &               1,1,1,330,0)
65       CONTINUE
        ENDIF
C
        LENGTH=NOCC*NVRT
        NWRITI=NWRITI+LENGTH*NVRT
        IF(LENGTH.NE.0) THEN
         DO 75 IQ=1,NVRT
          INDQ=IQ+NOCC
          IOFFAIBJ=INDXF(1,INDQ,NBAS2)
          IDISAIBJ=IOFFVO+INDXF(IQ,IND,NVRT)
          LOFFAIBJ=IOFFVO
          CALL BLKCPY2(BUF(IOFFAIBJ),NBAS,NBAS,BUF1,NVRT,NOCC,
     &                 NOCC+1,1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISAIBJ,LOFFAIBJ,
     &               1,1,1,325,0)
75       CONTINUE
        ENDIF
C
        IOFFB=1+NVRT*NOCC
        LENGTH=NOCC*NVRT
        NWRITI=NWRITI+LENGTH*NVRT
        IF(LENGTH.NE.0) THEN
         DO 76 IQ=1,NVRT
          INDQ=IQ+NOCC
          IOFFIABJ=INDXF(1,INDQ,NBAS2)
          IDISIABJ=IOFFVO+INDXF(IQ,IND,NVRT)
          LOFFIABJ=IOFFVO
          CALL BLKCPY2(BUF(IOFFIABJ),NBAS,NBAS,BUF1,NOCC,NVRT,
     &                 1,NOCC+1)
          CALL TRANSP(BUF1,BUF1(IOFFB),NVRT,NOCC)
          CALL ULIST(BUF1(IOFFB),BUF2,LENGTH,IDISIABJ,LOFFIABJ,
     &               1,1,1,321,0)
76       CONTINUE
        ENDIF
C
C UPDATE OFFSETS
C
100    CONTINUE
C
       IOFFOO=IOFFOO+NOCC*NOCC
       IOFFVV=IOFFVV+NVRT*NVRT
       IOFFVO=IOFFVO+NVRT*NOCC
C
1000  CONTINUE
C
      RETURN
      END
