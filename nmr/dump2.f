      SUBROUTINE DUMP2(AODINT,BUF,BUF1,BUF2,NSTART,NEND)
C
C THIS ROUTINE DUMPS TRANSFORMED INTEGRAL DERIVATIVES
C TO DISK. HERE, INTEGRALS FOR SYMMETRIC PERTURBATIONS
C OF TYPE IIJJ ARE HANDLED
C
CEND
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD,AOPOP,POP,VRT
C
      DIMENSION AODINT(1),BUF(1),BUF1(1),BUF2(1),NSTART(8),NEND(8)
      DIMENSION IOFFOO(8,8),IOFFVV(8,8),IOFFVO(8,8),IOFFOV(8,8)
C
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMINF/NDUMMY,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/AOPOPS/AOPOP(8)
      COMMON/INFOI/TIMEI,NWRITI
C
      INDXF(I,J,N)=I+N*(J-1)
C
C INITIALIZE OFFSETS
C
      IOFF=1
C
C DETERMINE OFFSETS
C
      DO 1 IRREP=2,NIRREP
C
       IOO=0
       IVV=0
       IVO=0
       IOV=0
C
       DO 1 IRREPR=1,NIRREP 
C
        IRREPL=DIRPRD(IRREP,IRREPR)
C
        IOFFOO(IRREPL,IRREPR)=IOO
        IOFFVV(IRREPL,IRREPR)=IVV
        IOFFVO(IRREPL,IRREPR)=IVO
        IOFFOV(IRREPL,IRREPR)=IOV
        IOO=IOO+POP(IRREPR,1)*POP(IRREPL,1)
        IVV=IVV+VRT(IRREPR,1)*VRT(IRREPL,1)
        IVO=IVO+VRT(IRREPL,1)*POP(IRREPR,1)
        IOV=IOV+VRT(IRREPR,1)*POP(IRREPL,1)
C
1     CONTINUE
C
C LOOP OVER ALL IRREPS
C
      DO 1000 IRREP1=1,NIRREP
C
       NBAS1=AOPOP(IRREP1)
       NOCC1=POP(IRREP1,1)
       NSTART1=NSTART(IRREP1)
       NEND1=NEND(IRREP1)
       NVRT1=VRT(IRREP1,1) 
C
       NSIZ3=0
       NSIZ2=0
C
       DO 1010 IRREP2=1,NIRREP
C
        IF(IRREP1.NE.IRREP2) THEN
         NSIZ3=NSIZ3+AOPOP(IRREP1)*AOPOP(IRREP2)*AOPOP(IRREP2)
         NSIZ2=NSIZ2+AOPOP(IRREP2)*AOPOP(IRREP2)
        ENDIF
C
1010   CONTINUE
C
       DO 100 IND=NSTART1,NEND1
C
        IOFF2=0
C
        DO 2000 IRREP2=1,NIRREP
C
         IF(IRREP1.EQ.IRREP2) GO TO 2000
C
         IRREP12=DIRPRD(IRREP1,IRREP2)
C
         NBAS2=AOPOP(IRREP2)
         NBAS22=NBAS2*NBAS2
         NBAS12=NBAS1*NBAS2
         NOCC2=POP(IRREP2,1)
         NVRT2=VRT(IRREP2,1)
C
         NBAS3=NBAS22*NBAS1

C REORDER ADOINT ARRAY, BUT ONLY INTEGRALS WHICH BELONG TO
C IRREP (THIS IS DONE IN EXPND5)
C
        IOFFB=1
        DO 3000 JMO=1,NBAS1
         IOFFA=IOFF+(JMO-1)*NSIZ2+IOFF2
         CALL SCOPY(NBAS22,AODINT(IOFFA),1,BUF(IOFFB),1)
         IOFFB=IOFFB+NBAS22
3000    CONTINUE
C
        IOFF2=IOFF2+AOPOP(IRREP2)*AOPOP(IRREP2)
C
        DO 3010 IP=1,NBAS2
         CALL SCOPY(NBAS12,BUF(IP),NBAS2,BUF2,1)
         CALL TRANSP(BUF2,BUF2(1+NBAS12),NBAS1,NBAS2)
         CALL SCOPY(NBAS12,BUF2(1+NBAS12),1,BUF(IP),NBAS2) 
3010    CONTINUE
C
C THE ORDERING IS NOW :     IRREP2,IRREP1; IRREP2, FIXED OCC(IRREP1)
C
        LENGTH=NOCC1*NOCC2
        NWRITI=NWRITI+LENGTH*NOCC2
        IF(LENGTH.NE.0) THEN
         DO 15 INDQ=1,NOCC2
          IOFFIJKL=INDXF(1,INDQ,NBAS12)
          IDISIJKL=IOFFOO(IRREP2,IRREP1)+INDXF(INDQ,IND,NOCC2)
          LOFFIJKL=IOFFOO(IRREP2,IRREP1)
          CALL BLKCPY2(BUF(IOFFIJKL),NBAS2,NBAS1,BUF1,NOCC2,NOCC1,1,1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISIJKL,LOFFIJKL,
     &               1,IRREP12,IRREP12,313,0)
15       CONTINUE
        ENDIF
C
        LENGTH=NOCC1*NOCC2
        NWRITI=NWRITI+LENGTH*NVRT2
        IF(LENGTH.NE.0) THEN
         DO 35 IQ=1,NVRT2
          INDQ=IQ+NOCC2
          IOFFIJKA=INDXF(1,INDQ,NBAS12)
          IDISIJKA=IOFFOV(IRREP1,IRREP2)+INDXF(IND,IQ,NOCC1)
          LOFFIJKA=IOFFOO(IRREP1,IRREP2)
          CALL BLKCPY2(BUF(IOFFIJKA),NBAS2,NBAS1,BUF1,NOCC2,NOCC1,1,1)
          CALL TRANSP(BUF1,BUF1(1+LENGTH),NOCC1,NOCC2)
          CALL ULIST(BUF1(1+LENGTH),BUF2,LENGTH,IDISIJKA,LOFFIJKA,
     &               1,IRREP12,IRREP12,310,0)
35       CONTINUE
        ENDIF
C
        LENGTH=NVRT1*NVRT2
        NWRITI=NWRITI+LENGTH*NOCC2
        IF(LENGTH.NE.0) THEN
         DO 45 INDQ=1,NOCC2
          IOFFABIJ=INDXF(1,INDQ,NBAS12)
          IDISABIJ=IOFFOO(IRREP2,IRREP1)+INDXF(INDQ,IND,NOCC2)
          LOFFABIJ=IOFFVV(IRREP2,IRREP1) 
          CALL BLKCPY2(BUF(IOFFABIJ),NBAS2,NBAS1,BUF1,NVRT2,NVRT1,
     &                 NOCC2+1,NOCC1+1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISABIJ,LOFFABIJ,
     &               1,IRREP12,IRREP12,316,0)
45       CONTINUE
        ENDIF
C
        LENGTH=NVRT1*NVRT2
        NWRITI=NWRITI+LENGTH*NVRT2
        IF(LENGTH.NE.0) THEN
         DO 65 IQ=1,NVRT2
          INDQ=IQ+NOCC2
          IOFFABCI=INDXF(1,INDQ,NBAS12)
          IDISABCI=IOFFVO(IRREP2,IRREP1)+INDXF(IQ,IND,NVRT2)
          LOFFABCI=IOFFVV(IRREP2,IRREP1)
          CALL BLKCPY2(BUF(IOFFABCI),NBAS2,NBAS1,BUF1,NVRT2,NVRT1,
     &                 NOCC2+1,NOCC1+1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISABCI,LOFFABCI,
     &               1,IRREP12,IRREP12,330,0)
65       CONTINUE
        ENDIF
C
        LENGTH=NOCC1*NVRT2
        NWRITI=NWRITI+LENGTH*NVRT2
        IF(LENGTH.NE.0) THEN
         DO 75 IQ=1,NVRT2
          INDQ=IQ+NOCC2
          IOFFAIBJ=INDXF(1,INDQ,NBAS12)
          IDISAIBJ=IOFFVO(IRREP2,IRREP1)+INDXF(IQ,IND,NVRT2)
          LOFFAIBJ=IOFFVO(IRREP2,IRREP1)
          CALL BLKCPY2(BUF(IOFFAIBJ),NBAS2,NBAS1,BUF1,NVRT2,NOCC1,
     &                 NOCC2+1,1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISAIBJ,LOFFAIBJ,
     &               1,IRREP12,IRREP12,325,0)
75       CONTINUE
        ENDIF
C
        LENGTH=NOCC2*NVRT1
        NWRITI=NWRITI+LENGTH*NVRT2
        IF(LENGTH.NE.0) THEN
         DO 76 IQ=1,NVRT2
          INDQ=IQ+NOCC2
          IOFFIABJ=INDXF(1,INDQ,NBAS12)
          IDISIABJ=IOFFVO(IRREP2,IRREP1)+INDXF(IQ,IND,NVRT2)
          LOFFIABJ=IOFFVO(IRREP1,IRREP2)
          CALL BLKCPY2(BUF(IOFFIABJ),NBAS2,NBAS1,BUF1,NOCC2,NVRT1,
     &                 1,NOCC1+1)
          CALL TRANSP(BUF1,BUF1(1+LENGTH),NVRT1,NOCC2)
          CALL ULIST(BUF1(1+LENGTH),BUF2,LENGTH,IDISIABJ,LOFFIABJ,
     &               1,IRREP12,IRREP12,321,0)
76       CONTINUE
        ENDIF
C
2000    CONTINUE
C
        IOFF=IOFF+NSIZ3
C
100    CONTINUE
C
1000  CONTINUE
C
      RETURN
      END
