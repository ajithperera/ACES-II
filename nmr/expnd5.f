      SUBROUTINE EXPND5(Z,NUMP,NUMR,SCR,IANTI)
C
C THIS ROUTINE PERFORMS THE EXPANSION AND TRANSPOSITION REQUIRED FOR THE
C  FOLLOWING REARRANGEMENT OF INDICES
C
C         Z(P<=Q,R) --> Z(Q,P,R)
C
C
C IT IS PERFORMED BY THE TWO-STAGE PROCESS AND IS DONE IN PLACE
C
C         Z(P<=Q,R) --> Z(P,Q,R) --> Z(Q,P,R) 
C
C   Z  : THE (P<=Q,R) ARRAY TO BE MANIPULATED
C NUMP : THE NUMBER OF P ELEMENTS (EQUAL TO THE NUMBER OF Q)
C NUMR : THE NUMBER OF R ELEMENTS
C  SCR : A SCRATCH ARRAY OF LENGTH   
C
C             MAX(NUMP*NUMP,2*NUMP*NUMR) IF NUMP.NE.NUMR
C             NUMP*NUMP                  IF NUMP.EQ.NUMR
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION Z(1),SCR(1)
      NNP1O2(I)=(I*(I+1))/2
      NUMP2=NUMP*NUMP
      NUMPR=NUMP*NUMR
      IF(IANTI.EQ.0) THEN
       IOFF1=NNP1O2(NUMP)*(NUMR-1)+1
       IOFF2=NUMP2*(NUMR-1)+1
       DO 10 I=1,NUMR
        CALL EXPND2(Z(IOFF1),SCR,NUMP)
        CALL SCOPY(NUMP2,SCR,1,Z(IOFF2),1)
        IOFF2=IOFF2-NUMP2
        IOFF1=IOFF1-NNP1O2(NUMP)  
10     CONTINUE
      ENDIF
C
C NOW THE ENTIRE Z(P,Q,R) MATRIX IS IN CORE.  WE NEED TO TRANSPOSE
C  THE LAST TWO INDICES
C
      IF(NUMP.NE.NUMR)THEN
       DO 20 P=1,NUMP
        CALL SCOPY(NUMPR,Z(P),NUMP,SCR,1)
        CALL TRANSP(SCR,SCR(NUMPR+1),NUMR,NUMP)
        CALL SCOPY(NUMPR,SCR(NUMPR+1),1,Z(P),NUMP)
20     CONTINUE
      ELSE
       DO 21 P=1,NUMP
        CALL SCOPY(NUMPR,Z(P),NUMP,SCR,1)
        CALL MTRAN2(SCR,NUMP)
        CALL SCOPY(NUMPR,SCR,1,Z(P),NUMP)
21     CONTINUE
      ENDIF
C
      RETURN
      END
