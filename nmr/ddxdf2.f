      SUBROUTINE DDXDF2(FFMAT,DDVO,ICORE,MAXCOR,IUHF,ISTART,IEND)
C
C THIS ROUTINE CALCULATES THE CONTRACTIONS OF
C THE DERIVATIVES OF THE DENSITY MATRIX (VIRTUAL,OCCUPIED
C BLOCK) WITH THE DERIVATIVES OF THE FOCK MATRICES
C
CEND
C 
C CODED SEPTEMBER/91
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL QRHF,ROHF,SEMI,JSO,GIAO
      INTEGER POP,VRT
      DOUBLE PRECISION MSZ
C
      DIMENSION DDVO(1),
     &          ICORE(MAXCOR),FFMAT(1)
C
      COMMON/REF/ROHF,QRHF,SEMI
      COMMON/JNMR/JSO
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/DSYM/IRREPX,JPERT,NDT(2),NDF1(2),NDF2(2),
     &            IOFFIJ(8,2),IOFFAB(8,2),IOFFAI(8,2)
      COMMON/PERT2/NTP,NFP,NTPERT(8),NFPERT(8),IPERT(8),IFPERT(8)
C
      COMMON/GAUGE/GIAO
      COMMON/MAGN/MSZ(9)
      COMMON/PERTINF/INIT,MPERT(8)
C
      DATA ONE,TWO,FOUR,TWOM,FOURM /1.D0,2.D0,4.D0,-2.D0,-4.D0/
C
      iprint=0
      FACT=TWO
      IOFF=IPERT(IRREPX)
      IOFFF=IFPERT(IRREPX)
      IF(JSO) IOFFF=IPERT(IRREPX)
      NPERT=NTPERT(IRREPX) 
      NPERTF=NFPERT(IRREPX)
      IF(IUHF.EQ.0) FACT=FOUR
C
C LOOP OVER SPIN CASES
C
      DO 1000 ISPIN=1,1+IUHF
C
      IF(ISPIN.EQ.1) THEN
       IOFFVO=1
      ELSE
       IOFFVO=1+NPERTF*NDT(1)
      ENDIF
C
C LOOP OVER ALL PERTURBATIONS
C
      DO 100 I2PERT=1,NPERT
C
C READ IN DERIVATIVES OF OCCUPIED-VIRTUAL BLOCK OF FOCK MATRICES
C
      CALL GETLST(ICORE,I2PERT+NPERTF,1,1,IRREPX,179+ISPIN)
C
C LOOP OVER ALL FIELD PERTURBATIONS
C
      DO 100 I1PERT=ISTART,IEND
C
       IOFFD=IOFFVO+(I1PERT-ISTART)*NDT(ISPIN)
C
       IND1=I1PERT+IOFFF+(I2PERT+IOFF-1)*NFP
C
       VALUE=SDOT(NDT(ISPIN),DDVO(IOFFD),1,ICORE,1)
C
       FFMAT(IND1)=FFMAT(IND1)+FACT*VALUE
C
100   CONTINUE
C
1000  CONTINUE
C
      if(iprint.ge.1) then
c      write(6,6000)
6000   FORMAT(' Contribution of d D(P,q)/d B to d E(MBPT(2)/d B d In :',
     &        '\n   symmetry adapted nuclear magnetic moment',
     &        '    contribution')
       do 400 i2pert=1,npert
        ind1=i1pert+ioffF+(i2pert+ioff-1)*nfp
        write(6,6001) i2pert,ffmat(ind1)
6001    FORMAT(13X,I10,13X,F12.8)
400    continue
      endif
C
      IF(.NOT.GIAO) THEN
C
C CALCULATE CONTRIBUTION TO MAGNETIC SUSCEPTIBILITY TENSOR, CURRENTLY ONLY
C AVAILABLE FOR CONVENTIONAL MBPT(2)
C
C LOOP OVER SPIN CASES
C
       DO 2000 ISPIN=1,1+IUHF
C
        IF(ISPIN.EQ.1) THEN
         IOFFVO=1
        ELSE
         IOFFVO=1+NPERTF*NDT(1)
        ENDIF
C
C PROCESS DERIVATIVES OF THE VIRTUAL-OCCUPIED BLOCK OF FOCK MATRICES
C
        DO 1300 I2PERT=1,NPERTF
C
         CALL GETLST(ICORE,I2PERT,1,1,IRREPX,179+ISPIN)
C
         DO 1100 I1PERT=ISTART,IEND
C
          IND1=I1PERT+IOFFF+(I2PERT+IOFFF-1)*3
          IOFFD=IOFFVO+(I1PERT-ISTART)*NDT(ISPIN)
C
          VALUE=SDOT(NDT(ISPIN),DDVO(IOFFD),1,ICORE,1)
C
          MSZ(IND1)=MSZ(IND1)+FACT*VALUE
C
1100     CONTINUE
1300    CONTINUE
C
2000   CONTINUE
C
      ENDIF
C
      RETURN
      END
