
      SUBROUTINE GHTRAN(ZAO,ZMO,C,IOFFA,IOFFB,IRREPA,IRREPB,BUF,
     &                  IACC,IOVER,NSIZEA,NSIZEB,NSIZEA2,NSIZEB2)
C
C THIS ROUTINE PERFORMS THE GENERAL HALF-TRANSFORMATION
C
C             +
C  ZMO(PQ) = C [mu(a),P(a)] ZAO[mu(a)nu(b),lambda(c)sigma(d)] C[nu(b),Q(b)]
C
C WHERE C IS THE MATRIX OF EIGENVECTORS, Z IS A FOUR-INDEX QUANTITY IN
C THE ATOMIC ORBITAL BASIS, AND Q IS A HALF-TRANSFORMED [P(a),Q(b)]
C ARRAY IN THE MO BASIS.  THE SYMBOLS a,b,c AND d REFER TO THE IRREPS
C OF THE ORBITALS.  
C
C IOFFST(IRREP,ISPIN) IS AN ARRAY WHICH GIVES THE OFFSET IN C AT
C WHICH EACH IRREP BEGINS
C
CEND
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION ZAO(NSIZEA,NSIZEB),ZMO(NSIZEA2,NSIZEB2),BUF(1)
      DIMENSION C(1)
C
      DATA ZILCH /0.0/
      DATA ONE   /1.0/
C
      IF(IOVER.EQ.0)THEN
C
       IF(IACC.EQ.0)CALL ZERO(ZMO,NSIZEA2*NSIZEB2)
       IF(MIN(NSIZEA,NSIZEB,NSIZEA2,NSIZEB2).NE.0)THEN
        CALL XGEMM('T','N',NSIZEA2,NSIZEB,NSIZEA,ONE,
     &             C(IOFFA),NSIZEA2,ZAO,NSIZEA,ZILCH,BUF,NSIZEA2)
        CALL XGEMM('N','N',NSIZEA2,NSIZEB2,NSIZEB,ONE,
     &             BUF,NSIZEA2,C(IOFFB),NSIZEB,ONE,ZMO,NSIZEA2)
       ENDIF
C
      ELSE
C
       IF(MIN(NSIZEA,NSIZEB,NSIZEA2,NSIZEB2).NE.0)THEN
        CALL XGEMM('T','N',NSIZEA2,NSIZEB,NSIZEA,ONE,
     &             C(IOFFA),NSIZEA,ZAO,NSIZEA,ZILCH,BUF,NSIZEA2)
        CALL XGEMM('N','N',NSIZEA2,NSIZEB2,NSIZEB,ONE,
     &             BUF,NSIZEA2,C(IOFFB),NSIZEB,ZILCH,ZMO,NSIZEA2)
       ENDIF
C
      ENDIF
C
      RETURN
      END
