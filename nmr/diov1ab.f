      SUBROUTINE DIOV1AB(G,W,T,DIOV,FACT,ISPIN,POP1,POP2,
     &                   VRT1,VRT2,DISSYG,NUMSYG,DISSYW,
     &                   NUMSYW,LISTG,LISTW,IRREPGL,IRREPL,IRREPR,
     &                   TMP,IUHF)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DISSYG,DISSYW,DIRPRD,POP1,POP2,VRT1,VRT2
      DIMENSION G(NUMSYG,1),W(DISSYW,1),T(1),DIOV(1),TMP(1)
      DIMENSION POP1(8),POP2(8),VRT1(8),VRT2(8) 
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
C
      DATA ONE,ONEM,TWO /1.0D0,-1.D0,2.D0/
C
C PICK UP FIRST THE G AMPLITUDES
C
      CALL GETLST(T,1,NUMSYG,1,IRREPGL,LISTG)
C
C  USE SPIN ADAPTED SCHEME FOR RHF
C
      IF(IUHF.EQ.0) THEN
       CALL SPINAD1(IRREPGL,POP1,DISSYG,T,TMP,TMP(1+DISSYG))
      ENDIF
C
      CALL TRANSP(T,G,NUMSYG,DISSYG)
C
C  GET INTEGRALS
C
      CALL GETLST(W,1,NUMSYW,2,IRREPR,LISTW)
C
C  TRANSPOSE THE LAST TWO INDICES IN THE AA CASE
C
      IF(ISPIN.EQ.2.OR.IUHF.EQ.0) THEN
       CALL SYMTR1(IRREPL,VRT1,VRT2,NUMSYG,G,TMP,TMP(1+NUMSYG),
     &             TMP(1+2*NUMSYG))
       CALL SYMTR1(IRREPR,POP1,VRT2,DISSYW,W,TMP,TMP(1+DISSYW),
     &              TMP(1+2*DISSYW))
      ENDIF
C
C PERFORM MULTIPLICATION
C
C JOFFW AND JOFFG OFFSET IN THE VIRTUAL-VIRTUAL BLOCK OF G AND W
C IOFF OFFSET IN AIVV
C
      IOFF=1
      JOFFW=1
      DO 90 IRREPIR=1,NIRREP
C          
C GET NUMBER OF VIRTUAL ORBITALS FOR IRREPJ
C
       NOCCIR=POP1(IRREPIR)
C
C DETERMINE IRREPI WHOSE DIRECT PRODUCT WITH IRREPJ GIVES IRREP
C
       IRREPJ=DIRPRD(IRREPR,IRREPIR)
C
C GET NUMBER OF VIRTUAL ORBITALS FOR KRREP
C
       NVRTJ=VRT2(IRREPJ)
C
       IRREPIL=DIRPRD(IRREPL,IRREPJ)
C
       NVRTIL=VRT1(IRREPIL)
C
       JOFFG=1
       DO 89 IRREP=1,IRREPIL-1
        JOFFG=JOFFG+VRT1(IRREP)*VRT2(DIRPRD(IRREPL,IRREP))
89     CONTINUE
C
C IF NVRTIL, NOCCIR, OR NVRTJ EQUAL ZERO, NOTHING TO COMPUTE
C
       IF(MIN(NVRTJ,NVRTIL,NOCCIR).NE.0) THEN
C
          CALL XGEMM('T','N',NVRTIL,NOCCIR,NUMSYG*NVRTJ,FACT,
     &               G(1,JOFFG),NVRTJ*NUMSYG,W(1,JOFFW),
     &               NVRTJ*NUMSYG,ONE,DIOV(IOFF),NVRTIL)
       ENDIF
C
C UPDATE THE OFFSETS
C
       JOFFW=JOFFW+NOCCIR*NVRTJ
       IOFF=IOFF+NVRTIL*NOCCIR
C
90    CONTINUE
C
      RETURN
      END
