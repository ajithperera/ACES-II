
      SUBROUTINE LINEQ1(IRREP,NPERT,AMAT,BIA,BIANEW,UPDATE,ASMALL,
     &                  ASQUARE,ASCALE,ICONV,EVAL,CONV,N,KMAX,NOCC)
C
C  THIS PROGRAM SOLVES THE LINEAR CPHF EQUATION
C
C  SUM A I (A(BJ,AI) + (EA-EI) DELTA(BA) DELTA(IJ)) U(A,I) = B(B,J)
C
C  USING AN ITERATIVE EXPANSION IN A SUBSPACE SPANNED BY THE
C  VECTORS B(AI) A'**N (BJ,AI). THE METHOD HAS BEEN FIRST USED
C  BY POPLE AND COWORKERS FOR SOLVING THE CPHF-EQUATIONS.
C 
C  THIS VERSION USES EXPLICITELY SYMMETRY (ONLY THE BLOCK OF 
C  THE A-MATRIX WHICH CORRESPONDS TO IRREP OF THE PERTURBATION
C  IS NEEDED) AND IS EFFICIENTLY VECTORIZED. 
C
C  IRREP ... IRREP OF THE PERTURBATIONS TREATED
C  NPERT ... NUMBER OF PERTURBATIONS TREATED
C  AMAT .... CONTAINS THE MODIFIED A-MATRIX AS PROVIDED BY MKARHF
C  BIA  .... B(AI) AS INPUT, U(AI) AS OUTPUT
C  BIANEW .. SCRATCH VECTOR OF LENGTH MAX(N,2*KMAX)
C  UPDATE .. CONTAINS ALL THE EXPANSION VECTOR (LENGTH KMAX*N*NPERT)
C  ASMALL .. THE A MATRIX WITHIN THE ITERATIVE SUBSPACE ( LENGTH 
C            KMAX**2)*NPERT
C  ASQUARE . A VECTOR OF LENGTH (KMAX+1)*NPERT  WHICH HOLDS THE NORMS OF THE 
C            EXPANSION VECTORS
C  ASCALE .. A VECTOR OF LENGTH NPERT WHICH HOLDS SOME SCALING FACTORS
C  ICONV ... A VECTOR HOLDING THE NUMBER OF ITERATIONS REQUIRED TO 
C            CONVERGE A SINGLE PERTURBATION (LENGTH NPERT)
C  EVAL .... EIGENVALUES
C  CONV .... CONVERGENCE CRITERION
C  N ....... NUMBER OF ELEMENTS IN B(AI) OR U(AI)
C  KMAX .... MAXIMUM NUMBER OF ITERATION ALLOWED IN THE ITERATIVE EXPANSION
C  NOCC .... NUMBER OF OCCUPIED ORBITALS
C
CEND
C 
C CODED AUGUST/90  JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD,POP,VRT
      DIMENSION AMAT(N,N),BIA(N,NPERT),BIANEW(N,NPERT),
     &          UPDATE(N,NPERT,KMAX),ASMALL(KMAX,KMAX,NPERT),
     &          ASQUARE(KMAX+1,NPERT),ASCALE(NPERT),
     &          EVAL(1),ICONV(NPERT)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYM/POP(8,2),VRT(8,2),NJUNK(6)
C
      DATA AZERO,ONE,SMALL/0.D+0,1.0D+0,1.D-24/
C
      IF(N.LE.0) RETURN
C
      CUTOFF=CONV*CONV
      TOL=AZERO
C
C     CLEAR SMALL A MATRIX
C
      CALL IZERO(ICONV,NPERT)
      CALL ZERO(ASMALL,KMAX*KMAX*NPERT)
C
C     NORMALIZE INITIAL VECTOR BIA AND COPY INTIAL VECTOR TO UPDATE
C
      DO 10 IPERT=1,NPERT
      ANORM=SDOT(N,BIA(1,IPERT),1,BIA(1,IPERT),1)
C
C     DETERMINE SCALE FACTOR
C
      ASCALE(IPERT)=SQRT(ANORM)
C
C  IF INITIAL VECTOR EQUAL ZERO, WRITE WARNING
C
      IF(ASCALE(IPERT)/N.LT.CUTOFF) THEN
       WRITE(6,*) ' Warning from Lineq1 : The initial vector is zero.'
       ASCALE(IPERT)=ONE
      ENDIF
C
C     SCALE INITIAL VECTOR WITH ASCALE
C
      SCALE=ONE/ASCALE(IPERT)
      CALL SSCAL(N,SCALE,BIA(1,IPERT),1)
C
      ASQUARE(1,IPERT)=ONE
10    CONTINUE
C
c YAU : old
c     CALL ICOPY(IINTFP*N*NPERT,BIA(1,1),1,UPDATE(1,1,1),1)
c YAU : new
      CALL DCOPY(N*NPERT,BIA(1,1),1,UPDATE(1,1,1),1)
c YAU : end
C
C    LOOP OVER K UNTIL THE NEW VECTOR IS LINEAR DEPENDENT OR
C    KMAX IS REACHED
C
      DO 1000 K=1,KMAX
C
      CALL XGEMM('T','N',N,NPERT,N,ONE,AMAT,N,BIA,N,AZERO,BIANEW,N)
      CALL FORMU(IRREP,NPERT,N,BIANEW,EVAL,POP(1,1),VRT(1,1),NOCC)
C
C    FORM ELEMENTS OF MATRIX ASMALL
C
      DO 20 IPERT=1,NPERT
      DO 100 L=1,K
      ASMALL(L,K,IPERT)=SDOT(N,UPDATE(1,IPERT,L),1,BIANEW(1,IPERT),1)
100   CONTINUE
      IF(K.GT.1) ASMALL(K,K-1,IPERT)=ASQUARE(K,IPERT)
C
C    ORTHOGONALIZE XIANEW TO PREVIOUS VECTORS
C
      DO 200 L=1,K
      SCALE=-ASMALL(L,K,IPERT)/ASQUARE(L,IPERT)
      CALL SAXPY(N,SCALE,UPDATE(1,IPERT,L),1,BIANEW(1,IPERT),1)
200   CONTINUE
C
      ASQUARE(K+1,IPERT)=SDOT(N,BIANEW(1,IPERT),1,BIANEW(1,IPERT),1)
20    CONTINUE
c YAU : old
c     CALL ICOPY(IINTFP*N*NPERT,BIANEW,1,BIA,1)
c     CALL ICOPY(IINTFP*N*NPERT,BIANEW,1,UPDATE(1,1,K+1),1)
c YAU : new
      CALL DCOPY(N*NPERT,BIANEW,1,BIA,1)
      CALL DCOPY(N*NPERT,BIANEW,1,UPDATE(1,1,K+1),1)
c YAU : end
C
C  CHECK FOR CONVERGENCE OF THE CREATED ITERATIVE SUBSPACE
C
      SUM=AZERO
      DO 30 IPERT=1,NPERT
      TEST=ASQUARE(K+1,IPERT)/N
      IF(TEST.LE.SMALL) THEN
       ASQUARE(K+1,IPERT)=ONE
       IF(ICONV(IPERT).EQ.0)ICONV(IPERT)=K
      ENDIF
      SUM=SUM+TEST
30    CONTINUE
      IF(SUM.LT.CUTOFF) THEN
C
C      ITERATIVE EXPANSION HAS CONVERGED, EXIT THE LOOP
C
       GO TO 300
      ENDIF
1000  CONTINUE
C
C  IF WE REACH THIS POINT, THE ITERATIVE EXPANSION HAS NOT CONVERGED
C
      WRITE(6,3000) KMAX
3000  FORMAT(' CPHF did not converge after ',I4,
     &       ' cycles,',' abort !')
      WRITE(6,3001) TEST
3001  FORMAT(' Convergence is : ',E20.10)
      CALL ERREX
C
C   NOW INVERT THE ASMALL MATRIX
C
  300 CONTINUE
C
      WRITE(6,3002) K
3002  FORMAT(' Convergence reached after ',I3,
     &       ' iterations.')
C
C  SUBTRACT THE DIAGONAL PART 
C
C  NOTE THE ASMALL MATRIX CONSISTS OF SEVERAL PARTS
C
C  FIRST THE UPPER TRIANGLE WITH L LT K
C 
C    THAT'S SIMPLY    Y(L)  A Y(K)
C
C  THE DIAGONAL ELEMENTS
C
C   THAT'S   Y(K) A Y(K) - Y(K) Y(K)
C
C  THE LOWER TRIANGLE WITH L GT K+1
C
C  THESE ARE ZERO SINCE Y(L) SPANS A DIMENSION NOT COVERED BY A Y(K)
C
C  THE LOWER TRIANGLE WITH L EQ K+1
C
C  THESE ARE  Y(K+1) A(K) Y(K) =  Y(K+1) Y(K+1) = ASQUARE(K+1)
C
      CALL ZERO(BIA,N*NPERT)
C
      DO 40 IPERT=1,NPERT
C
      K1=K
      IF(ICONV(IPERT).NE.0) K1=ICONV(IPERT)
C
      DO 310 I=1,K1
       ASMALL(I,I,IPERT)=ASMALL(I,I,IPERT)-ASQUARE(I,IPERT)
310   CONTINUE
C
C  INVERT A (XIANEW IS HERE USED AS SCRATCH AND NO LONGER USED
C  FOR HOLDING A*Y
C
      CALL MINV(ASMALL(1,1,IPERT),K1,KMAX,BIANEW,DET,TOL,0,1)
C
      DO 330 I=1,K1
       ASQUARE(I,IPERT)=-ASCALE(IPERT)*ASMALL(I,1,IPERT)
330   CONTINUE
C
C     NOW FORM SOLUTION IN XIA
C
      DO 340 I=1,K1
       CALL SAXPY(N,ASQUARE(I,IPERT),UPDATE(1,IPERT,I),1,
     & BIA(1,IPERT),1)
340   CONTINUE
40    CONTINUE 
C
C   ALL DONE, RETURN
C
      RETURN
      END
