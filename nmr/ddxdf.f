      SUBROUTINE DDXDF(DDOO,DDVV,DDVO,FFMAT,ICORE,MAXCOR,IUHF)
C
C THIS ROUTINE CALCULATES THE CONTRACTIONS OF
C THE DERIVATIVES OF THE DENSITY MATRIX WITH THE
C DERIVATIVES OF THE FOCK MATRICES
C
CEND
C 
C CODED APRIL/91 JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL QRHF,ROHF,SEMI,JSO,GIAO
      INTEGER POP,VRT
      DOUBLE PRECISION MSZ
C
      DIMENSION DDOO(1),DDVV(1),DDVO(1),
     &          ICORE(MAXCOR),FFMAT(1)
C
      COMMON/REF/ROHF,QRHF,SEMI
      COMMON/JNMR/JSO
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/DSYM/IRREPX,I1PERT,NDT(2),NDF1(2),NDF2(2),
     &            IOFFIJ(8,2),IOFFAB(8,2),IOFFAI(8,2)
      COMMON/PERT2/NTP,NFP,NTPERT(8),NFPERT(8),IPERT(8),IFPERT(8)
C
      COMMON/GAUGE/GIAO
      COMMON/MAGN/MSZ(9)
      COMMON/PERTINF/INIT,MPERT(8)
C
      DATA ONE,TWO /1.D0,2.D0/
C
      IPRINT=0
      FACT=ONE
      IOFF=IPERT(IRREPX)
      IOFFF=IFPERT(IRREPX)
      NPERT=NTPERT(IRREPX) 
      NPERTF=NFPERT(IRREPX)
      IOFFL=0
      IF(JSO) THEN
       IOFFL=10
       IOFFF=IPERT(IRREPX)
      ENDIF
      IF(IUHF.EQ.0) FACT=TWO
C
C LOOP OVER SPIN CASES
C
      DO 1000 ISPIN=1,1+IUHF
C
      IF(ISPIN.EQ.1) THEN
       IOFFOO=1
       IOFFVV=1
       IOFFVO=1
      ELSE
       IOFFOO=1+NDF1(1)
       IOFFVV=1+NDF2(1)
       IOFFVO=1+NDT(1)
      ENDIF
C
C LOOP OVER ALL PERTURBATIONS
C
      DO 100 I2PERT=1,NPERT
C
C READ IN DERIVATIVES OF OCCUPIED-OCCUPIED BLOCK OF FOCK MATRICES
C
      CALL GETLST(ICORE,I2PERT+NPERTF,1,1,IRREPX,175+ISPIN+IOFFL)
C
      IND1=I1PERT+IOFFF+(I2PERT+IOFF-1)*NFP
C
      VALUE=SDOT(NDF1(ISPIN),DDOO(IOFFOO),1,ICORE,1)
C
      FFMAT(IND1)=FFMAT(IND1)+FACT*VALUE
C
100   CONTINUE
C
C LOOP OVER ALL PERTURBATIONS
C
      DO 200 I2PERT=1,NPERT
C
C READ IN DERIVATIVES OF VIRTUAL-VIRTUAL BLOCK OF FOCK MATRICES
C
      CALL GETLST(ICORE,I2PERT+NPERTF,1,1,IRREPX,177+ISPIN+IOFFL)
C
      IND1=I1PERT+IOFFF+(I2PERT+IOFF-1)*NFP
C
      VALUE=SDOT(NDF2(ISPIN),DDVV(IOFFVV),1,ICORE,1)
C
      FFMAT(IND1)=FFMAT(IND1)+FACT*VALUE
C
200   CONTINUE
C
C PROCESS DERIVATIVES OF THE VIRTUAL-OCCUPIED BLOCK OF FOCK MATRICES
C
      DO 300 I2PERT=1,NPERT
C
      CALL GETLST(ICORE,I2PERT+NPERTF,1,1,IRREPX,179+ISPIN)
C
      IND1=I1PERT+IOFFF+(I2PERT+IOFF-1)*NFP
C
      VALUE=SDOT(NDT(ISPIN),DDVO(IOFFVO),1,ICORE,1)
C
      FFMAT(IND1)=FFMAT(IND1)+TWO*FACT*VALUE
C
300   CONTINUE
C
1000  CONTINUE
C
      if(iprint.ge.1) then
c       write(6,6000)
6000   FORMAT(' Contribution of d D(P,q)/d B to d E(MBPT(2)/d B d In :',
     &        '\n   symmetry adapted nuclear magnetic moment',
     &        '    contribution')         
       do 400 i2pert=1,npert
        ind1=i1pert+ioffF+(i2pert+ioff-1)*nfp
        write(6,6001) i2pert,ffmat(ind1)
6001    FORMAT(13X,I10,13X,F12.8)
400    continue
      endif
C
      IF(.NOT.GIAO) THEN
C
C CALCULATE CONTRIBUTION TO MAGNETIC SUSCEPTIBILITY TENSOR, CURRENTLY ONLY
C AVAILABLE FOR CONVENTIONAL MBPT(2)
C
C LOOP OVER SPIN CASES
C
       DO 2000 ISPIN=1,1+IUHF
C
        IF(ISPIN.EQ.1) THEN
         IOFFOO=1
         IOFFVV=1
         IOFFVO=1
        ELSE
         IOFFOO=1+NDF1(1)
         IOFFVV=1+NDF2(1)
         IOFFVO=1+NDT(1)
        ENDIF
C
C LOOP OVER ALL PERTURBATIONS
C
        DO 1100 I2PERT=1,NPERTF
C
C READ IN DERIVATIVES OF OCCUPIED-OCCUPIED BLOCK OF FOCK MATRICES
C
         CALL GETLST(ICORE,I2PERT,1,1,IRREPX,185+ISPIN)
C
         IND1=I1PERT+IOFFF+(I2PERT+IOFFF-1)*3
C
         VALUE=SDOT(NDF1(ISPIN),DDOO(IOFFOO),1,ICORE,1)
C
         MSZ(IND1)=MSZ(IND1)+FACT*VALUE
C
1100    CONTINUE
C
C LOOP OVER ALL PERTURBATIONS
C
        DO 1200 I2PERT=1,NPERTF
C
C READ IN DERIVATIVES OF VIRTUAL-VIRTUAL BLOCK OF FOCK MATRICES
C
         CALL GETLST(ICORE,I2PERT,1,1,IRREPX,187+ISPIN)
C
         IND1=I1PERT+IOFFF+(I2PERT+IOFFF-1)*3
C
         VALUE=SDOT(NDF2(ISPIN),DDVV(IOFFVV),1,ICORE,1)
C
         MSZ(IND1)=MSZ(IND1)+FACT*VALUE
C
1200    CONTINUE
C
C PROCESS DERIVATIVES OF THE VIRTUAL-OCCUPIED BLOCK OF FOCK MATRICES
C
        DO 1300 I2PERT=1,NPERTF
C
         CALL GETLST(ICORE,I2PERT,1,1,IRREPX,179+ISPIN)
C
         IND1=I1PERT+IOFFF+(I2PERT+IOFFF-1)*3
C
         VALUE=SDOT(NDT(ISPIN),DDVO(IOFFVO),1,ICORE,1)
C
         MSZ(IND1)=MSZ(IND1)+TWO*FACT*VALUE
C
1300    CONTINUE
C
2000   CONTINUE
C
      ENDIF
C
      RETURN
C
      END
