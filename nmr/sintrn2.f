      SUBROUTINE SINTRN2(EVEC,AOINT,BUF1,RHF,NSTART,NEND,NOCC,
     &                   NVRT,ISPIN,IRREPX)
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL RHF
      INTEGER DIRPRD,DISZAO,DISZMO,AOPOP
C
      DIMENSION EVEC(1),AOINT(1),BUF1(1),NVRT(8),NOCC(8),
     &          NSTART(8),NEND(8)
C
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/AOPOPS/AOPOP(8)
      COMMON/AOOFST/INDOCC(8,2)   
      COMMON/SYMINF/NDUMMY,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/INFOT/NPASS1,NPASS2,NPASS3,NPASS4,
     &             NREAD1,NREAD2,NREAD3,NREAD4,
     &             TIME1L,TIME2L,TIME3L,TIME4L,
     &             TIME1S,TIME2S,TIME3S,TIME4S
      COMMON /TIMEINFO/ TIMEIN, TIMENOW, TIMETOT, TIMENEW 
C
      DATA AZERO,ONE/0.D0,1.D0/
C
      CALL TIMER(1)
C
      IOFF2=1
      IOFF3=1
      IOFF4=1
      IOFF5=1
C
C LOOP OVER ALL IRREPS
C
      DO 1000 IRREP1=1,NIRREP
C
       IRREP1R=DIRPRD(IRREP1,IRREPX)
       NAOI=AOPOP(IRREP1)
       NMOI=NVRT(IRREP1)
       NOCC1=NOCC(IRREP1)
       NSTARTI=NSTART(IRREP1R)
       NENDI=NEND(IRREP1R)
       NUMDIS=NMOI*(NENDI-NSTARTI+1)
C
C DETERMINE SIZE OF TWO-DIMENSIONAL INTEGRAL ARRAY
C
      DISZAO=0
      DO 10 IRREP2=1,NIRREP
       IF(IRREP1.NE.IRREP2) THEN
        DISZAO=DISZAO+AOPOP(IRREP2)*(AOPOP(IRREP2)+1)/2
       ENDIF
10    CONTINUE
C
C LOOP OVER ALL OCCUPIED ORBITALS
C
       IOFFC1=INDOCC(IRREP1,ISPIN)+NOCC1*NAOI
C
       DO 100 IOCC=NSTARTI,NENDI
C
C  TRANSFORM SECOND INDEX  (Mu I | SIGMA RHO) --> (P I | SIGMA RHO )
C
        CALL XGEMM('T','N',NMOI,DISZAO,NAOI,ONE,EVEC(IOFFC1),
     &             NAOI,AOINT(IOFF2),NAOI,AZERO,BUF1,NMOI)
C
        CALL TRANSP(BUF1,AOINT(IOFF3),DISZAO,NMOI)
C
        IOFF2=IOFF2+NAOI*DISZAO
        IOFF3=IOFF3+NMOI*DISZAO
C
100    CONTINUE
C
C  NOW WE HAVE THE (PI,SIGMA RHO) INTEGRALS ORDERED AS   X X P I 
C
C NOW TRANSFORM NEW LHS TO RS, SPIN CASE ISPIN,ISPIN
C
C LOOP OVER NUMBER OF DISTRIBUTION (I,P OF IRREP IRREP)
C
       DO 200 IDIS=1,NUMDIS
C
C LOOP OVER SECOND IRREP IRREP2
C
        DO 2000 IRREP2=1,NIRREP
C
         IF(IRREP2.EQ.IRREP1) GO TO 2000
C
         NAOI2=AOPOP(IRREP2)
         NMOI2=NVRT(IRREP2)
         NOCC2=NOCC(IRREP2)
C
         IX=1+NAOI2*NAOI2
         IOFFC2=INDOCC(IRREP2,ISPIN)+NOCC2*NAOI2
C
         CALL EXPND2(AOINT(IOFF4),BUF1,NAOI2)
C     
         CALL GHTRAN(BUF1,BUF1,EVEC,IOFFC2,IOFFC2,1,1,BUF1(IX),
     &               0,1,NAOI2,NAOI2,NMOI2,NMOI2)
C
         CALL SQUEZ2(BUF1,AOINT(IOFF5),NMOI2)
C
         IOFF4=IOFF4+NAOI2*(NAOI2+1)/2
         IOFF5=IOFF5+NMOI2*(NMOI2+1)/2
C
2000    CONTINUE
C
200    CONTINUE
C
1000  CONTINUE
C
      IOFFBUF=1
      DO 3000 IRREP1=1,NIRREP
       DO 4000 IRREP2=1,NIRREP
        IF(IRREP1.EQ.IRREP2) GO TO 4000 
        IOFFBUF=MAX(IOFFBUF,1+NVRT(IRREP1)*NVRT(IRREP2)*
     &              NVRT(IRREP2))
4000   CONTINUE
3000  CONTINUE
C
      CALL SDUMP2(AOINT,BUF1,BUF1(IOFFBUF),NSTART,NEND,IRREPX)
C
      CALL TIMER(1)
      TIME2S=TIME2S+TIMENEW
C
      RETURN
      END
