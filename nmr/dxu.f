      SUBROUTINE DXU(DDOO,DDVV,DDVO,DOO,DVV,DVO,UAI,UIA,SIJ,
     &               SAB,ICORE,MAXCOR,IUHF,ANTI)
C
C THIS ROUTINE CALCULATES THE U*D CONTRIBUTIONS TO THE DERIVATIVES
C OF THE DENSITY D(P,Q)^chi
C
C THE TERMS ARE :
C
C     SUM R U(PR) D(RQ) + SUM R D(PR) U(QR)
C
C THIS GIVES 
C
C   2 SUM R U(I,R) D(R,J)
C
C   2 SUM R U(A,R) D(R,B)
C
C     SUM R U(A,R) D(R,I) + SUM R D(A,R) U(I,R)
C
C THERE ARE TWO SPIN CASES FOR UHF/ROHF, ONE FOR RHF
C
C FOR ANTI TRUE THERE ARE SOME MODIFICATIONS TO BE CONSIDERED
C
CEND
C
C CODED OCT/91 JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER POP,VRT,DIRPRD
      LOGICAL ANTI
C
      DIMENSION DDOO(1),DDVV(1),DDVO(1),DOO(1),DVV(1),DVO(1),
     &          UAI(1),UIA(1),SIJ(1),SAB(1),ICORE(MAXCOR)
C
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/DSYM/IRREPX,IPERT,NDT(2),NDF1(2),NDF2(2),
     &            IOFFIJ(8,2),IOFFAB(8,2),IOFFAI(8,2) 
C
      DATA ONE,ONEM,TWO,TWOM /1.D0,-1.D0,2.D0,-2.D0/
C 
C     LOOP OVER SPIN CASES
C
      DO 1000 ISPIN=1,IUHF+1
C
       IF(ISPIN.EQ.1) THEN
C
        IDOO=1
        IDVV=1
        IDVO=1 
        IDDOO=1
        IDDVV=1
        IDDVO=1
        IUAI=1
        IUIA=1
        ISIJ=1
        ISAB=1
C
       ELSE
C
        IDOO=1+NF1(1)
        IDVV=1+NF2(1)
        IDVO=1+NT(1)
        IDDOO=1+NDF1(1)
        IDDVV=1+NDF2(1)
        IDDVO=1+NDT(1)
        IUAI=1+NDT(1)
        IUIA=1+NDT(1)
        ISIJ=1+NDF1(1)
        ISAB=1+NDF1(1)
C
       ENDIF
C
C   D(I,M) S(J,M)
C
       IOFFS=ISIJ
       IOFFDD=IDDOO
C
       FACT=TWO
       IF(ANTI) FACT=TWOM
C    
       DO 100 IRREPR=1,NIRREP
C
        IRREPL=DIRPRD(IRREPR,IRREPX)
C
        IOFFD=IDOO
        DO 101 IRREP1=1,IRREPL-1
         IOFFD=IOFFD+POP(IRREP1,ISPIN)*POP(IRREP1,ISPIN)
101     CONTINUE
C
        NOCCI=POP(IRREPL,ISPIN)
        NOCCJ=POP(IRREPR,ISPIN)
        NOCCM=NOCCI
C
C THE TERM IS D(I,M) S(M,J)
C
C  IN CASE OF ANTI THIS SHOULD
C   
C     TWO D(I,M) U(J,M) = - TWO D(I,M) U(M,J)
C
        CALL XGEMM('N','N',NOCCI,NOCCJ,NOCCM,FACT,DOO(IOFFD),NOCCI,
     &             SIJ(IOFFS),NOCCM,ONE,DDOO(IOFFDD),NOCCI)
C
        IOFFS=IOFFS+NOCCM*NOCCJ
        IOFFDD=IOFFDD+NOCCI*NOCCJ
C
100    CONTINUE
C
C   D(I,E) U(J,E)
C
       IOFFU=IUIA
       IOFFDD=IDDOO
C
       DO 200 IRREPR=1,NIRREP
C
        IRREPL=DIRPRD(IRREPX,IRREPR)
C
        IOFFD=IDVO
        DO 201 IRREP1=1,IRREPL-1
         IOFFD=IOFFD+POP(IRREP1,ISPIN)*VRT(IRREP1,ISPIN)
201     CONTINUE
C
        NOCCI=POP(IRREPL,ISPIN)
        NOCCJ=POP(IRREPR,ISPIN)
        NVRTE=VRT(IRREPL,ISPIN)
C
        CALL XGEMM('T','N',NOCCI,NOCCJ,NVRTE,TWO,DVO(IOFFD),NVRTE,
     &             UIA(IOFFU),NVRTE,ONE,DDOO(IOFFDD),NOCCI)
C
        IOFFU=IOFFU+NVRTE*NOCCJ
        IOFFDD=IOFFDD+NOCCJ*NOCCI
C
200    CONTINUE
C
C    D(A,E) S(B,E)
C
C FOR A DISCUSSION oF THE SIGN, COMPARE TO D(I,M) S(J,M)
C
       IOFFS=ISAB
       IOFFDD=IDDVV
C
       DO 300 IRREPR=1,NIRREP
C
        IRREPL=DIRPRD(IRREPR,IRREPX)
C
        IOFFD=IDVV
        DO 301 IRREP1=1,IRREPL-1
         IOFFD=IOFFD+VRT(IRREP1,ISPIN)*VRT(IRREP1,ISPIN)
301     CONTINUE
C
        NVRTA=VRT(IRREPL,ISPIN)
        NVRTB=VRT(IRREPR,ISPIN)
        NVRTE=NVRTA
C
        CALL XGEMM('N','N',NVRTA,NVRTB,NVRTE,FACT,DVV(IOFFD),NVRTA,
     &             SAB(IOFFS),NVRTE,ONE,DDVV(IOFFDD),NVRTA)
C
        IOFFS=IOFFS+NVRTE*NVRTB
        IOFFDD=IOFFDD+NVRTA*NVRTB
C
300    CONTINUE
C
C   D(A,M) U(B,M)
C
       IOFFDD=IDDVV
C       
       DO 400 IRREPR=1,NIRREP
C
        IRREPL=DIRPRD(IRREPX,IRREPR)
C
        IOFFD=IDVO
        IOFFU=IUAI
        DO 401 IRREP1=1,IRREPL-1
         IOFFD=IOFFD+POP(IRREP1,ISPIN)*VRT(IRREP1,ISPIN)
         IOFFU=IOFFU+POP(IRREP1,ISPIN)*
     &               VRT(DIRPRD(IRREP1,IRREPX),ISPIN)
401     CONTINUE
C
        NVRTA=VRT(IRREPL,ISPIN)
        NVRTB=VRT(IRREPR,ISPIN)
        NOCCM=POP(IRREPL,ISPIN)
C
        CALL XGEMM('N','T',NVRTA,NVRTB,NOCCM,TWO,DVO(IOFFD),NVRTA,
     &             UAI(IOFFU),NVRTB,ONE,DDVV(IOFFDD),NVRTA)
C
        IOFFDD=IOFFDD+NVRTA*NVRTB
C
400    CONTINUE
C
C    D(A,M) S(I,M)
C
C FOR ANTI EQUALS TRUE, THIS GIVES - D(A,M) S(M,I)
C
       IOFFS=ISIJ
       IOFFDD=IDDVO
C
       FACT=ONE
       IF(ANTI) FACT=ONEM
C
       DO 500 IRREPR=1,NIRREP
C
        IRREPL=DIRPRD(IRREPR,IRREPX)
C
        IOFFD=IDVO
        DO 501 IRREP1=1,IRREPL-1
         IOFFD=IOFFD+POP(IRREP1,ISPIN)*VRT(IRREP1,ISPIN)
501     CONTINUE
C
        NVRTA=VRT(IRREPL,ISPIN)
        NOCCI=POP(IRREPR,ISPIN)
        NOCCM=POP(IRREPL,ISPIN)
C
        CALL XGEMM('N','N',NVRTA,NOCCI,NOCCM,FACT,DVO(IOFFD),NVRTA,
     &             SIJ(IOFFS),NOCCM,ONE,DDVO(IOFFDD),NVRTA)
C
        IOFFS=IOFFS+NOCCM*NOCCI
        IOFFDD=IOFFDD+NVRTA*NOCCI
C
500    CONTINUE
C
C   D(A,E) U(I,E)
C
       IOFFU=IUIA
       IOFFDD=IDDVO 
C
       DO 600 IRREPR=1,NIRREP
C
        IRREPL=DIRPRD(IRREPX,IRREPR)
C
        IOFFD=IDVV
        DO 601 IRREP1=1,IRREPL-1
         IOFFD=IOFFD+VRT(IRREP1,ISPIN)*VRT(IRREP1,ISPIN)
601     CONTINUE
C
        NVRTA=VRT(IRREPL,ISPIN)
        NOCCI=POP(IRREPR,ISPIN)
        NVRTE=NVRTA
C
        CALL XGEMM('N','N',NVRTA,NOCCI,NVRTE,ONE,DVV(IOFFD),NVRTA,
     &             UIA(IOFFU),NVRTE,ONE,DDVO(IOFFDD),NVRTA)
C
        IOFFU=IOFFU+NVRTE*NOCCI
        IOFFDD=IOFFDD+NVRTA*NOCCI
C
600    CONTINUE
C
C
C    D(M,I) U(A,M) 
C
C FOR IMAGINARY PERTURBATIONS THE COMPLEX CONJUGATE
C OF U(A,M) IS REQUIRED CHANGE SIGN TO ONEM
C 
       IOFFU=IUAI
       IOFFD=IDOO
       IOFFDD=IDDVO
C
       DO 700 IRREPR=1,NIRREP
C
        IRREPL=DIRPRD(IRREPR,IRREPX)
C
        NVRTA=VRT(IRREPL,ISPIN)
        NOCCI=POP(IRREPR,ISPIN)
        NOCCM=POP(IRREPR,ISPIN)
C
        CALL XGEMM('N','N',NVRTA,NOCCI,NOCCM,FACT,UAI(IOFFU),NVRTA,
     &             DOO(IOFFD),NOCCM,ONE,DDVO(IOFFDD),NVRTA)
C
        IOFFU=IOFFU+NOCCM*NVRTA
        IOFFD=IOFFD+NOCCM*NOCCI
        IOFFDD=IOFFDD+NOCCI*NVRTA
C
700    CONTINUE
C
C   D(E,I) S(A,E)
C
C FOR IMAGINARY PERTURBATIONS, THE COMPLEX CONJUGATE OF 
C S(A,E) IS REQUIRED, CHANGE FACTOR TO ONEM
C
       IOFFS=ISAB
       IOFFD=IDVO
       IOFFDD=IDDVO
C
       DO 800 IRREPR=1,NIRREP
C
        IRREPL=DIRPRD(IRREPX,IRREPR)
C
        NVRTA=VRT(IRREPL,ISPIN)
        NOCCI=POP(IRREPR,ISPIN)
        NVRTE=VRT(IRREPR,ISPIN)
C
        CALL XGEMM('N','N',NVRTA,NOCCI,NVRTE,FACT,SAB(IOFFS),NVRTA,
     &             DVO(IOFFD),NVRTE,ONE,DDVO(IOFFDD),NVRTA)
C
        IOFFS=IOFFS+NVRTE*NVRTA
        IOFFD=IOFFD+NVRTE*NOCCI
        IOFFDD=IOFFDD+NVRTA*NOCCI
C
800    CONTINUE
C
1000  CONTINUE
C
      RETURN
      END
