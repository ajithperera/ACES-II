      SUBROUTINE DSYMMET(IRREPX,A,B,NUM,IOFFPQ,ANTI)
C
C   THIS ROUTINE (ANTI)SYMMETRIZES A GIVEN MATRIX A
C   WHICH TRANSFORMS AS IRREP IRREPX
C
C     A(PQ) = 1/2 ( A(PQ)+A(QP))     ANTI=.FALSE.
C 
C     A(PQ) = 1/2 ( A(PQ)-A(QP))     ANTI=.TRUE. 
C
C   WHERE A IS A SYMMETRY PACKED MATRIX AND
C   NUM THE CORRESPONDING POPULATION VECTOR
C   IOFFPQ IS AN OFFSET ARRAY.
C
C   THE (ANTI)SYMMETRIZED ARRAY IS RETURNED IN A,
C   B IS ONLY USED AS A SCRATCH ARRAY. 
C
CEND
C
C CODED APRIL/91 JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL ANTI
      INTEGER DIRPRD
      DIMENSION A(1),B(1),NUM(8),IOFFPQ(8)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      DATA HALF /0.5D+0/
C
      IOFFR=0
      DO 1000 IRREPR=1,NIRREP
C
       IRREPL=DIRPRD(IRREPX,IRREPR)
C
       IOFFL=IOFFPQ(IRREPL)-1
C
       NUML=NUM(IRREPL)
       NUMR=NUM(IRREPR)
C
       IF(ANTI) THEN
C
        DO 90 I=1,NUMR
        DO 90 J=1,NUML
         B(IOFFR+(I-1)*NUML+J)=HALF*(A(IOFFR+(I-1)*NUML+J)
     &                         -A(IOFFL+(J-1)*NUMR+I))
90      CONTINUE
C
       ELSE
C
        DO 100 I=1,NUMR
        DO 100 J=1,NUML
         B(IOFFR+(I-1)*NUML+J)=HALF*(A(IOFFR+(I-1)*NUML+J)
     &                         +A(IOFFL+(J-1)*NUMR+I))
100     CONTINUE
C
       ENDIF
C
       IOFFR=IOFFR+NUMR*NUML
1000  CONTINUE
C
C  COPY B BACK TO A
C
      DO 2000 I=1,IOFFR
       A(I)=B(I)
2000  CONTINUE
C
      RETURN
      END
