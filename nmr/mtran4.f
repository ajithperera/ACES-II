      SUBROUTINE MTRAN4(A,NROW,NCOL,MAXCOR)
C
C GENERAL ROUTINE FOR TRANSPOSITION OF A GENERAL RECTANGULAR
C  MATRIX.  IT IS DONE AS A STRAIGHTFOWARD A->B TRANSFORMATION
C  WHEN SUFFICIENT MEMORY IS AVAILABLE.  IF NOT, IT CHECKS TO
C  SEE IF IT CAN DO THE TRANSPOSITION BY FIRST SQUARING OUT THE
C  A MATRIX, DOING AN IN-PLACE SQUARE MATRIX TRANSPOSITION AND
C  THEN COMPRESSING IT TO REGULAR FORM
C
C       A - MATRIX TO BE TRANSPOSED
C    NROW - ROW DIMENSION OF *INPUT* MATRIX
C    NCOL - COLUMN DIMENSION OF *INPUT* MATRIX
C  MAXCOR - TOTAL AVAILABLE CORE ABOVE A(NROW*NCOL)
C
CEND
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      DIMENSION A(1)
      NSIZE=NROW*NCOL
      IF(NSIZE.LT.MAXCOR/IINTFP)THEN
       CALL TRANSP(A,A(NROW*NCOL+1),NCOL,NROW)
       CALL SCOPY (NROW*NCOL,A(NROW*NCOL+1),1,A,1)
       RETURN
      ELSE
C
C ALTERNATIVE ALGORITHM.  FIRST CHECK TO SEE IF THERE IS ENOUGH
C  CORE TO DO IT.
C
       MAXDIM=MAX(NROW,NCOL)
       NEXTRA=MAXDIM*MAXDIM-NSIZE
       IF(NEXTRA.GT.MAXCOR/IINTFP)THEN
        WRITE(6,1000)
1000    FORMAT(T3,'@MTRAN4-F, Insufficient core for transposition.')
        CALL ERREX
       ENDIF
C
C OK.  WE CAN DO IT.
C
       CALL ZERO(A(NROW*NCOL+1),NEXTRA)
C
       IF(NROW.GT.NCOL)THEN
C
C CASE I.  NROW > NCOL.  WE HAVE TO ADD ZERO COLUMNS, CALL MTRAN2 AND
C          THEN SQUEEZE THE ZEROS BACK OUT.
C
        CALL MTRAN2(A,MAXDIM)
        IOFF1=1
        IOFF2=1
        DO 10 ICOL=1,NCOL
         CALL SCOPY(NROW,A(IOFF1),1,A(IOFF2),1)
         IOFF1=IOFF1+MAXDIM
         IOFF2=IOFF2+NROW
10      CONTINUE
C
       ELSE
C
C CASE II.  NCOL > NROW.  WE HAVE TO ADD ZERO ROWS.  A BIT TRICKIER THAN
C           CASE I.
C
        IOFF1=NCOL*(NROW-1)+1
        IOFF2=MAXDIM*(MAXDIM-1)+1
        DO 20 ICOL=NCOL,1,-1
         CALL SCOPY(NROW,A(IOFF1),1,A(IOFF2),1)
         CALL ZERO(A(IOFF1),NROW)
         IOFF1=IOFF1-NROW
         IOFF2=IOFF2-MAXDIM
20      CONTINUE
        CALL MTRAN2(A,MAXDIM)
C
       ENDIF
C
      ENDIF
C
      RETURN
      END
