      SUBROUTINE READU(UAIA,UAIB,UIAA,UIAB,SIJA,SIJB,
     &                 SABA,SABB,IP,IRREPX,IUHF,STERM,ANTI)
C
C THIS ROUTINE READS THE CPHF COEFFICIENTS FROM THE
C GAMLAM FILE
C
CEND
C
C CODED JUNE/91 JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL STERM,ANTI
C
      DIMENSION UAIA(1),UAIB(1),UIAA(1),UIAB(1),
     &          SIJA(1),SIJB(1),SABA(1),SABB(1)
C
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/CPHF/LENUAI(2),LENSIJ(2),LENSAB(2),
     &            LUAI1,LUAI2,LSIJ1,LSIJ2,LSAB1,
     &            LSAB2,LSAI1,LSAI2
C
      DATA  HALFM,ONEM,ONE /-0.5D0,-1.D0,1.D0/
C
      IF(ANTI) THEN
       FACT=ONE
      ELSE
       FACT=ONEM
      ENDIF
C
      CALL GETLST(UAIA,IP,1,1,IRREPX,LUAI1)
      IF(IUHF.EQ.1) THEN
       CALL GETLST(UAIB,IP,1,1,IRREPX,LUAI2)
      ENDIF
C
C FOR DERIVATIVES WITH RESPECT TO NUCLEAR COORDINATES
C READ ALSO THE OVERLAP DERIVATIVES
C
      IF(STERM) THEN
       CALL GETLST(SIJA,IP,1,1,IRREPX,LSIJ1)
       CALL SSCAL(LENSIJ(1),HALFM,SIJA,1)
       CALL GETLST(SABA,IP,1,1,IRREPX,LSAB1)
       CALL SSCAL(LENSAB(1),HALFM,SABA,1)
       CALL GETLST(UIAA,IP,1,1,IRREPX,LSAI1)
       IF(.NOT.ANTI) CALL VMINUS(UIAA,LENUAI(1))
       IF(IUHF.EQ.1) THEN
        CALL GETLST(SIJB,IP,1,1,IRREPX,LSIJ2)
        CALL SSCAL(LENSIJ(2),HALFM,SIJB,1)
        CALL GETLST(SABB,IP,1,1,IRREPX,LSAB2)
        CALL SSCAL(LENSAB(2),HALFM,SABB,1)
        CALL GETLST(UIAB,IP,1,1,IRREPX,LSAI2)
        IF(.NOT.ANTI) CALL VMINUS(UIAB,LENUAI(2))
       ENDIF
      ELSE
       CALL IZERO(SIJA,IINTFP*LENSIJ(1))
       CALL IZERO(SABA,IINTFP*LENSAB(1))
       CALL IZERO(UIAA,IINTFP*LENUAI(1))
       IF(IUHF.EQ.1) THEN
        CALL IZERO(SIJB,IINTFP*LENSIJ(2))
        CALL IZERO(SABB,IINTFP*LENSAB(2))
        CALL IZERO(UIAB,IINTFP*LENUAI(2))
       ENDIF
      ENDIF
C
C CONTRUCT THE U(I,A) COEFFICIENTS : U(I,A) = FACT*(S(A,I)+U(A,I))
C
C  WITH FACT= -1 FOR REAL PERTURBATIONS AND FACT=1 FOR IMAGINARY
C  PERTURBATIONS
C
      CALL SAXPY(LENUAI(1),FACT,UAIA,1,UIAA,1)
      IF(IUHF.EQ.1) THEN
       CALL SAXPY(LENUAI(2),FACT,UAIB,1,UIAB,1)
      ENDIF
C
      RETURN 
C
      END
