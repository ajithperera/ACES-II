      SUBROUTINE LOADS2(PCMO,W,W2,BUF,IBUF,ISYMAO,NBAS,
     &                  NSTART,NEND,ISIZE,ISIZT,IOFFAO,IOFFI,
     &                  NSIZE,ILNBUF,IPERT,ISPIN,IRREPX)
C
C THIS ROUTINE LOADS AO INTEGRALS FROM
C THE INTEGRAL FILE IIJJ AND TRANSFORMS THE FIRST
C INDEX (PARTIAL TRANSFORMATION ONLY)
C
CEND
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL LAST
      CHARACTER*80 FNAME
      INTEGER DIRPRD,AND,POP,VRT
C
      DIMENSION IBUF(ILNBUF),BUF(ILNBUF),W(1),W2(1),PCMO(1)
      DIMENSION NBAS(8),NSTART(8),NEND(8),ISIZE(8),IOFFAO(8),IOFFI(8)
      DIMENSION ISIZT(8,8)
      DIMENSION ISYMAO(100),IOFFPC(8,2) 
C
      COMMON/MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMINF/NDUMMY,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYM/POP(8,2),VRT(8,2),NDUM(6)
      COMMON/INFOT/NPASS1,NPASS2,NPASS3,NPASS4,
     &             NREAD1,NREAD2,NREAD3,NREAD4,
     &             TIME1L,TIME2L,TIME3L,TIME4L,
     &             TIME1S,TIME2S,TIME3S,TIME4S
      COMMON /TIMEINFO/ TIMEIN, TIMENOW, TIMETOT, TIMENEW 
C
      DATA HALF/0.5D0/
C
      IUPKI(IX)=AND(IX,IALONE)
      IUPKJ(IX)=AND(ISHFT(IX,-IBITWD),IALONE)
      IUPKK(IX)=AND(ISHFT(IX,-2*IBITWD),IALONE)
      IUPKL(IX)=AND(ISHFT(IX,-3*IBITWD),IALONE)
      INDXT(I,J)=J+(I*(I-1))/2
      INDXF(I,J,N)=I+N*(J-1)
C
      CALL TIMER(1)
C
      IOFF=1
      DO 11 IRREPR=1,NIRREP
       IRREPL=DIRPRD(IRREPR,IRREPX)
       IOFFPC(IRREPR,1)=IOFF
       IOFF=IOFF+NBAS(IRREPL)*POP(IRREPR,1)
11    CONTINUE
C
      LAST=.TRUE.
      LUINT=30
      CALL GFNAME('IIJJ    ',FNAME,ILENGTH)
      OPEN(UNIT=LUINT,FILE=FNAME(1:ILENGTH),FORM='UNFORMATTED',
     &     ACCESS='SEQUENTIAL',STATUS='OLD') 
      CALL LOCATE(LUINT,'TWOELSUP')
C
      CALL ZERO(W,NSIZE)
C
C READ IN INTEGRALS
C
      NAOBUF=0
      NAOINT=0
1     READ(LUINT) BUF,IBUF,NUT
      NAOBUF=NAOBUF+1
      DO 10 INT=1,NUT
       X=BUF(INT)
       ITMP=IBUF(INT)
C
C  X VALUE, IX,JX,KX,LX INDICES  
C
       IX=IUPKI(ITMP)
       JX=IUPKJ(ITMP)
       KX=IUPKK(ITMP)
       LX=IUPKL(ITMP)
C
C GET IRREP OF INTEGRAL
C
       IRREPX1=ISYMAO(IX)
       IRREPX1R=DIRPRD(IRREPX1,IRREPX)
       IRREPX2=ISYMAO(KX)
       IRREPX2R=DIRPRD(IRREPX2,IRREPX)
C
C  OFFSET WITHIN BASIS FUNCTIONS
C
       IOFF1=IOFFAO(IRREPX1)
       IOFF2=IOFFAO(IRREPX2)
C
C  INDICES WITHIN IRREP
C
       IX=IX-IOFF1
       JX=JX-IOFF1
       KX=KX-IOFF2
       LX=LX-IOFF2
C
       I=MAX(IX,JX)
       J=MIN(IX,JX)
       K=MAX(KX,LX)
       L=MIN(KX,LX)
C
C  OFFSET FOR CMO WITHIN IRREPX1 AND IRREPX2
C
       IOFFC1=IOFFPC(IRREPX1R,ISPIN)
       IOFFC2=IOFFPC(IRREPX2R,ISPIN)
C
C  NUMBER OF OCCUPIED ORBITALS WITHIN IRREPX1 AND IRREPX2
C
       NSTART1=NSTART(IRREPX1R)
       NSTART2=NSTART(IRREPX2R)
       NEND1=NEND(IRREPX1R)
       NEND2=NEND(IRREPX2R)
C
C  NUMBER OF BASIS FUNCTIONS WITHIN IRREPX 
C
       NBASX1=NBAS(IRREPX1)
       NBASX2=NBAS(IRREPX2)
C
C  OFFSET WITHIN W
C
       IOFFW1=IOFFI(IRREPX1)
       IOFFW2=IOFFI(IRREPX2)
C
       ISIZEX1=ISIZE(IRREPX1)
       ISIZEX2=ISIZE(IRREPX2)
C
C THERE ARE A TOTAL OF EIGHT CONTRIBUTIONS
C
C   (IJ|KL) (JI|KL) (IJ|LK) (JI|LK)
C   (KL|IJ) (KL|JI) (LK|IJ) (LK|JI)
C
C HOWEVER, WE NEED ONLY FOUR SINCE WE STORE THEM AS
C
C   NU, SIGMA >= RHO,  I   
C
C FIRST ELEMENT:     TRANSFORM INDEX L TO MO BASIS
C
        IND=INDXT(I,J)+ISIZT(IRREPX2,IRREPX1)
        X1=X
        IF(K.EQ.L) X1=X1*HALF 
        DO 9001 IOCC=NSTART2,NEND2
         W2(IOCC)=PCMO(IOFFC2+LX-1+(IOCC-1)*NBASX2)*X1 
9001    CONTINUE 
C
C INCREMENT L(KX,IX,JX;I)
C
        DO 9002 IOCC=NSTART2,NEND2
         IADR=IOFFW2+KX+NBASX2*(IND-1)+ISIZEX2*(IOCC-NSTART2)
         W(IADR)=W(IADR)+W2(IOCC)
9002    CONTINUE
C
C SECOND ELEMENT:     TRANSFORM INDEX K TO MO BASIS
C
        IND=INDXT(I,J)+ISIZT(IRREPX2,IRREPX1) 
        DO 9003 IOCC=NSTART2,NEND2
         W2(IOCC)=PCMO(IOFFC2+KX-1+(IOCC-1)*NBASX2)*X1
9003    CONTINUE 
C
C INCREMENT L(LX,JX,IX;I)
C
        DO 9004 IOCC=NSTART2,NEND2
         IADR=IOFFW2+LX+NBASX2*(IND-1)+ISIZEX2*(IOCC-NSTART2)
         W(IADR)=W(IADR)+W2(IOCC)
9004    CONTINUE
C
C THIRD ELEMENT:     TRANSFORM INDEX J TO MO BASIS
C
        IND=INDXT(K,L)+ISIZT(IRREPX1,IRREPX2)
        X2=X
        IF(I.EQ.J) X2=X2*HALF
        DO 9005 IOCC=NSTART1,NEND1
         W2(IOCC)=PCMO(IOFFC1+JX-1+(IOCC-1)*NBASX1)*X2
9005    CONTINUE 
C
C INCREMENT L(IX,KX,LX;I)
C
        DO 9006 IOCC=NSTART1,NEND1
         IADR=IOFFW1+IX+NBASX1*(IND-1)+ISIZEX1*(IOCC-NSTART1)
         W(IADR)=W(IADR)+W2(IOCC)
9006    CONTINUE
C
C FOURTH ELEMENT:     TRANSFORM INDEX I TO MO BASIS
C
        IND=INDXT(K,L)+ISIZT(IRREPX1,IRREPX2)
        DO 9007 IOCC=NSTART1,NEND1
         W2(IOCC)=PCMO(IOFFC1+IX-1+(IOCC-1)*NBASX1)*X2
9007    CONTINUE 
C
C INCREMENT L(JX,LX,KX;I)
C
        DO 9008 IOCC=NSTART1,NEND1
         IADR=IOFFW1+JX+NBASX1*(IND-1)+ISIZEX1*(IOCC-NSTART1)
         W(IADR)=W(IADR)+W2(IOCC)
9008    CONTINUE
10    CONTINUE
C
      IF(NUT.EQ.ILNBUF) THEN
       NAOINT=NAOINT+NUT
       GO TO 1
      ENDIF
      IF(NUT.EQ.-1) NUT=0
C
      NAOINT=NAOINT+NUT
      IF(LAST) THEN
       CLOSE(UNIT=LUINT,STATUS='KEEP')
      ELSE
       REWIND(LUINT)
       CALL LOCATE(LUINT,'TWOELSUP') 
      ENDIF
      NREAD2=NAOINT
      NPASS2=NPASS2+1
      CALL TIMER(1)
      TIME2L=TIME2L+TIMENEW      
C      
      RETURN
900   WRITE(6,1000)
1000  FORMAT(T3,'@LOADS2-F, Unexpected end-of-file on integral file.')
      CALL ERREX
      RETURN
901   WRITE(6,1000)
1001  FORMAT(T3,'@LOADS2-F, I/O error on unit ',I5,'.')
      CALL ERREX
      RETURN
      END
