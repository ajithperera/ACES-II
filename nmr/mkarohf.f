      SUBROUTINE MKAROHF(IRREP,AMAT,SCRATCH,FAB,FIJ,FAJ,
     &                   LENAB,LENIJ,LENAJ)
C
C   THIS ROUTINE FORMS THE A MATRIX REQUIRED FOR THE SOLUTION 
C   OF THE CPHF EQUATION FOR GENERAL UHF WAVEFUNCTION
C
C   NOTE, THAT ONLY ONE BLOCK OF THE A MATRIX IS COMPUTED.
C   THE IRREP OF THIS BLOCK CORRESPONDS TO THE IRREP OF THE
C   PERTURBATION. 
C
C   THE FORMULAS ARE GIVEN BY
C
C   A(AI,BJ) = <AB//IJ> + <AJ//IB> + DEL(IJ)*F(AB) - DEL(AB)*F(IJ)  AA-BLOCK
C
C   A(ai,bj) = <ab//ij> + <aj//ib> + DEL(ij)*f(ab) - DEL(ab)*f(ij)  BB-BLOCK
C
C   A(AI,bj) = 2  <Ab//Ij> + DEL(Ib)*f(Aj)                          AB-BLOCK
C
C   A(ai,BJ) = 2  <aB//iJ> + DEL(Aj)*f(Ib)                          BA-BLOCK
C                                                      (TRANPOSE OF AB-BLOCK)
C
C IRREP IS THE IRREP OF THE PERTURBATION CONSIDERED  
C AMAT HOLDS ALL FOUR PARTS OF THE A MATRIX 
C SCRATCH IS A SCRATCH ARRAY REQUIRED FOR CONTRUCTING A
C FIJ,FAB,FAI ARE ARRAYS HOLDING FOCK MATRIX ELEMENTS
CEND
C
C CODED MARCH/91 JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DISSYW,DIRPRD,POP,VRT,A,B
      DIMENSION AMAT(1),SCRATCH(1)
      DIMENSION FAB(LENAB,2),FIJ(LENIJ,2),FAJ(LENAJ)
      COMMON/INFO/NOCCO(2),NVRTO(2)
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NJUNK(6)
C
      DATA ONE,ONEM,TWO /1.0D+0,-1.0D+0,2.0D+0/
C
      INDX2(I,J,N)=I+(J-1)*N
C
C  NOW PICK UP THE FOCK MATRIX ELEMENTS  
C
      CALL GETLST(FIJ(1,1),1,1,1,3,91)
      CALL GETLST(FIJ(1,2),1,1,1,4,91)
      CALL GETLST(FAB(1,1),1,1,1,3,92)
      CALL GETLST(FAB(1,2),1,1,1,4,92)
      CALL GETLST(FAJ,1,1,1,4,93)
C
C     FORM FIRST THE AA AND BB-BLOCK
C
C  LOOP OVER SPINS
C
      IOFFA=1
      DO 1000 ISPIN=1,2
C
C FIRST READ IN THE INTEGRALS <AB//IJ> (ORDERING AI,BJ)
C THEY ARE STORED ON LIST 19
C
      LISTW=18+ISPIN
      NUMSYW=IRPDPD(IRREP,ISYTYP(2,LISTW))
      DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
C
      CALL GETLST(AMAT(IOFFA),1,NUMSYW,2,IRREP,LISTW)
C
C NOW ADD THE INTEGRALS <AJ//BI> (ORDERING AI,BJ)
C THEY ARE STORED ON LIST 23, HOWEVER TAKE CARE OF
C THE SIGN, SINCE THEY ARE ACTUALLY <JA//BI>
C
      LISTW=22+ISPIN
      NUMSYW=IRPDPD(IRREP,ISYTYP(2,LISTW))
      DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
C
      CALL GETLST(SCRATCH,1,NUMSYW,2,IRREP,LISTW)
      CALL SAXPY(NUMSYW*DISSYW,ONEM,SCRATCH,1,AMAT(IOFFA),1)
C
C  Now add the fock matrix contributions.  This first section covers
C  THE ALL ALPHA AND ALL BETA PARTS.
C
      IOFF1=0
C
C LOOP OVER IRREPS (IRREP1) OF OCCUPIED ORBITALS
C 
      DO 300 IRREP1=1,NIRREP
C
C  IRREP2 DENOTES IRREP OF VIRTUAL ORBITALS
C
        IRREP2=DIRPRD(IRREP,IRREP1)
        NOCC=POP(IRREP1,ISPIN)
        NVRT=VRT(IRREP2,ISPIN)
        IOFAB=1
        DO 303 IRREPAB=1,IRREP2-1
         IOFAB=IOFAB+VRT(IRREPAB,ISPIN)*VRT(IRREPAB,ISPIN)
303     CONTINUE
C
        IOFIJ=1
        DO 304 IRREPIJ=1,IRREP1-1
         IOFIJ=IOFIJ+POP(IRREPIJ,ISPIN)*POP(IRREPIJ,ISPIN)
304     CONTINUE
C
        IOFF2=IOFFA+DISSYW*IOFF1+IOFF1
C
C  loop over occupied orbitals i and j
C
        DO 301 I=1,NOCC
C
C  loop over virtual orbitals b
C
          DO 302 B=1,NVRT
C
C  GET THE OFFSETS OF f(AB) AND A(AI,BJ). 
C
            ISTART=IOFF2+(INDX2(B,I,NVRT)-1)*DISSYW+(I-1)*NVRT
C
            IOFFAB=IOFAB+(B-1)*NVRT
C
C   ADD FOCK MATRIX ELEMENTS f(A,B) FOR FIXED B.
C
            CALL SAXPY(NVRT,ONE,FAB(IOFFAB,ISPIN),1,AMAT(ISTART),1)
C
C END OF LOOP OVER ALL INDICES.
C
  302     CONTINUE
  301   CONTINUE
C
C NOW ADD IN THE CONTRIBUTION FROM f(I,J).
C
C LOOP OVER THE VIRTUAL ORBITALS A AND B.
C
        DO 311 A=1,NVRT
C
C NOW LOOP OVER THE OCCUPIED ORBITAL I.
C
          DO 312 J=1,NOCC
C
C CALCULATE THE OFFSETS OF f(i,j) ANFD A(AI,BJ).
C
            ISTART=IOFF2+(INDX2(A,J,NVRT)-1)*DISSYW+(A-1)
            IOFFIJ=IOFIJ+(J-1)*NOCC
C
C SUBTRACT THE FOCK MATRIX ELEMENTS f(i,j) FOR FIXED j.
C
            CALL SAXPY(NOCC,ONEM,FIJ(IOFFIJ,ISPIN),1,
     &                 AMAT(ISTART),NVRT)
C
  312     CONTINUE
  311   CONTINUE
C
        IOFF1=IOFF1+NOCC*NVRT
300   CONTINUE
C
C  UPDATE THE OFFSET FOR THE A MATRIX PART
C
      IOFFA=IOFFA+NUMSYW*DISSYW
C
1000  CONTINUE
C
C   FORM NOW THE AB BLOCK (THE BA BLOCK IS THE TRANSPOSED AS LONG
C   AS WE NOT SCALE WITH THE ORBITAL ENERGY DENOMINATOR
C
      LISTW=18
      NUMSYW=IRPDPD(IRREP,ISYTYP(2,LISTW))
      DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
C
      CALL GETLST(AMAT(IOFFA),1,NUMSYW,2,IRREP,LISTW)
      CALL SSCAL(NUMSYW*DISSYW,TWO,AMAT(IOFFA),1)
C
C  NOW ADD IN  THE CONTRIBUTION FROM THE FOCK MATRIX.
C
      IOFF1=0
C
C  LOOP OVER IRREPS OF OCCUPIED BETA (VIRTUAL ALPHA) ORBITALS
C
      DO 400 IRREP1=1,NIRREP
C
C IRREP2 IS IRREP OF OCCUPIED ALPHA AND VIRTUAL BETA ORBITALS
C
       IRREP2=DIRPRD(IRREP1,IRREP)
       IOFF3=0
C
       DO 403 IRREP3=1,IRREP2-1
        IRREP4=DIRPRD(IRREP,IRREP3)
        IOFF3=IOFF3+POP(IRREP3,1)*VRT(IRREP4,1)
403    CONTINUE
C
C WE NEED THE IRREP1 BLOCK OF f(a,j)
C 
       IOFAJ=1
       DO 404 IRREP3=1,IRREP1-1
        IOFAJ=IOFAJ+POP(IRREP3,2)*VRT(IRREP3,2)
404    CONTINUE
C
C GET THE CORRESPONDING NUMBERS OF VIRTUAL AND OCCUPIED ORBITALS
C
       NOCCA=POP(IRREP2,1)
       NOCCAS=POP(IRREP2,2)
       NOCCB=POP(IRREP1,2)
       NVRTA=VRT(IRREP1,1)
       NVRTB=VRT(IRREP2,2)
       NVRTBS=VRT(IRREP1,2)
       NOP2=NOCCA-NOCCAS
       NOP1=NVRTBS-NVRTA
C
C IF THERE ARE NO OPEN ORBITALS SKIP
C
       IF(NOP2.EQ.0) GOTO 410
C
C GET OFFSET FOR A MATRIX
C
       IOFF2=IOFFA+DISSYW*IOFF1+IOFF3
C
C TEH CONTRIBUTION WE NEED TO ADD IS DEL(Ib)* F(Aj).
C
       DO 401 I=NOCCAS+1,NOCCA
        B=I-NOCCAS
        DO 402 J=1,NOCCB
         ISTART=IOFF2+(INDX2(B,J,NVRTB)-1)*DISSYW+(I-1)*NVRTA
         IOFFAJ=IOFAJ+(J-1)*NVRTBS+NOP1
C
         CALL SAXPY(NVRTA,ONE,FAJ(IOFFAJ),1,AMAT(ISTART),1)
C
  402     CONTINUE
  401   CONTINUE
  410   IOFF1=IOFF1+NOCCB*NVRTB
C
  400 CONTINUE
C
C  ALL DONE, RETURN
C

      RETURN
      END
