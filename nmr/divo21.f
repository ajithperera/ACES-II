      SUBROUTINE DIVO21(DIVO,DDOO,ICORE,MAXCOR,IUHF,IP,ANTI)
C
C THIS SUBROUTINE CALCULATES THE X(AI) CONTRIBUTION 
C DUE TO THE OCCUPIED-OCCUPIED BLOCK OF THE DENSITY
C MATRIX 
C
C      DI(AI)= - SUM M,N <IM//NA> d D(MN)/ d chi
C
C (INDEPENDENT OF THE CONSIDERED METHOD)
C
C
C   SPIN CASES :
C
C      DI(AI)         DD(MN)         <IM//NA>            <IN//MA>
C
C      AA             AA               AAAA                AAAA  
C                     BB               ABBA                ABBA
C
C      BB             AA               BAAB                BAAB
C                     BB               BBBB                BBBB
C
C THIS ROUTINE USES EXPLICITELY SYMMETRY
C
CEND
C
C CODED   SEPT/91     JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL ANTI
      INTEGER DIRPRD,DISSYW,POP,VRT
      DIMENSION DIVO(1),DDOO(1),ICORE(MAXCOR)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/DSYM/IRREPX,IPERT,NDT(2),NDF1(2),NDF2(2),
     &            IOFFIJ(8,2),IOFFAB(8,2),IOFFAI(8,2)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
C
      MXCOR=MAXCOR 
C
      DO 1000 ISPIN=1,IUHF+1
C
       IF(ISPIN.EQ.1) THEN
        IOFFX=1
        IOFFDA=1
        IOFFDB=1+NDF1(1)*IUHF
       ELSE
        IOFFX=1+NDT(1)
        IOFFDA=1+NDF1(1)
        IOFFDB=1
       ENDIF
C
C  FIRST DO AAAA PART
C
       IF(IUHF.NE.0) THEN
C
        LISTW=6+ISPIN
C
        DO 100 IRREP=1,NIRREP
C 
         NOCCSQ=0
         DO 101 IRREPJ=1,NIRREP
          NOCCSQ=NOCCSQ+POP(IRREPJ,ISPIN)*
     &                  POP(DIRPRD(IRREP,IRREPJ),ISPIN)
101      CONTINUE
C 
         NUMSYW=IRPDPD(IRREP,ISYTYP(2,LISTW))
         DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
C 
         I001=1
         I002=I001+IINTFP*NOCCSQ*NUMSYW
         IF(I002.LT.MXCOR) THEN
C
          CALL DIVO21AA(ICORE(I001),DDOO(IOFFDA),DIVO(IOFFX),ISPIN,
     &                  POP(1,ISPIN),VRT(1,ISPIN),DISSYW,NUMSYW,
     &                  NOCCSQ,LISTW,IRREP,IRREPX)
         ELSE   
          CALL INSMEM('DIVO21AA',I002,MXCOR)
         ENDIF
100     CONTINUE
C
       ENDIF
C 
       LISTW=8+ISPIN
       IF(IUHF.EQ.0) LISTW=10
C
       DO 200 IRREP=1,NIRREP
C
        NUMSYW=IRPDPD(IRREP,ISYTYP(2,LISTW))
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
C
        I001=1
        I002=I001+IINTFP*NUMSYW*DISSYW
        I003=I002+IINTFP*2*MAX(NUMSYW,DISSYW)
        IF(I003.LT.MXCOR) THEN
C
         CALL DIVO21AB(ICORE(I001),DDOO(IOFFDB),DIVO(IOFFX),ISPIN,
     &                 POP(1,ISPIN),VRT(1,ISPIN),POP(1,3-ISPIN),
     &                 VRT(1,3-ISPIN),DISSYW,NUMSYW,LISTW,IRREP,
     &                 IRREPX,IUHF,ICORE(I002))
        ELSE
         CALL INSMEM('DIVO21AB',I003,MXCOR)
        ENDIF
200    CONTINUE
       call checksum('divo21  ',divo(ioffx),ndt(ispin))
1000  CONTINUE
      RETURN
      END
