      SUBROUTINE DIVO21AA(W,DDOO,DIVO,ISPIN,POP,VRT,
     &                   DISSYW,NUMSYW,NOCCSQ,LISTW,IRREP,IRREPX)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DISSYW,DIRPRD,POP,VRT
      DIMENSION W(NOCCSQ,1),DDOO(1),DIVO(1),POP(8),VRT(8),IPD(8)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
C
C   OFFSETS FOR DENSITY MATRIX
C
      IPD(1)=0
      DO 1 IRREPJ=1,NIRREP-1
       IPD(IRREPJ+1)=IPD(IRREPJ)+POP(IRREPJ)*
     &               POP(DIRPRD(IRREPX,IRREPJ))
1     CONTINUE
C
C      PICK UP THE INTEGRALS 
C
      CALL GETLST(W,1,NUMSYW,1,IRREP,LISTW)
      CALL SYMEXP2(IRREP,POP,NOCCSQ,DISSYW,NUMSYW,W,W)
C
C  LOOP OVER IRREPS OF VIRTUAL ORBITAL A AND OCCUPIED ORBITAL I
C
C  OFFSETS
C
      IND=0
      IOFFWA=0
C
      DO 100 IRREP1R=1,NIRREP
C
C  POPULATION WHICH CORRESPONDS TO A
C
       NVRT1=VRT(IRREP1R)
C
C  DETERMINE IRREP OF N
C
       IRREP2R=DIRPRD(IRREP,IRREP1R)
       IRREP2L=DIRPRD(IRREPX,IRREP2R)
C
C  POPULATION WHICH CORRESPONDS TO M AND N
C
       NOCC2R=POP(IRREP2R)
       NOCC2L=POP(IRREP2L)
C
C  POPULATION WHICH CORRESPONDS TO I
C
       IRREP1L=DIRPRD(IRREP2L,IRREP)
C
       NOCC1=POP(IRREP1L)
C
       IOFFWB=0
       DO 2 IRREP1=1,IRREP1L-1
        IOFFWB=IOFFWB*POP(IRREP1)*POP(DIRPRD(IRREP1,IRREP))
2      CONTINUE
  
C  OFFSET IN D
C
       IOFFD1=IPD(IRREP2R)
C
       DO 10 I=1,NOCC1
       IOFFW1=IOFFWB+(I-1)*NOCC2L
       DO 10 IA=1,NVRT1
       IOFFW2=IOFFWA+(IA-1)*NOCC2R
       IND=IND+1
       DO 10 N=1,NOCC2R
       IOFFW3=IOFFW2+N
       IOFFD2=IOFFD1+(N-1)*NOCC2L
       DIVO(IND)=DIVO(IND)
     &            -SDOT(NOCC2L,DDOO(IOFFD2+1),1,W(IOFFW1+1,IOFFW3),1)
10     CONTINUE
       IOFFWA=IOFFWA+NOCC2R*NVRT1
100    CONTINUE
C
      RETURN
      END
