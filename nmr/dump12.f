      SUBROUTINE DUMP12(AODINT,BUF,BUF1,BUF2,NSTART,NEND,IRREPX)
C
C THIS ROUTINE DUMPS TRANSFORMED INTEGRAL DERIVATIVES
C TO DISK. HERE, INTEGRALS FOR NON-SYMMETRIC PERTURBATIONS
C OF TYPE JIKI AND JIIK ARE HANDLED
C
CEND
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD,AOPOP,POP,VRT
C
      DIMENSION AODINT(1),BUF(1),BUF1(1),
     &          BUF2(1),NSTART(8),NEND(8)
      DIMENSION IOFFOO(8,8),IOFFVO(8,8),IOFFOV(8,8),
     &          IOFFVV(8,8)
C
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMINF/NDUMMY,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/AOPOPS/AOPOP(8)
      COMMON/INFOI/TIMEI,NWRITI
C
      INDXF(I,J,N)=I+N*(J-1)
C
C INITIALIZE OFFSETS
C
      DO 1 IRREP=1,NIRREP
       IOFF1=0
       IOFF2=0
       IOFF3=0
       IOFF4=0
       DO 2 IRREP1=1,NIRREP
        IRREP2=DIRPRD(IRREP1,IRREP)
        IOFFOO(IRREP2,IRREP1)=IOFF1
        IOFFVO(IRREP2,IRREP1)=IOFF2
        IOFFOV(IRREP2,IRREP1)=IOFF3
        IOFFVV(IRREP2,IRREP1)=IOFF4
        IOFF1=IOFF1+POP(IRREP1,1)*POP(IRREP2,1)
        IOFF2=IOFF2+VRT(IRREP2,1)*POP(IRREP1,1)
        IOFF3=IOFF3+VRT(IRREP1,1)*POP(IRREP2,1)
        IOFF4=IOFF4+VRT(IRREP1,1)*VRT(IRREP2,1)
2      CONTINUE
1     CONTINUE
C
C LOOP OVER ALL IRREPS
C
      IOFF=1
C
      DO 1000 IRREP1=1,NIRREP
C
       NBAS1=AOPOP(IRREP1)
       NOCC1=POP(IRREP1,1)
       NSTART1=NSTART(IRREP1)
       NEND1=NEND(IRREP1)
       NVRT1=VRT(IRREP1,1) 
C
       DO 2000 IRREP2=1,NIRREP
C
C IF IRREP1 = IRREP2   --> NO CONTRIBUTION, SKIP
C 
        IRREP12=DIRPRD(IRREP1,IRREP2)
        IF(IRREP12.EQ.1) GO TO 2000
C
        NBAS2=AOPOP(IRREP2)
        NOCC2=POP(IRREP2,1)
        NVRT2=VRT(IRREP2,1)
C
C DETERMINE TOTAL SIZE OF IRREP3*IRREP4-ARRAY FOR FIXED IRREP1 AND IRREP2
C
        NSIZE34=0
        DO 2001 IRREP3=1,NIRREP
         IRREP4=DIRPRD(IRREPX,DIRPRD(IRREP3,IRREP12))
         IRREP13=DIRPRD(IRREP1,IRREP3)
         IRREP23=DIRPRD(IRREP2,IRREP3)
         IRREP14=DIRPRD(IRREP1,IRREP4)
         IRREP24=DIRPRD(IRREP2,IRREP4)
         IRREP34=DIRPRD(IRREP3,IRREP4)
         IF(IRREP13.EQ.1.OR.IRREP23.EQ.1.OR.IRREP34.EQ.1.OR.
     &      IRREP14.EQ.1.OR.IRREP24.EQ.1) GO TO 2001
         NSIZE34=NSIZE34+AOPOP(IRREP3)*AOPOP(IRREP4)
2001    CONTINUE
C
        IOFF34=0
C
C LOOP OVER IRREP3, THERE IS OONLY A CONTRIBUTION IF IRREP3 AND
C IRREP4 DIFFER FROM IRREP1 AND IRREP2 AND IRREP3 IS NE IRREP4
C
        DO 3000 IRREP3=1,NIRREP
         IRREP4=DIRPRD(IRREPX,DIRPRD(IRREP3,IRREP12))
         IRREP13=DIRPRD(IRREP1,IRREP3)
         IRREP23=DIRPRD(IRREP2,IRREP3)
         IRREP14=DIRPRD(IRREP1,IRREP4)
         IRREP24=DIRPRD(IRREP2,IRREP4)
         IRREP34=DIRPRD(IRREP3,IRREP4)
         IF(IRREP13.EQ.1.OR.IRREP23.EQ.1.OR.IRREP34.EQ.1.OR.
     &      IRREP14.EQ.1.OR.IRREP24.EQ.1) GO TO 3000
C
         NBAS3=AOPOP(IRREP3)
         NOCC3=POP(IRREP3,1)
         NVRT3=VRT(IRREP3,1)
         NBAS4=AOPOP(IRREP4)
         NOCC4=POP(IRREP4,1)
         NVRT4=VRT(IRREP4,1)
C
         NBAS12=NBAS1*NBAS2
         NBAS13=NBAS1*NBAS3
         NBAS23=NBAS2*NBAS3
         NBAS14=NBAS1*NBAS4
         NBAS24=NBAS2*NBAS4
         NBAS34=NBAS3*NBAS4
         NBAS234=NBAS2*NBAS34
C
C NOW WE HAVE A FIXED COMBINATION OF IRREP1, IRREP2, IRREP3, IRREP4
C    LOOP OVER OCCUPIED ORBITALS OF IRREP1 AND GET ALL INTEGRLAS
C    CORRESPONDING TO IRREP2, IRREP3, AND IRREP4
C    WE HAVE TO KEEP TRACK OF THE OFFSETS !
C
         DO 100 IND=NSTART1,NEND1
C
C GET INTEGRALS FOR GIVEN IND AS IRREP2,IRREP3,IRREP4
C
C OFFSET FOR TARGET ARRAY
C
          IOFF1=1
C
C OFFSET FOR SET OF INTEGRALS COPIED OVER TO THE TARGET ARRAY
C
          IOFF2=IOFF34+IOFF+(IND-NSTART1)*NBAS2*NSIZE34
C  
C LOOP OVER ORBITALS OF IRREP2, ALL INTEGRALS CORREESPONDING
C  TO IRREP3 AND IRREP4 ARE STANDING TOGETHER AND CA BE COPIED
C  AT ONCE
C
          DO 3001 IND2=1,NBAS2
           CALL SCOPY(NBAS34,AODINT(IOFF2),1,BUF(IOFF1),1)
           IOFF1=IOFF1+NBAS34
           IOFF2=IOFF2+NSIZE34
3001      CONTINUE
C
C REORDER THEM AS IRREP1,IRREP3,IRREP2,IRREP4 (slowest to fastest)
C
          DO 3110 IP=1,NBAS4
C
           CALL SCOPY(NBAS23,BUF(IP),NBAS4,BUF2,1)
           CALL TRANSP(BUF2,BUF2(1+NBAS23),NBAS2,NBAS3)
           CALL SCOPY(NBAS23,BUF2(1+NBAS23),1,BUF(IP),NBAS4) 
C
3110      CONTINUE
C
C THE ORDERING IS NOW :  (FASTEST TO SLOWEST) IRREP4,IRREP2; IRREP3,
C                                             FIXED OCC(IRREP1)
C
        LENGTH=NOCC2*NOCC4
        NWRITI=NWRITI+LENGTH*NOCC3
        IF(LENGTH.NE.0) THEN
         DO 115 INDQ=1,NOCC3
          IOFFIJKL=INDXF(1,INDQ,NBAS24)
          IDISIJKL=IOFFOO(IRREP3,IRREP1)+INDXF(INDQ,IND,NOCC3)
          LOFFIJKL=IOFFOO(IRREP4,IRREP2)
          CALL BLKCPY2(BUF(IOFFIJKL),NBAS4,NBAS2,BUF1,NOCC4,NOCC2,1,1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISIJKL,LOFFIJKL,
     &               1,IRREP24,IRREP13,313,1)
115      CONTINUE
        ENDIF
C
        LENGTH=NOCC2*NOCC4
        NWRITI=NWRITI+LENGTH*NVRT3
        IF(LENGTH.NE.0) THEN
         DO 135 IQ=1,NVRT3
          INDQ=IQ+NOCC3
          IOFFIJKA=INDXF(1,INDQ,NBAS24)
          IDISIJKA=IOFFOV(IRREP1,IRREP3)+INDXF(IND,IQ,NOCC1)
          LOFFIJKA=IOFFOO(IRREP2,IRREP4)
          CALL BLKCPY2(BUF(IOFFIJKA),NBAS4,NBAS2,BUF1,NOCC4,NOCC2,1,1)
          CALL TRANSP(BUF1,BUF1(1+LENGTH),NOCC2,NOCC4)
          CALL ULIST(BUF1(1+LENGTH),BUF2,LENGTH,IDISIJKA,LOFFIJKA,
     &               1,IRREP24,IRREP13,310,1)
135       CONTINUE
        ENDIF
C
        LENGTH=NVRT2*NVRT4
        NWRITI=NWRITI+LENGTH*NOCC3
        IF(LENGTH.NE.0) THEN
         DO 145 INDQ=1,NOCC3
          IOFFABIJ=INDXF(1,INDQ,NBAS24)
          IDISABIJ=IOFFOO(IRREP3,IRREP1)+INDXF(INDQ,IND,NOCC3)
          LOFFABIJ=IOFFVV(IRREP4,IRREP2)
          CALL BLKCPY2(BUF(IOFFABIJ),NBAS4,NBAS2,BUF1,NVRT4,NVRT2,
     &                 NOCC4+1,NOCC2+1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISABIJ,LOFFABIJ,
     &               1,IRREP24,IRREP13,316,1)
145       CONTINUE
        ENDIF
C
        LENGTH=NVRT2*NVRT4
        NWRITI=NWRITI+LENGTH*NVRT3
        IF(LENGTH.NE.0) THEN
         DO 165 IQ=1,NVRT3
          INDQ=IQ+NOCC3
          IOFFABCI=INDXF(1,INDQ,NBAS24)
          IDISABCI=IOFFVO(IRREP3,IRREP1)+INDXF(IQ,IND,NVRT3)
          LOFFABCI=IOFFVV(IRREP4,IRREP2)
          CALL BLKCPY2(BUF(IOFFABCI),NBAS4,NBAS2,BUF1,NVRT4,NVRT2,
     &                 NOCC4+1,NOCC2+1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISABCI,LOFFABCI,
     &               1,IRREP24,IRREP13,330,1)
165       CONTINUE
        ENDIF
C
        LENGTH=NOCC2*NVRT4
        NWRITI=NWRITI+LENGTH*NVRT3
        IF(LENGTH.NE.0) THEN
         DO 175 IQ=1,NVRT3
          INDQ=IQ+NOCC3
          IOFFAIBJ=INDXF(1,INDQ,NBAS24)
          IDISAIBJ=IOFFVO(IRREP3,IRREP1)+INDXF(IQ,IND,NVRT3)
          LOFFAIBJ=IOFFVO(IRREP4,IRREP2)
          CALL BLKCPY2(BUF(IOFFAIBJ),NBAS4,NBAS2,BUF1,NVRT4,NOCC2,
     &                 NOCC4+1,1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISAIBJ,LOFFAIBJ,
     &               1,IRREP24,IRREP13,325,1)
175       CONTINUE
        ENDIF
C
        LENGTH=NOCC4*NVRT2
        NWRITI=NWRITI+LENGTH*NVRT3
        IF(LENGTH.NE.0) THEN
         DO 176 IQ=1,NVRT3
          INDQ=IQ+NOCC3
          IOFFIABJ=INDXF(1,INDQ,NBAS24)
          IDISIABJ=IOFFVO(IRREP3,IRREP1)+INDXF(IQ,IND,NVRT3)
          LOFFIABJ=IOFFVO(IRREP2,IRREP4)
          CALL BLKCPY2(BUF(IOFFIABJ),NBAS4,NBAS2,BUF1,NOCC4,NVRT2,
     &                 1,NOCC2+1)
          CALL TRANSP(BUF1,BUF1(1+LENGTH),NVRT2,NOCC4)
          CALL ULIST(BUF1(1+LENGTH),BUF2,LENGTH,IDISIABJ,LOFFIABJ,
     &               1,IRREP24,IRREP13,321,1)
176       CONTINUE
        ENDIF
C
100    CONTINUE
C
C UPDATE OFFSET IOFF34
C
       IOFF34=IOFF34+NBAS3*NBAS4
C
3000   CONTINUE
C
C UPDATE OFFSET IOFF
C
       IOFF=IOFF+(NEND1-NSTART1+1)*NBAS2*NSIZE34
C
2000   CONTINUE
C
1000  CONTINUE
C
      RETURN
      END
