      SUBROUTINE DUMP8(AODINT,BUF,BUF1,BUF2,NSTART,NEND,IRREPX)
C
C THIS ROUTINE DUMPS TRANSFORMED INTEGRAL DERIVATIVES
C TO DISK. HERE, INTEGRALS FOR NON-SYMMETRIC PERTURBATIONS
C OF TYPE IIJK ARE HANDLED
C
CEND
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD,AOPOP,POP,VRT
C
      DIMENSION AODINT(1),BUF(1),BUF1(1),
     &          BUF2(1),NSTART(8),NEND(8)
      DIMENSION IOFFOO(8,8),IOFFVO(8,8),IOFFOV(8,8),
     &          IOFFVV(8,8)
C
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMINF/NDUMMY,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/AOPOPS/AOPOP(8)
      COMMON/INFOI/TIMEI,NWRITI
C
      INDXF(I,J,N)=I+N*(J-1)
C
C INITIALIZE OFFSETS
C
      IOFF=1
C
      DO 1 IRREP=1,NIRREP
       IOFF1=0
       IOFF2=0
       IOFF3=0
       IOFF4=0
       DO 2 IRREP1=1,NIRREP
        IRREP2=DIRPRD(IRREP1,IRREP)
        IOFFOO(IRREP2,IRREP1)=IOFF1
        IOFFVO(IRREP2,IRREP1)=IOFF2
        IOFFOV(IRREP2,IRREP1)=IOFF3
        IOFFVV(IRREP2,IRREP1)=IOFF4
        IOFF1=IOFF1+POP(IRREP1,1)*POP(IRREP2,1)
        IOFF2=IOFF2+VRT(IRREP2,1)*POP(IRREP1,1)
        IOFF3=IOFF3+VRT(IRREP1,1)*POP(IRREP2,1)
        IOFF4=IOFF4+VRT(IRREP1,1)*VRT(IRREP2,1)
2      CONTINUE
1     CONTINUE
C
C LOOP OVER ALL IRREPS
C
      DO 1000 IRREP1=1,NIRREP
C
       NBAS1=AOPOP(IRREP1)
       NOCC1=POP(IRREP1,1)
       NSTART1=NSTART(IRREP1)
       NEND1=NEND(IRREP1)
       NVRT1=VRT(IRREP1,1) 
C
       LEN=0
       DO 1001 IRREP3=1,NIRREP
        IRREP4=DIRPRD(IRREPX,IRREP3)
        IF(IRREP3.NE.IRREP1.AND.IRREP4.NE.IRREP1) THEN
         LEN=LEN+AOPOP(IRREP3)*AOPOP(IRREP4)
        ENDIF
1001   CONTINUE
C
       DO 100 IND=NSTART1,NEND1
C
        IOFFA=IOFF
        DO 2000 IRREP3=1,NIRREP
         IRREP4=DIRPRD(IRREP3,IRREPX)
         IF(IRREP3.EQ.IRREP1.OR.IRREP4.EQ.IRREP1) GO TO 2000
C
         IRREP13=DIRPRD(IRREP1,IRREP3)
         IRREP14=DIRPRD(IRREP1,IRREP4)
         NBAS3=AOPOP(IRREP3)
         NBAS4=AOPOP(IRREP4)
         NOCC3=POP(IRREP3,1)
         NVRT3=VRT(IRREP3,1)
         NOCC4=POP(IRREP4,1)
         NVRT4=VRT(IRREP4,1)
         NBAS34=NBAS3*NBAS4
         NBAS13=NBAS1*NBAS3
         NBAS14=NBAS1*NBAS4
         NBAS134=NBAS1*NBAS34
C
C GET INTEGRALS FOR GIVEN IND AS IRREP2,IRREP1,IRREP2
C
         IOFF2=IOFFA
         IOFF1=1
         DO 2001 IND2=1,NBAS1
          CALL SCOPY(NBAS34,AODINT(IOFF2),1,BUF(IOFF1),1)
          IOFF1=IOFF1+NBAS34
          IOFF2=IOFF2+LEN
2001     CONTINUE
C
C REORDER THEM AS IRREP1,IRREP3,IRREP1,IRREP4 (slowest to fastest)
C
        DO 3010 IP=1,NBAS4
         CALL SCOPY(NBAS13,BUF(IP),NBAS4,BUF2,1)
         CALL TRANSP(BUF2,BUF2(1+NBAS13),NBAS1,NBAS3)
         CALL SCOPY(NBAS13,BUF2(1+NBAS13),1,BUF(IP),NBAS4) 
3010    CONTINUE
C
C THE ORDERING IS NOW :  (FASTEST TO SLOWEST) IRREP4,IRREP1; IRREP3,
C                                             FIXED OCC(IRREP1)
C
        LENGTH=NOCC4*NOCC1
        NWRITI=NWRITI+LENGTH*NOCC3
        IF(LENGTH.NE.0) THEN
         DO 15 INDQ=1,NOCC3
          IOFFIJKL=INDXF(1,INDQ,NBAS14)
          IDISIJKL=IOFFOO(IRREP3,IRREP1)+INDXF(INDQ,IND,NOCC3)
          LOFFIJKL=IOFFOO(IRREP4,IRREP1)
          CALL BLKCPY2(BUF(IOFFIJKL),NBAS4,NBAS1,BUF1,NOCC4,NOCC1,1,1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISIJKL,LOFFIJKL,
     &               1,IRREP14,IRREP13,313,1)
15       CONTINUE
        ENDIF
C
        LENGTH=NOCC4*NOCC1
        NWRITI=NWRITI+LENGTH*NVRT3
        IF(LENGTH.NE.0) THEN
         DO 35 IQ=1,NVRT3
          INDQ=IQ+NOCC3
          IOFFIJKA=INDXF(1,INDQ,NBAS14)
          IDISIJKA=IOFFOV(IRREP1,IRREP3)+INDXF(IND,IQ,NOCC1)
          LOFFIJKA=IOFFOO(IRREP1,IRREP4)
          CALL BLKCPY2(BUF(IOFFIJKA),NBAS4,NBAS1,BUF1,NOCC4,NOCC1,1,1)
          CALL TRANSP(BUF1,BUF1(1+LENGTH),NOCC1,NOCC4)
          CALL ULIST(BUF1(1+LENGTH),BUF2,LENGTH,IDISIJKA,LOFFIJKA,
     &               1,IRREP14,IRREP13,310,1)
35       CONTINUE
        ENDIF
C
        LENGTH=NVRT4*NVRT1
        NWRITI=NWRITI+LENGTH*NOCC3
        IF(LENGTH.NE.0) THEN
         DO 45 INDQ=1,NOCC3
          IOFFABIJ=INDXF(1,INDQ,NBAS14)
          IDISABIJ=IOFFOO(IRREP3,IRREP1)+INDXF(INDQ,IND,NOCC3)
          LOFFABIJ=IOFFVV(IRREP4,IRREP1)
          CALL BLKCPY2(BUF(IOFFABIJ),NBAS4,NBAS1,BUF1,NVRT4,NVRT1,
     &                 NOCC4+1,NOCC1+1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISABIJ,LOFFABIJ,
     &               1,IRREP14,IRREP13,316,1)
45       CONTINUE
        ENDIF
C
        LENGTH=NVRT4*NVRT1
        NWRITI=NWRITI+LENGTH*NVRt3
        IF(LENGTH.NE.0) THEN
         DO 65 IQ=1,NVRT3
          INDQ=IQ+NOCC3
          IOFFABCI=INDXF(1,INDQ,NBAS14)
          IDISABCI=IOFFVO(IRREP3,IRREP1)+INDXF(IQ,IND,NVRT3)
          LOFFABCI=IOFFVV(IRREP4,IRREP1)
          CALL BLKCPY2(BUF(IOFFABCI),NBAS4,NBAS1,BUF1,NVRT4,NVRT1,
     &                 NOCC4+1,NOCC1+1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISABCI,LOFFABCI,
     &               1,IRREP14,IRREP13,330,1)
65       CONTINUE
        ENDIF
C
        LENGTH=NOCC1*NVRT4
        NWRITI=NWRITI+LENGTH*NVRT3
        IF(LENGTH.NE.0) THEN
         DO 75 IQ=1,NVRT3
          INDQ=IQ+NOCC3
          IOFFAIBJ=INDXF(1,INDQ,NBAS14)
          IDISAIBJ=IOFFVO(IRREP3,IRREP1)+INDXF(IQ,IND,NVRT3)
          LOFFAIBJ=IOFFVO(IRREP4,IRREP1)
          CALL BLKCPY2(BUF(IOFFAIBJ),NBAS4,NBAS1,BUF1,NVRT4,NOCC1,
     &                 NOCC4+1,1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISAIBJ,LOFFAIBJ,
     &               1,IRREP14,IRREP13,325,1)
75       CONTINUE
        ENDIF
C
        LENGTH=NOCC4*NVRT1
        NWRITI=NWRITI+LENGTH*NVRT3
        IF(LENGTH.NE.0) THEN
         DO 76 IQ=1,NVRT3
          INDQ=IQ+NOCC3
          IOFFIABJ=INDXF(1,INDQ,NBAS14)
          IDISIABJ=IOFFVO(IRREP3,IRREP1)+INDXF(IQ,IND,NVRT3)
          LOFFIABJ=IOFFVO(IRREP1,IRREP4)
          CALL BLKCPY2(BUF(IOFFIABJ),NBAS4,NBAS1,BUF1,NOCC4,NVRT1,
     &                 1,NOCC1+1)
          CALL TRANSP(BUF1,BUF1(1+LENGTH),NVRT1,NOCC4)
          CALL ULIST(BUF1(1+LENGTH),BUF2,LENGTH,IDISIABJ,LOFFIABJ,
     &               1,IRREP14,IRREP13,321,1)
76       CONTINUE
        ENDIF
C
        IOFFA=IOFFA+NBAS34
C
2000    CONTINUE
C
        IOFF=IOFF+LEN*NBAS1
C
100    CONTINUE
C
1000  CONTINUE
C
      RETURN
      END
