      SUBROUTINE DIVO22AB(W,DDVV,DIVO,ISPIN,POP1,VRT1,POP2,VRT2,
     &                    DISSYW,NUMSYW,LISTW,IRREP,IRREPX,IUHF,
     &                    TMP)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DISSYW,DIRPRD,POP1,VRT1,POP2,VRT2
      DIMENSION W(DISSYW,1),DDVV(1),DIVO(1),POP1(8),VRT1(8),
     &          POP2(8),VRT2(8),IPD(8),TMP(1)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
C
      IF(ISPIN.EQ.2.OR.IUHF.EQ.0) THEN
C
C  OFFSETS FOR DENSITY MATRIX
C
      IPD(1)=0
      DO 1 IRREPJ=1,NIRREP-1
       IPD(IRREPJ+1)=IPD(IRREPJ)+VRT2(IRREPJ)
     &               *VRT2(DIRPRD(IRREPJ,IRREPX))
1     CONTINUE
C
C  OFFSETS
C
      IND=0
      IOFFSET=1
C
C  LOOP OVER IRREP OF OCCUPIED ORBITAL I
C
      DO 100 IRREP1R=1,NIRREP
C
C  POPULATION CORRESPONDING TO I
C
       NOCC1=POP1(IRREP1R)
C
C  DETERMINE IRREP OF M AND N
C
       IRREP2R=DIRPRD(IRREP,IRREP1R)
       IRREP2L=DIRPRD(IRREPX,IRREP2R)
C
C  POPULATION WHICH CORRESPONDS TO M AND N
C
       NVRT2R=VRT2(IRREP2R)
       NVRT2L=VRT2(IRREP2L) 
C
       IRREP1L=DIRPRD(IRREP2L,IRREP)
C
       NVRT1=VRT1(IRREP1L)
C
       IOFFWA=0
       DO 3 IRREP1=1,IRREP1L-1
        IOFFWA=IOFFWA+VRT2(IRREP1)*VRT1(DIRPRD(IRREP,IRREP1))
3      CONTINUE
C
C  OFFSET IN D
C
       IOFFD1=IPD(IRREP2R)
C
       DO 10 I=1,NOCC1
C
C PICK UP THE INTEGRALS 
C
       CALL GETLST(W,IOFFSET,NVRT2R,1,IRREP,LISTW)
C
C FOR RHF, SPIN ADAPT INTEGRALS
C
       IF(IUHF.EQ.0) THEN
        CALL SPINAD3(IRREP,VRT1,DISSYW,NVRT2R,W,TMP,TMP(1+NUMSYW))
       ENDIF
C
       IOFFSET=IOFFSET+NVRT2R
       IOFFW1=0
       DO 10 IA=1,NVRT1
       IOFFW2=IOFFWA+(IA-1)*NVRT2L
       IND=IND+1
       DO 10 N=1,NVRT2R
       IOFFW3=IOFFW1+N
       IOFFD2=IOFFD1+(N-1)*NVRT2L
       DIVO(IND)=DIVO(IND)
     &      -SDOT(NVRT2L,DDVV(IOFFD2+1),1,W(IOFFW2+1,IOFFW3),1)
10     CONTINUE
       IOFFWB=IOFFWB+NVRT2R*NOCC1
100    CONTINUE
C
      ELSE IF(ISPIN.EQ.1.AND.IUHF.NE.0) THEN
C
C PICK UP THE INTEGRALS 
C
      CALL GETLST(W,1,NUMSYW,1,IRREP,LISTW)
C
C   OFFSETS FOR DIVO MATRIX
C
      IPD(1)=0
      DO 2 IRREPJ=1,NIRREP-1
       IRREPI=DIRPRD(IRREPJ,IRREPX)
       IPD(IRREPJ+1)=IPD(IRREPJ)+POP1(IRREPJ)*VRT1(IRREPI)
2     CONTINUE
C
C  LOOP OVER IRREPS OF VIRTUAL ORBITAL A AND OCCUPIED ORBITAL I
C
C  OFFSETS
C
      IOFFD=0
      IOFFWA=0
      IOFFWB=0
C
      DO 200 IRREP2R=1,NIRREP
C
C  POPULATION WHICH CORRESPONDS TO M AND N
C
       IRREP2L=DIRPRD(IRREP2R,IRREPX)
       NVRT2R=VRT2(IRREP2R) 
       NVRT2L=VRT2(IRREP2L)
C
C  DETERMINE IRREP OF M AND N
C
       IRREP1R=DIRPRD(IRREP,IRREP2R)
       IRREP1L=DIRPRD(IRREP,IRREP2L)
C
C  POPULATION CORRESPONDING TO A AND I
C
       NVRT1=VRT1(IRREP1L)
       NOCC1=POP1(IRREP1R)
C
C  OFFSET IN D
C
       IND=IPD(IRREP1R)
C
       DO 20 I=1,NOCC1
       IOFFW2=IOFFWB+I
       DO 20 IA=1,NVRT1
       IOFFW1=IOFFWA+IA
       IND=IND+1
       DO 20 N=1,NVRT2R
       IOFFW3=IOFFW2+(N-1)*NOCC1
       IOFFD2=IOFFD+(N-1)*NVRT2L
       DIVO(IND)=DIVO(IND)
     &          -SDOT(NVRT2L,DDVV(IOFFD2+1),1,W(IOFFW1,IOFFW3),NVRT1)
20     CONTINUE
       IOFFD=IOFFD+NVRT2R*NVRT2L 
       IOFFWB=IOFFWB+NVRT2R*NOCC1
200    CONTINUE
C
      ENDIF
      RETURN
      END
