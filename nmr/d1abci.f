      SUBROUTINE D1ABCI(UAI,SIJ,UIA,SAB,ICORE,MXCOR,
     &                  IRREPX,IPERT,ISPIN,STERM,ANTI)
C
C  THIS ROUTINE CALCULATES THE DERIVATIVE INTEGRALS d <AB||CI> / d chi
C
C   THERE ARE EIGHT DIFFERENT TERMS
C
CEND
C
C CODED APRIL/91 JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD,DISSYW,DISSYD,POP,VRT
      LOGICAL FIELD,GEOM,ROHF,QRHF,SEMI,STERM,ANTI
      DIMENSION ICORE(MXCOR) 
      DIMENSION UAI(1),UIA(1),SIJ(1),SAB(1)
C
      COMMON/OFFSETS/IOFFU(8,2),IOFFS1(8,2),IOFFS2(8,2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMINF/NSTART,NIRREP,IRREPAA(255,2),DIRPRD(8,8)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),NJUNK(18)
      COMMON/DTRAN/FIELD,GEOM,ROHF,QRHF,SEMI
C
      DATA AZERO,ONE /0.D0,1.D0/
C
C  INCORE ALGORITHM
C
C  DO HERE ALPHA,ALPHA (BETA,BETA) SPIN CASE
C
C  LOOP OVER ALL IRREPS OF THE DERIVATIVE INTEGRALS
C
      DO 1000 IRREPR=1,NIRREP
C
C  THE IRREP ON THE LEFT SIDE IS THEN GIVEN AS THE DIRECT PRODUCT
C   OF IRREPP AND IRREPR
C
       IRREPL=DIRPRD(IRREPR,IRREPX)
C
       NUMSYD=IRPDPD(IRREPR,ISYTYP(2,26+ISPIN))
       DISSYD=IRPDPD(IRREPL,ISYTYP(1,26+ISPIN))
C
C DETERMINE FULL SIZE OF LEFT SIDE
C
       NVSQL=0
       NOSQL=0
       NVSQR=0
       DO 1 IRREP=1,NIRREP
        NVSQL=NVSQL+VRT(IRREP,ISPIN)*VRT(DIRPRD(IRREPL,IRREP),ISPIN)
        NOSQL=NOSQL+POP(IRREP,ISPIN)*POP(DIRPRD(IRREPL,IRREP),ISPIN)
        NVSQR=NVSQR+VRT(IRREP,ISPIN)*VRT(DIRPRD(IRREPR,IRREP),ISPIN)
1      CONTINUE
C
C  ALLOCATE CORE MEMORY
C
       ID=1
       IW=ID+IINTFP*NUMSYD*NVSQL
C
C  ZERO TARGET LIST
C
        CALL ZERO(ICORE(ID),NUMSYD*NVSQL)
C
C CONSIDER FIRST ALL TERMS WHICH INCLUDE U(AI)
C
C    <CI//AM> U(MB) + <CI//MB> U(MA)
C
C  THE IRREP ON THE RIGHT SIDE IS UNCHANGED, SO IRREPR DETERMINES
C  THE LISTS OF ORIGINAL ZEROTH ORDER INTEGRALS
C
       LISTW=22+ISPIN
       NUMSYW=IRPDPD(IRREPR,ISYTYP(2,LISTW))
       DISSYW=IRPDPD(IRREPR,ISYTYP(1,LISTW))
C
C ALLOCATE MEMORY
C
       ITMP=IW+IINTFP*NUMSYW*DISSYW
       IEND=ITMP+3*IINTFP*MAX(NUMSYW,DISSYW,NUMSYD,NOSQL)
C
       IF(IEND.GE.MXCOR) CALL INSMEM('D1ABCI',IEND,MXCOR)
C
C  READ IN THE <CI||AM> (ORDER  C,I, A,M)
C
        CALL GETLST(ICORE(IW),1,NUMSYW,1,IRREPR,LISTW) 
C
C  (CI,AB)(R,L) <---- (CI,AM)(R,R) (M,B) (X)
C
        CALL DFINDT(ICORE(ID),ICORE(IW),UIA,NUMSYD,NVSQL,
     &              DISSYW,NUMSYW,VRT(1,ISPIN),POP(1,ISPIN),
     &              VRT(1,ISPIN),IRREPR,IRREPL,IRREPX,
     &              IOFFU(1,ISPIN),2)
C
C
        IF(STERM) THEN
C
C CONSIDER ALL TERMS WHICH INCLUDE S(M,I)
C
C    <CI||AB> S(E,B) + <CI||EB> S(E,A)
C
C  THE IRREP ON THE RIGHT SIDE IS UNCHANGED, SO IRREPR DETERMINES
C  THE LISTS OF ORIGINAL ZEROTH ORDER INTEGRALS
C
         LISTW=26+ISPIN
         NUMSYW=IRPDPD(IRREPR,ISYTYP(2,LISTW))
         DISSYW=IRPDPD(IRREPR,ISYTYP(1,LISTW))
C
C  ALLOCATE CORE MEMORY
C
         IW2=IW+IINTFP*NUMSYW*DISSYW
         IEND=IW2+IINTFP*NUMSYW*DISSYW
C
         IF(MXCOR.GE.IEND) CALL INSMEM('D1ABCI',IEND,MXCOR)
C
C  READ IN THE <CI||AB> AS A,B;C,I
C
         CALL GETLST(ICORE(IW2),1,NUMSYW,1,IRREPR,LISTW)
C
C  TRANSPOSE <AB||CI> TO <CI||AB>
C
         CALL TRANSP(ICORE(IW2),ICORE(IW),NUMSYW,DISSYW)
C
         NUMTMP=NUMSYW
         NUMSYW=DISSYW
         DISSYW=NUMTMP 
C
         CALL SYMEXP(IRREPR,VRT(1,ISPIN),DISSYW,ICORE(IW))
C
C  (CI;AB) (R,L) <----- (CI,AE) (R,R) (E,B) (X)
C
         CALL DFINDT(ICORE(ID),ICORE(IW),SAB,NUMSYD,NVSQL,
     &               DISSYW,NVSQR,VRT(1,ISPIN),VRT(1,ISPIN),
     &               VRT(1,ISPIN),IRREPR,IRREPL,IRREPX,
     &               IOFFS2(1,ISPIN),1)
C
        ENDIF
C
C  TRANSPOSE TARGET LIST
C
        ID2=IW
        IEND=ID2+IINTFP*NUMSYD*DISSYD
        IF(IEND.GE.MXCOR) CALL INSMEM('D1ABCI',IEND,MXCOR)
C
        CALL ASSYM(IRREPL,VRT(1,ISPIN),NUMSYD,NUMSYD,ICORE(ID2),
     &             ICORE(ID))
C
        CALL TRANSP(ICORE(ID2),ICORE(ID),DISSYD,NUMSYD)
C
C NEW ADDRESS FOR INTEGRALS
C
        IW=ID+IINTFP*DISSYD*NUMSYD
C
C FOR GEOMETRICAL PERTURBATIONS, DEAL FIRST WITH THE S-TERMS
C
        IF(STERM) THEN
C
         NUMSYW=IRPDPD(IRREPL,ISYTYP(2,LISTW))
         DISSYW=IRPDPD(IRREPL,ISYTYP(1,LISTW))
C
         ITMP=IW+IINTFP*DISSYW*NUMSYW
         IEND=ITMP+3*IINTFP*MAX(NUMSYW,NUMSYD,DISSYW,DISSYD)
         IF(IEND.GE.MXCOR) CALL INSMEM('D1ABCI',IEND,MXCOR)
C
C   (AB,CI) (L,R) <----- (AB,CM) (L,L) S(M,I) (X)
C
         CALL DFINDT(ICORE(ID),ICORE(IW),SIJ,DISSYD,NUMSYD,DISSYW,
     &               NUMSYW,VRT(1,ISPIN),POP(1,ISPIN),POP(1,ISPIN),
     &               IRREPL,IRREPR,IRREPX,IOFFS1(1,ISPIN),1)
C
C TRANSPOSE INTEGRAL AND TARGET LIST FROM AB;CI TO AB;IC
C
         CALL SYMTR1(IRREPL,VRT(1,ISPIN),POP(1,ISPIN),DISSYW,
     &               ICORE(IW),ICORE(ITMP),ICORE(ITMP+IINTFP*
     &               DISSYW),ICORE(ITMP+2*IINTFP*DISSYW))
C
        ENDIF
C
         CALL SYMTR1(IRREPR,VRT(1,ISPIN),POP(1,ISPIN),DISSYD,
     &               ICORE(ID),ICORE(ITMP),ICORE(ITMP+IINTFP*
     &               DISSYD),ICORE(ITMP+2*IINTFP*DISSYD))
C
        IF(STERM) THEN
C
C (AB,IC) (L,R) <----- (AB,IE) (L,L) S(E,C) (X)
C
         CALL DFINDT(ICORE(ID),ICORE(IW),SAB,DISSYD,NUMSYD,DISSYW,
     &               NUMSYW,POP(1,ISPIN),VRT(1,ISPIN),VRT(1,ISPIN),
     &               IRREPL,IRREPR,IRREPX,IOFFS2(1,ISPIN),1)
C
       ENDIF
C
C NOW DEAL WITH THE RIGHT HAND SIDE
C
C  TERM   <AB||IM> U(M,C)
C
       LISTW=13+ISPIN
       DISSYW=IRPDPD(IRREPL,ISYTYP(1,LISTW))
       NUMSYW=IRPDPD(IRREPL,ISYTYP(2,LISTW))
C
C ALLOCATE MEMORY
C
       IEND=IW+IINTFP*NVSQL*DISSYW
       IF(IEND.GT.MXCOR) CALL INSMEM('D1ABCI',IEND,MXCOR)
C
C READ IN <AB||IJ> INTEGRALS, ORDER IS A<B;I<J
C
       CALL GETLST(ICORE(IW),1,NUMSYW,1,IRREPL,LISTW)
       CALL VMINUS(ICORE(IW),NUMSYW*DISSYW)
C
C EXPAND THE LAST TWO INDICES
C
       CALL SYMEXP(IRREPL,POP(1,ISPIN),DISSYW,ICORE(IW))
C
C  (AB,IC) (L,R) <----- (AB,IM) (L,L) (M,C) (X)
C
       CALL DFINDT(ICORE(ID),ICORE(IW),UIA,DISSYD,NUMYSD,
     &             DISSYW,NOSQL,POP(1,ISPIN),POP(1,ISPIN),
     &             VRT(1,ISPIN),IRREPL,IRREPR,IRREPX,
     &             IOFFU(1,ISPIN),2)
C
C TRANSPOSE THE LAST TWO INDICES IN THE TARGET ARRAY
C
       CALL SYMTR1(IRREPR,POP(1,ISPIN),VRT(1,ISPIN),DISSYD,
     &             ICORE(ID),ICORE(IW),ICORE(IW+IINTFP*DISSYD),
     &             ICORE(IW+2*IINTFP*DISSYD))
C
       LISTW=230+ISPIN
C
       NUMSYW=IRPDPD(IRREPL,ISYTYP(2,LISTW))
       DISSYW=IRPDPD(IRREPL,ISYTYP(1,LISTW))
C
       IEND=IW+IINTFP*DISSYW*NVSQL
       IF(IEND.GE.MXCOR) CALL INSMEM('D1ABCI',IEND,MXCOR)
C
       CALL GETLST(ICORE(IW),1,NUMSYW,1,IRREPL,LISTW)
C
       CALL SYMEXP(IRREPL,VRT(1,ISPIN),DISSYW,ICORE(IW))
C
C  (AB,CI) (L,R) <----- (AB,CE) (L,L)( (E,I) (X)
C
       CALL DFINDT(ICORE(ID),ICORE(IW),UAI,DISSYD,NUMSYD,
     &             DISSYW,NVSQL,VRT(1,ISPIN),VRT(1,ISPIN),
     &             POP(1,ISPIN),IRREPL,IRREPR,IRREPX,
     &             IOFFU(1,ISPIN),1)
C
      CALL PUTLST(ICORE(ID),1,NUMSYD,1,IRREPR,326+ISPIN)
      call checksum('d1abci',icore(id),numsyd*dissyd)
1000  CONTINUE
C
C ALL DONE SO FAR
C
      RETURN
      END
