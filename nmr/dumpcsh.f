      SUBROUTINE DUMPCSH(CSH,NCOORD,SCR)
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD 
C
      DIMENSION CSH(3,NCOORD),SCR(1)
C
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/PERT3/IXYZSYM(3)
C
      DATA IONE/1/
      DATA CONVERT/26.62566914D+00/
C
      OPEN(UNIT=82,FILE='CSHIFT',STATUS='OLD',FORM='FORMATTED')
      REWIND(82)
C
      NATOMS=NCOORD/3
C
C CONVERT CHEMICAL SHIFT CONTRIBUTION TO P.P.M.
C
      CALL SSCAL(3*NCOORD,CONVERT,CSH,1)
C
C FOR CASES WITH SYMMETRY, TRANSFORM THE CHEMICAL SHIFT TENSORS
C BACK TO THE NON-SYMMETRIC REPRESENTATION
C
      IF(NIRREP.NE.1) THEN
C
C REORDER ALSO ALL FIELD DIRECTIONS
C
       DO 10 IFIELD=1,3
        INEW=IXYZSYM(IFIELD)
        DO 10 ICOORD=1,NCOORD
         SCR(INEW+(ICOORD-1)*3)=CSH(IFIELD,ICOORD)
10     CONTINUE
C
       CALL ZERO(CSH,NCOORD*3)
       CALL TRACSH(SCR,CSH,SCR(1+3*IINTFP*NCOORD),NCOORD)
C
      ENDIF
C
      CALL GETREC(20,'JOBARC','NINDATOM',IONE,NUCIND)
      CALL GETREC(20,'JOBARC','MULTATOM',NUCIND,SCR)
      CALL GETREC(20,'JOBARC','NAMCOORD',IINTFP*3*NUCIND,SCR(1+NUCIND))
C
      write(6,6001)
6001  FORMAT(/,' MBPT(2) contribution to the paramagnetic part',
     &       ' of the shielding tensor:',/)
      CALL CSHPRI(CSH,NCOORD,NUCIND,SCR,SCR(1+NUCIND),0)
C
      DO 100 IATOM=1,NATOMS
C
       DO 100 IFIELD=1,3
C
        READ(82,5101) CSHX,CSHY,CSHZ
5101    FORMAT(4F20.10)
C
        CSH(IFIELD,3*IATOM-2)=CSH(IFIELD,3*IATOM-2)+CSHX
        CSH(IFIELD,3*IATOM-1)=CSH(IFIELD,3*IATOM-1)+CSHY
        CSH(IFIELD,3*IATOM)=CSH(IFIELD,3*IATOM)+CSHZ
C
100   CONTINUE
C
C REWIND CSHIFT FILE
C
      REWIND(82)
C
      DO 200 IATOM=1,NATOMS
C
       DO 200 IFIELD=1,3
C
        WRITE(82,5101) CSH(IFIELD,3*IATOM-2),CSH(IFIELD,3*IATOM-1),
     &                 CSH(IFIELD,3*IATOM) 
200   CONTINUE
C
       write(6,6002)
6002   FORMAT(/,' Total shielding tensor at the MBPT(2) level:',/)
      CALL CSHPRI(CSH,NCOORD,NUCIND,SCR,SCR(1+NUCIND),0)
C
      RETURN
      END
