      SUBROUTINE DIVO11(DIVO,DOO,ICORE,MAXCOR,IUHF,IP,ANTI)
C
C THIS SUBROUTINE CALCULATES THE X(AI) CONTRIBUTION 
C DUE TO THE OCCUPIED-OCCUPIED BLOCK OF THE DENSITY
C MATRIX 
C
C      d I(AI)/ d chi = - SUM M,N d <IM||NA>/d chi D(MN)
C
C (INDEPENDENT OF THE CONSIDERED METHOD)
C
C
C   SPIN CASES :
C
C d I(AI)/ d chi     D(MN)     d <IM||NA>/ d chi   
C
C      AA             AA               AAAA         
C                     BB               ABBA       
C
C      BB             AA               BAAB     
C                     BB               BBBB    
C
CEND
C
C CODED   SEPT/91     JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL ANTI
      INTEGER DIRPRD,DISSYW,POP,VRT
      DIMENSION DIVO(1),DOO(1),ICORE(MAXCOR)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/DSYM/IRREPX,IPERT,NDT(2),NDF1(2),NDF2(2),
     &            IOFFIJ(8,2),IOFFAB(8,2),IOFFAI(8,2)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
C
      MXCOR=MAXCOR 
C
      DO 1000 ISPIN=1,IUHF+1
C
       IF(ISPIN.EQ.1) THEN
        IOFFX=1
        IOFFDA=1
        IOFFDB=1+NF1(1)*IUHF
       ELSE
        IOFFX=1+NDT(1)
        IOFFDA=1+NF1(1)
        IOFFDB=1
       ENDIF
C
C  FIRST DO AAAA PART
C
       IF(IUHF.NE.0) THEN
C
        LISTW=306+ISPIN
C
        DO 100 IRREPR=1,NIRREP
C 
         IRREPL=DIRPRD(IRREPX,IRREPR)
C
         NOCCSQ=0
         DO 101 IRREPJ=1,NIRREP
          NOCCSQ=NOCCSQ+POP(IRREPJ,ISPIN)
     &           *POP(DIRPRD(IRREPL,IRREPJ),ISPIN)
101      CONTINUE
C
         NUMSYW=IRPDPD(IRREPR,ISYTYP(2,LISTW))
         DISSYW=IRPDPD(IRREPL,ISYTYP(1,LISTW))
C
         I001=1
         I002=I001+IINTFP*NOCCSQ*NUMSYW
         IF(I002.LT.MXCOR) THEN
C
          CALL DIVO11AA(ICORE(I001),DOO(IOFFDA),DIVO(IOFFX),ISPIN,
     &                  POP(1,ISPIN),VRT(1,ISPIN),DISSYW,NUMSYW,
     &                  NOCCSQ,LISTW,IRREPL,IRREPR)
         ELSE   
          CALL INSMEM('DIVO11AA',I002,MXCOR)
         ENDIF
100     CONTINUE
C
       ENDIF
C 
       LISTW=308+ISPIN
       IF(IUHF.EQ.0) LISTW=310
C
       DO 200 IRREPR=1,NIRREP
C
        IRREPL=DIRPRD(IRREPR,IRREPX)
C
        NUMSYW=IRPDPD(IRREPR,ISYTYP(2,LISTW))
        DISSYW=IRPDPD(IRREPL,ISYTYP(1,LISTW))
C
        I001=1
        I002=I001+IINTFP*NUMSYW*DISSYW
        I003=I002+IINTFP*2*MAX(NUMSYW,DISSYW)
        IF(I003.LT.MXCOR) THEN
C
         CALL DIVO11AB(ICORE(I001),DOO(IOFFDB),DIVO(IOFFX),ISPIN,
     &                POP(1,ISPIN),VRT(1,ISPIN),POP(1,3-ISPIN),
     &                VRT(1,3-ISPIN),DISSYW,NUMSYW,LISTW,IRREPL, 
     &                IRREPR,IUHF,ICORE(I002))
        ELSE
         CALL INSMEM('DIVO11AB',I003,MXCOR)
        ENDIF
200    CONTINUE
       call checksum('divo11  ',divo(ioffx),ndt(ispin))
1000  CONTINUE
      RETURN
      END
