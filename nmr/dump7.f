      SUBROUTINE DUMP7(AODINT,BUF,BUF1,BUF2,NSTART,NEND,IRREPX)
C
C THIS ROUTINE DUMPS TRANSFORMED INTEGRAL DERIVATIVES
C TO DISK. HERE, INTEGRALS FOR NON-SYMMETRIC PERTURBATIONS
C OF TYPE IIIJ AND IIJI ARE HANDLED
C
CEND
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD,AOPOP,POP,VRT
C
      DIMENSION AODINT(1),BUF(1),BUF1(1),
     &          BUF2(1),NSTART(8),NEND(8)
      DIMENSION IOFFOOX2(8),IOFFVOX2(8),IOFFOVX2(8)
C
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMINF/NDUMMY,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/AOPOPS/AOPOP(8)
      COMMON/INFOI/TIMEI,NWRITI
C
      INDXF(I,J,N)=I+N*(J-1)
C
C INITIALIZE OFFSETS
C
      IOFF=1
      IOFFOO=0
      IOFFVV=0
      IOFFVO=0
      IOFFOOX=0
      IOFFVVX=0
      IOFFVOX=0
C
      IOFF1=0
      IOFF2=0
      IOFF3=0
      DO 1 IRREP1=1,NIRREP
       IRREP2=DIRPRD(IRREP1,IRREPX)
       IOFFOOX2(IRREP2)=IOFF1
       IOFFVOX2(IRREP2)=IOFF2
       IOFFOVX2(IRREP2)=IOFF3
       IOFF1=IOFF1+POP(IRREP1,1)*POP(IRREP2,1)
       IOFF2=IOFF2+VRT(IRREP2,1)*POP(IRREP1,1)
       IOFF3=IOFF3+VRT(IRREP1,1)*POP(IRREP2,1)
1     CONTINUE
C
C LOOP OVER ALL IRREPS
C
      DO 1000 IRREP1=1,NIRREP
C
       IRREP2=DIRPRD(IRREP1,IRREPX)

       NBAS1=AOPOP(IRREP1)
       NBAS11=NBAS1*NBAS1
       NOCC1=POP(IRREP1,1)
       NSTART1=NSTART(IRREP1)
       NEND1=NEND(IRREP1)
       NVRT1=VRT(IRREP1,1) 
C
       NBAS2=AOPOP(IRREP2)
       NBAS12=NBAS1*NBAS2
       NOCC2=POP(IRREP2,1)
       NVRT2=VRT(IRREP2,1)
C
       NBAS3=NBAS12*NBAS1
C
       DO 100 IND=NSTART1,NEND1
C
C GET INTEGRALS FOR GIVEN IND AS IRREP2,IRREP1,IRREP2
C
        CALL SCOPY(NBAS3,AODINT(IOFF),1,BUF,1)
C
C REORDER THEM AS IRREP1,IRREP1,IRREP2,IRREP2
C
        DO 3010 IP=1,NBAS2
         CALL SCOPY(NBAS11,BUF(IP),NBAS2,BUF2,1)
         CALL TRANSP(BUF2,BUF2(1+NBAS11),NBAS1,NBAS1)
         CALL SCOPY(NBAS11,BUF2(1+NBAS11),1,BUF(IP),NBAS2) 
3010    CONTINUE
C
C THE ORDERING IS NOW :  (FASTEST TO SLOWEST) IRREP2,IRREP1; IRREP1,
C                                             FIXED OCC(IRREP1)
C
        LENGTH=NOCC2*NOCC1
        NWRITI=NWRITI+LENGTH*NOCC1
        IF(LENGTH.NE.0) THEN
         DO 15 INDQ=1,NOCC1
          IOFFIJKL=INDXF(1,INDQ,NBAS12)
          IDISIJKL=IOFFOO+INDXF(INDQ,IND,NOCC1)
          LOFFIJKL=IOFFOOX
          CALL BLKCPY2(BUF(IOFFIJKL),NBAS2,NBAS1,BUF1,NOCC2,NOCC1,1,1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISIJKL,LOFFIJKL,
     &               1,IRREPX,1,313,1)
15       CONTINUE
        ENDIF
C
        LENGTH=NOCC2*NOCC1
        NWRITI=NWRITI+LENGTH*NVRT1
        IF(LENGTH.NE.0) THEN
         DO 35 IQ=1,NVRT1
          INDQ=IQ+NOCC1
          IOFFIJKA=INDXF(1,INDQ,NBAS12)
          IDISIJKA=IOFFVO+INDXF(IND,IQ,NOCC1)
          LOFFIJKA=IOFFOOX2(IRREP1)
          CALL BLKCPY2(BUF(IOFFIJKA),NBAS2,NBAS1,BUF1,NOCC2,NOCC1,1,1)
          CALL TRANSP(BUF1,BUF1(1+LENGTH),NOCC1,NOCC2)
          CALL ULIST(BUF1(1+LENGTH),BUF2,LENGTH,IDISIJKA,LOFFIJKA,
     &               1,IRREPX,1,310,1)
35       CONTINUE
        ENDIF
C
        LENGTH=NVRT2*NVRT1
        NWRITI=NWRITI+LENGTH*NOCC1
        IF(LENGTH.NE.0) THEN
         DO 45 INDQ=1,NOCC1
          IOFFABIJ=INDXF(1,INDQ,NBAS12)
          IDISABIJ=IOFFOO+INDXF(INDQ,IND,NOCC1)
          LOFFABIJ=IOFFVVX 
          CALL BLKCPY2(BUF(IOFFABIJ),NBAS2,NBAS1,BUF1,NVRT2,NVRT1,
     &                 NOCC2+1,NOCC1+1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISABIJ,LOFFABIJ,
     &               1,IRREPX,1,316,1)
45       CONTINUE
        ENDIF
C
        LENGTH=NVRT2*NVRT1
        NWRITI=NWRITI+LENGTH*NVRT1
        IF(LENGTH.NE.0) THEN
         DO 65 IQ=1,NVRT1
          INDQ=IQ+NOCC1
          IOFFABCI=INDXF(1,INDQ,NBAS12)
          IDISABCI=IOFFVO+INDXF(IQ,IND,NVRT1)
          LOFFABCI=IOFFVVX
          CALL BLKCPY2(BUF(IOFFABCI),NBAS2,NBAS1,BUF1,NVRT2,NVRT1,
     &                 NOCC2+1,NOCC1+1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISABCI,LOFFABCI,
     &               1,IRREPX,1,330,1)
65       CONTINUE
        ENDIF
C
        LENGTH=NOCC1*NVRT2
        NWRITI=NWRITI+LENGTH*NVRT1
        IF(LENGTH.NE.0) THEN
         DO 75 IQ=1,NVRT1
          INDQ=IQ+NOCC1
          IOFFAIBJ=INDXF(1,INDQ,NBAS12)
          IDISAIBJ=IOFFVO+INDXF(IQ,IND,NVRT1)
          LOFFAIBJ=IOFFVOX
          CALL BLKCPY2(BUF(IOFFAIBJ),NBAS2,NBAS1,BUF1,NVRT2,NOCC1,
     &                 NOCC2+1,1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISAIBJ,LOFFAIBJ,
     &               1,IRREPX,1,325,1)
75       CONTINUE
        ENDIF
C
        LENGTH=NOCC2*NVRT1
        NWRITI=NWRITI+LENGTH*NVRT1
        IF(LENGTH.NE.0) THEN
         DO 76 IQ=1,NVRT1
          INDQ=IQ+NOCC1
          IOFFIABJ=INDXF(1,INDQ,NBAS12)
          IDISIABJ=IOFFVO+INDXF(IQ,IND,NVRT1)
          LOFFIABJ=IOFFVOX2(IRREP1)
          CALL BLKCPY2(BUF(IOFFIABJ),NBAS2,NBAS1,BUF1,NOCC2,NVRT1,
     &                 1,NOCC1+1)
          CALL TRANSP(BUF1,BUF1(1+LENGTH),NVRT1,NOCC2)
          CALL ULIST(BUF1(1+LENGTH),BUF2,LENGTH,IDISIABJ,LOFFIABJ,
     &               1,IRREPX,1,321,1)
76       CONTINUE
        ENDIF
C
C GET INTEGRALS FOR GIVEN IND AS IRREP2,IRREP1,IRREP2
C
        IOFFA=IOFF+NBAS3
        CALL SCOPY(NBAS3,AODINT(IOFFA),1,BUF,1)
C
C REORDER THEM AS IRREP1,IRREP1,IRREP2,IRREP2
C
        DO 4010 IP=1,NBAS1
         CALL SCOPY(NBAS12,BUF(IP),NBAS1,BUF2,1)
         CALL TRANSP(BUF2,BUF2(1+NBAS12),NBAS1,NBAS2)
         CALL SCOPY(NBAS12,BUF2(1+NBAS12),1,BUF(IP),NBAS1) 
4010    CONTINUE
C
C THE ORDERING IS NOW :  (FASTEST TO SLOWEST) IRREP1,IRREP2; IRREP2,
C                                             FIXED OCC(IRREP1)
C
        LENGTH=NOCC1*NOCC1
        NWRITI=NWRITI+LENGTH*NOCC2
        IF(LENGTH.NE.0) THEN
         DO 115 INDQ=1,NOCC2
          IOFFIJKL=INDXF(1,INDQ,NBAS11)
          IDISIJKL=IOFFOOX+INDXF(INDQ,IND,NOCC2)
          LOFFIJKL=IOFFOO
          CALL BLKCPY2(BUF(IOFFIJKL),NBAS1,NBAS1,BUF1,NOCC1,NOCC1,1,1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISIJKL,LOFFIJKL,
     &               1,1,IRREPX,313,1)
115      CONTINUE
        ENDIF
C
        LENGTH=NOCC1*NOCC1
        NWRITI=NWRITI+LENGTH*NVRT2
        IF(LENGTH.NE.0) THEN
         DO 135 IQ=1,NVRT2
          INDQ=IQ+NOCC2
          IOFFIJKA=INDXF(1,INDQ,NBAS11)
          IDISIJKA=IOFFOVX2(IRREP1)+INDXF(IND,IQ,NOCC1)
          LOFFIJKA=IOFFOO
          CALL BLKCPY2(BUF(IOFFIJKA),NBAS1,NBAS1,BUF1,NOCC1,NOCC1,1,1)
          CALL TRANSP(BUF1,BUF1(1+LENGTH),NOCC1,NOCC1)
          CALL ULIST(BUF1(1+LENGTH),BUF2,LENGTH,IDISIJKA,LOFFIJKA,
     &               1,1,IRREPX,310,1)
135      CONTINUE
        ENDIF
C
        LENGTH=NVRT1*NVRT1
        NWRITI=NWRITI+LENGTH*NOCC2
        IF(LENGTH.NE.0) THEN
         DO 145 INDQ=1,NOCC2
          IOFFABIJ=INDXF(1,INDQ,NBAS11)
          IDISABIJ=IOFFOOX+INDXF(INDQ,IND,NOCC2)
          LOFFABIJ=IOFFVV 
          CALL BLKCPY2(BUF(IOFFABIJ),NBAS1,NBAS1,BUF1,NVRT1,NVRT1,
     &                 NOCC1+1,NOCC1+1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISABIJ,LOFFABIJ,
     &               1,1,IRREPX,316,1)
145       CONTINUE
        ENDIF
C
        LENGTH=NVRT1*NVRT1
        NWRITI=NWRITI+LENGTH*NVRT2
        IF(LENGTH.NE.0) THEN
         DO 165 IQ=1,NVRT2
          INDQ=IQ+NOCC2
          IOFFABCI=INDXF(1,INDQ,NBAS11)
          IDISABCI=IOFFVOX+INDXF(IQ,IND,NVRT2)
          LOFFABCI=IOFFVV
          CALL BLKCPY2(BUF(IOFFABCI),NBAS1,NBAS1,BUF1,NVRT1,NVRT1,
     &                 NOCC1+1,NOCC1+1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISABCI,LOFFABCI,
     &               1,1,IRREPX,330,1)
165       CONTINUE
        ENDIF
C
        LENGTH=NOCC1*NVRT1
        NWRITI=NWRITI+LENGTH*NVRT2
        IF(LENGTH.NE.0) THEN
         DO 175 IQ=1,NVRT2
          INDQ=IQ+NOCC2
          IOFFAIBJ=INDXF(1,INDQ,NBAS11)
          IDISAIBJ=IOFFVOX+INDXF(IQ,IND,NVRT2)
          LOFFAIBJ=IOFFVO
          CALL BLKCPY2(BUF(IOFFAIBJ),NBAS1,NBAS1,BUF1,NVRT1,NOCC1,
     &                 NOCC1+1,1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISAIBJ,LOFFAIBJ,
     &               1,1,IRREPX,325,1)
175       CONTINUE
        ENDIF
C
        LENGTH=NOCC1*NVRT1
        NWRITI=NWRITI+LENGTH*NVRT2
        IF(LENGTH.NE.0) THEN
         DO 176 IQ=1,NVRT2
          INDQ=IQ+NOCC2
          IOFFIABJ=INDXF(1,INDQ,NBAS11)
          IDISIABJ=IOFFVOX+INDXF(IQ,IND,NVRT2)
          LOFFIABJ=IOFFVO
          CALL BLKCPY2(BUF(IOFFIABJ),NBAS1,NBAS1,BUF1,NOCC1,NVRT1,
     &                 1,NOCC1+1)
          CALL TRANSP(BUF1,BUF1(1+LENGTH),NVRT1,NOCC1)
          CALL ULIST(BUF1(1+LENGTH),BUF2,LENGTH,IDISIABJ,LOFFIABJ,
     &               1,1,IRREPX,321,1)
176       CONTINUE
        ENDIF
C
2000    CONTINUE
C
        IOFF=IOFF+2*NBAS3
C
100    CONTINUE
C
       IOFFOO=IOFFOO+POP(IRREP1,1)*POP(IRREP1,1)
       IOFFVV=IOFFVV+VRT(IRREP1,1)*VRT(IRREP1,1)
       IOFFVO=IOFFVO+VRT(IRREP1,1)*POP(IRREP1,1)
       IOFFOOX=IOFFOOX+POP(IRREP1,1)*POP(IRREP2,1)
       IOFFVVX=IOFFVVX+VRT(IRREP1,1)*VRT(IRREP2,1)
       IOFFVOX=IOFFVOX+POP(IRREP1,1)*VRT(IRREP2,1)
1000  CONTINUE
C
      RETURN
      END
