      SUBROUTINE DUMP6(AODINT,BUF,BUF1,BUF2,NSTART,NEND,IRREPX)
C
C THIS ROUTINE DUMPS TRANSFORMED INTEGRAL DERIVATIVES
C TO DISK. HERE, INTEGRALS FOR NON-SYMMETRIC PERTURBATIONS
C OF TYPE IIJI ARE HANDLED
C
CEND
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD,AOPOP,POP,VRT
C
      DIMENSION IOFFOOX2(8),IOFFVOX2(8),IOFFVVX2(8)
      DIMENSION AODINT(1),BUF(1),BUF1(1),BUF2(1),NSTART(8),NEND(8)
C
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMINF/NDUMMY,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/AOPOPS/AOPOP(8)
      COMMON/INFOI/TIMEI,NWRITI
C
      INDXF(I,J,N)=I+N*(J-1)
C
C INITIALIZE OFFSETS
C
      IOFF=1
      IOFFOOX=0
      IOFFVOX=0
      IOFFVVX=0
      IOFFOO=0
      IOFFVV=0
      IOFFVO=0
C 
      IOFF1=0
      IOFF2=0
      IOFF3=0
      DO 1 IRREP1=1,NIRREP
       IRREP2=DIRPRD(IRREP1,IRREPX) 
       IOFFOOX2(IRREP2)=IOFF1
       IOFFVOX2(IRREP2)=IOFF2
       IOFFVVX2(IRREP2)=IOFF3
       IOFF1=IOFF1+POP(IRREP1,1)*POP(IRREP2,1)
       IOFF2=IOFF2+VRT(IRREP2,1)*POP(IRREP1,1)
       IOFF3=IOFF3+VRT(IRREP2,1)*VRT(IRREP1,1)
1     CONTINUE
C
C LOOP OVER ALL IRREPS
C
      DO 1000 IRREP1=1,NIRREP
C
       IRREP2=DIRPRD(IRREPX,IRREP1)
       NBAS=AOPOP(IRREP1)
       NBAS1=AOPOP(IRREP2)
       NBAS2=NBAS*NBAS
       NBAS12=NBAS*NBAS1
       NBAS3=NBAS1*NBAS2
       NOCC1=POP(IRREP1,1)
       NSTART1=NSTART(IRREP1)
       NEND1=NEND(IRREP1)
       NOCC2=POP(IRREP2,1)
       NVRT1=VRT(IRREP1,1) 
       NVRT2=VRT(IRREP2,1) 
C
       DO 100 IND=NSTART1,NEND1
C
C REORDER ADOINT ARRAY, BUT ONLY INTEGRALS WHICH BELONG TO
C IRREP 
C
        CALL SCOPY(NBAS3,AODINT(IOFF),1,BUF,1)
        IOFF=IOFF+NBAS3
C
        DO 3000 IP=1,NBAS
         CALL SCOPY(NBAS12,BUF(IP),NBAS,BUF2,1)
         CALL TRANSP(BUF2,BUF2(1+NBAS12),NBAS1,NBAS)
         CALL SCOPY(NBAS12,BUF2(1+NBAS12),1,BUF(IP),NBAS)
3000    CONTINUE
C
C THE ORDER IS NOW (SLOWEST --> FASTEST)  IRREP1, IRREP1, IRREP2, IRREP1
C
        LENGTH=NOCC2*NOCC1
        NWRITI=NWRITI+LENGTH*NOCC1
        IF(LENGTH.NE.0) THEN
         DO 15 INDQ=1,NOCC1
          IOFFIJKL=INDXF(1,INDQ,NBAS12)
          IDISIJKL=IOFFOO+INDXF(INDQ,IND,NOCC1)
          LOFFIJKL=IOFFOOX2(IRREP1)
          CALL BLKCPY2(BUF(IOFFIJKL),NBAS,NBAS1,BUF1,NOCC1,NOCC2,1,1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISIJKL,LOFFIJKL,
     &               1,IRREPX,1,313,0)
15       CONTINUE
        ENDIF
C
        LENGTH=NOCC2*NOCC1
        NWRITI=NWRITI+LENGTH*NVRT1
        IF(LENGTH.NE.0) THEN
         DO 35 IQ=1,NVRT1
          INDQ=IQ+NOCC1
          IOFFIJKA=INDXF(1,INDQ,NBAS12)
          IDISIJKA=IOFFVO+INDXF(IND,IQ,NOCC1)
          LOFFIJKA=IOFFOOX
          CALL BLKCPY2(BUF(IOFFIJKA),NBAS,NBAS1,BUF1,NOCC1,NOCC2,1,1)
          CALL TRANSP(BUF1,BUF1(1+LENGTH),NOCC2,NOCC1)
          CALL ULIST(BUF1(1+LENGTH),BUF2,LENGTH,IDISIJKA,
     &               LOFFIJKA,1,IRREPX,1,310,0)
35       CONTINUE
        ENDIF
C
        LENGTH=NVRT1*NVRT2
        NWRITI=NWRITI+LENGTH*NOCC1
        IF(LENGTH.NE.0) THEN
         DO 45 INDQ=1,NOCC1
          IOFFABIJ=INDXF(1,INDQ,NBAS12)
          IDISABIJ=IOFFOO+INDXF(INDQ,IND,NOCC1)
          LOFFABIJ=IOFFVVX2(IRREP1)
          CALL BLKCPY2(BUF(IOFFABIJ),NBAS,NBAS1,BUF1,NVRT1,NVRT2,
     &                 NOCC1+1,NOCC2+1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISABIJ,LOFFABIJ,
     &               1,IRREPX,1,316,0)
45       CONTINUE
        ENDIF
C
        LENGTH=NVRT1*NVRT2
        NWRITI=NWRITI+LENGTH*NVRT1
        IF(LENGTH.NE.0) THEN
         DO 65 IQ=1,NVRT1
          INDQ=IQ+NOCC1
          IOFFABCI=INDXF(1,INDQ,NBAS12)
          IDISABCI=IOFFVO+INDXF(IQ,IND,NVRT1)
          LOFFABCI=IOFFVVX2(IRREP1)
          CALL BLKCPY2(BUF(IOFFABCI),NBAS,NBAS1,BUF1,NVRT1,NVRT2,
     &                 NOCC1+1,NOCC2+1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISABCI,LOFFABCI,
     &               1,IRREPX,1,330,0)
65       CONTINUE
        ENDIF
C
        LENGTH=NOCC2*NVRT1
        NWRITI=NWRITI+LENGTH*NVRT1
        IF(LENGTH.NE.0) THEN
         DO 75 IQ=1,NVRT1
          INDQ=IQ+NOCC1
          IOFFAIBJ=INDXF(1,INDQ,NBAS12)
          IDISAIBJ=IOFFVO+INDXF(IQ,IND,NVRT1)
          LOFFAIBJ=IOFFVOX2(IRREP1)
          CALL BLKCPY2(BUF(IOFFAIBJ),NBAS,NBAS1,BUF1,NVRT1,NOCC2,
     &                 NOCC1+1,1)
          CALL ULIST(BUF1,BUF2,LENGTH,IDISAIBJ,LOFFAIBJ,
     &               1,IRREPX,1,325,0)
75       CONTINUE
        ENDIF
C
        IOFFB=1+NVRT2*NOCC1
        LENGTH=NOCC1*NVRT2
        NWRITI=NWRITI+LENGTH*NVRT1
        IF(LENGTH.NE.0) THEN
         DO 76 IQ=1,NVRT1
          INDQ=IQ+NOCC1
          IOFFIABJ=INDXF(1,INDQ,NBAS12)
          IDISIABJ=IOFFVO+INDXF(IQ,IND,NVRT1)
          LOFFIABJ=IOFFVOX
          CALL BLKCPY2(BUF(IOFFIABJ),NBAS,NBAS1,BUF1,NOCC1,NVRT2,
     &                 1,NOCC2+1)
          CALL TRANSP(BUF1,BUF1(IOFFB),NVRT2,NOCC1)
          CALL ULIST(BUF1(IOFFB),BUF2,LENGTH,IDISIABJ,LOFFIABJ,
     &               1,IRREPX,1,321,0)
76       CONTINUE
        ENDIF
C
C UPDATE OFFSETS
C
100    CONTINUE
C
       IOFFOOX=IOFFOOX+NOCC1*NOCC2
       IOFFVOX=IOFFVOX+NVRT2*NOCC1
       IOFFVVX=IOFFVVX+NVRT2*NVRT1
       IOFFOO=IOFFOO+NOCC1*NOCC1
       IOFFVO=IOFFVO+NVRT1*NOCC1
       IOFFVV=IOFFVV+NVRT1*NVRT1
C
1000  CONTINUE
C
      RETURN
      END
