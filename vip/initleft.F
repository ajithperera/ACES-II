      SUBROUTINE INITLEFT(BUF, BUF3, SCR, MAXCOR, SPINAD, IRREPX, ICALC)
C
C  THIS ROUTINE INITIALIZES THE LEFTHAND EIGENVECTOR USING THE 
C  LEFTHAND EIGENVECTOR OF THE RIGHT HAND REDUCED MATRIX, 
C  AND THE RIGHT HAND ITERATIVE SUBSPACE TO DEFINE AN APPROXIMATE
C  LEFT HAND EIGENVECTOR WHICH HAS THE CONVERGED EIGENVALUE AS ITS 
C  STARTING ROOT.
C
C  SUSPECT, BECAUSE SUBSPACE MATRIX MAY NOT BE SUITED FOR LEFT
C  HAND MATRIX, IF TRNSPACE IS USED.
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL SPINAD, PRINT2, MIMICR, EXCICORE,
     $   LEFTHAND, SINGONLY,DROPCORE
C
      PARAMETER (MAXORD=100)
      PARAMETER (MAXROOT=100)
C
      DIMENSION BUF(MAXORD*(2*MAXORD+3)+5),SCR(MAXCOR)
      DIMENSION BUF3(MAXORD*MAXORD),sjunk(100)
C
      COMMON/LISTDAV/LISTC, LISTHC, LISTH0
      COMMON/EXTINF/NDIMR,IOLDEST
      COMMON/EXTINF2/ROOT
      COMMON/EXTINF3/IROOT,LOCROOT,ITROOT
      COMMON/EXTRAP/MAXEXP,NREDUCE,NTOL,NSIZEC
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/RMAT/ R(10000), P(10000)
      COMMON/EACALC/LEFTHAND, EXCICORE, SINGONLY, DROPCORE
      COMMON/FLAGS/IFLAGS(100)
C
      INDXF(I,J,N)=I+(J-1)*N
C
C  MIMIC RIGHT HAND ITERATIONS, IF MIMICR = .TRUE.
C
      MIMICR = .TRUE.
C
      IF (MIMICR) THEN
         WRITE(6,*)
         WRITE(6,*) ' LEFT HAND INITIAL GUESS SAME AS  RIGHT HANDS'
         I000 = 1
         I010 = I000 + NSIZEC
         CALL GETLST(SCR(I000), 3,1,1,1, LISTH0)
C
      ELSE
C
C DIAGONALIZE TRANSPOSED SUBSPACE MATRIX
C
      PRINT2 = .FALSE.
      CALL TRANSP(R, SCR, MAXORD, MAXORD)
      CALL BLKCPY2(SCR,MAXORD,MAXORD,BUF3,NDIMR,NDIMR,1,1)
      IF(PRINT2)THEN
       WRITE(6,1100)
1100   FORMAT(T3,'@NEXTDAV-I, Initial lefthand subspace Hamiltonian: ')
       CALL OUTPUT(BUF3,1,NDIMR,1,NDIMR,NDIMR,NDIMR,1)
      ENDIF
C
      I000=1
      I010=I000+NDIMR
      I020=I010+NDIMR
      ILOCVEC=I020
      I030=I020+NDIMR*NDIMR
      I040=I030+MAXORD*MAXORD
      I050=I040+NDIMR
      IBUFTMP=I040
      CALL MN_GEEV(NDIMR,NDIMR,BUF3,BUF(I000),BUF(I010),BUF(I020),
     &             BUF(I030),MAXORD*MAXORD,IERR)
      CALL AFTERRG(NDIMR,BUF(I000),BUF(I010),BUF(I020),BUF(I030),
     &             BUF(I040),.false.)
      IF(PRINT2)THEN
       WRITE(6,1101)
1101   FORMAT(T3,'@NEXTDAV-I, Current subspace eigenvalues: ')
       WRITE(6,'(5f12.6)')(BUF(I),I=I000,I000+NDIMR-1)
       WRITE(6,*)' Imaginary part '
       WRITE(6,'(5f12.6)')(BUF(I),I=I010,I010+NDIMR-1)
      ENDIF
C
C FIND EIGENVECTOR ASSOCIATED WITH RIGHT HAND EIGENVALUE.
C
      CALL FNDCLOSE(NDIMR, BUF, ROOT, XCLOSE, ILOC)
      IF (ABS(ROOT - XCLOSE).GT. 1.E-5) THEN
         WRITE(6,*) ' SOMETHING WRONG IN INITLEFT'
      ENDIF
C
      IBUFLC=I020-1+INDXF(1,ILOC,NDIMR)
C
C  POSITION OF SOUGHT FOR EIGENVECTOR IN BUF
C
      ROOT=BUF(ILOC)
C
      CALL ZERO(SJUNK,NDIMR+1)
      CALL SCOPY(NDIMR,BUF(IBUFLC),1,SJUNK(2),1)
C      CALL PUTREC(20,'JOBARC','LASTVCTR',100*IINTFP,SJUNK)
C
      I010 = I000 + NSIZEC
      I020 = I010 + NSIZEC
      CALL FORMS(NSIZEC,NDIMR,SCR(I000),SCR(I010),BUF(IBUFLC),
     &           1,LISTC,IOLDEST,MAXORD)
      IF (EXCICORE) THEN
C
C PROJECT OUT THE COMPONENTS THAT CORRESPOND TO SPIN-FLIP EXCITATIONS
C    (SHOULD NOT BE NECESSARY)
C
         CALL GETLST(SCR(I010),6,1,1,1,LISTH0)
         CALL VECPRD(SCR(I000),SCR(I010),SCR(I000),NSIZEC)
      ENDIF
C
C  NORMALIZE VECTOR
C
      CALL SCOPY(NSIZEC,SCR(I000), 1, SCR(I010), 1)
      IF(SPINAD)THEN
       CALL SPNTSING(NSIZEC,SCR(I010),SCR(I020),2*NSIZEC, IRREPX, ICALC)
      ENDIF 
      X=SQRT(SDOT(NSIZEC,SCR(I000),1,SCR(I010),1))
      CALL SSCAL(NSIZEC,1.0d0/X,SCR(I000),1)      
C
C DETERMINE OVERLAP WITH RIGHT HAND VECTOR
C IF LESS THAN 0.90 THE LEFT HAND VECTOR IS INITIALIZED AS THE RIGHT HAND
C
      CALL GETLST(SCR(I010),2, 1, 1, 1, LISTH0)
      IF(SPINAD)THEN
       CALL SPNTSING(NSIZEC,SCR(I010),SCR(I020),2*NSIZEC, IRREPX, ICALC)
      ENDIF 
      X=SQRT(ABS(SDOT(NSIZEC,SCR(I000),1,SCR(I010),1)))
      IF (X .LT. 0.90) THEN
         WRITE(6,*) ' INITIAL LEFT HAND EQUALS CONVERGED RIGHT HAND'
         CALL GETLST(SCR(I000),2, 1, 1, 1, LISTH0)
      ENDIF
C
      ENDIF
C
      RETURN
      END
