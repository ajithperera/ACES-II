      SUBROUTINE SPNTSING(N, SCR, TMP, MAXCOR, IRREPX, CALCFLAG)
C
C IF CALCFLAG = 1 THEN ROUTINE APPLIES TO EE-EOM CALCULATIONS.
C THE ROUTINE THEN ACCEPTS THE T1(AA) AND T2(ABAB) VECTORS AND
C RETURNS THE SPIN-ADAPTED T VECTOR [2*T1(AA) ; 2 T(IJ,AB) - T(IJ,BA)].
C
C IF CALCFLAG = 2 THEN EA-EOM VECTOR IS SPIN-ADAPTED
C         S(AB,IP) -> 2 S(AB, IP) - S(BA, IP)
C IF CALCFLAG = 3 THEN IP-EOM VECTOR IS SPIN-ADAPTED
C         S(IJ,AP) -> 2 S(IJ, AP) - S(JA, AP)
CEND
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,POP,VRT,DISSIZ, DISSYS, CALCFLAG
      DIMENSION SCR(N),TMP(MAXCOR)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
C
      DATA TWO /2.0D0/
C
C  EE-EOM VECTOR IS SPIN ADADAPTED
C
      LENC1A=IRPDPD(IRREPX,9)
      LENC2AB=IDSYMSZ(IRREPX,ISYTYP(1,46),ISYTYP(2,46))
      I0C1A=1
      I0C2AB=I0C1A+IRPDPD(IRREPX,9)
      ITOP =I0C2AB+LENC2AB

      IF ( CALCFLAG .EQ. 1 ) THEN
C
      IOFF=I0C2AB
      ITMP1=1
      DO 10 IRREPR=1,NIRREP
       IRREPL=DIRPRD(IRREPR,IRREPX)
       DISSIZ=IRPDPD(IRREPL,ISYTYP(1,16))
       NUMDIS=IRPDPD(IRREPR,ISYTYP(2,16))
       ITMP2=ITMP1+MAX(DISSIZ,NUMDIS)
       CALL SPINAD3(IRREPL,VRT(1,1),DISSIZ,NUMDIS,SCR(IOFF),TMP(ITMP1),
     &              TMP(ITMP2))
       IOFF=IOFF+DISSIZ*NUMDIS
10    CONTINUE
      CALL SSCAL(LENC1A,TWO,SCR(I0C1A),1)
C
      ELSEIF (CALCFLAG.EQ.2) THEN
C
C  Spin adaptation for EA-EOM calculation
C
      IOFF = 1 + VRT(IRREPX, 1)
      DO 20 IRREP = 1, NIRREP
         MIRREP = DIRPRD(IRREP, IRREPX)
         DISSYS = IRPDPD(IRREP, 15)
         NUMDSS = POP(MIRREP, 1)
         I000 = 1
         I010 = I000 + MAX(DISSYS, NUMDSS)
         IF (DISSYS * NUMDSS .GT. 0 ) THEN
         CALL SPINAD3(IRREP, VRT(1,1), DISSYS, NUMDSS, SCR(IOFF),
     $      TMP(I000), TMP(I010))
         ENDIF
         IOFF = IOFF + DISSYS * NUMDSS
 20   CONTINUE

      ELSEIF (CALCFLAG.EQ.3) THEN
C
C  Spin adaptation for IP-EOM calculation
C
      IOFF = 1 + POP(IRREPX, 1)
      DO IRREP = 1, NIRREP
         MIRREP = DIRPRD(IRREP, IRREPX)
         DISSYS = IRPDPD(IRREP, 14)
         NUMDSS = VRT(MIRREP, 1)
         I000 = 1
         I010 = I000 + MAX(DISSYS, NUMDSS)
         IF (DISSYS * NUMDSS .GT. 0 ) THEN
         CALL SPINAD3(IRREP, POP(1,1), DISSYS, NUMDSS, SCR(IOFF),
     $      TMP(I000), TMP(I010))
         ENDIF
         IOFF = IOFF + DISSYS * NUMDSS
      ENDDO
CSSS      CALL SSCAL(LENC1A,TWO,SCR(I0C1A),1)
      ENDIF
C
      RETURN
      END

