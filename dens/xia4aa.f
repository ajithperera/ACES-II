      SUBROUTINE XIA4AA(G,W,XIA,FACT,ISPIN,POP,VRT,
     &                 DISSYT,NUMSYT,DISSYW,NUMSYW,
     &                 LISTG,LISTW,IRREP)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DISSYT,DISSYW,DIRPRD,POP,VRT
      DIMENSION G(DISSYT,1),W(DISSYW,1),XIA(1),POP(8),VRT(8)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),DIRPRD(8,8)
      common /dropgeo/ ndrgeo
C
      DATA ONEM,ONE,HALF /-1.0D0,1.D0,0.5D0/
C
C  PICK UP THE G-AMPLITUDES AND THE INTEGRALS
C
      if (ndrgeo.eq.0) CALL GETLST(G,1,NUMSYT,1,IRREP,LISTG)
      if (ndrgeo.eq.1) 
     x     CALL GETGO2U(G,W,1,NUMSYT,1,IRREP,LISTG,ispin,listg,dissyt)
c
      CALL GETLST(W,1,NUMSYW,2,IRREP,LISTW)
C
C  EXPAND THE OCCUPIED-OCCUPIED BLOCK OF THE G
C
      CALL SYMEXP(IRREP,POP,DISSYT,G)
C
C  PERFORM MULTIPLICATION
C
C  JOFFG OFFSET IN THE OCCUPIED-OCCUPIED BLOCK OF G 
C  JOFFW OFFSET IN THE OCCUPIED-VIRTUAL BLOCK OF W
C  IOFF OFFSET IN DOO
      JOFFG=1
      JOFFW=1
      IOFF=1
      DO 90 IRREPJ=1,NIRREP
C          
C        GET OCCUPATION NUMBERS FOR JRREP     
C
       NOCCJ=POP(IRREPJ)
       NVRTJ=VRT(IRREPJ)
C
C        DETERMINE IRREPI WHOSE DIRECT PRODUCT WITH JRREP GIVES IRREP
C
       IRREPI=DIRPRD(IRREP,IRREPJ)
C
C        GET OCCUPATION NUMBER FOR IRREPI
C
       NOCCI=POP(IRREPI)
C
C        IF NOCCI OR NVRTJ OR NOCCJ EQUAL ZERO, NOTHING TO COMPUTE
C
       IF(MIN(NOCCJ,NOCCI,NVRTJ).NE.0) THEN
C
        CALL XGEMM('T','N',NVRTJ,NOCCJ,DISSYT*NOCCI,FACT,
     &              W(1,JOFFW),NOCCI*DISSYT,G(1,JOFFG),      
     &              NOCCI*DISSYT,ONE,XIA(IOFF),NVRTJ)
       ENDIF
C
C UPDATE THE OFFSETS
C
       JOFFG=JOFFG+NOCCJ*NOCCI
       JOFFW=JOFFW+NOCCI*NVRTJ
       IOFF=IOFF+NOCCJ*NVRTJ
90    CONTINUE
C
      RETURN
      END
