
      SUBROUTINE QRHFZ(XOVA,XOVB,X1IA,X1IB,XA2A,XA2B,
     &                 EVAL,ICORE,MAXCOR)
C
C  THIS ROUTINE EVALUATES THE FOLLOWING QUANTITIES FOR 
C  QRHF-CCSD GRADIENT CALCULATIONS.
C
C  OPEN-SHELL ORBITALS WHICH ARE OCCUPIED IN RHF REFERENCE:
C
C   Z(1,I) = X(1,I)/(EVAL(1,RHF)-EVAL(I,RHF)
C
C  OPEN-SHELL ORBITALS WHICH ARE UNOCCUPIED IN RHF REFERENCE
C
C   Z(A,1) = X(A,1)/(EVAL(A,RHF)-EVAL(1,RHF)
C
C  WHERE: 
C         1 RUNS OVER ALL APPROPRIATE OPEN-SHELL ORBITALS
C         A IS A *DOUBLY UNOCCUPIED* ORBITAL IN THE QRHF REF
C         I IS A *DOUBLY OCCUPIED* ORBITAL IN THE QRHF REF
C  
C  IN ADDITION, THE MODIFIED X INTERMEDIATE (XTW) IS FORMED
C
C  XTW(A,I) = X(A,I) - SUM Z(1,J) A(1J,AI) - SUM Z(A,1) A(E1,AI)
C                      1,J                   1,A
C
C  NOTE THAT WE ACTUALLY CALCULATE -Z(A,1) SINCE THIS
C  CORRESPONDS EXACTLY TO THE DENSITy MATRIX ELEMENT.
CEND
C
C  HACKED FROM ORIGINAL NONHFZ ROUTINE 11/91 JFS
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,DISSYW,DISFUL,POP,VRT,POPRHF,VRTRHF
      INTEGER POPDOC,VRTDOC,A,B,C,D
      DIMENSION XOVA(1),XOVB(1),EVAL(1),ICORE(MAXCOR)
      DIMENSION X1IA(1),X1IB(1),XA2A(1),XA2B(1)
C
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/FLAGS/IFLAGS(100)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2) 
      COMMON/QRHFINF/POPRHF(8),VRTRHF(8),NOSH1(8),NOSH2(8),
     &               POPDOC(8),VRTDOC(8),NAI,N1I,NA2,
c&line mod
     &               NUMISCF,NUMASCF,ISPINP,ISPINM,IQRHF
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
      COMMON/INFO/NOCCO(2),NVRTO(2)
C
      INDXF(I,J,N)=I+(J-1)*N
C
      DATA ZERO,ONE,ONEM,TWO /0.D0,1.D0,-1.D0,2.D0/
C
C SET MAXIMUM CORE SIZE
C
      MXCOR=MAXCOR
C
C READ IN THE RHF ORBITAL ENERGIES
C
      NORB=NOCCO(1)+NVRTO(1) 
      CALL GETREC(20,'JOBARC','RHFEVAL ',IINTFP*NORB,EVAL)
C
C SET SOME COUNTERS
C
      IOFFEVLI=0
      IOFFEVLA=NUMISCF
      IOFF1I =0
      IOFFA2 =0
C
C LOOP OVER THE SYMMETRY BLOCKS
C       
      DO 10 IRREP=1,NIRREP
       NOCCRHF=POPRHF(IRREP)
       NVRTRHF=VRTRHF(IRREP)
       NOCCA  =POP(IRREP,1)
       NOCCB  =POP(IRREP,2)
       NVRTA  =VRT(IRREP,1)
       NVRTB  =VRT(IRREP,2)
       NDOCC  =POPDOC(IRREP)
       NDVRT  =VRTDOC(IRREP)
C
C COMPUTE NUMBER AND TYPE OF OPEN SHELL ORBITALS IN THIS SYMMETRY
C BLOCK
C
C   NUM1 - NUMBER OF OPEN SHELL ORBITALS WHICH ARE OCCUPIED IN
C          RHF REFERENCE (QRHF_P TYPE)
C   NUM2 - NUMBER OF OPEN SHELL ORBITALS WHICH ARE UNOCCUPIED 
C          IN RHF REFERENCE (QRHF_M TYPE)
C
       NUM1=NOSH1(IRREP)
       NUM2=NOSH2(IRREP)
C
C DEAL FIRST WITH QRHF_P TYPE ORBITALS - Z(1I) - I RUNS OVER NDOCC
C
       IF(NUM1.NE.0)THEN
        DO 100 IOS1=1,NUM1
         EIG1=EVAL(IOFFEVLI+NDOCC+IOS1)
         DO 110 I=1,NDOCC
          EIGI=EVAL(IOFFEVLI+I)
          DENOM=ONE/(EIG1-EIGI)
          IPOS=INDXF(IOS1,I,NUM1)+IOFF1I
          X1IA(IPOS)=-X1IA(IPOS)*DENOM
          X1IB(IPOS)=-X1IB(IPOS)*DENOM
110      CONTINUE 
100     CONTINUE
       ENDIF
C
C NOW DEAL WITH QRHF_M TYPE ORBITALS - Z(A2) - A RUNS OVER NDVRT
C     
C
       IF(NUM2.NE.0)THEN
        DO 120 IOS2=1,NUM2
         EIG2=EVAL(IOFFEVLA+IOS2)
         DO 130 A=1,NDVRT
          EIGA=EVAL(IOFFEVLA+NUM2+A)
          DENOM=ONE/(EIGA-EIG2)
          IPOS=INDXF(A,IOS2,NDVRT)+IOFFA2
          XA2A(IPOS)=-XA2A(IPOS)*DENOM
          XA2B(IPOS)=-XA2B(IPOS)*DENOM
130      CONTINUE
120     CONTINUE
       ENDIF
C
C NOW FOR THE REAL PRICE PROGRAMMERS HAVE TO PAY FOR
C QRHF-CCSD GRADIENTS ...
C
C THE REALLY UGLY PART -- THE FORMATION OF THE
C MODIFIED "X(TWIDLE) = XTW" INTERMEDIATES, DEFINED BY
C
C  XTW(A,I)=X(A,I)
C
C          - SUM Z1I(1M) [<1A||MI> + <1I||MA>] (term I, QRHF_P)
C            1,M
C
C          - SUM Z1I(1m) [<1A||mI> + <1I||mA>] (term II)
C            1,M
C
C          - SUM ZA2(E2) [<EA||2I> + <EI||2A>] (term III)
C            2,A
C
C          - SUM ZA2(e2) [<eA||2I> + <eI||2A>] (term IV)
C            2,A
C
C
C THIS WORK IS PERFORMED BY THE FOLLOWING THREE SUBROUTINES:
C
C    QRHFXTW1 - HANDLES CASES WITH ONLY QRHF_P TYPE OPEN-SHELL
C               ORBITALS
C    QRHFXTW2 - HANDLES CASES WITH ONLY QRHF_M TYPE OPEN-SHELL
C               ORBITALS
C    QRHFXTW3 - HANDLES CASES WITH BOTH QRHF_M AND QRHF_P TYPE
C               OPEN SHELL ORBITALS.  NOT A PRETTY ROUTINE.
C
c&lines modif
      IF(NUM1.NE.0)
     &  CALL QRHFXTW1(XOVA,XOVB,X1IA(IOFF1I+1),X1IB(IOFF1I+1),
     &               IRREP,ICORE,MAXCOR)
      IF(NUM2.NE.0)
     &  CALL QRHFXTW2(XOVA,XOVB,XA2A(IOFFA2+1),XA2B(IOFFA2+1),
     &               IRREP,ICORE,MAXCOR)
c&end modif
C
C INCREMENT COUNTERS
C
      IOFFEVLI=IOFFEVLI+NOCCRHF
      IOFFEVLA=IOFFEVLA+NVRTRHF
      IOFF1I  =IOFF1I+NDOCC*NUM1
      IOFFA2  =IOFFA2+NDVRT*NUM2
C
10    CONTINUE
C
      RETURN
      END
