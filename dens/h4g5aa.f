      SUBROUTINE H4G5AA(H,G,G1,T1,FACT,DISSYH,DISSYG,NUMSYH,
     &                  NUMSYG,NVRTSQ,POP,VRT,LISTH,LISTG,IRREP,
     &                  TMP,TRIP,LISTG1)
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL TRIP,CIS,EOM,LTRP
      INTEGER DISSYH,DISSYG,DIRPRD,POP,VRT
      DIMENSION H(DISSYH,NUMSYH),G(NUMSYG,NVRTSQ),
     &          G1(DISSYG,NUMSYG),T1(1),TMP(1),POP(8),VRT(8)
C
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/EXCITE/CIS,EOM
      COMMON/LTRIP/LTRP
C
      DATA ONE,AZERO /1.0D0,0.D0/
C
      CALL ZERO(G,NVRTSQ*NUMSYG)
C
C  GET RESORTED H4 FROM DISK :  H4(AN,CI)
C
      CALL GETLST(H,1,NUMSYH,1,IRREP,LISTH)
C
C     THE ORDERING IS ALREADY C,I; A,N
C  
C  LOOP OVER IRREPS OF N AND MULTIPLY
C
      IOFFH=1
      IOFFG=1
      IOFFT=1
      DO 100 IRREPJ=1,NIRREP
C
C  GET POPULATIONS
C
       NVRTJ=VRT(IRREPJ)
       NOCCJ=POP(IRREPJ)
       IRREPI=DIRPRD(IRREPJ,IRREP)
       NVRTI=VRT(IRREPI)
C
C IF ONE OF THE POPULATIONS IS ZERO, SKIP MULTIPLICATION 
C
       IF(MIN(NVRTJ,NVRTI,NOCCJ).NE.0) THEN
C
        CALL XGEMM('N','T',DISSYH*NVRTI,NVRTJ,NOCCJ,FACT,H(1,IOFFH),
     &             DISSYH*NVRTI,T1(IOFFT),NVRTJ,AZERO,G(1,IOFFG),
     &             NUMSYG*NVRTI)
       ENDIF
C
C UPDATE OFFSETS
C
       IOFFT=IOFFT+NOCCJ*NVRTJ
       IOFFG=IOFFG+NVRTJ*NVRTI
       IOFFH=IOFFH+NVRTI*NOCCJ
C
100    CONTINUE
C
C  ANTISYMMETRIZE G IN THE LAST TWO INDICES    : C,I ; A<B
C
       CALL ASSYM(IRREP,VRT,NUMSYG,NUMSYG,G1,G)
C
C  TRANSPOSE THE WHOLE G MATRIX : --> A<B ; C,I
C
       CALL TRANSP(G1,G,DISSYG,NUMSYG)
C
C IN THE CASE OF TRIPLE EXCITATIONS ADD THEIR CONTRIBUTION
C
       IF(TRIP) THEN
        CALL GETLST(G1,1,NUMSYG,1,IRREP,LISTG1)
        CALL SAXPY(NUMSYG*DISSYG,ONE,G1,1,G,1)
       ENDIF
       IF(EOM .OR. LTRP) THEN
        CALL GETLST(G1,1,NUMSYG,1,IRREP,LISTG)
        CALL SAXPY(NUMSYG*DISSYG,ONE,G1,1,G,1)
       ENDIF
C
       CALL PUTLST(G,1,NUMSYG,1,IRREP,LISTG)
C
C  ALL DONE, RETURN
C
       RETURN
       END
