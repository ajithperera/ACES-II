      SUBROUTINE IV4AA(G,W,AIVV,FACT,ISPIN,POP,VRT,DISSYT,NUMSYT,
     &                  DISSYW,NUMSYW,LISTG,LISTW,IRREP,TMP)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DISSYT,DISSYW,DIRPRD,POP,VRT
      DIMENSION G(DISSYT,1),W(DISSYW,1),AIVV(1),
     &          POP(8),VRT(8),TMP(1)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      common /dropgeo/ ndrgeo
C
      DATA ONE /1.D0/
C
C      PICK UP THE G AMPLITUDES AND INTEGRALS REQUIRED
C
      if (ndrgeo.eq.0) CALL GETLST(G,1,NUMSYT,1,IRREP,LISTG)
      if (ndrgeo.eq.1) 
     x      CALL GETGO4U(G,W,1,NUMSYT,1,IRREP,LISTG,ispin,listw,dissyt)
C
      CALL GETLST(W,1,NUMSYW,2,IRREP,LISTW)   
C
C TRANSPOSE THE LAST TWO INDICES
C
      CALL SYMTR1(IRREP,VRT,POP,DISSYT,G,TMP,TMP(1+DISSYT),
     &            TMP(1+2*DISSYT))
      CALL SYMTR1(IRREP,VRT,POP,DISSYW,W,TMP,TMP(1+DISSYW),
     &            TMP(1+2*DISSYW))
C
C  PERFORM MULTIPLICATION 
C
C  JOFF OFFSET IN THE RIGHTMOST BLOCKS OF G AND W
C  IOFF OFFSET IN IOV
C
      IOFF=1
      JOFF=1
      DO 90 IRREPJ=1,NIRREP
C          
C        GET NUMBER OF VIRTUAL ORBITALS FOR IRREPJ     
C
       NVRTJ=VRT(IRREPJ)
C
C        DETERMINE IRREPI WHOSE DIRECT PRODUCT WITH IRREPJ GIVES IRREP
C
       IRREPI=DIRPRD(IRREP,IRREPJ)
C
C        GET NUMBER OF OCCUPIED ORBITALS FOR IRREPI
C
       NOCCI=POP(IRREPI)
C
C        IF NOCCI OR NVRTJ EQUAL ZERO, NOTHING TO COMPUTE
C
       IF(MIN(NVRTJ,NOCCI).NE.0) THEN
C
          CALL XGEMM('T','N',NVRTJ,NVRTJ,NOCCI*DISSYT,FACT,
     &             W(1,JOFF),NOCCI*DISSYT,G(1,JOFF),
     &             NOCCI*DISSYT,ONE,AIVV(IOFF),NVRTJ)
       ENDIF
C
C  UPDATE OFFSETS
C
       JOFF=JOFF+NOCCI*NVRTJ
       IOFF=IOFF+NVRTJ*NVRTJ 
90    CONTINUE
C
      RETURN
      END
