      SUBROUTINE PDCC_IO1AA(G,W,AIOO,FACT,ISPIN,POP,VRT,
     &                 DISSYT,NUMSYT,LISTG,LISTW,IRREP,igtype)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DISSYT,DIRPRD,POP,VRT
      DIMENSION G(DISSYT,1),W(DISSYT,1),AIOO(1),POP(8),VRT(8)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),DIRPRD(8,8)
      common /dropgeo/ ndrgeo
C
      DATA ONEM,ONE,HALF /-1.0D0,1.D0,0.5D0/
C
C  PICK UP THE G-AMPLITUDES AND THE INTEGRALS
C
      if (ndrgeo.eq.0) CALL GETLST(G,1,NUMSYT,1,IRREP,LISTG)
      if (ndrgeo.eq.1) then
        if (igtype.eq.1) 
     x    CALL GETGO1U(G,W,1,NUMSYT,1,IRREP,LISTG,ISPIN,listw,dissyt)
        if (igtype.eq.2) 
     x    CALL GETGO2U(G,W,1,NUMSYT,1,IRREP,LISTG,ISPIN,listw,dissyt)
      endif 
      CALL GETLST(W,1,NUMSYT,2,IRREP,LISTW)
C
C  EXPAND THE OCCUPIED-OCCUPIED BLOCK OF THE AMPLITUDES AND INTEGRALS
C
      CALL SYMEXP(IRREP,POP,DISSYT,G)
      CALL SYMEXP(IRREP,POP,DISSYT,W)
C
C  PERFORM MULTIPLICATION
C
C  JOFF OFFSET IN THE OCCUPIED-OCCUPIED BLOCK OF G AND W
C  IOFF OFFSET IN DOO
C
      JOFF=1
      IOFF=1
      DO 90 IRREPJ=1,NIRREP
C          
C        GET OCCUPATION NUMBER FOR JRREP     
C
       NOCCJ=POP(IRREPJ)
C
C        DETERMINE IRREPI WHOSE DIRECT PRODUCT WITH JRREP GIVES IRREP
C
       IRREPI=DIRPRD(IRREP,IRREPJ)
C
C        GET OCCUPATION NUMBER FOR IRREPI
C
       NOCCI=POP(IRREPI)
C
C        IF NOCCI OR NOCCJ EQUAL ZERO, NOTHING TO COMPUTE
C
       IF(MIN(NOCCJ,NOCCI).NE.0) THEN
C
        CALL XGEMM('T','N',NOCCJ,NOCCJ,DISSYT*NOCCI,FACT,
     &              W(1,JOFF),NOCCI*DISSYT,G(1,JOFF),      
     &              NOCCI*DISSYT,ONE,AIOO(IOFF),NOCCJ)
C
       ENDIF
C
C UPDATE THE OFFSETS
C
       JOFF=JOFF+NOCCJ*NOCCI
       IOFF=IOFF+NOCCJ*NOCCJ
C
90    CONTINUE
C
      RETURN
      END
