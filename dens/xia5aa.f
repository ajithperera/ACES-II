      SUBROUTINE XIA5AA(G,W,XIA,FACT,ISPIN,POP,VRT,DISSYT,
     &                  NUMSYT,DISSYW,NUMSYW,LISTG,LISTW,IRREP,TMP)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DISSYT,DISSYW,DIRPRD,POP,VRT
      DIMENSION G(DISSYT,NUMSYT),W(NUMSYW,DISSYW),XIA(1),
     &          POP(8),VRT(8),TMP(1)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      common /dropgeo/ ndrgeo
C
      DATA ONE /1.D0/
C
C  PICK UP THE GAMMA AMPLITUDES
C
C  THEIR ORDERING IS AFTER THE RESORT F,N ; E,I
C
      if (ndrgeo.eq.0) then
        CALL GETLST(G,1,NUMSYT,1,IRREP,LISTG)
      else  
        CALL GETGO3(G,W,1,NUMSYT,1,IRREP,LISTG,ispin,listg,dissyt)
      endif 
C
C      PICK UP THE INTEGRALS REQUIRED
C
C NOTE THE INTEGRALS HAVE THE ORDERING : (E < A  ; F   N)
C THE REQUIRED ORDERING IS :             ( F   N ; E   A)
C THUS. THE FOLLWONG OPERATIONS ARE REQUIRED :
C
C  TRANSPOSE F N  WITH A E     ( ---> GETTRN CALL) 
C  EXPAND A < E TO FULL LIST  ( ---> SYMEXP CALL)
C
      CALL GETTRN(W,TMP,DISSYW,NUMSYW,1,IRREP,LISTW)
      CALL SYMEXP(IRREP,VRT,NUMSYW,W) 
C
C  NOW ALL ARRAYS HAVE BEEN SET UP FOR THE MULTIPLICATION
C
C  PERFORM MULTIPLICATION 
C
C  JOFFG OFFSET IN THE OCCUPIED-VIRTUAL BLOCK OF G 
C  JOFFW OFFSET IN THE VIRTUAL-VIRTUAL BLOCK OF W
C  IOFF OFFSET IN IOV
C
      IOFF=1
      JOFFG=1
      JOFFW=1
      DO 90 IRREPJ=1,NIRREP
C          
C        GET NUMBER OF VIRTUAL ORBITALS FOR IRREPJ     
C
       NOCCJ=POP(IRREPJ)
       NVRTJ=VRT(IRREPJ)
C
C        DETERMINE IRREPI WHOSE DIRECT PRODUCT WITH IRREPJ GIVES IRREP
C
       IRREPI=DIRPRD(IRREP,IRREPJ)
C
C        GET NUMBER  OF VIRTUAL ORBITALS FOR IRREPI
C
       NVRTI=VRT(IRREPI)
C
C        IF NVRTI, NOCCJ OR NVRTJ EQUAL ZERO, NOTHING TO COMPUTE
C
       IF(MIN(NVRTI,NVRTJ,NOCCJ).NE.0) THEN
C
          CALL XGEMM('T','N',NVRTJ,NOCCJ,NVRTI*DISSYT,FACT,
     &             W(1,JOFFW),NVRTI*DISSYT,G(1,JOFFG),
     &             NVRTI*DISSYT,ONE,XIA(IOFF),NVRTJ)
       ENDIF
C
C  UPDATE OFFSETS
C
       JOFFG=JOFFG+NOCCJ*NVRTI
       JOFFW=JOFFW+NVRTI*NVRTJ
       IOFF=IOFF+NVRTJ*NOCCJ
90    CONTINUE
C
      RETURN
      END
