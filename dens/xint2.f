      SUBROUTINE XINT2(XIA,DVV,ICORE,MAXCOR,IUHF)
C
C THIS SUBROUTINE CALCULATES THE X(AI) CONTRIBUTION 
C DUE TO THE VIRTUAL-VIRTUAL BLOCK OF THE DENSITY
C MATRIX 
C
C      X(IA)= - SUM E,F <AE//FI/> D(EF)
C
C (INDEPENDENT OF THE CONSIDERED METHOD)
C
C
C   SPIN CASES :
C
C      X(AI)         D(MN)      <IM//NA>     <IN//MA>
C
C      AA             AA          AAAA         AAAA  
C                     BB          ABBA         ABBA
C
C      BB             AA          BAAB         BAAB
C                     BB          BBBB         BBBB
C
C THIS ROUTINE USES EXPLICITELY SYMMETRY
C
CEND
C
C CODED   JULY/90     JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD,DISSYW,POP,VRT
      DIMENSION XIA(1),DVV(1),ICORE(MAXCOR)
      COMMON /SYM2/ POP(8,2),VRT(8,2),NTAA,NTBB,NF1AA,NF1BB,NF2AA,NF2BB
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON /SYMPOP2/ IRPDPD(8,22)
      COMMON /SYMPOP/ IRP_DM(8,22),ISYTYP(2,500),NTOT(18)
      COMMON /SHIFT/ ISHIFT 
C
      MXCOR=MAXCOR 
C
      DO 1000 ISPIN=1,IUHF+1
C
       IF(ISPIN.EQ.1) THEN
        IOFFX=1
        IOFFDA=1
        IOFFDB=1+NF2AA*IUHF
       ELSE
        IOFFX=1+NTAA
        IOFFDA=1+NF2AA
        IOFFDB=1
       ENDIF
C
C  FIRST DO AAAA PART (UHF ONLY)
C
       IF(IUHF.EQ.1) THEN
C
        LISTW=26+ISPIN + ISHIFT 
C
        DO 100 IRREP=1,NIRREP
C
         NVRTSQ=0
         DO 101 IRREPJ=1,NIRREP
          NVRTSQ=NVRTSQ+VRT(IRREPJ,ISPIN)*
     &           VRT(DIRPRD(IRREP,IRREPJ),ISPIN)
101      CONTINUE
C
         NUMSYW=IRPDPD(IRREP,ISYTYP(2,LISTW))
         DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
C
         I001=1
         I002=I001+IINTFP*NVRTSQ*NUMSYW
         IF(I002.LT.MXCOR) THEN
C
          CALL XIA2AA(ICORE(I001),DVV(IOFFDA),XIA(IOFFX),ISPIN,
     &                POP(1,ISPIN),VRT(1,ISPIN),DISSYW,NUMSYW,
     &                NVRTSQ,LISTW,IRREP)
         ELSE   
          CALL INSMEM('XIA2AA',I002,MXCOR)
         ENDIF
100     CONTINUE
C
       ENDIF
C 
       LISTW=28+ISPIN + ISHIFT 
       IF(IUHF.EQ.0) LISTW=30 + ISHIFT 
C
       IF(IUHF.EQ.0) THEN
        NVRTM=0
        DO 199 IRREP=1,NIRREP
         NVRTM=MAX(NVRTM,VRT(IRREP,1))
199     CONTINUE
       ENDIF
C
       DO 200 IRREP=1,NIRREP
C
        NUMSYW=IRPDPD(IRREP,ISYTYP(2,LISTW))
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
C
        IF(IUHF.EQ.1) NVRTM=NUMSYW
        I001=1
        I002=I001+IINTFP*NVRTM*DISSYW
        I003=I002
        IF(IUHF.EQ.0) I003=I002+2*IINTFP*NVRTM
        IF(I003.LT.MXCOR) THEN
C
         CALL XIA2AB(ICORE(I001),DVV(IOFFDB),XIA(IOFFX),ISPIN,
     &               POP(1,ISPIN),VRT(1,ISPIN),POP(1,3-ISPIN),
     &               VRT(1,3-ISPIN),DISSYW,NUMSYW,LISTW,IRREP,IUHF,
     &               ICORE(I002))
        ELSE
         CALL INSMEM('XIA2AB',I003,MXCOR)
        ENDIF
200    CONTINUE
1000  CONTINUE
      RETURN
      END
