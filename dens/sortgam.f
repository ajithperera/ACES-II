      SUBROUTINE SORTGAM(ICORE,MAXCOR,IUHF)
C
C THIS ROUTINE DRIVES THE RESORT OF THE G(IA,JB) AMPLITUDES
C THE NEW ORDERING IS REQUIRED FOR THE CALCULATION OF THE
C EVALUATION OF THE G(IA,JB) INTERMEDIATES
C
CEND
C
C CODED AUGUST/90 JG
C MODIFIED MAY/94 JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL CHANGE
      INTEGER DIRPRD,DISSYT,POP,VRT
      CHARACTER*4 SPTYP,SPAAAA(2),SPABAB(2),SPABBA(2)
      DIMENSION ICORE(MAXCOR)
      COMMON/INFO/NOCCO(2),NVRTO(2)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      DATA SPAAAA/'AAAA','BBBB'/,SPABAB /'ABAB','BABA'/
      DATA SPABBA/'AABB','BBAA'/ 
C    
C LOOP OVER ISPINS TO PERFORM SORTS, WRITE ALL G-AMPLITUDES BACK TO
C THE SAME LIST
C
      DO 1000 ISPIN=1,IUHF+1
C
C    DO FIRST AAAA AND BBBB
C
       LISTG=122+ISPIN
       NSIZE=IDSYMSZ(1,ISYTYP(1,LISTG),ISYTYP(2,LISTG))
       I001=1
       I002=I001+IINTFP*NSIZE
       I003=I002+IINTFP*NSIZE
       I004=I003+2*NOCCO(ISPIN)*NVRTO(ISPIN)*IINTFP
       IF(MAXCOR.LT.I004) CALL INSMEM('SORTGAM',I004,MAXCOR)
       CALL GETALL(ICORE(I001),NSIZE,1,LISTG)
       SPTYP=SPAAAA(ISPIN)
       CALL SSTRNG(ICORE(I001),ICORE(I002),NSIZE,NSIZE,ICORE(I003),
     &             SPTYP)
       CALL PUTALL(ICORE(I002),NSIZE,1,LISTG)
C
C ABAB CASES 
C
       LISTG=124+ISPIN
       NSIZE=IDSYMSZ(1,ISYTYP(1,LISTG),ISYTYP(2,LISTG))
       I000=1
       I010=I000+NSIZE*IINTFP
       I020=I010+NSIZE*IINTFP
       IF(MAXCOR.LT.I020) CALL INSMEM('SORTGAM',I020,MAXCOR)
       CALL GETALL(ICORE(I000),NSIZE,1,LISTG)
       CALL SSTGEN(ICORE(I000),ICORE(I010),NSIZE,VRT(1,ISPIN),
     &             POP(1,3-ISPIN),VRT(1,ISPIN),POP(1,3-ISPIN),
     &             ICORE(I020),1,'1432')
       CALL PUTALL(ICORE(I010),NSIZE,1,LISTG)
C
C DO ABBA CASES
C
       IF(ISPIN.EQ.1) THEN
        LISTG=118
        NSIZE=IDSYMSZ(1,ISYTYP(1,LISTG),ISYTYP(2,LISTG))
        I001=1
        I002=I001+IINTFP*NSIZE
        I003=I002+IINTFP*NSIZE
        I004=I003+IINTFP*(NVRTO(1)+NVRTO(2))*(NOCCO(1)+NOCCO(2))
        IF(MAXCOR.LT.I004) CALL INSMEM('SORTGAM',I004,MAXCOR)
        CALL GETALL(ICORE(I001),NSIZE,1,LISTG)
        SPTYP=SPABBA(ISPIN)
        CALL SSTRNG(ICORE(I001),ICORE(I002),NSIZE,NSIZE,ICORE(I003),
     &              SPTYP)
        LISTN=21
C
        CHANGE=.TRUE.
        CALL NEWTYP(LISTG,ISYTYP(1,LISTN),ISYTYP(2,LISTN),CHANGE)
        IOFF=I002
        DO 110 IRREP=1,NIRREP
         NUMSYT=IRPDPD(IRREP,ISYTYP(2,LISTN))
         DISSYT=IRPDPD(IRREP,ISYTYP(1,LISTN))
         CALL PUTLST(ICORE(IOFF),1,NUMSYT,1,IRREP,LISTG)
         IOFF=IOFF+IINTFP*NUMSYT*DISSYT
110     CONTINUE 
        IF(IUHF.EQ.1) THEN
         LISTG=117
         I003=I002
         I004=I001
         DO 100 IRREP=1,NIRREP
          NUMSYT=IRPDPD(IRREP,ISYTYP(2,LISTN))
          DISSYT=IRPDPD(IRREP,ISYTYP(1,LISTN))
          CALL TRANSP(ICORE(I003),ICORE(I004),NUMSYT,DISSYT)
          I003=I003+IINTFP*NUMSYT*DISSYT
          I004=I004+IINTFP*NUMSYT*DISSYT
100      CONTINUE
         CALL NEWTYP(LISTG,ISYTYP(2,LISTN),ISYTYP(1,LISTN),CHANGE)
         IOFF=I001
         DO 120 IRREP=1,NIRREP
          NUMSYT=IRPDPD(IRREP,ISYTYP(1,LISTN))
          DISSYT=IRPDPD(IRREP,ISYTYP(2,LISTN))
          CALL PUTLST(ICORE(IOFF),1,NUMSYT,1,IRREP,LISTG)
          IOFF=IOFF+IINTFP*NUMSYT*DISSYT
120      CONTINUE 
        ENDIF
       ENDIF
1000  CONTINUE
C
      RETURN
      END
