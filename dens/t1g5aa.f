      SUBROUTINE T1G5AA(LT,Z,G,MAXDIS,T1,POP,VRT,DISSYLT,
     &                  DISSYG,NUMSYLT,NUMSYG,LISTL,LISTT,
     &                  LISTG,IRREP,ISPIN)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DISSYLT,DISSYG,DIRPRD,POP,VRT
      DOUBLE PRECISION LT(NUMSYLT*DISSYLT)
      DIMENSION Z(NUMSYLT*NUMSYG),T1(*)
      DIMENSION G(DISSYG*MAXDIS),POP(8),VRT(8)
C
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
C
      DATA AZERO,ONE,ONEM,HALFM /0.0D0,1.0D0,-1.0D0,-0.5D0/
C
C  GET LAMBDAS L(MN,EF) FROM LISTL, THE ORDER IS C<E ; M<N 
C  AND TRANSPOSE L      C<E ; M<N --> M<N ; C<E
C
C
      CALL GETTRN(LT,Z,DISSYLT,NUMSYLT,1,IRREP,LISTL)
C
C  EXPAND THE VIRT.-VIRT> PART OF L
C
C   --->  M<N ; C,E
C
      CALL SYMEXP(IRREP,VRT,NUMSYLT,LT)
C
      CALL ZERO(Z,NUMSYLT*NUMSYG)
C
C   PERFORM NOW THE MULTIPLICATION
C
      IOFFT=1
      IOFFL=1
      IOFFZ=1
      DO 100 IRREPJ=1,NIRREP
C
C  GET POPULATIONS FOR E AND I
C
       NOCCJ=POP(IRREPJ)
       NVRTJ=VRT(IRREPJ)
C
C   GET IRREPI AND POPULATION FOR C
C
       IRREPI=DIRPRD(IRREP,IRREPJ)
C
       NVRTI=VRT(IRREPI)
C
       IF(MIN(NOCCJ,NVRTJ,NVRTI).NE.0) THEN
C
        CALL XGEMM('N','N',NUMSYLT*NVRTI,NOCCJ,NVRTJ,ONE,LT(IOFFL),
     &             NUMSYLT*NVRTI,T1(IOFFT),NVRTJ,AZERO,Z(IOFFZ),
     &             NUMSYLT*NVRTI)
       ENDIF
C
C  UPDATE OFFSETS
C
       IOFFT=IOFFT+NVRTJ*NOCCJ
       IOFFL=IOFFL+NVRTJ*NVRTI*NUMSYLT
       IOFFZ=IOFFZ+NVRTI*NOCCJ*NUMSYLT
C
100   CONTINUE       
C
C   SECOND MULTIPLICATION
C
C    GET FIRST T AMPLITUDES AND FORM TAU AMPLITUDES
C
      CALL GETLST(LT,1,NUMSYLT,1,IRREP,LISTT)
C
      CALL FTAU(LT,T1,T1,DISSYLT,NUMSYLT,POP,POP,VRT,VRT,IRREP,
     &          ISPIN,ONE)
C
C  PERFORM MULTIPLICATION
C
      IOFFLIST=1
      IOFFZ=1
      NLEFT=NUMSYG
C
1     CONTINUE
C
       NREAD=MIN(NLEFT,MAXDIS)
       NLEFT=NLEFT-NREAD
C
       CALL GETLST(G,IOFFLIST,NREAD,1,IRREP,LISTG)
C
       CALL XGEMM('N','N',DISSYG,NREAD,NUMSYLT,HALFM,LT,DISSYLT,
     &            Z(IOFFZ),NUMSYLT,ONE,G,DISSYG)
C
C   EVERYTHING DONE, SAVE G ON LISTG
C
       CALL PUTLST(G,IOFFLIST,NREAD,1,IRREP,LISTG)
C
       IOFFZ=IOFFZ+NUMSYLT*NREAD
       IOFFLIST=IOFFLIST+NREAD
C
      IF(NLEFT.NE.0) GO TO 1
C
      RETURN
      END
