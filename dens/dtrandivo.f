      SUBROUTINE DTRANDIVO(DIVO,EVEC,SCR,DIVOMO,DIOOMO,
     &                     FACT,NAO,NMO,IUHF,IRREPXAO)
C
C TRANSFORMS THE DIVO(I,MU) ARRAY THE DIVO(I,A).
C
C INPUT:
C
C   DIVO........ARRAY X(MU,I)
C   EVEC........SCRATCH ARRAY FOR SCF EIGENVECTORS
C   SCR.........SCRATCH ARRAY
C   FACT........FACTOR FOR MULTIPLICATION
C   NAO.........NUMBER OF AOS
C   NMO.........NUMBER OF MOS
C   IUHF........UHF FLAG
C   IRREPXAO....IRREP OF X(MU,I)
C
C OUTPUT
C
C   DIVOMO......UPDATED X(A,I)
C   DIOOMO......UPDATED X(I,J)  
C
CEND
C
C  A routine borrowed from Jurgen, modifications to ACES II
C  GNV version by A. Perera, 05/2005.
C 
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD,POP,VRT
C     
      DIMENSION SCR(*)
      DIMENSION DIVO(*),EVEC(NAO,NMO),DIVOMO(*),DIOOMO(*)
C
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/AOSYM/IAOPOP(8),IOFFAO(8),IOFFV(8,2),IOFFO(8,2),
     &             IRPDPDAO(8),IRPDPDAOS(8),ISTART(8,8),
     &             ISTARTMO(8,8,3)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/INFO/NOCCO(2),NVRTO(2)
C
      DATA IONE/1/
      DATA AZERO,ONE,ONEM /0.0D0,1.D0,-1.D0/
C
      IRREPXMO=IRREPXAO
C     
      IOFFS=1
      IOFFI=1
C     
      DO ISPIN=1, 1+IUHF
C     
         IOFFX = 1 + (ISPIN-1)*NOCCO(1)*NAO 
C
         IF(ISPIN.EQ.1) THEN
            CALL GETREC(20,'JOBARC','SCFEVECA',IINTFP*NAO*NMO,EVEC)
         ELSE
            CALL GETREC(20,'JOBARC','SCFEVECB',IINTFP*NAO*NMO,EVEC) 
         ENDIF
C     
         DO IRREPR=1,NIRREP
C     
            IRREPLAO = DIRPRD(IRREPR,IRREPXAO)
            IRREPL   = DIRPRD(IRREPR,IRREPXMO)
C     
            NOCCI  = POP(IRREPR,ISPIN)
            NOCCJ  = POP(IRREPL,ISPIN)
            NVRTI  = VRT(IRREPL,ISPIN)
            NAOIL  = IAOPOP(IRREPLAO)
            IOFFRV = IOFFV(IRREPL,ISPIN)
            IOFFRO = IOFFO(IRREPL,ISPIN)
            IOFFLL = IOFFAO(IRREPLAO)
C     
            FACTM=-FACT
            CALL XGEMM('T','N',NVRTI,NOCCI,NAOIL,FACTM,
     &           EVEC(IOFFLL,IOFFRV),NAO,DIVO(IOFFX),NAOIL,
     &           ONE,DIVOMO(IOFFS),NVRTI)
C     
            CALL XGEMM('T','N',NOCCJ,NOCCI,NAOIL,FACT,
     &           EVEC(IOFFLL,IOFFRO),NAO,DIVO(IOFFX),NAOIL,
     &           ONE,DIOOMO(IOFFI),NOCCJ)

            IOFFX = IOFFX + NAOIL*NOCCI
            IOFFS = IOFFS + NVRTI*NOCCI
            IOFFI = IOFFI + NOCCJ*NOCCI
C     
        ENDDO
      ENDDO
C     
      RETURN
      END
