
      SUBROUTINE QRHFX(AIOOA,AIVVA,AIOVA,AIVOA,AIOOB,AIVVB,
     &                 AIOVB,AIVOB,XAIA,XAIB,X1IA,X1IB,XA2A,
     &                 XA2B,SCR)
C
C  THIS ROUTINE FORMS THE X-VECTOR REQUIRED IN QRHF-CCSD
C  CALCULATIONS TO SOLVE THE Z-VECTOR EQUATION.
C
C  THE X ARE DEFINED AS
C
C     X(A,I) = [I(A,I) - I(I,A)]
C
C     X(A,1) = [I(A,1) - I(1,A)] [TYPE I ONLY]
C
C     X(1,I) = [I(1,I) - I(I,1)] [TYPE II ONLY]
C
C  WHERE I DENOTES DOUBLY OCCUPIED ORBITALS, A VIRTUAL
C  ORBITALS AND 1 OPEN-SHELL ORBITALS
C
C  THERE ARE TWO SPIN CASES TO CONSIDER
C
CEND
C
C  CODED OCT/90 JG
C  MODIFIED JAN/94 PS
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER POP,VRT,POPRHF,VRTRHF,POPDOC,VRTDOC,DIRPRD
      DIMENSION AIOOA(1),AIVVA(1),AIOVA(1),AIVOA(1),XAIA(1),
     &          X1IA(1),XA2A(1)
      DIMENSION AIOOB(1),AIVVB(1),AIOVB(1),AIVOB(1),XAIB(1),
     &          X1IB(1),XA2B(1)      
      DIMENSION SCR(*)
C
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/FLAGS/IFLAGS(100)
      COMMON/QRHFINF/POPRHF(8),VRTRHF(8),NOSH1(8),NOSH2(8),
     &               POPDOC(8),VRTDOC(8),NAI,N1I,NA2,
c&line mod
     &               NUMISCF,NUMASCF,ISPINP,ISPINM,IQRHF
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
C
      DATA ONE,ONEM /1.0D0,-1.D0/
C
C LOOP OVER SYMMETRY BLOCKS
C
      IOFFAI=1
      IOFF1I=1
      IOFFA2=1
      IOFFOOA=1
      IOFFOOB=1
      IOFFVOA=1
      IOFFVOB=1
      IOFFVVA=1
      IOFFVVB=1
C
      DO 10 IRREP=1,NIRREP
C
       NOCCA  =POP(IRREP,1)
       NOCCB  =POP(IRREP,2)
       NVRTA  =VRT(IRREP,1)
       NVRTB  =VRT(IRREP,2)
       NOCCRHF=POPRHF(IRREP)
       NVRTRHF=VRTRHF(IRREP)
       NDOCC  =POPDOC(IRREP)
       NDVRT  =VRTDOC(IRREP)
C
C  NUM1 - NUMBER OF OPEN-SHELL ORBITALS WHICH ARE
C         OCCUPIED IN RHF REFERENCE   (QRHF_P TYPE)
C  NUM2 - NUMBER OF OPEN-SHELL ORBITALS WHICH ARE 
C         UNOCCUPIED IN RHF REFERENCE (QRHF_M TYPE)
C
       NUM1   =NOSH1(IRREP)
       NUM2   =NOSH2(IRREP)
       NSIZE  =NOCCA+NVRTA
       I000=1
       I010=I000+NSIZE*NSIZE
       I020=I010+NSIZE*NSIZE
       I030=I020+NSIZE*NSIZE
       I040=I030+NSIZE*NSIZE
C
C FORM SQUARED-OUT I AND X MATRICES FOR BOTH SPINS.  
C
C    I(alpha) HELD IN SCR(I000)
C    I(beta)  HELD IN SCR(I010)
C    X(alpha) HELD IN SCR(I020) [ALSO USED AS SCRATCH AREA]
C    X(beta)  HELD IN SCR(I030)
C
       CALL VMINUS(AIVOA,NT(1))
       CALL VMINUS(AIVOB,NT(2))
       CALL TRANSP(AIOVA(IOFFVOA),SCR(I020),NOCCA,NVRTA)
       CALL SQROUT(AIOOA(IOFFOOA),AIVOA(IOFFVOA),SCR(I020),
     &             AIVVA(IOFFVVA),NOCCA,NVRTA,SCR(I000),1)
       CALL TRANSP(AIOVB(IOFFVOB),SCR(I020),NOCCB,NVRTB)
       CALL SQROUT(AIOOB(IOFFOOB),AIVOB(IOFFVOB),SCR(I020),
     &             AIVVB(IOFFVVB),NOCCB,NVRTB,SCR(I010),1)
       CALL VMINUS(AIVOA,NT(1))
       CALL VMINUS(AIVOB,NT(2))
       CALL SCOPY(NSIZE*NSIZE,SCR(I000),1,SCR(I020),1)
       CALL SCOPY(NSIZE*NSIZE,SCR(I010),1,SCR(I030),1)
       CALL MMMT (SCR(I020),NSIZE)
       CALL MMMT (SCR(I030),NSIZE)
       CALL VMINUS(SCR(I020),NSIZE*NSIZE)
       CALL VMINUS(SCR(I030),NSIZE*NSIZE)
C
C FORM THE X(AI) VECTORS.  AI REFER TO THE RHF INDICES. 
C
       CALL BLKCPY2(SCR(I020),NSIZE,NSIZE,XAIA(IOFFAI),
     &              NVRTRHF,NOCCRHF,NOCCRHF+1,1)
       CALL BLKCPY2(SCR(I030),NSIZE,NSIZE,XAIB(IOFFAI),
     &              NVRTRHF,NOCCRHF,NOCCRHF+1,1)
C
C FORM THE X(1I) VECTORS (IF REQUIRED)
C
       IF(NUM1.NE.0)THEN
        CALL BLKCPY2(SCR(I020),NSIZE,NSIZE,X1IA(IOFF1I),NUM1,
     &               NDOCC,NDOCC+1,1)
        CALL BLKCPY2(SCR(I030),NSIZE,NSIZE,X1IB(IOFF1I),NUM1,
     &               NDOCC,NDOCC+1,1)
       ENDIF
C
C FORM THE X(A2) VECTORS (IF REQUIRED)
C
       IF(NUM2.NE.0)THEN
        CALL BLKCPY2(SCR(I020),NSIZE,NSIZE,XA2A(IOFFA2),NDVRT,
     &               NUM2,NOCCRHF+NUM2+1,NOCCRHF+1)
        CALL BLKCPY2(SCR(I030),NSIZE,NSIZE,XA2B(IOFFA2),NDVRT,
     &               NUM2,NOCCRHF+NUM2+1,NOCCRHF+1)
       ENDIF
C 
       IOFFOOA=IOFFOOA+POP(IRREP,1)*POP(IRREP,1)
       IOFFVOA=IOFFVOA+VRT(IRREP,1)*POP(IRREP,1)
       IOFFVVA=IOFFVVA+VRT(IRREP,1)*VRT(IRREP,1)
       IOFFOOB=IOFFOOB+POP(IRREP,2)*POP(IRREP,2)
       IOFFVOB=IOFFVOB+VRT(IRREP,2)*POP(IRREP,2)
       IOFFVVB=IOFFVVB+VRT(IRREP,2)*VRT(IRREP,2)
       IOFFAI =IOFFAI+NOCCRHF*NVRTRHF
       IOFF1I =IOFF1I+NUM1*NDOCC
       IOFFA2 =IOFFA2+NUM2*NDVRT
C
10    CONTINUE
C
      RETURN
      END
