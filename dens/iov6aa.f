      SUBROUTINE IOV6AA(G,W,AIOV,FACT,ISPIN,POP,VRT,DISSYT,NUMSYT,
     &                  DISSYW,NUMSYW,LISTG,LISTW,IRREP,TMP)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DISSYT,DISSYW,DIRPRD,POP,VRT
      DIMENSION G(DISSYT,1),W(DISSYW,1),AIOV(1),
     &          POP(8),VRT(8),TMP(1)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      common /dropgeo/ ndrgeo
C
      DATA ONE /1.D0/
C
C      PICK UP THE G AMPLITUDES AND INTEGRALS REQUIRED
C
      if (ndrgeo.eq.0) CALL GETLST(G,1,NUMSYT,1,IRREP,LISTG)
      if (ndrgeo.eq.1) 
     x     CALL GETGO6U(G,W,1,NUMSYT,1,IRREP,LISTG,ispin,listg,dissyt)
C
      CALL GETLST(W,1,NUMSYW,2,IRREP,LISTW)   
C
C EXPAND THE OCC. OCC. BLOCK SO THAT WE HAVE M<N,O,I
C
      CALL SYMEXP(IRREP,POP,DISSYW,W)
C
C  PERFORM MULTIPLICATION 
C
C  JOFFG AND JOFFW OFFSET IN THE RIGHTMOST BLOCKS OF G AND W
C  IOFF OFFSET IN IOV
C
      IOFF=1
      JOFFG=1
      JOFFW=1
      DO 90 IRREPJ=1,NIRREP
C          
C        GET NUMBER OF OCCUPIED AND VIRTUAL ORBITALS FOR IRREPJ     
C
       NOCCJ=POP(IRREPJ)
       NVRTJ=VRT(IRREPJ)
C
C        DETERMINE IRREPI WHOSE DIRECT PRODUCT WITH IRREPJ GIVES IRREP
C
       IRREPI=DIRPRD(IRREP,IRREPJ)
C
C        GET NUMBER OF OCCUPIED ORBITALS FOR IRREPI
C
       NOCCI=POP(IRREPI)
C
C        IF NOCCI, NOCCJ, OR NVRTJ EQUAL ZERO, NOTHING TO COMPUTE
C
       IF(MIN(NOCCI,NVRTJ,NOCCJ).NE.0) THEN
C
          CALL XGEMM('T','N',NVRTJ,NOCCJ,NOCCI*DISSYT,FACT,
     &             G(1,JOFFG),NOCCI*DISSYT,W(1,JOFFW),
     &             NOCCI*DISSYT,ONE,AIOV(IOFF),NVRTJ)
       ENDIF
C
C  UPDATE OFFSETS
C
       JOFFG=JOFFG+NOCCI*NVRTJ
       JOFFW=JOFFW+NOCCI*NOCCJ
       IOFF=IOFF+NVRTJ*NOCCJ
90    CONTINUE
C
      RETURN
      END
