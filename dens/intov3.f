C
C THIS ROUTINE CALCULATES THE THIRD TERM OF THE OCCUPIED-VIRTUAL BLOCK
C OF THE INTERMEDIATE I
C
C THE GENERAL FORMULA IS
C
C - SUM M,N,E <MI//NE> G(MA,NE)
C
C          SPIN TYPES : AA   AAAA AAAA
C                            BABA BABA
C                            BAAB BAAB
C                       BB   BBBB BBBB
C                            ABAB ABAB
C                            ABBA ABBA
C
C THIS SUBROUTINE USES EXPLICITELY SYMMETRY
C
C NOTE THAT THE GAMMA AMPLITUDES HAVE TO BE RESORTED BEFORE
C CALLING INTOV3. THIS IS DONE IN SORTGAM
C
C CODED  AUGUST/90  JG
C 
      SUBROUTINE INTOV3(AIOV,ICORE,MAXCOR,IUHF)     
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,DISSYT,DISSYW,POP,VRT
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD,UCC
      DIMENSION ICORE(MAXCOR),AIOV(1)
      COMMON /SYM2/ POP(8,2),VRT(8,2),NTAA,NTBB,NF1AA,NF1BB,NF2AA,NF2BB
      COMMON /INFO/ NOCCO(2),NVRTO(2)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /FILES/ LUOUT,MOINTS
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD,UCC 
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYMPOP2/IRPDPD(8,22)
      COMMON/SYMPOP/IRP_DM(8,22),ISYTYP(2,500),NTOT(18) 
      COMMON /CONTROL/ IPRNT,IXXX,IXXX2
      COMMON /SHIFT/ ISHIFT
C
      DATA ONE,ONEM /1.0D+0,-1.0D+0/
c 
      MXCOR=MAXCOR
C
      DO 1000 ISPIN=1,IUHF+1 
C
       IF(ISPIN.EQ.1) THEN
        IOFF=1
       ELSE
        IOFF=NTAA+1
       ENDIF
C
C      AA AND BB SPIN CASES
C
C  LISTG :    GAMMA AMPLITUDES
C  LISTW :    INTEGRALS 
C  FACT :     PREFACTOR
C
C
c      call izero(aiov(ioff),ntaa*iintfp)
       LISTW=6+ISPIN + ISHIFT 
       LISTG=122+ISPIN
       FACT=ONEM
C
C LOOP OVER IRREPS OF EF BLOCK (THE SAME IRREPS AS THE IN AND MN BLOCKS 
C HAVE
C
       DO 100 IRREP=1,NIRREP
C
C DETERMINE LENGTH OF EXPANDED OCCUPIED-OCCUPIED BLOCK
C
        NOCCSQ=0 
        DO 110 IRREPJ=1,NIRREP
         NOCCSQ=NOCCSQ+POP(IRREPJ,ISPIN)
     &                    *POP(DIRPRD(IRREPJ,IRREP),ISPIN)
110     CONTINUE 
        DISSYT=IRPDPD(IRREP,ISYTYP(1,LISTG))
        NUMSYT=IRPDPD(IRREP,ISYTYP(2,LISTG))
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
        NUMSYW=IRPDPD(IRREP,ISYTYP(2,LISTW))  
        I001=1
        I002=I001+IINTFP*MAX(NUMSYW*DISSYW,NUMSYT*DISSYT)
        I003=I002+IINTFP*NUMSYW*NOCCSQ
        I004=I003+3*IINTFP*MAX(NUMSYT,NUMSYW,DISSYT,DISSYW)
        IF(MIN(NUMSYT,DISSYT,NUMSYW,DISSYW).NE.0) THEN
         IF(MXCOR.GE.I004) THEN
C  
C         IN CORE VERSION
C
          CALL IOV3AA(ICORE(I001),ICORE(I002),AIOV(IOFF),FACT,
     &                ISPIN,POP(1,ISPIN),VRT(1,ISPIN),DISSYT,NUMSYT,
     &                DISSYW,NUMSYW,LISTG,LISTW,IRREP,ICORE(I003))
         ELSE
          STOP 'IOV3AA'
         ENDIF
        ELSE
        ENDIF 
100    CONTINUE
c       do 8976 ii=1,ntaa
c        write(*,*) ii, AIOV(ioff-1+ii)
c8976   continue
c       call izero(aiov(ioff),iintfp*ntaa)
C
C       AB SPIN CASE
C
       LISTW=8+ISPIN+1-IUHF + ISHIFT 
       LISTG=124+ISPIN
       FACT=ONEM
C
C      LOOP OVER IRREPS.
C
       DO 200 IRREP=1,NIRREP
C
        DISSYT=IRPDPD(IRREP,ISYTYP(1,LISTG))
        NUMSYT=IRPDPD(IRREP,ISYTYP(2,LISTG))
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
        NUMSYW=IRPDPD(IRREP,ISYTYP(2,LISTW))
        I001=1
        I002=I001+IINTFP*MAX(NUMSYT*DISSYT,NUMSYW*DISSYW)
        I003=I002+IINTFP*NUMSYW*DISSYW
        IF(MIN(NUMSYT,DISSYT,NUMSYW,DISSYW).NE.0) THEN
         I004=I003+3*IINTFP*MAX(NUMSYT,DISSYT,NUMSYW,DISSYW)
         IF(MXCOR.GE.I004) THEN 
C
C         IN CORE VERSION
C
          CALL IOV3AB(ICORE(I001),ICORE(I002),
     &                AIOV(IOFF),FACT,ISPIN,POP(1,ISPIN),
     &                POP(1,3-ISPIN),VRT(1,ISPIN),VRT(1,3-ISPIN),
     &                DISSYT,NUMSYT,DISSYW,NUMSYW,LISTG,LISTW,
     &                IRREP,ICORE(I003),IUHF)
         ELSE
          STOP 'IOV3AB'
         ENDIF
        ELSE
C
C
        ENDIF
200   CONTINUE
c       do 8977 ii=1,ntaa
c        write(*,*) ii, AIOV(ioff-1+ii)
c8977   continue
c       call izero(aiov(ioff),iintfp*ntaa)

C       BA SPIN CASE
C
       LISTW=11-ISPIN + ISHIFT 
       LISTG=116+ISPIN+1-IUHF
C
C DETERMINE LISTNUMBERS FOR GETTING SYMMETRY STUFF
C
       IF(LISTG.EQ.117) LISTGN=117
       IF(LISTG.EQ.118) LISTGN=118
       FACT=ONEM
C
C      LOOP OVER IRREPS.
C
       DO 300 IRREP=1,NIRREP
C
        DISSYT=IRPDPD(IRREP,ISYTYP(1,LISTGN))
        NUMSYT=IRPDPD(IRREP,ISYTYP(2,LISTGN))
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
        NUMSYW=IRPDPD(IRREP,ISYTYP(2,LISTW))
        I001=1
        I002=I001+IINTFP*MAX(NUMSYT*DISSYT,NUMSYW*DISSYW)
        I003=I002+IINTFP*NUMSYW*DISSYW
        IF(MIN(NUMSYT,DISSYT,NUMSYW,DISSYW).NE.0) THEN
         I004=I003+3*IINTFP*MAX(NUMSYT,DISSYT,NUMSYW,DISSYW)
         IF(MXCOR.GE.I004) THEN
C
C         IN CORE VERSION
C
          CALL IOV3BA(ICORE(I001),ICORE(I002),
     &                AIOV(IOFF),FACT,ISPIN,POP(1,ISPIN),
     &                POP(1,3-ISPIN),VRT(1,ISPIN),VRT(1,3-ISPIN),
     &                DISSYT,NUMSYT,DISSYW,NUMSYW,LISTG,LISTW,
     &                IRREP,ICORE(I003),IUHF)
         ELSE
          STOP 'IOV3BA'
         ENDIF
        ELSE
C
C
        ENDIF
300   CONTINUE
C      Print*, "After phph"
C       do 8973 ii=1,ntaa
C        write(*,*) ii, AIOV(ioff-1+ii)
C8973   continue
1000  CONTINUE
C
      RETURN
      END
