      SUBROUTINE XIA4AB(G,W,XIA,FACT,ISPIN,POP1,POP2,VRT1,VRT2,
     &                  DISSYT,NUMSYT,DISSYW,NUMSYW,LISTG,LISTW,IRREP,
     &                  TMP,IUHF)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DISSYT,DISSYW,DIRPRD,POP1,POP2,VRT1,VRT2
      DIMENSION G(DISSYT,1),W(DISSYW,1),XIA(1),TMP(1)
      DIMENSION POP1(8),POP2(8),VRT1(8),VRT2(8) 
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      common /dropgeo/ ndrgeo
C
      DATA ONE,ONEM,TWO /1.0D0,-1.0D0,2.0D0/
C
C      PICK UP FIRST THE G AMPLITUDES AND INTEGRALS
C
       if (ndrgeo.eq.0) CALL GETLST(G,1,NUMSYT,1,IRREP,LISTG)
       if (ndrgeo.eq.1) 
     x      CALL GETGO2(G,W,1,NUMSYT,1,IRREP,LISTG,ispin,listg,dissyt)
C
C SPIN ADAPTED CODE FOR RHF ( FORM SPIN ADAPTED GAMMA AMPLITUDES)
C
       IF(IUHF.EQ.0) THEN
        CALL SPINAD1(IRREP,POP1,DISSYT,G,TMP,TMP(1+DISSYT))
       ENDIF
C
       CALL GETLST(W,1,NUMSYW,2,IRREP,LISTW)
C
C  TRANSPOSE THE LAST TWO INDICES IN THE AA CASE (IN RHF TAKE ADVANTAGE
C  OF THE SPIN SYMMETRY AND OMIT THE TRANSPOSITION)
C
      IF(ISPIN.EQ.1.AND.IUHF.EQ.1) THEN
        CALL SYMTR1(IRREP,POP1,POP2,DISSYT,G,TMP,TMP(1+DISSYT),
     &              TMP(1+2*DISSYT))
        CALL SYMTR1(IRREP,VRT1,POP2,DISSYW,W,TMP,TMP(1+DISSYW),
     &              TMP(1+2*DISSYW))
      ENDIF

C
C  PERFORM MULTIPLICATION
C
C  JOFFW AND JOFFG OFFSET IN THE OCCUPIED-OCCUPIED BLOCK OF G AND
C  THE OCCUPIED-VIRTUAL BLOCK OF W
C  IOFF OFFSET IN AIVV
C
      IOFF=1
      JOFFW=1
      JOFFG=1
      DO 90 IRREPI=1,NIRREP
C          
C        GET NUMBER OF VIRTUAL AND OCCUPIED ORBITALS FOR IRREPI
C
       NVRTI=VRT1(IRREPI)
       NOCCI=POP1(IRREPI)
C
C        DETERMINE IRREPJ WHOSE DIRECT PRODUCT WITH IRREPI GIVES IRREP
C
       IRREPJ=DIRPRD(IRREP,IRREPI)
C
C        GET NUMBER OF OCCUPIED ORBITALS FOR IRREJP
C
       NOCCJ=POP2(IRREPJ)
C
C
       IF(MIN(NOCCJ,NVRTI,NOCCI).NE.0) THEN
C
          CALL XGEMM('T','N',NVRTI,NOCCI,DISSYT*NOCCJ,FACT,
     &               W(1,JOFFW),NOCCJ*DISSYT,G(1,JOFFG),
     &               NOCCJ*DISSYT,ONE,XIA(IOFF),NVRTI)
       ENDIF
C
C  UPDATE THE OFFSETS
C
       JOFFW=JOFFW+NVRTI*NOCCJ
       JOFFG=JOFFG+NOCCJ*NOCCI
       IOFF=IOFF+NVRTI*NOCCI
90    CONTINUE
C
      RETURN
      END
