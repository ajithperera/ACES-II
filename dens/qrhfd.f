      SUBROUTINE QRHFD (XAIA,XAIB,X1IA,X1IB,XA2A,XA2B,
     &                  DROOA,DROOB,DRVVA,DRVVB,DRVOA,DRVOB,
     &                  SCR)  
C
C THIS ROUTINE REORDERS THE RELAXATION CONTRIBUTION TO THE
C DENSITY MATRIX, SO THAT FINALLY THE FULL UNPACKED RELAXATION
C CONTRIBUTION TO THE DENSITY CAN BE FORMED.
C
C THIS ROUTINE IS ONLY REQUIRED FOR QRHF CASES WHERE THE
C HF- REFERENCE FUNCTION HAS A DIFFERENT OCCUPATION THAN THE 
C REFERENCE FUNCTION OF THE CC CALCULATION.
C
C THE REORDERED CONTRIBUTION ARE RETURNED IN DROOA, DROOB,
C DRVVA, DRVVB, DRVOA, AND DRVOB.
C
CEND 
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,POP,VRT,POPRHF,VRTRHF,POPDOC,VRTDOC
      DIMENSION XAIA(1),XAIB(1),X1IA(1),X1IB(1),XA2A(1),XA2B(1)
      DIMENSION DROOA(1),DRVOA(1),DRVVA(1)
      DIMENSION DROOB(1),DRVOB(1),DRVVB(1)
      DIMENSION SCR(1)
C
      COMMON/QRHFINF/POPRHF(8),VRTRHF(8),NOSH1(8),NOSH2(8),
     &               POPDOC(8),VRTDOC(8),NAI,N1I,N2A,
c&line mod
     &               NUMISCF,NUMASCF,ISPINP,ISPINM,IQRHF
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/FLAGS/IFLAGS(100)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
C
      IOFFAI=1
      IOFF1I=1
      IOFFA2=1
      IOFFOOA=1
      IOFFVOA=1
      IOFFVVA=1
      IOFFOOB=1
      IOFFVOB=1
      IOFFVVB=1
C
C LOOP OVER SYMMETRY BLOCKS
C
      DO 10 IRREP=1,NIRREP
C
       NOCCA  =POP(IRREP,1)
       NOCCB  =POP(IRREP,2)
       NVRTA  =VRT(IRREP,1)
       NVRTB  =VRT(IRREP,2)
       NOCCRHF=POPRHF(IRREP)
       NVRTRHF=VRTRHF(IRREP)
       NDOCC  =POPDOC(IRREP)
       NDVRT  =VRTDOC(IRREP)
C
C  NUM1 - NUMBER OF OPEN-SHELL ORBITALS WHICH ARE
C         OCCUPIED IN RHF REFERENCE   (QRHF_P TYPE)
C  NUM2 - NUMBER OF OPEN-SHELL ORBITALS WHICH ARE 
C         UNOCCUPIED IN RHF REFERENCE (QRHF_M TYPE)
C
       NUM1   =NOSH1(IRREP)
       NUM2   =NOSH2(IRREP)
       NSIZE  =NOCCA+NVRTA
       I000   =1
       I010   =I000+NSIZE*NSIZE
C
C EXPAND THE LOWER TRIANGLE ALPHA RELAXATION DENSITY TO FULL FORMAT
C AND THEN COPY BACK INTO THE QRHF OCCUPATION
C
       CALL ZERO(SCR,NSIZE*NSIZE)
       CALL BLKCPY(XAIA(IOFFAI),NVRTRHF,NOCCRHF,SCR,NSIZE,NSIZE,
     &             NOCCRHF+1,1)
       CALL BLKCPY(X1IA(IOFF1I),NUM1,NDOCC,SCR,NSIZE,NSIZE,
     &             NDOCC+1,1)
       CALL BLKCPY(XA2A(IOFFA2),NDVRT,NUM2,SCR,NSIZE,NSIZE,
     &             NOCCRHF+NUM2+1,NOCCRHF+1)
C
C FILL UPPER TRIANGLE
C
       CALL SQUEZ2 (SCR,SCR(I010),NSIZE)
       CALL EXPND2 (SCR(I010),SCR,NSIZE)
C
       CALL BLKCPY2(SCR,NSIZE,NSIZE,DROOA(IOFFOOA),
     &              NOCCA,NOCCA,1,1)
       CALL BLKCPY2(SCR,NSIZE,NSIZE,DRVOA(IOFFVOA),
     &              NVRTA,NOCCA,NOCCA+1,1)
       CALL BLKCPY2(SCR,NSIZE,NSIZE,DRVVA(IOFFVVA),
     &              NVRTA,NVRTA,NOCCA+1,NOCCA+1)
C
C NOW DO SAME FOR BETA
C
       CALL ZERO(SCR,NSIZE*NSIZE)
       CALL BLKCPY(XAIB(IOFFAI),NVRTRHF,NOCCRHF,SCR,NSIZE,NSIZE,
     &             NOCCRHF+1,1)
       CALL BLKCPY(X1IB(IOFF1I),NUM1,NDOCC,SCR,NSIZE,NSIZE,
     &             NDOCC+1,1)
       CALL BLKCPY(XA2B(IOFFA2),NDVRT,NUM2,SCR,NSIZE,NSIZE,
     &             NOCCRHF+NUM2+1,NOCCRHF+1)
C
C FILL UPPER TRIANGLE
C
       CALL SQUEZ2 (SCR,SCR(I010),NSIZE)
       CALL EXPND2 (SCR(I010),SCR,NSIZE)
C
       CALL BLKCPY2(SCR,NSIZE,NSIZE,DROOB(IOFFOOB),
     &              NOCCB,NOCCB,1,1)
       CALL BLKCPY2(SCR,NSIZE,NSIZE,DRVOB(IOFFVOB),
     &              NVRTB,NOCCB,NOCCB+1,1)
       CALL BLKCPY2(SCR,NSIZE,NSIZE,DRVVB(IOFFVVB),
     &              NVRTB,NVRTB,NOCCB+1,NOCCB+1)
C
       IOFFAI=IOFFAI+NOCCRHF*NVRTRHF
       IOFF1I=IOFF1I+NUM1*NDOCC
       IOFFA2=IOFFA2+NUM2*NDVRT
       IOFFOOA=IOFFOOA+POP(IRREP,1)*POP(IRREP,1)
       IOFFVOA=IOFFVOA+VRT(IRREP,1)*POP(IRREP,1)
       IOFFVVA=IOFFVVA+VRT(IRREP,1)*VRT(IRREP,1)
       IOFFOOB=IOFFOOB+POP(IRREP,2)*POP(IRREP,2)
       IOFFVOB=IOFFVOB+VRT(IRREP,2)*POP(IRREP,2)
       IOFFVVB=IOFFVVB+VRT(IRREP,2)*VRT(IRREP,2)
C
10    CONTINUE
C
C ALL DONE!
C
      RETURN
      END
