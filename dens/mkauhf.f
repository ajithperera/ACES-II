
      SUBROUTINE MKAUHF(AMAT,SCRATCH,icore,maxcor,bRedundant)
C
C   THIS ROUTINE FORMS THE A MATRIX REQUIRED FOR THE SOLUTION 
C   OF THE CPHF EQUATION FOR GENERAL UHF WAVEFUNCTION
C
C   NOTE, THAT ONLY ONE BLOCK OF THE A MATRIX IS COMPUTED 
C
C   THE FORMULAS ARE GIVEN BY
C
C   A(AI,BJ) = <AB//IJ> + <AJ//IB>      AA-BLOCK
C
C   A(ai,bj) = <ab//ij> + <aj//ib>      BB-BLOCK
C
C   A(AI,bj) = 2  <Ab//Ij>              AB-BLOCK
C
C   A(ai,BJ) = 2  <aB//iJ>              BA-BLOCK (TRANPOSE OF AB-BLOCK)
C  
C AMAT HOLDS ALL FOUR PARTS OF THE A MATRIX 
C SCRATCH IS A SCRATCH ARRAY REQUIRED FOR CONTRUCTING A
C IRREP IS THE CORRESPONDING IRREP OF THE FORMED A MATRIX
C
CEND
C
C CODED JULY/90  JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DISSYW,DIRPRD,POP,VRT,maxcor
      integer reflist
      LOGICAL bRedundant
      character*4 SPINCASE
      DIMENSION AMAT(1),SCRATCH(1),icore(1)
      COMMON/INFO/NOCCO(2),NVRTO(2)
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYMPOP2/IRPDPD(8,22)
      COMMON/SYMPOP/IRP_DM(8,22),ISYTYP(2,500),NTOT(18)
      COMMON/SYM2/POP(8,2),VRT(8,2),NJUNK(6)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      common /dropgeo/ ndrgeo 
      COMMON /SHIFT/ ISHIFT 
C
      DATA ONE,ONEM,TWO /1.0D+0,-1.0D+0,2.0D+0/
C
C     FORM FIRST THE AA AND BB-BLOCK
C
C  LOOP OVER SPINS
C
      IOFFA=1
      DO 1000 ISPIN=1,2
C
C FIRST READ IN THE INTEGRALS <AB//IJ> (ORDERING AI,BJ)
C THEY ARE STORED ON LIST 19
C
      LISTW=18+ISPIN + ISHIFT 

      NUMSYW=IRPDPD(1,ISYTYP(2,LISTW))
      DISSYW=IRPDPD(1,ISYTYP(1,LISTW))
C
      IF(bRedundant) THEN
        CALL GETLST(AMAT(IOFFA),1,NUMSYW,2,1,LISTW)
      ELSE
        CALL GETLST_NR(AMAT(IOFFA),ICORE(1),MAXCOR,LISTW,1)
      ENDIF

C
C NOW ADD THE INTEGRALS <AJ//BI> (ORDERING AI,BJ)
C THEY ARE STORED ON LIST 23, HOWEVER TAKE CARE OF
C THE SIGN, SINCE THEY ARE ACTUALLY <JA//BI>
C
      LISTW=22+ISPIN + ISHIFT 
      NUMSYW=IRPDPD(1,ISYTYP(2,LISTW))
      DISSYW=IRPDPD(1,ISYTYP(1,LISTW))
C
      CALL GETLST(SCRATCH,1,NUMSYW,2,1,LISTW)
      CALL SAXPY(NUMSYW*DISSYW,ONEM,SCRATCH,1,AMAT(IOFFA),1)
C
C  UPDATE THE OFFSET FOR THE A MATRIX PART
C
      IOFFA=IOFFA+NUMSYW*DISSYW
C
1000  CONTINUE
C
C   FORM NOW THE AB BLOCK (THE BA BLOCK IS THE TRANSPOSED AS LONG
C   AS WE NOT SCALE WITH THE ORBITAL ENERGY DENOMINATOR
C
      LISTW=18 + ISHIFT 

      NUMSYW=IRPDPD(1,ISYTYP(2,LISTW))
      DISSYW=IRPDPD(1,ISYTYP(1,LISTW))
C
      IF(bRedundant) THEN
        CALL GETLST(AMAT(ioffa),1,NUMSYW,2,1,LISTW)
      ELSE
        CALL GETLST_NR(AMAT(IOFFA),ICORE(1),MAXCOR,LISTW,1)
      ENDIF

      CALL SSCAL(NUMSYW*DISSYW,TWO,AMAT(IOFFA),1)
C
C  ALL DONE, RETURN
C
      RETURN
      END
