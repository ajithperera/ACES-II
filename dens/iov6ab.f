      SUBROUTINE IOV6AB(G,W,AIOV,FACT,ISPIN,POP1,POP2,VRT1,VRT2,
     &                  DISSYT,NUMSYT,DISSYW,NUMSYW,LISTG,LISTW,IRREP,
     &                  TMP,IUHF)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DISSYT,DISSYW,DIRPRD,POP1,POP2,VRT1,VRT2
      DIMENSION G(DISSYT,1),W(DISSYW,1),AIOV(1),TMP(1)
      DIMENSION POP1(8),POP2(8),VRT1(8),VRT2(8) 
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      common /dropgeo/ ndrgeo
C
      DATA ONE,ONEM,TWO /1.0D0,-1.D0,2.D0/
C
C PCIK UP THE G AMPLITUDES
C
       if (ndrgeo.eq.0) then
         CALL GETLST(G,1,NUMSYT,1,IRREP,LISTG)
       else 
        if (iuhf.eq.1 .and. ispin.eq.1) then 
         CALL GETGO6R(G,W,1,NUMSYT,1,IRREP,LISTG,ispin,listg,dissyt)
        else 
         CALL GETGO6(G,W,1,NUMSYT,1,IRREP,LISTG,ispin,listg,dissyt)
        endif 
       endif 
C
C      PICK UP THE INTEGRALS
C
       CALL GETLST(W,1,NUMSYW,2,IRREP,LISTW)
C
C  SPIN ADAPTED CODE FOR RHF
C
      IF(IUHF.EQ.0) THEN
       CALL SPINAD1(IRREP,POP1,DISSYW,W,TMP,TMP(1+DISSYW))
      ENDIF
C
C  TRANSPOSE THE LAST TWO INDICES IN THE AA CASE
C
      IF(ISPIN.EQ.1.AND.IUHF.EQ.1) THEN
        CALL SYMTR1(IRREP,VRT1,POP2,DISSYT,G,TMP,TMP(1+DISSYT),
     &              TMP(1+2*DISSYT))
        CALL SYMTR1(IRREP,POP1,POP2,DISSYW,W,TMP,TMP(1+DISSYW),
     &              TMP(1+2*DISSYW))
      ENDIF
C
C  PERFORM MULTIPLICATION
C
C  JOFFW AND JOFFG OFFSET IN THE RIGHTMOST BLOCK OF G AND W
C  IOFF OFFSET IN AIVV
C
      IOFF=1
      JOFFW=1
      JOFFG=1
      DO 90 IRREPI=1,NIRREP
C          
C        GET NUMBER OF OCCUPIED AND VIRTUAL ORBITALS FOR IRREPJ
C
       NVRTI=VRT1(IRREPI)
       NOCCI=POP1(IRREPI)
C
C        DETERMINE IRREPI WHOSE DIRECT PRODUCT WITH IRREPJ GIVES IRREP
C
       IRREPJ=DIRPRD(IRREP,IRREPI)
C
C        GET NUMBER OF OCCUPIED ORBITALS FOR KRREP
C
       NOCCJ=POP2(IRREPJ)
C
C        IF NVRTI, NOCCI OR NOCCJ EQUAL ZERO, NOTHING TO COMPUTE
C
       IF(MIN(NOCCJ,NVRTI,NOCCI).NE.0) THEN
C
          CALL XGEMM('T','N',NVRTI,NOCCI,DISSYT*NOCCJ,FACT,
     &               G(1,JOFFG),NOCCJ*DISSYT,W(1,JOFFW),
     &               NOCCJ*DISSYT,ONE,AIOV(IOFF),NVRTI)
       ENDIF
C
C  UPDATE THE OFFSETS
C
       JOFFW=JOFFW+NOCCI*NOCCJ
       JOFFG=JOFFG+NOCCJ*NVRTI
       IOFF=IOFF+NVRTI*NOCCI
90    CONTINUE
C
      RETURN
      END
