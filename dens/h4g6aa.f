
      SUBROUTINE H4G6AA(H,H1,G,G1,T1,FACT,DISSYH,DISSYG,NUMSYH,
     &                  NUMSYG,NOCCSQ,POP,VRT,LISTH,LISTG,IRREP,
     &                  TMP,TRIP,LISTG1)
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL TRIP,CIS,EOM,LTRP
      INTEGER DISSYH,DISSYG,DIRPRD,POP,VRT
      DIMENSION H1(DISSYH,NUMSYH),H(NUMSYH,DISSYH),
     &          G(NUMSYG,NOCCSQ),G1(DISSYG,NUMSYG),T1(1),TMP(1)
      DIMENSION POP(8),VRT(8)
C
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/EXCITE/CIS,EOM
      COMMON/LTRIP/LTRP
C
      DATA ONE,AZERO /1.0D0,0.D0/
C
C  GET RESORTED H4 FROM DISK
C
      CALL GETLST(H1,1,NUMSYH,1,IRREP,LISTH)
C
C  TRANPOSE H4    FROM F,I ; A,K  TO A,K ; F,I
C
      CALL TRANSP(H1,H,NUMSYH,DISSYH)
C
C  ZERO OUTPUT ARRAY
C
      CALL ZERO(G,NUMSYG*NOCCSQ)
C
C  TRANSPOSE THE LAST TWO INDICES:  A,K ; F,I TO A,K ; I,F
C
      CALL SYMTR1(IRREP,VRT,POP,NUMSYH,H,TMP,TMP(1+NUMSYH),
     &             TMP(1+2*NUMSYH))
C
C  LOOP OVER IRREPS OF F AND MULTIPLY
C
      IOFFH=1
      IOFFG=1
      IOFFT=1
      DO 100 IRREPJ=1,NIRREP
C
C  GET POPULATIONS
C
       NVRTJ=VRT(IRREPJ)
       NOCCJ=POP(IRREPJ)
       IRREPI=DIRPRD(IRREPJ,IRREP)
       NOCCI=POP(IRREPI)
C
C IF ONE OF THE POPULATIONS IS ZERO, SKIP MULTIPLICATION 
C
       IF(MIN(NVRTJ,NOCCI,NOCCJ).NE.0) THEN
C
        CALL XGEMM('N','N',NUMSYH*NOCCI,NOCCJ,NVRTJ,FACT,H(1,IOFFH),
     &             NUMSYH*NOCCI,T1(IOFFT),NVRTJ,AZERO,G(1,IOFFG),
     &             NUMSYG*NOCCI)
       ENDIF
C
C UPDATE OFFSETS
C
       IOFFT=IOFFT+NOCCJ*NVRTJ
       IOFFG=IOFFG+NOCCI*NOCCJ
       IOFFH=IOFFH+NOCCI*NVRTJ
C
100    CONTINUE
C
C  ANTISYMMETRIZE G IN THE LAST TWO INDICES    : A,K ; I<J
C
       CALL ASSYM(IRREP,POP,NUMSYG,NUMSYG,G1,G)
C
C  TRANSPOSE THE WHOLE G MATRIX : --> I<J ; A,K
C
       CALL TRANSP(G1,G,DISSYG,NUMSYG)
C
C  TRANPOSE LAST TWO INDICES IN G --> I<J ; K,A
C
       CALL SYMTR1(IRREP,VRT,POP,DISSYG,G,TMP,TMP(1+DISSYG),
     &             TMP(1+2*DISSYG))
C
C  IN CASE OF TRIPLE EXCITATIONS, ADD HERE THEIR CONTRIBUTION
C
       IF(TRIP) THEN
        CALL GETLST(G1,1,NUMSYG,1,IRREP,LISTG1)
        CALL SAXPY(NUMSYG*DISSYG,ONE,G1,1,G,1)
       ENDIF
       IF(EOM .OR. LTRP) THEN
        CALL GETLST(G1,1,NUMSYG,1,IRREP,LISTG)
        CALL SAXPY(NUMSYG*DISSYG,ONE,G1,1,G,1)
       ENDIF
C
C  CURRENTLY, SAVE IT
C
       CALL PUTLST(G,1,NUMSYG,1,IRREP,LISTG)
C
C  ALL DONE, RETURN
C
       RETURN
       END
