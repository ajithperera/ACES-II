      SUBROUTINE T12ING4(ICORE,MAXCOR,IUHF,bRedundant)
C
C THIS ROUTINE CALCULATES THE FOLLOWING TERM REQUIRED FOR CCSD
C
C  - 1/2  P((IB),(JA)) SUM M E  T(E,I) T(A,M) L(JM,EB)
C
C  IN RHF THERE ARE TWO REMAINING TERMS
C
C         - SUM m E T(e,i) T(A,M) L(jM,eB)   (TARGET LIST 125)
C
C         - SUM m e T(e,i) T(a,m) L(Jm,eB)   (TARGET LIST 118)
C
C  IN UHF , THERE ARE FOUR ADDITIONAL TERMS REQUIRED
C
C         - SUM M E T(E,I) T(A,M) L(JM,EB)  (TARGET LIST 123)
C
C         - SUM m e T(e,i) T(a,m) L(jm,eb)  (TARGET LIST 124)
C
C         - SUM M E T(E,I) T(A,M) L(jM,Eb)  (TARGET LIST 117)
C
C         - SUM M e T(E,I) T(a,m) L(Jm,Eb)  (TARGET LIST 126)
C
C  THE MULTIPLICATION IS CARRIED OUT IN T12G41 AND T12G42 
C
C  123, 124, 125, AND 126 CAN BE CALCULATED SEPARATELY AND
C  THE SYMMETRIZED.
C
C  117 AND 118 MUST BE CALCULATED TOGETHER SINCE THE SYMTTERIZATION
C  REQUIRES BOTH LISTS
C
C  NOTE THAT FOR ALL TERMS THE OVERALL SIGN IS ONEM, FOR 117,118
C  THE LISTS ARE STORED AS THE NEGATIVE OF THE ACTUAL TARGET, BUT
C  THE CORRESPONDING LAMBDA IS ALSO THE NEGATIVE  
C
CEND
C
C   CODED  SEPTEMBER/90
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,DISSYW,DISSYZ,DISTMP,VRT,POP
      INTEGER REFSIZT, TARSIZT,REFLIST
      logical bRedundant
      CHARACTER*4 SPINCASE
      DIMENSION ICORE(MAXCOR),IOFFT1(2)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWO
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &                DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
      COMMON /SYM/ POP(8,2),VRT(8,2),NTAA,NTBB,NF1AA,NF1BB,
     &             NF2AA,NF2BB
      COMMON /INFO/ NOCC(2),NVRTO(2)
C
      DATA AZERO,ONE,ONEM /0.D0,1.D0,-1.D0/
C
C ALLOCATE MEMORY FOR T1 AMPLITUDE AND GET THEM FROM DISK
C
      MXCOR=MAXCOR
      IOFFT1(1)=MXCOR+1-IINTFP*NTAA 
      MXCOR=MXCOR-IINTFP*NTAA
      CALL GETLST(ICORE(IOFFT1(1)),1,1,1,1,90)
      IF(IUHF.EQ.0) THEN
       IOFFT1(2)=IOFFT1(1)
      ELSE
       IOFFT1(2)=IOFFT1(1)-IINTFP*NTBB
       MXCOR=MXCOR-IINTFP*NTBB
       CALL GETLST(ICORE(IOFFT1(2)),1,1,1,2,90)
      ENDIF
C 
C    AAAA AND BBBB SPIN CASES (ONLY NECCESARY FOR UHF)
C
C         - SUM M E T(E,I) T(A,M) L(JM,EB)  (TARGET LIST 123)
C
C         - SUM m e T(e,i) T(a,m) L(jm,eb)  (TARGET LIST 124)
C
      IF(IUHF.EQ.1) THEN
C
       DO 100 ISPIN=1,2
C
C     PREFACTOR OF THE CORRESPONDING CONTRIBUTION
C
        FACT=ONEM
C
C     LAMBDA (LISTW) AND TARGET (LISTZ) LIST
C
        LISTW=133+ISPIN
        LISTZ=122+ISPIN
 
C
C    LOOP OVER IRREP
C
        DO 50 IRREP=1,NIRREP
C
         NOCCSQ=0
         DO 45 IRREPJ=1,NIRREP
          IRREPI=DIRPRD(IRREP,IRREPJ)
          NOCCSQ=NOCCSQ+POP(IRREPJ,ISPIN)*POP(IRREPI,ISPIN)
45       CONTINUE
C
         DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
         NUMSYW=IRPDPD(IRREP,ISYTYP(2,LISTW))
         DISSYZ=IRPDPD(IRREP,ISYTYP(1,LISTZ))
         NUMSYZ=IRPDPD(IRREP,ISYTYP(2,LISTZ))
         DISTMP=DISSYZ
         NUMTMP=NUMSYZ
         MSIZE=MAX(NUMSYW*DISSYW,NUMSYZ*DISSYZ,DISSYZ*NOCCSQ) 
         I001=1
         I002=I001+IINTFP*MSIZE
         I003=I002+IINTFP*MSIZE
         IF(I003.LT.MXCOR) THEN

C
C  IN CORE MULTIPLICATION
C
          CALL T12G41(ICORE(I001),ICORE(I002),ICORE(I003),
     &                MXCOR-I003,ICORE(IOFFT1(ISPIN)),
     &                ICORE(IOFFT1(ISPIN)),DISSYW,DISSYZ,DISTMP,
     &                NUMSYW,NUMSYZ,NUMTMP,NOCCSQ,POP(1,ISPIN),
     &                POP(1,ISPIN),VRT(1,ISPIN),VRT(1,ISPIN),LISTW,
     &                LISTZ,FACT,IRREP,bRedundant)
         ELSE
C 
C    SORRY, NO OUT CORE ALGORITHM AVAILABLE 
C
          CALL INSMEM('T12G4',I003,MXCOR)
         ENDIF
50      CONTINUE
100    CONTINUE
      ENDIF
C
C   ABAB SPIN CASE
C
C         - SUM M,e T(e,i) T(A,M) L(jM,eB)   (TARGET LIST 125)
C
C         - SUM m,E T(E,I) T(a,m) L(Jm,Eb)  (TARGET LIST 126)
C
      DO 200 ISPIN=1,IUHF+1
C
       FACT=ONEM
C
C    ON THE FIRST PASS, INITIALIZE OUTPUT LIST AND DO NOT TRANSPOSE
C    RESULT, ON THE SECOND PASS, UPDATE OUTPUT LIST AND TRANSPOSE
C
       I0T1=IOFFT1(3-ISPIN)
       I0T2=IOFFT1(ISPIN)
C
C  INTEGRAL AND TARGET LIST
C
       LISTW=140-ISPIN
       LISTZ=124+ISPIN

       DO 150 IRREP=1,NIRREP
C
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
        NUMSYW=IRPDPD(IRREP,ISYTYP(2,LISTW))
        DISSYZ=IRPDPD(IRREP,ISYTYP(1,LISTZ))
        NUMSYZ=IRPDPD(IRREP,ISYTYP(2,LISTZ))
        DISTMP=DISSYZ
        NUMTMP=NUMSYZ
        NOCCSQ=IRPDPD(IRREP,14)
        MSIZE=MAX(NUMSYW*DISSYW,NUMSYZ*DISSYZ,DISSYZ*NOCCSQ)
        I001=1
        I002=I001+IINTFP*MSIZE
        I003=I002+IINTFP*MSIZE 
        IF(I003.LT.MXCOR) THEN
C
C   IN CORE ALGORITHM
C
CJDW 9/29/95.
C    IUHF was formerly the final argument, but it does not appear in
C    T12G41, so now it has been removed.
CJDW END

         CALL T12G41(ICORE(I001),ICORE(I002),ICORE(I003),MXCOR-I003,
     &               ICORE(I0T1),ICORE(I0T2),
     &               DISSYW,DISSYZ,DISTMP,NUMSYW,NUMSYZ,NUMTMP,NOCCSQ,
     &               POP(1,ISPIN),POP(1,3-ISPIN),VRT(1,ISPIN),
     &               VRT(1,3-ISPIN),LISTW,LISTZ,FACT,IRREP,bRedundant)
        ELSE
C
C   SORRY, NO OUT CORE ALGORITHM AVAILABLE
C
         CALL INSMEM('T12G4',I003,MXCOR)
        ENDIF
150    CONTINUE
200   CONTINUE
C 
C     ABAB SPIN CASE SECOND CONTRIBUTION
C
      DO 300 ISPIN=1,IUHF+1
C
       FACT=ONEM
C
C ONE THE FIRST PASS, INITIALIZE TARGET LIST AND DO NOT TRANSPOSE
C CALCULATED ARRAY, IN THE SECOND PASS, UPDATE TARGET LIST AND
C TRANSPOSE THE RESULT ARRAY
C
       I0T1=IOFFT1(3-ISPIN)
C
C  LAMBDA AND TARGET LIST
C
       LISTW=138-ISPIN
       LISTZ1=119-ISPIN
       LISTZ2=116+ISPIN


       DO 250 IRREP=1,NIRREP
C
        NOCCSQ=0
        DO 245 IRREPJ=1,NIRREP
         IRREPI=DIRPRD(IRREP,IRREPJ)
         NOCCSQ=NOCCSQ+POP(IRREPJ,3-ISPIN)*POP(IRREPI,3-ISPIN)
245     CONTINUE
C
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
        NUMSYW=IRPDPD(IRREP,ISYTYP(2,LISTW))
        DISSYZ=IRPDPD(IRREP,ISYTYP(1,LISTZ1))
        NUMSYZ=IRPDPD(IRREP,ISYTYP(2,LISTZ1))
        DISTMP=DISSYZ
        NUMTMP=NUMSYZ
        MSIZE=MAX(NUMSYW*DISSYW,NUMSYZ*DISSYZ,DISTMP*NOCCSQ,
     &            NUMTMP*NOCCSQ)
        I001=1
        I002=I001+IINTFP*MSIZE
        I003=I002+IINTFP*MSIZE
        IF(I003.LT.MXCOR) THEN
C
C IN CORE ALGORITHM
C
         CALL T12G42(ICORE(I001),ICORE(I002),ICORE(I003),MXCOR-I003,
     &               ICORE(I0T1),
     &               DISSYW,DISSYZ,DISTMP,NUMSYW,NUMSYZ,NUMTMP,NOCCSQ,
     &               POP(1,3-ISPIN),VRT(1,3-ISPIN),LISTW,LISTZ1,LISTZ2,
     &               FACT,IRREP,IUHF,bRedundant)
        ELSE 
C
C SORRY, NO OUT CORE ALGORITHM
C
         CALL INSMEM('T12G4',I003,MXCOR)
        ENDIF
250    CONTINUE
300   CONTINUE
C
C ALL DONE, RETURN
C
      RETURN
      END
