      SUBROUTINE IV4AB(G,W,AIVV,FACT,ISPIN,POP1,POP2,VRT1,VRT2,
     &                  DISSYT,NUMSYT,DISSYW,NUMSYW,LISTG,LISTW,IRREP,
     &                  TMP,IUHF)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DISSYT,DISSYW,DIRPRD,POP1,POP2,VRT1,VRT2
      DIMENSION G(DISSYT,1),W(DISSYW,1),AIVV(1000),TMP(1)
      DIMENSION POP1(8),POP2(8),VRT1(8),VRT2(8) 
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      common /dropgeo/ ndrgeo
C
      DATA ONE,ONEM,TWO /1.0D0,-1.D0,2.D0/
C
C PICK UP G AMPLITUDES AND INTEGRALS
C
       if (ndrgeo.eq.0) then
         CALL GETLST(G,1,NUMSYT,1,IRREP,LISTG)
       else 
        if (iuhf.eq.1 .and. ispin.eq.2) then 
         CALL GETGO4R(G,W,1,NUMSYT,1,IRREP,LISTG,ispin,listw,dissyt)
        else 
         CALL GETGO4(G,W,1,NUMSYT,1,IRREP,LISTG,ispin,listw,dissyt)
        endif 
       endif 
C
       CALL GETLST(W,1,NUMSYW,2,IRREP,LISTW)
C
C  SPIN ADAPTED CODE FOR RHF
C
      IF(IUHF.EQ.0) THEN
       CALL SPINAD3(IRREP,VRT1,DISSYW,NUMSYW,W,TMP,TMP(1+NUMSYW))
      ENDIF
C
C
C  TRANSPOSE THE LAST TWO INDICES IN THE AA CASE
C
      IF(ISPIN.EQ.1) THEN
        CALL SYMTR1(IRREP,VRT1,POP2,DISSYT,G,TMP,TMP(1+DISSYT),
     &              TMP(1+2*DISSYT))
        CALL SYMTR1(IRREP,VRT1,POP2,DISSYW,W,TMP,TMP(1+DISSYW),
     &              TMP(1+2*DISSYW))
      ENDIF
C
C  PERFORM MULTIPLICATION
C
C  JOFF OFFSET IN THE RIGHTMOST BLOCK OF G AND W
C  IOFF OFFSET IN AIVV
C
      IOFF=1
      JOFF=1
      DO 90 IRREPI=1,NIRREP
C          
C        GET NUMBER OF VIRTUAL ORBITALS FOR IRREPJ
C
       NVRTI=VRT1(IRREPI)
C
C        DETERMINE IRREPI WHOSE DIRECT PRODUCT WITH IRREPJ GIVES IRREP
C
       IRREPJ=DIRPRD(IRREP,IRREPI)
C
C        GET NUMBER OF OCCUPIED ORBITALS FOR KRREP
C
       NOCCJ=POP2(IRREPJ)
C
C        IF NVRTI OR NOCCJ EQUAL ZERO, NOTHING TO COMPUTE
C
       IF(MIN(NOCCJ,NVRTI).NE.0) THEN
C
          CALL XGEMM('T','N',NVRTI,NVRTI,DISSYT*NOCCJ,FACT,
     &               W(1,JOFF),NOCCJ*DISSYT,G(1,JOFF),
     &               NOCCJ*DISSYT,ONE,AIVV(IOFF),NVRTI)
       ENDIF
C
C  UPDATE THE OFFSETS
C
       JOFF=JOFF+NVRTI*NOCCJ
       IOFF=IOFF+NVRTI*NVRTI
90    CONTINUE
C
      RETURN
      END
