      SUBROUTINE GAMMA5(ICORE,MAXCOR,IUHF)
C
C
C  THIS ROUTINE COMPUTES THE GAMMA INTERMEDIATE 5
C
C  GAMMA 5 IS ONLY REQUIRED FOR FOURTH ORDER MBPT AND    
C  ALL CC METHODS WHICH INVOLVE SINGLES
C
C MBPT(4) AND UCC :
C
C  G(AB,CI) = SUM M T(M,C) T(MI,AB)
C
C QCISD :
C
C  G(AB,CI) = 1/2 SUM M L(M,C) T(MI,AB) + 1/2 SUM M T(M,C) L(MI,AB)
C
C CCSD :
C
C  G(AB,CI) = 1/2 SUM M L(M,C) TAU(MI,AB) + 1/2 SUM M T(M,C) L(MI,AB)
C
C             - 1/4 SUM M,N SUM F TAU(MN,AB) L(MN,EF) T(I,F)
C
C             - 1/2 P(AB) G(C,A) T(I,B)
C
C             - 1/2 P(AB) SUM N H(NC,IA) T(N,B)
C
C  THE FOLLOWING SPIN CASES HAVE TO BE CONSIDERED
C
C  AAAA    AA    AAAA    (UHF ONLY)
C  BBBB    BB    BBBB    (UHF ONLY)
C  ABBA    BB    BAAB    (UHF ONLY) STORED WITh NEGATIVE SIGN)
C  ABAB    AA    ABAB
C
C  THIS ROUTINE USES EXPLICITELY SYMMETRY
C
CEND
C
C  CODED AUGUST/90   JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD,DISSYT,DISSYG,DISSYH,DISSYH1,DISSYH2,POP,VRT
      LOGICAL LAMBDA,MBPT4,TAU,INCORE
      LOGICAL DENS,GRAD,QRHF,NONHF,ROHF,SEMI,CANON,TRIP1,TRIP2,
     &        GABCD,RELAXED,TRULY_NONHF
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD,UCC
      DIMENSION ICORE(MAXCOR)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/METH/MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD,UCC
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NTAA,NTBB,NF1AA,NF1BB,NF2(2) 
      COMMON/DERIV/DENS,GRAD,QRHF,NONHF,ROHF,SEMI,CANON,TRIP1,
     &             TRIP2,GABCD,RELAXED,TRULY_NONHF
      COMMON/FLAGS/IFLAGS(100)
C
      DATA ONE,ONEM,HALF,HALFM,TWO /1.D0,-1.D0,0.5D0,-0.5D0,2.D0/
C
      MXCOR=MAXCOR
      LAMBDA=CCSD.OR.QCISD
      TAU=CCSD
C
      IOFFLISTH=0
      IF(IFLAGS(3).EQ.2) IOFFLISTH=200
C
C NOTE MBPT(3) IS HERE INCLUDED FOR THE SAKE OF ROHF-MBPT(3)
C WHICH REQUIRED THE FOURTH-ORDER LOGIC
C
      MBPT4=M4SDQ.OR.M4SDTQ.OR.UCC.OR.MBPT3
C
C  ALLOCATE FIRST MEMORY FOR T1-AMPLITUDES
C
C   STRUCTURE OF THE MEMORY ALLOCATION
C
C    I0T1A =       T1 AMPLITUDES AA
C
C    I0T2A =       L1 AMPLITUDES AA  (IF CCSD OR QCISD)
C
C    I0T1B =       T1 AMPLITUDES BB                      UHF ONLY
C
C    I0T2B =       L1 AMPLITUDES BB  (IF CCSD OR QCISD)  UHF ONLY
C
      I0T1A=MXCOR+1-NTAA*IINTFP
      MXCOR=MXCOR-NTAA*IINTFP
      CALL GETLST(ICORE(I0T1A),1,1,1,1,90)  
      IF(LAMBDA) THEN
       I0T2A=I0T1A-NTAA*IINTFP
       MXCOR=MXCOR-NTAA*IINTFP
       CALL GETLST(ICORE(I0T2A),1,1,2,1,190)
      ELSE
       I0T2A=I0T1A
      ENDIF
      IF(IUHF.EQ.0) THEN
       I0T1B=I0T1A
       I0T2B=I0T2A 
      ELSE
       I0T1B=I0T2A-NTBB*IINTFP
       MXCOR=MXCOR-NTBB*IINTFP
       CALL GETLST(ICORE(I0T1B),1,1,1,2,90)
       IF(LAMBDA) THEN
        I0T2B=I0T1B-NTBB*IINTFP
        MXCOR=MXCOR-NTBB*IINTFP
        CALL GETLST(ICORE(I0T2B),1,1,2,2,190)
       ELSE
        I0T2B=I0T1B
       ENDIF
      ENDIF
C
C  NOW PERFORM MULTIPLICATION
C
      DO 1000 ISPIN=1,IUHF+1
C
      IF(ISPIN.EQ.1) THEN
       IOFFT=I0T2A
       I0T1=I0T1A
       I0T2=I0T1B
       NT=NTAA
      ELSE
       IOFFT=I0T2B
       I0T1=I0T1B
       I0T2=I0T1A
       NT=NTBB
      ENDIF
      IF(IUHF.EQ.1) THEN
C
C  FIST PERFORM AAAA MULTIPLICATION
C
C  NOTE THAT THE ACTUAl SIGN MUST HERE THE NEGATIVE OF 
C  THE SIGN GIVEN ABOVE BECAUSE WE ARE SWITCHING INDICES
C
      LISTG=126+ISPIN
      IF(MBPT4) THEN
       LISTT=43+ISPIN
       FACT=ONEM
      ELSE IF(LAMBDA) THEN
       LISTT=43+ISPIN
       LISTL=143+ISPIN
       FACT=HALFM
      ENDIF
      IF(CCSD) THEN
       LISTH=53+ISPIN+IOFFLISTH
       LISTG1=165+ISPIN
       FACTH=HALF
      ENDIF
C
      DO 100 IRREP=1,NIRREP

       NOCCSQ=0
       NVRTSQ=0
       DO 101 IRREPJ=1,NIRREP
        IRREPI=DIRPRD(IRREP,IRREPJ)
        NOCCSQ=NOCCSQ+POP(IRREPJ,ISPIN)*POP(IRREPI,ISPIN)
        NVRTSQ=NVRTSQ+VRT(IRREPJ,ISPIN)*VRT(IRREPI,ISPIN)
101    CONTINUE
       DISSYT=IRPDPD(IRREP,ISYTYP(1,LISTT))
       NUMSYT=IRPDPD(IRREP,ISYTYP(2,LISTT))
       DISSYG=IRPDPD(IRREP,ISYTYP(1,LISTG))
       NUMSYG=IRPDPD(IRREP,ISYTYP(2,LISTG))
C
C  FOR CCSD CALCULATE FIRST THE CONTRIBUTION OF H4 TO GAMMA5
C
       IF(CCSD) THEN
        DISSYH=IRPDPD(IRREP,ISYTYP(1,LISTH))
        NUMSYH=IRPDPD(IRREP,ISYTYP(2,LISTH))
        I001=1
        I002=I001+IINTFP*MAX(NUMSYH*DISSYH,DISSYG*NUMSYG) 
        I003=I002+IINTFP*NVRTSQ*NUMSYG
        I004=I003+3*IINTFP*MAX(NUMSYG,DISSYG,NUMSYH,DISSYH)
        IF(I004.LT.MXCOR) THEN
         CALL H4G5AA(ICORE(I001),ICORE(I002),
     &               ICORE(I001),ICORE(I0T1),FACTH,DISSYH,
     &               DISSYG,NUMSYH,NUMSYG,NVRTSQ,POP(1,ISPIN),
     &               VRT(1,ISPIN),LISTH,LISTG,IRREP,ICORE(I003),
     &               TRIP1,LISTG1)
        ELSE
         CALL INSMEM('H4G5AA',I004,MXCOR)
        ENDIF
C
C  CALCULATE IN ADDITION THE T1*T2*L2 CONTRIBUTION TO GAMMA5
C
        I001=1
        I002=I001+IINTFP*NUMSYT*NVRTSQ
        I003=I002+IINTFP*NUMSYT*NUMSYG
        MAXDIS=(MXCOR-I003)/(IINTFP*MAX(1,DISSYG))
C
        IF(MIN(NUMSYG,DISSYG,NUMSYT,DISSYT).NE.0) THEN
        IF(MAXDIS.GT.1) THEN
C
         CALL T1G5AA(ICORE(I001),ICORE(I002),ICORE(I003),MAXDIS,
     &               ICORE(I0T1),POP(1,ISPIN),VRT(1,ISPIN),DISSYT,
     &               DISSYG,NUMSYT,NUMSYG,LISTL,LISTT,LISTG,IRREP,ISPIN)
        ELSE
         CALL INSMEM('T1G5AA',MAXDIS*DISSYG,DISSYG)
        ENDIF
        ENDIF
C
       ENDIF
C
       I001=1
       I002=I001+IINTFP*MAX(NOCCSQ*DISSYT,NF2(ISPIN))
       I003=I002+IINTFP*NUMSYG*DISSYG
       IF(MIN(NUMSYG,DISSYG).NE.0) THEN
        I004=I003+3*IINTFP*MAX(NUMSYG,NUMSYT,DISSYG,DISSYT)
        IF(MXCOR.GT.I004) THEN
         CALL G5AA(ICORE(I001),ICORE(I002),ICORE(IOFFT),ICORE(I0T1),
     &             ICORE(I001),FACT,LAMBDA,TAU,TRIP1,ISPIN,
     &             POP(1,ISPIN),VRT(1,ISPIN),NT,DISSYT,NUMSYT,
     &             DISSYG,NUMSYG,LISTT,LISTL,LISTG,IRREP,
     &             ICORE(I003))
        ELSE
         CALL INSMEM('G5AA',I004,MXCOR)
        ENDIF
       ENDIF
100   CONTINUE
      ENDIF
C
C  AB SPIN CASE
C
      LISTG=131-ISPIN
      IF(MBPT4) THEN
       LISTT=46
       FACT=ONE
      ELSE IF(LAMBDA) THEN
       LISTT=46
       LISTL=146 
       FACT=HALF
      ENDIF
      IF(CCSD) THEN
       LISTH1=55+ISPIN+IOFFLISTH
       LISTH2=57+ISPIN+IOFFLISTH
       LISTG1=170-ISPIN
       FACTH=HALF
      ENDIF
C
C  LOOP OVER IRREPS
C
      DO 200 IRREP=1,NIRREP
C
       DISSYT=IRPDPD(IRREP,ISYTYP(1,LISTT))
       NUMSYT=IRPDPD(IRREP,ISYTYP(2,LISTT))
       DISSYG=IRPDPD(IRREP,ISYTYP(1,LISTG))
       NUMSYG=IRPDPD(IRREP,ISYTYP(2,LISTG))
C
C  FOR CCSD CALCULATE FIRST THE CONTRIBUTION OF H4 TO GHAMMA5
C
       IF(CCSD) THEN
        DISSYH1=IRPDPD(IRREP,ISYTYP(1,20+ISPIN))
        DISSYH2=IRPDPD(IRREP,ISYTYP(1,LISTH2))
        NUMSYH1=IRPDPD(IRREP,ISYTYP(2,20+ISPIN))
        NUMSYH2=IRPDPD(IRREP,ISYTYP(2,LISTH2))
        I001=1
        I002=I001+IINTFP*MAX(NUMSYH1*DISSYH1,NUMSYH2*DISSYH2,
     &                       DISSYG*NUMSYG)
        I003=I002+IINTFP*NUMSYG*DISSYG
        I004=I003+3*IINTFP*MAX(NUMSYH1,NUMSYH2,NUMSYG,DISSYH1,
     &                         DISSYH2,DISSYG)
        IF(I004.LT.MXCOR) THEN
         INCORE=.TRUE.
        ELSE
         I002=I001+IINTFP*MAX(NUMSYH1*DISSYH1,NUMSYH2*DISSYH2,
     &                        2*DISSYG)
         I003=I002+IINTFP*NUMSYG*DISSYG
         I004=I003+3*IINTFP*MAX(NUMSYH1,NUMSYH2,NUMSYG,DISSYH1,
     &                         DISSYH2,DISSYG)
         INCORE=.FALSE.
        ENDIF
        IF(I004.LT.MXCOR) THEN
         CALL H4G5AB(ICORE(I001),ICORE(I001),ICORE(I002), 
     &               ICORE(I001),ICORE(I0T1),ICORE(I0T2),FACTH,
     &               DISSYH1,DISSYH2,DISSYG,NUMSYH1,NUMSYH2,NUMSYG,
     &               POP(1,ISPIN),POP(1,3-ISPIN),VRT(1,ISPIN),
     &               VRT(1,3-ISPIN),LISTH1,LISTH2,LISTG,ISPIN,
     &               IRREP,IUHF,ICORE(I003),TRIP1,LISTG1,INCORE)
        ELSE
         CALL INSMEM('H4G5AB',I004,MXCOR)
        ENDIF
C
C  CALCULATE IN ADDITION THE T1*T2*L2 CONTRIBUTION TO GAMMA5
C
        I001=1
        I002=I001+IINTFP*NUMSYT*DISSYT
        I003=I002+IINTFP*NUMSYT*NUMSYG
        I004=I003+3*IINTFP*MAX(NUMSYT,NUMSYG,DISSYT,DISSYG) 
        MAXDIS=(MXCOR-I004)/(IINTFP*MAX(1,DISSYG))
        IF(MIN(NUMSYT,DISSYT,NUMSYG,DISSYG).NE.0) THEN
        IF(MAXDIS.GT.1) THEN
         CALL T1G5AB(ICORE(I001),ICORE(I002),ICORE(I004),MAXDIS,
     &               ICORE(I0T1),ICORE(I0T2),POP(1,ISPIN),
     &               POP(1,3-ISPIN),VRT(1,ISPIN),VRT(1,3-ISPIN),
     &               DISSYT,DISSYG,NUMSYT,NUMSYG,LISTL,LISTT,
     &               LISTG,IRREP,ISPIN,ICORE(I003))
        ELSE
         CALL INSMEM('T1G5AB',MAXDIS*DISSYG,DISSYG)
        ENDIF
        ENDIF
       ENDIF
C
       I001=1
       I002=I001+MAX(IINTFP*NUMSYT*DISSYT,IINTFP*NF2(ISPIN))
       I003=I002+IINTFP*NUMSYG*DISSYG
       IF(MIN(NUMSYG,DISSYG).NE.0) THEN
        I004=I003+3*IINTFP*MAX(NUMSYG,NUMSYT,DISSYG,DISSYT)
        IF(MXCOR.GT.I004) THEN
         CALL G5AB(ICORE(I001),ICORE(I002),ICORE(IOFFT),
     &             ICORE(I0T1A),ICORE(I0T1B),ICORE(I001),
     &             FACT,LAMBDA,TAU,TRIP1,ISPIN,POP(1,ISPIN),
     &             POP(1,3-ISPIN),VRT(1,ISPIN),VRT(1,3-ISPIN),NT,
     &             DISSYT,NUMSYT,DISSYG,NUMSYG,LISTT,LISTL, 
     &             LISTG,IRREP,ICORE(I003))
        ELSE 
         CALL INSMEM('G5AB',I004,MXCOR)
        ENDIF
       ENDIF
200   CONTINUE
1000  CONTINUE
C
C ALL DONE
C
      TWO=4.D0/DFLOAT(1+IUHF)
      if(iuhf.eq.0) then
       call checkgam1(icore,30,130,two,iuhf,2,vrt)
      endif
      IF(IUHF.EQ.1) THEN
       CALL CHECKGAM(ICORE,30,130,TWO)
       CALL CHECKGAM(ICORE,27,127,TWO)
       CALL CHECKGAM(ICORE,28,128,TWO)
       CALL CHECKGAM(ICORE,29,129,TWO)
      ENDIF
C
      RETURN
      END
