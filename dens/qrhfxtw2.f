      SUBROUTINE QRHFXTW2(XOVA,XOVB,ZA2A,ZA2B,
     &                    IRREPZ,ICORE,MAXCOR)
C
C  THIS ROUTINE FORMS CONTRIBUTIONS TO THE MODIFIED X
C  INTERMEDIATE REQUIRED FOR QRHF-CCSD GRADIENTS. 
C
C  THIS HANDLES CASES WHERE THERE ARE ONLY QRHF_M TYPE
C  OPEN SHELL ORBITALS.
C
C   XTW(A,I) = X(A,I) - SUM Z(E,2) [<E2||AI> + <A2||EI>]
C                       E,2
C
C  HERE A,I ARE THE VIRTUAL AND OCCUPIED ORBITALS IN THE RHF
C       REFERENCE,
C       2,E ARE THE OPEN-SHELL AND DOUBLY OCCUPIED ORBITALS
C       IN THE QRHF REFERENCE
C
C  THE SPIN SYMMETRY OF THE ORBITALS IS USED, AS ONLY ONE
C  SPIN CASE OF THE Z*A CONTRACTION IS EVALUATED.
C
CEND
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,DISSYW,DISFUL,POP,VRT,POPRHF,VRTRHF
      INTEGER POPDOC,VRTDOC,E
      DIMENSION XOVA(1),XOVB(1),ICORE(MAXCOR)
      DIMENSION ZA2A(1),ZA2B(1)
C
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2) 
      COMMON/QRHFINF/POPRHF(8),VRTRHF(8),NOSH1(8),NOSH2(8),
     &               POPDOC(8),VRTDOC(8),NAI,N1I,NA2,
c&line mod
     &               NUMISCF,NUMASCF,ISPINP,ISPINM,IQRHF
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
      COMMON/INFO/NOCCO(2),NVRTO(2)
C
      INDXF(I,J,N)=I+(J-1)*N
C
      DATA ZERO,ONE,ONEM,TWO /0.D0,1.D0,-1.D0,2.D0/
C
C SET MAXIMUM CORE SIZE
C
      MXCOR=MAXCOR
C
      NDOCC=POPDOC(IRREPZ)
      NDVRT=VRTDOC(IRREPZ)
C
C
C TERM I:  SUM Z(E,2) * [<EA||2I> + <2A||EI>]  Z - BB   W - BBBB
C          E,2
C
c&lines modif
      IF(ISPINM.EQ.1) THEN
C
C THE OPEN SHELL ORBITAL IS UNOCCUPIED IN THE BETA SPIN CASE,
C HENCE THE <EA||2I> INTEGRALS ARE OF TYPE <ab||ci>
C
         LISTW=28
      ELSE
C
C THE OPEN SHELL ORBITAL IS UNOCCUPIED IN THE ALPHA SPIN CASE,
C HENCE THE <EA||2I> INTEGRALS ARE OF TYPE <AB||CI>
C
         LISTW=27
      ENDIF
c&end modif
C
C LOOP OVER IRREPS OF MI AND 1A
C
      DO 1000 IRREP=1,NIRREP
C
C GET THE SIZE OF THE EXPANDED INTEGRAL LIST
C
       NUMSYW=IRPDPD(IRREP,ISYTYP(2,LISTW))
       DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
c&line modif
       DISFUL=IRPDPD(IRREP,21-ISPINM)
C
C ALOOCATE MEMORY
C
       I000=1
       I010=I000+IINTFP*DISFUL*NUMSYW
       I020=I010+IINTFP*NAI
       IF(I020.GE.MAXCOR)CALL INSMEM('QRHFXTW2',I020,MAXCOR)
C
C GET THE INTEGRALS 
C
       CALL GETLST(ICORE(I000),1,NUMSYW,2,IRREP,LISTW)
C
C EXPAND THE VRT-VRT BLOCK
C
c&line modif
       CALL SYMEXP2(IRREP,VRT(1,3-ISPINM),DISFUL,DISSYW,NUMSYW,
     &              ICORE(I000),ICORE(I000))
C
C IRREPX IS THE IRREP WHICH BELONGS TO I AND A IN THE FOLLOWING
C
       IRREPX=DIRPRD(IRREP,IRREPZ)
       NUMI=POPRHF(IRREPX)
       NUMA=VRTRHF(IRREPX)
C
C  GET OFFSETS FOR EA AND 2I
C
       IOFFR=0
       IOFFL=0
       IOFFX=1
       DO 210 IRREPJ=1,IRREPX-1
        IRREPI=DIRPRD(IRREPJ,IRREP)
c&two lines modif
        IOFFL=IOFFL+VRT(IRREPI,3-ISPINM)*VRT(IRREPJ,3-ISPINM)
        IOFFR=IOFFR+VRT(IRREPI,3-ISPINM)*POP(IRREPJ,3-ISPINM)
        IOFFX=IOFFX+POPRHF(IRREPJ)*VRTRHF(IRREPJ)
210    CONTINUE
C
C   LOOP NOW OVER E (ONLY OVER DOUBLY UNOCCUPIED) AND 1 (ALL TYPE "M"
C   OPEN-SHELL ORBITALS)
C
       IPOS=1
       DO 220 IOS2=1,NOSH2(IRREPZ)
        DO 220 E=1,NDVRT
C
         CALL IZERO(ICORE(I010),IINTFP*NAI)
C
C      OFFSETS FOR EA AND 2A (LEFT HAND SIDE) :
C
c&two lines modif
         IOFFEAL=IOFFL+INDXF(NOSH2(IRREPZ)+E,1,VRT(IRREPZ,3-ISPINM))
         IOFF2AL=IOFFL+INDXF(IOS2,1,VRT(IRREPZ,3-ISPINM))
C
C COPY ONE A,I BLOCK TO SCRATCH
C
         IF(MIN(NUMI,NUMA).NE.0)THEN
          DO 230 I=1,NUMI
C
C      OFFSETS FOR 2I AND EI (RIGHT HAND SIDE) :
C
c&two lines modif
           IOFF2IR=IOFFR+INDXF(IOS2,I,VRT(IRREPZ,3-ISPINM))
           IOFFEIR=IOFFR+INDXF(NOSH2(IRREPZ)+E,I,VRT(IRREPZ,3-ISPINM))
C
C  NOW COPY OVER BOTH REQUIRED PIECES 
C
           IOFFEA2I=I000+IINTFP*(INDXF(IOFFEAL,IOFF2IR,DISFUL)-1)
           IOFF2AEI=I000+IINTFP*(INDXF(IOFF2AL,IOFFEIR,DISFUL)-1)
           IOFFAI  =I010+IINTFP*(INDXF(1,I,NUMA)-1)
c&two lines modif
           CALL SAXPY(NUMA,ONE,ICORE(IOFFEA2I),VRT(IRREPZ,3-ISPINM),
     &                ICORE(IOFFAI),1)
           CALL SAXPY(NUMA,ONE,ICORE(IOFF2AEI),VRT(IRREPZ,3-ISPINM),
     &                ICORE(IOFFAI),1)
230       CONTINUE
C
C  MULTIPLY THE SCRATCH WITH Z1A OR Z1B
C
          LENGTH=NUMI*NUMA
          CALL SAXPY(LENGTH,ZA2A(IPOS),ICORE(I010),1,
     &               XOVA(IOFFX),1)
          CALL SAXPY(LENGTH,ZA2B(IPOS),ICORE(I010),1,
     &               XOVB(IOFFX),1)
         ENDIF
         IPOS=IPOS+1
C
220     CONTINUE
C
1000   CONTINUE
C
C TERM II:
C
C ALPHA-BETA CONTRIBUTION FROM Z(1,m) :
C
C         Z(E,2) * <Ea||2i>
C
C         FACTOR TWO SINCE <Ea||2i> = <2a||Ei> [2 IS OCCUPIED]
C
c&lines modif
      IF(ISPINM.EQ.1) THEN
C
C THE OPEN SHELL ORBITAL IS OCCUPIED FOR SPIN CASE ALPHA, SO
C THE <Ea||2i> INTEGRALS ARE OF TYPE <Ab|Ij>.  LIST 17
C ORDERS THEM (ai,E2) WHICH IS CONVENIENT FOR OUR PURPOSES.
C
         LISTW=17
      ELSE
C
C THE OPEN SHELL ORBITAL IS OCCUPIED FOR SPIN CASE BETA, SO
C THE <Ea||2i> INTEGRALS ARE OF TYPE <Ab|Ij>.  LIST 18
C ORDERS THEM (ai,E2) WHICH IS CONVENIENT FOR OUR PURPOSES.
C
         LISTW=18
      ENDIF
C
C  WE NEED TO CONSIDER ONLY IRREP 1 OF THE INTEGRAL LIST
C
C CALCULATE OFFSET TO WHERE THE E2 PAIRS WITH 
C  IRREP(2)=IRREP(E)=IRREPZ BEGIN.
C
c&end modif
       IOFFDIS=0
       DO 3000 IRREP=1,IRREPZ-1
c&line modif
        IOFFDIS=IOFFDIS+VRT(IRREP,ISPINM)*POP(IRREP,ISPINM)
3000   CONTINUE
C
C  LOOP OVER E AND 2.
C
       IPOS=1
       DO 3100 IOS2=1,NOSH2(IRREPZ)
        DO 3200 E=1,NDVRT
C
C  CALCULATE THE DISTRIBUTION NUMBER WE WANT HERE AND READ
C  IN THE CORRESPONDING W(AI,1m) ARRAY
C
c&line modif
         IDIS=IOFFDIS+INDXF(E,NDOCC+IOS2,VRT(IRREPZ,ISPINM))
         CALL GETLST(ICORE,IDIS,1,1,1,LISTW)
C
C  NOW SCALE ALL ELEMENTS OF W(AI) WITH Z(E,2) AND ADD THIS
C  TO EXISTING X PIECES
C
c&two lines modif
         CALL SAXPY(NT(IQRHF),TWO*ZA2B(IPOS),ICORE,1,XOVA,1)
         CALL SAXPY(NT(IQRHF),TWO*ZA2A(IPOS),ICORE,1,XOVB,1)
         IPOS=IPOS+1
C
3200    CONTINUE
3100   CONTINUE
C
      RETURN
      END
