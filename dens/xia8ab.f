      SUBROUTINE XIA8AB(G,W,XIA,FACT,ISPIN,POP1,POP2,VRT1,VRT2,
     &                  DISSYT,NUMSYT,DISSYW,NUMSYW,LISTG,LISTW,IRREP,
     &                  TMP,IUHF)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DISSYT,DISSYW,DIRPRD,POP1,POP2,VRT1,VRT2
      DIMENSION G(DISSYT,1),W(NUMSYW,1),XIA(1),TMP(1)
      DIMENSION POP1(8),POP2(8),VRT1(8),VRT2(8) 
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      common /dropgeo/ ndrgeo
C
      DATA ONE,ONEM,TWO /1.0D0,-1.D0,2.D0/
C
C      PICK UP FIRST THE INTEGRALS
C
       CALL GETLST(G,1,NUMSYW,2,IRREP,LISTW)   
C
C  SPIN ADAPTED CODE FOR RHF
C
      IF(IUHF.EQ.0) THEN
C
       CALL SPINAD1(IRREP,POP1,DISSYW,G,TMP,TMP(1+DISSYW))
      ENDIF
      CALL TRANSP(G,W,NUMSYW,DISSYW)
C
C PICK UP TH G AMPLITUDES
C
      if (ndrgeo.eq.0) then
        CALL GETLST(G,1,NUMSYT,1,IRREP,LISTG)
      else  
       if (iuhf.eq.1 .and. ispin.eq.2) then 
        CALL GETGO6R(G,TMP,1,NUMSYT,1,IRREP,LISTG,ispin,listg,dissyt)
       else  
        CALL GETGO6(G,TMP,1,NUMSYT,1,IRREP,LISTG,ispin,listg,dissyt)
       endif 
      endif 
C
C  TRANSPOSE THE LAST TWO INDICES IN THE AA CASE
C
      IF(ISPIN.EQ.1) THEN
        CALL SYMTR1(IRREP,POP1,VRT2,DISSYT,G,TMP,TMP(1+DISSYT),
     &              TMP(1+2*DISSYT))
        CALL SYMTR1(IRREP,VRT1,VRT2,NUMSYW,W,TMP,TMP(1+NUMSYW),
     &              TMP(1+2*NUMSYW))
      ENDIF
C
C  PERFORM MULTIPLICATION
C
C  JOFFW AND JOFFG OFFSET IN THE RIGHTMOST BLOCK OF G AND W
C  IOFF OFFSET IN AIVV
C
      IOFF=1
      JOFFW=1
      JOFFG=1
      DO 90 IRREPI=1,NIRREP
C          
C        GET NUMBER OF VIRTUAL AND OCCUPIED ORBITALS FOR IRREPJ
C
       NVRTI=VRT1(IRREPI)
       NOCCI=POP1(IRREPI)
C
C        DETERMINE IRREPI WHOSE DIRECT PRODUCT WITH IRREPJ GIVES IRREP
C
       IRREPJ=DIRPRD(IRREP,IRREPI)
C
C        GET NUMBER OF VIRTUAL ORBITALS FOR KRREP
C
       NVRTJ=VRT2(IRREPJ)
C
C        IF NOCCI, NVRTI OR NVRTJ EQUAL ZERO, NOTHING TO COMPUTE
C
       IF(MIN(NVRTJ,NVRTI,NOCCI).NE.0) THEN
C
          CALL XGEMM('T','N',NVRTI,NOCCI,DISSYT*NVRTJ,FACT,
     &               W(1,JOFFW),NVRTJ*DISSYT,G(1,JOFFG),
     &               NVRTJ*DISSYT,ONE,XIA(IOFF),NVRTI)
       ENDIF
C
C  UPDATE THE OFFSETS
C
       JOFFW=JOFFW+NVRTI*NVRTJ
       JOFFG=JOFFG+NOCCI*NVRTJ
       IOFF=IOFF+NVRTI*NOCCI
90    CONTINUE
C
      RETURN
      END
