

      SUBROUTINE VOINOO(IOO,DVO,ICORE,MXCOR,IUHF)
C
C THIS ROUTINE COMPUTES THE CONTRIBUTIONS:
C
C IOO(I,J) = - P (I,J) SUM [D(M,E) * <IM||JE> + D(m,e) * <Im|Je>] (ISPIN=1)
C               +                                 
C
C IOO(i,j) = - P (i,j) SUM [D(m,e) * <im||je> + D(M,E) * <Mi|Ej>] (ISPIN=1)
C               +                                 
C
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION IOO,DVO,ONEM
      DIMENSION DVO(1),IOO(1),ICORE(MXCOR)
      COMMON /SYM2/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYMPOP2/ IRPDPD(8,22)
      COMMON /SYMPOP/ IRP_DM(8,22),ISYTYP(2,500),ID(18)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      common /dropgeo/ ndrgeo
      COMMON /SHIFT/ ISHIFT 
      DATA ONEM /-1.0/
C
      IF(IUHF.EQ.0)THEN
C
C SPIN ADAPTED RHF CODE
C      
       IOFFI=1
       LISTW=10 + ISHIFT 
       DO 120 IRREP=1,NIRREP
        DISSIZ=IRPDPD(IRREP,ISYTYP(1,LISTW))
        NUMDIS=IRPDPD(IRREP,ISYTYP(2,LISTW))
        I000=1
        I010=I000+IINTFP*NFMI(1)
        I020=I010+IINTFP*NUMDIS*DISSIZ
        I030=I020+IINTFP*NUMDIS
        I040=I030+IINTFP*NUMDIS
        CALL IZERO(ICORE(I000),IINTFP*NFMI(1))
        CALL GETLST(ICORE(I010),1,NUMDIS,1,IRREP,LISTW)
        CALL SPINAD3(IRREP,POP(1,1),DISSIZ,NUMDIS,ICORE(I010),
     &               ICORE(I020),ICORE(I030))
        CALL DOT24(IRREP,ICORE(I000),DVO,ICORE(I010),
     &             ICORE(I020),DISSIZ,POP(1,1),POP(1,1),
     &             POP(1,1),POP(1,1),POP(1,1),VRT(1,1),'TSTS')
        IOFF=I000
        DO 121 IRREPX=1,NIRREP
         N=POP(IRREPX,1)
         CALL MPMT(ICORE(IOFF),N)
         IOFF=IOFF+N*N*IINTFP
121     CONTINUE
        CALL SAXPY(NFMI(1),ONEM,ICORE(I000),1,IOO(IOFFI),1)
120    CONTINUE
      ELSEIF(IUHF.NE.0)THEN
       DO 10 ISPIN=1,1+IUHF
        LISTW1=6+ISPIN + ISHIFT 
        LISTW2=11-ISPIN + ISHIFT 
        DO 20 IRREP=1,NIRREP
         NDSZ1=IRPDPD(IRREP,ISYTYP(1,LISTW1))
         NDIS1=IRPDPD(IRREP,ISYTYP(2,LISTW1))
         NDSZ1F=IRPDPD(IRREP,20+ISPIN)
         NDSZ2=IRPDPD(IRREP,ISYTYP(1,LISTW2))
         NDIS2=IRPDPD(IRREP,ISYTYP(2,LISTW2))
         IOFFI=1+(ISPIN-1)*NFMI(1)
C
C DO FIRST PART   D(M,E) * <IM||JE>  (ISPIN=1)
C                 D(m,e) * <im||je>  (ISPIN=2)
C
C AND KEEP RESULT AT BOTTOM OF CORE
C
         I000=1
         I010=I000+IINTFP*NFMI(ISPIN)
         I020=I010+IINTFP*NDSZ1F*NDIS1
         I030=I020+IINTFP*NT(ISPIN)
         CALL IZERO(ICORE(I000),NFMI(ISPIN)*IINTFP)
         IOFFD=1+(ISPIN-1)*NT(1)
         CALL GETLST(ICORE(I010),1,NDIS1,2,IRREP,LISTW1)
         CALL SYMEXP2(IRREP,POP(1,ISPIN),NDSZ1F,NDSZ1,NDIS1,ICORE(I010),
     &                ICORE(I010))
         CALL DOT24(IRREP,ICORE(I000),DVO(IOFFD),ICORE(I010),
     &              ICORE(I020),NDSZ1F,POP(1,ISPIN),POP(1,ISPIN),
     &              POP(1,ISPIN),POP(1,ISPIN),POP(1,ISPIN),VRT(1,ISPIN),
     &              'TSTS')
C
C NOW DO SECOND PART 
C
C         D(m,e) * <Im|Je> (ISPIN = 1)
C         D(M,E) * <Mi|Ej> (ISPIN = 2)
C
         I020=I010+IINTFP*NDSZ2*NDIS2
         I030=I020+NT(3-ISPIN)
         IOFFD=1+(2-ISPIN)*NT(1)
         CALL GETLST(ICORE(I010),1,NDIS2,2,IRREP,LISTW2)
         IF(ISPIN.EQ.1)THEN
          CALL DOT24(IRREP,ICORE(I000),DVO(IOFFD),ICORE(I010),
     &               ICORE(I020),NDSZ2,POP(1,1),POP(1,1),
     &               POP(1,1),POP(1,2),POP(1,1),VRT(1,2),'TSTS')
         ELSE
          CALL DOT24(IRREP,ICORE(I000),DVO(IOFFD),ICORE(I010),
     &               ICORE(I020),NDSZ2,POP(1,2),POP(1,2),
     &               POP(1,1),POP(1,2),VRT(1,1),POP(1,2),'STST')
         ENDIF
C
C NOW FORM TARGET(I,J)+TARGET(J,I) [i,j;j,i] AND DECREMENT IOO.
C
         IOFF=I000
         DO 21 IRREPX=1,NIRREP
          N=POP(IRREPX,ISPIN)
          CALL MPMT(ICORE(IOFF),N)
          IOFF=IOFF+N*N*IINTFP
21       CONTINUE
         CALL SAXPY(NFMI(ISPIN),ONEM,ICORE(I000),1,IOO(IOFFI),1)
20      CONTINUE
10     CONTINUE
      ENDIF
      RETURN
      END
