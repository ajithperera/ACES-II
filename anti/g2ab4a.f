      SUBROUTINE G2AB4A(GAMMA,BUF,GOFF,LENGAM,FAIBJ)
C
C THIS ROUTINE PROCESSES RING TYPE GAMMAS NEEDED FOR THE
C  SPIN CASE AABB MO GAMMA LIST FOR UHF CALCULATIONS.
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 
      DOUBLE PRECISION FABIJ,FAIBJ,FABCD,FIJKL,FABCI,FIJKA
      INTEGER GOFF(8,8),IOFFSR(8),IOFFSL(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
      NNM1O2(I)   =(I*(I-1))/2
C
C FIRST DO LIST 125
C 
C            11 22         12 12
C          G(AB,ij) <--- g<Ai,Bj>
C
C            11 22         12 12
C          G(BA,ij) <--- g<Bi,Aj>
C
C WITH THE FOLLOWING SYMMETRIZATION:
C 
C          G(AB,ij) = G(AB,ij) + G(BA,ij)
C 
      LISTAIBJ=125
      DO 100 IRREPGET=2,NIRREP
       IOFFR=1
       IOFFL=1
       DO 1100 IRREP1=1,NIRREP
        IRREP2=DIRPRD(IRREP1,IRREPGET)
        IOFFSR(IRREP1)=IOFFR
        IOFFSL(IRREP1)=IOFFL
        IOFFR=IOFFR+VRT(IRREP2,1)*POP(IRREP1,2)
        IOFFL=IOFFL+VRT(IRREP2,1)*POP(IRREP1,2)
1100   CONTINUE
       DO 110 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPGET)
        IF(IRREPL.GT.IRREPR)GOTO 110
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPR)-1
        NUMJ=POP(IRREPR,2)
        NUMB=VRT(IRREPL,1)
        NUMI=POP(IRREPR,2)
        NUMA=VRT(IRREPL,1)
        MAXL=NNP1O2(MOPOP(IRREPL))
        DO 120 INDXJ=1,NUMJ
         DO 130 INDXB=1,NUMB
          INDB=INDXB+POP(IRREPL,1)
          INDJ=INDXJ
          CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTAIBJ)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          DO 140 INDXI=1,INDXJ
           INDI=INDXI
           GINDXR=INDXT(INDI,INDJ)
           DO 150 INDXA=1,INDXB
            IPOS=INDXF(INDXA,INDXI,NUMA)+IFIRST
            X=BUF(IPOS)*FAIBJ
            INDA=INDXA+POP(IRREPL,1)
            GINDXL=INDXT(INDA,INDB)
            GINDX0=INDXF(GINDXL,GINDXR,MAXL)
            GINDX =GINDX0+GOFF(IRREPL,IRREPR)
            GAMMA(GINDX)=GAMMA(GINDX)+X
150        CONTINUE
           DO 151 INDXA=INDXB,NUMA
            IPOS=INDXF(INDXA,INDXI,NUMA)+IFIRST
            X=BUF(IPOS)*FAIBJ
            INDA=INDXA+POP(IRREPL,1)
            INDI=INDXI
            GINDXL=INDXT(INDB,INDA)
            GINDX0=INDXF(GINDXL,GINDXR,MAXL)
            GINDX =GINDX0+GOFF(IRREPL,IRREPR)
            GAMMA(GINDX)=GAMMA(GINDX)+X
151        CONTINUE
140       CONTINUE
130      CONTINUE
120     CONTINUE
110    CONTINUE
100   CONTINUE
C
C NOW DO LIST 126
C 
C            11 22         21 21
C          G(IJ,ab) <--- g<aI,bJ>
C
C            11 22         21 21
C          G(JI,ab) <--- g<aI,bJ>
C
C WITH THE FOLLOWING SYMMETRIZATION:
C 
C          G(IJ,ab) = G(IJ,ab) + G(JI,ab)
C 
      LISTAIBJ=126
      DO 200 IRREPGET=2,NIRREP
       IOFFR=1
       IOFFL=1
       DO 2100 IRREP1=1,NIRREP
        IRREP2=DIRPRD(IRREP1,IRREPGET)
        IOFFSR(IRREP1)=IOFFR
        IOFFSL(IRREP1)=IOFFL
        IOFFR=IOFFR+VRT(IRREP2,2)*POP(IRREP1,1)
        IOFFL=IOFFL+VRT(IRREP2,2)*POP(IRREP1,1)
2100   CONTINUE
       DO 210 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPGET)
        IF(IRREPL.LT.IRREPR)GOTO 210
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPR)-1
        NUMJ=POP(IRREPR,1)
        NUMB=VRT(IRREPL,2)
        NUMI=POP(IRREPR,1)
        NUMA=VRT(IRREPL,2)
        MAXL=NNP1O2(MOPOP(IRREPR))
        DO 220 INDXJ=1,NUMJ
         DO 230 INDXB=1,NUMB
          INDB=INDXB+POP(IRREPL,2)
          INDJ=INDXJ
          CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTAIBJ)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          DO 240 INDXI=1,INDXJ
           INDI=INDXI
           GINDXR=INDXT(INDI,INDJ)
           DO 250 INDXA=1,INDXB
            IPOS=INDXF(INDXA,INDXI,NUMA)+IFIRST
            X=BUF(IPOS)*FAIBJ
            INDA=INDXA+POP(IRREPL,2)
            GINDXL=INDXT(INDA,INDB)
            GINDX0=INDXF(GINDXR,GINDXL,MAXL)
            GINDX =GINDX0+GOFF(IRREPR,IRREPL)
            GAMMA(GINDX)=GAMMA(GINDX)+X
250        CONTINUE
           DO 251 INDXA=INDXB,NUMA
            IPOS=INDXF(INDXA,INDXI,NUMA)+IFIRST
            X=BUF(IPOS)*FAIBJ
            INDA=INDXA+POP(IRREPL,2)
            INDI=INDXI
            GINDXL=INDXT(INDB,INDA)
            GINDX0=INDXF(GINDXR,GINDXL,MAXL)
            GINDX =GINDX0+GOFF(IRREPR,IRREPL)
            GAMMA(GINDX)=GAMMA(GINDX)+X
251        CONTINUE
240       CONTINUE
230      CONTINUE
220     CONTINUE
210    CONTINUE
200   CONTINUE
C
C NOW DO LIST 118
C 
C            11 22         12 21
C          G(BI,aj) <--- g<Bj,aI>
C
      LISTAIBJ=118
      DO 300 IRREPGET=2,NIRREP
       IOFFR=1
       IOFFL=1
       DO 3100 IRREP1=1,NIRREP
        IRREP2=DIRPRD(IRREP1,IRREPGET)
        IOFFSR(IRREP1)=IOFFR
        IOFFSL(IRREP1)=IOFFL
        IOFFR=IOFFR+VRT(IRREP2,2)*POP(IRREP1,1)
        IOFFL=IOFFL+VRT(IRREP2,1)*POP(IRREP1,2)
3100   CONTINUE
       DO 310 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPGET)
        IF(IRREPL.LT.IRREPR)GOTO 310
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPL)-1
        NUMI=POP(IRREPR,1)
        NUMA=VRT(IRREPL,2)
        NUMJ=POP(IRREPL,2)
        NUMB=VRT(IRREPR,1)
        MAXL=NNP1O2(MOPOP(IRREPR))
        DO 320 INDXI=1,NUMI
         DO 330 INDXA=1,NUMA
          INDA=INDXA+POP(IRREPL,2)
          INDI=INDXI
          CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTAIBJ)
          ILOGREC=ILOGREC+1
          DO 340 INDXJ=1,NUMJ
           DO 350 INDXB=1,NUMB
            IPOS=INDXF(INDXB,INDXJ,NUMB)+IFIRST
            X=BUF(IPOS)*FAIBJ
            INDB=INDXB+POP(IRREPR,1)
            INDJ=INDXJ
            GINDXL=INDXT(INDI,INDB)
            GINDXR=INDXT(INDJ,INDA)
            GINDX0=INDXF(GINDXL,GINDXR,MAXL)
            GINDX =GINDX0+GOFF(IRREPR,IRREPL)
            GAMMA(GINDX)=GAMMA(GINDX)+X
350        CONTINUE
340       CONTINUE
330      CONTINUE
320     CONTINUE
310    CONTINUE
300   CONTINUE
      RETURN
      END
