      SUBROUTINE G3AA6(GAMMA,BUF,BUF1,BUF2,GOFF,LENGAM,ISPIN,FABCD)

C THIS ROUTINE PROCESSES THE G(A<B,C<D) GAMMA LISTS FOR SPIN CASES
C  AAAA AND BBBB AND FILLS THE SYMMETRY CASE ABAB MULLIKEN ORDERED 
C  GAMMA LIST.  THIS CODE IS USED FOR UHF CALCULATIONS ONLY.
C
C       21 21        22 11
C     G(AC,BD) <-- g<AB,CD>
C  
C       21 21        12 21        21 21
C     G(CA,BD) <-- g<AB,CD> = - g<BA,CD>
C   
C IN ADDITION, THE FOLLOWING SYMMETRIZATION IS REQUIRED:
C   
C      21 21      21 21      21 21
C    G(AC,BD) = G(AC,BD) + G(CA,BD)
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(*),X,X1,X2
      DOUBLE PRECISION BUF1(*),BUF2(*),ONE,TWO 
      DOUBLE PRECISION FABIJ,FAIBJ,FABCD,FIJKL,FABCI,FIJKA
      INTEGER GOFF(8,8),IOFFSL(8),IOFFSR(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      LOGICAl MBPT4
      LOGICAL GABCD,DABCD
      LOGICAL TDA,EOM
C
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
      COMMON /IABCD/ IOFFABCD
      COMMON /ABCD/  GABCD,DABCD
      COMMON /EXCITE/ TDA,EOM
      COMMON /STATSYM/IRREPX
C
      DATA ONE,TWO /1.D0,2.D0/
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
      NNM1O2(I)   =(I*(I-1))/2
C
C COMPUTE OFFSETS TO FIRST ELEMENT OF ALL G(XX,YY) IRREP POSITIONS
C  AND PUT IN FIRST CONTRIBUTION.
C
      NPASS=1
      IF(DABCD.AND.EOM) NPASS=2
C
      LISTABCD=IOFFABCD+130+ISPIN
C
      DO 90 IPASS=1,NPASS
C
      IOFFR=1
      DO 1000 IRREPR=1,NIRREP
       IOFFSR(IRREPR)=IOFFR
       IOFFSL(IRREPR)=IOFFR
       IOFFR=IOFFR+NNM1O2(VRT(IRREPR,ISPIN))
1000  CONTINUE
C
      IF(DABCD) THEN
C
       IF(IPASS.EQ.1) THEN
C
        NUMDSZ=IRPDPD(1,ISYTYP(2,143+ISPIN))
        DISDSZ=IRPDPD(1,ISYTYP(1,143+ISPIN))
        CALL GETLST(BUF1,1,NUMDSZ,1,1,153+ISPIN)
        CALL GETLST(BUF2,1,NUMDSZ,1,1,143+ISPIN) 
        IF(CCSD) THEN
         I0TA=1
         CALL GETLST(BUF(I0TA),1,1,1,ISPIN,157)
         CALL FTAU(BUF1,BUF(I0TA),BUF(I0TA),DISDSZ,NUMDSZ,
     &             POP(1,ISPIN),POP(1,ISPIN),VRT(1,ISPIN),
     &             VRT(1,ISPIN),1,ISPIN,ONE)
        ENDIF
        MBPT4=M4DQ.OR.M4SDQ.OR.M4SDTQ
        IF(MBPT4) THEN
         CALL SSCAL(NUMDSZ*DISDSZ,TWO,BUF2,1)
         CALL SAXPY(NUMDSZ*DISDSZ,ONE,BUF1,1,BUF2,1)
        ENDIF
C
       ELSE
C
C SECOND PASS, ONLY FOR EOM-CCSD REQUIRED
C
        IRREPGET=DIRPRD(1,IRREPX)
        NUMDSZ=IRPDPD(IRREPGET,ISYTYP(2,460+ISPIN))
        DISDSZ=IRPDPD(1,ISYTYP(1,460+ISPIN))
C
        CALL GETLST(BUF1,1,NUMDSZ,1,IRREPGET,460+ISPIN)
        CALL GETLST(BUF2,1,NUMDSZ,1,IRREPGET,440+ISPIN)
C
        I0TA=1
        I0RA=I0TA+NT(ISPIN)
        CALL GETLST(BUF(I0TA),1,1,1,ISPIN,157)
        CALL GETLST(BUF(I0RA),1,1,1,2+ISPIN,490)
C
        CALL DTAU(1,IRREPGET,1,IRREPX,BUF1,
     &            BUF(I0TA),BUF(I0TA),BUF(I0RA),BUF(I0RA),
     &            ISPIN,ONE)
C
       ENDIF
C
      ENDIF
C
      DO 100 IRREPDO=2,NIRREP
       DO 110 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        IF(IRREPL.LT.IRREPR)THEN
         ILOGREC=IOFFSR(IRREPR)
         IFIRST =IOFFSL(IRREPL)
         NUMD=VRT(IRREPR,ISPIN)
         NUMC=VRT(IRREPR,ISPIN)
         NUMB=VRT(IRREPL,ISPIN)
         NUMA=VRT(IRREPL,ISPIN)
         MAXL=MOPOP(IRREPL)*MOPOP(IRREPR)
         MAXA=MOPOP(IRREPR)
         DO 120 INDXD=2,NUMD
          DO 130 INDXC=1,INDXD-1
           INDD=INDXD+POP(IRREPR,ISPIN)
           INDC=INDXC+POP(IRREPR,ISPIN)
C
           IF(.NOT.DABCD) THEN
C
C READ G(AB,CD) FROM DISK
C
            CALL GETLST(BUF,ILOGREC,1,1,1,LISTABCD)
C
           ELSE
C
            CALL DIRG2(BUF,BUF1,BUF2,BUF1,BUF2,NUMDSZ,DISDSZ,
     &                 ILOGREC,1,1)
C
           ENDIF
           ILOGREC=ILOGREC+1
           IPOS=IFIRST
           DO 140 INDXB=2,NUMB
*VOCL LOOP,NOVREC
CDIR$ IVDEP
            DO 150 INDXA=1,INDXB-1
             X=BUF(IPOS)*FABCD
             IPOS=IPOS+1
             INDA=INDXA+POP(IRREPL,ISPIN)
             INDB=INDXB+POP(IRREPL,ISPIN)
             GINDXL=INDXF(INDC,INDA,MAXA)
             GINDXR=INDXF(INDD,INDB,MAXA)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDXL=INDXF(INDC,INDB,MAXA)
             GINDXR=INDXF(INDD,INDA,MAXA)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)-X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)-X
150         CONTINUE
140        CONTINUE
130       CONTINUE
120      CONTINUE
        ENDIF
110    CONTINUE
100   CONTINUE
C
C NOW WE MUST LOOP OVER ALL OTHER IRREPS AND PUT THE OTHER PIECE IN
C
C       21 21        12 21        12 12
C     G(KI,JL) <-- g<IJ,KL> = - g<IJ,LK>
C
      DO 160 IRREPDO=2,NIRREP
C
C COMPUTE OFFSETS TO ALL AB AB POSITIONS
C
      IOFFR=1
      IOFFL=1
      DO 1200 IRREPR=1,NIRREP
       IRREPL=DIRPRD(IRREPR,IRREPDO)
       IF(IRREPL.GT.IRREPR)GOTO 1200
       IOFFSR(IRREPR)=IOFFR
       IOFFSL(IRREPR)=IOFFR
       IOFFR=IOFFR+VRT(IRREPL,ISPIN)*VRT(IRREPR,ISPIN)
1200  CONTINUE
C
      IF(DABCD) THEN
C
       IF(IPASS.EQ.1) THEN
        NUMDSZ=IRPDPD(IRREPDO,ISYTYP(2,143+ISPIN))
        DISDSZ=IRPDPD(IRREPDO,ISYTYP(1,143+ISPIN))
        CALL GETLST(BUF1,1,NUMDSZ,1,IRREPDO,153+ISPIN)
        CALL GETLST(BUF2,1,NUMDSZ,1,IRREPDO,143+ISPIN) 
        IF(CCSD) THEN
         I0TA=1
         CALL GETLST(BUF(I0TA),1,1,1,ISPIN,157)
         CALL FTAU(BUF1,BUF(I0TA),BUF(I0TA),DISDSZ,NUMDSZ,
     &             POP(1,ISPIN),POP(1,ISPIN),VRT(1,ISPIN),
     &             VRT(1,ISPIN),IRREPDO,ISPIN,ONE)
        ENDIF
        MBPT4=M4DQ.OR.M4SDQ.OR.M4SDTQ
        IF(MBPT4) THEN
         CALL SSCAL(NUMDSZ*DISDSZ,TWO,BUF2,1)
         CALL SAXPY(NUMDSZ*DISDSZ,ONE,BUF1,1,BUF2,1)
        ENDIF
C
       ELSE
C
C SECOND PASS, ONLY FOR EOM-CCSD REQUIRED
C
        IRREPGET=DIRPRD(IRREPDO,IRREPX)
        NUMDSZ=IRPDPD(IRREPGET,ISYTYP(2,460+ISPIN))
        DISDSZ=IRPDPD(IRREPDO,ISYTYP(1,460+ISPIN))
C
        CALL GETLST(BUF1,1,NUMDSZ,1,IRREPGET,460+ISPIN)
        CALL GETLST(BUF2,1,NUMDSZ,1,IRREPGET,443+ISPIN)
C
        I0TA=1
        I0RA=1+NT(1)
        CALL GETLST(BUF(I0TA),1,1,1,ISPIN,157)
        CALL GETLST(BUF(I0RA),1,1,1,2+ISPIN,490)
C 
        CALL DTAU(IRREPDO,IRREPGET,1,IRREPX,BUF1,
     &            BUF(I0TA),BUF(I0TA),BUF(I0RA),BUF(I0RA),
     &            ISPIN,ONE)
C
       ENDIF
C
      ENDIF
C
      DO 170 IRREPB=1,NIRREP
       IRREPA=DIRPRD(IRREPB,IRREPDO)
       IF(IRREPA.LT.IRREPB)THEN
        ILOGREC=IOFFSR(IRREPB)
        IFIRST=IOFFSL(IRREPB)
        NUMD=VRT(IRREPB,ISPIN)
        NUMC=VRT(IRREPA,ISPIN)
        NUMB=VRT(IRREPB,ISPIN)
        NUMA=VRT(IRREPA,ISPIN)
        MAXL=MOPOP(IRREPA)*MOPOP(IRREPB)
        MAXA=MOPOP(IRREPA)
        MAXB=MOPOP(IRREPB)
        DO 180 INDXD=1,NUMD
         DO 185 INDXC=1,NUMC
          INDD=INDXD+POP(IRREPB,ISPIN)
          INDC=INDXC+POP(IRREPA,ISPIN)
C
          IF(.NOT.DABCD) THEN
C
C READ G(AB,CD) FROM DISK
C
           CALL GETLST(BUF,ILOGREC,1,1,IRREPDO,LISTABCD)
C
          ELSE
C
C CALCULATE G(AB,CD) ON THE FLY
C
           CALL DIRG2(BUF,BUF1,BUF2,BUF1,BUF2,NUMDSZ,DISDSZ,
     &                ILOGREC,IRREPDO,1)
C
          ENDIF
C         
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
           DO 190 INDXB=1,NUMB
            DO 195 INDXA=1,NUMA
             INDB=INDXB+POP(IRREPB,ISPIN)
             INDA=INDXA+POP(IRREPA,ISPIN)
             X=BUF(IPOS)*FABCD
             IPOS=IPOS+1
             GINDXL=INDXF(INDD,INDA,MAXB)
             GINDXR=INDXF(INDB,INDC,MAXB)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX=GINDX0+GOFF(IRREPB,IRREPA)
             GAMMA(GINDX)=GAMMA(GINDX)-X
195         CONTINUE
190        CONTINUE
185      CONTINUE
180     CONTINUE
       ENDIF
170   CONTINUE
160   CONTINUE
90    CONTINUE
      RETURN
      END
