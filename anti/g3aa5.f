      SUBROUTINE G3AA5(GAMMA,BUF,GOFF,LENGAM,ISPIN,FABCI)
C
C THIS ROUTINE PROCESSES THE G(I<J,KA) GAMMA LISTS FOR SPIN CASES
C  AAAA AND BBBB AND FILLS THE SYMMETRY CASE ABAB MULLIKEN ORDERED 
C  GAMMA LIST.  THIS CODE IS USED FOR UHF CALCULATIONS ONLY.
C
C       21 21        22 11        22 11
C     G(BI,AC)  =  G(AC,BI) <-- g<AB,CI>
C  
C       21 21        21 21        11 22
C     G(IB,CA)  =  G(CA,IB) <-- g<AB,CI>
C
C IN ADDITION, THE FOLLOWING SYMMETRIZATION IS PERFORMED
C   
C      21 21      21 21      21 21
C    G(AC,BI) = G(AC,BI) + G(CA,BI) 
C
C VIA
C
C       21 21        21 21        12 21
C     G(BI,CA)  =  G(CA,IB) <-- g<AB,CI>
C
C       21 21        21 21        21 12        12 12
C     G(IB,AC)  =  G(AC,IB) <-- g<AB,CI> = - g<BA,CI>
C   
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 
      DOUBLE PRECISION FABIJ,FAIBJ,FIJKL,FABCD,FABCI,FIJKA
      INTEGER GOFF(8,8),IOFFSL(8),IOFFSR(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
      NNM1O2(I)   =(I*(I-1))/2
C
C COMPUTE OFFSETS TO FIRST ELEMENT OF ALL G(XX,YY) IRREP POSITIONS
C  AND PUT IN FIRST CONTRIBUTIONS.
C
      LISTABCI=126+ISPIN
      IOFFR=1
      IOFFL=1
      DO 1000 IRREPR=1,NIRREP
       IOFFSR(IRREPR)=IOFFR
       IOFFSL(IRREPR)=IOFFL
       IOFFR=IOFFR+VRT(IRREPR,ISPIN)*POP(IRREPR,ISPIN)
       IOFFL=IOFFL+NNM1O2(VRT(IRREPR,ISPIN))
1000  CONTINUE
      DO 100 IRREPDO=2,NIRREP
       DO 110 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPL)
        NUMI=POP(IRREPR,ISPIN)
        NUMC=VRT(IRREPR,ISPIN)
        NUMB=VRT(IRREPL,ISPIN)
        NUMA=VRT(IRREPL,ISPIN)
        MAXL=MOPOP(IRREPL)*MOPOP(IRREPR)
        MAXA=MOPOP(IRREPR)
        MAXB=MOPOP(IRREPL)
        DO 120 INDXI=1,NUMI
         DO 130 INDXC=1,NUMC
          INDI=INDXI
          INDC=INDXC+POP(IRREPR,ISPIN)
          CALL GETLST(BUF,ILOGREC,1,1,1,LISTABCI)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          IF(IRREPL.LT.IRREPR)THEN
C  
C       21 21        21 21        11 22
C     G(AC,BI)  =  G(BI,AC) <-- g<AB,CI>
C
           DO 140 INDXB=2,NUMB
*VOCL LOOP,NOVREC
CDIR$ IVDEP
            DO 150 INDXA=1,INDXB-1
             X=BUF(IPOS)*FABCI
             IPOS=IPOS+1
             INDA=INDXA+POP(IRREPL,ISPIN)
             INDB=INDXB+POP(IRREPL,ISPIN)
             GINDXL=INDXF(INDC,INDA,MAXA)
             GINDXR=INDXF(INDI,INDB,MAXA)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDXL=INDXF(INDC,INDB,MAXA)
             GINDXR=INDXF(INDI,INDA,MAXA)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)-X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)-X
150         CONTINUE
140        CONTINUE
          ELSE
C
C       21 21        22 11        22 11
C     G(BI,AC)  =  G(AC,BI) <-- g<AB,CI>
C  
           DO 141 INDXB=2,NUMB
*VOCL LOOP,NOVREC
CDIR$ IVDEP
            DO 151 INDXA=1,INDXB-1
             X=BUF(IPOS)*FABCI
             IPOS=IPOS+1
             INDA=INDXA+POP(IRREPL,ISPIN)
             INDB=INDXB+POP(IRREPL,ISPIN)
             GINDXL=INDXF(INDA,INDC,MAXB)
             GINDXR=INDXF(INDB,INDI,MAXB)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDXL=INDXF(INDB,INDC,MAXB)
             GINDXR=INDXF(INDA,INDI,MAXB)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)-X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)-X
151         CONTINUE
141        CONTINUE
          ENDIF
130      CONTINUE
120     CONTINUE
110    CONTINUE
100   CONTINUE
C
C NOW WE MUST LOOP OVER ALL OTHER IRREPS AND PUT THE OTHER PIECE IN
C
C       21 21        21 21        12 21
C     G(BI,CA)  =  G(CA,BI) <-- g<AB,CI>
C
C       21 21        21 21        21 12        12 12
C     G(IB,AC)  =  G(AC,IB) <-- g<AB,CI> = - g<BA,CI>
C   
      DO 160 IRREPDO=2,NIRREP
C
C COMPUTE OFFSETS TO ALL AB AB POSITIONS
C
      IOFFR=1
      IOFFL=1
      DO 1200 IRREPR=1,NIRREP
       IRREPL=DIRPRD(IRREPR,IRREPDO)
       IOFFSR(IRREPR)=IOFFR
       IOFFR=IOFFR+VRT(IRREPL,ISPIN)*POP(IRREPR,ISPIN)
       IF(IRREPL.GT.IRREPR)GOTO 1200
       IOFFSL(IRREPR)=IOFFL
       IOFFL=IOFFL+VRT(IRREPL,ISPIN)*VRT(IRREPR,ISPIN)
1200  CONTINUE
      DO 170 IRREPB=1,NIRREP
       IRREPA=DIRPRD(IRREPB,IRREPDO)
       ILOGREC=IOFFSR(IRREPB)
       IFIRST=IOFFSL(IRREPA)
       IFIRST2=IOFFSL(IRREPB)
       NUMI=POP(IRREPB,ISPIN)
       NUMC=VRT(IRREPA,ISPIN)
       NUMB=VRT(IRREPA,ISPIN)
       NUMA=VRT(IRREPB,ISPIN)
       MAXL=MOPOP(IRREPA)*MOPOP(IRREPB)
       MAXA=MOPOP(IRREPA)
       MAXB=MOPOP(IRREPB)
       DO 180 INDXI=1,NUMI
        DO 185 INDXC=1,NUMC
         INDI=INDXI
         INDC=INDXC+POP(IRREPA,ISPIN)
         CALL GETLST(BUF,ILOGREC,1,1,IRREPDO,LISTABCI)
         ILOGREC=ILOGREC+1
         IF(IRREPA.LT.IRREPB)THEN
C
C       21 21        21 21        21 12        12 12
C     G(IB,AC)  =  G(AC,IB) <-- g<AB,CI> = - g<BA,CI>
C   
          IPOS=IFIRST2
          DO 190 INDXA=1,NUMA
*VOCL LOOP,NOVREC
CDIR$ IVDEP
            DO 195 INDXB=1,NUMB
             INDB=INDXB+POP(IRREPA,ISPIN)
             INDA=INDXA+POP(IRREPB,ISPIN)
             X=BUF(IPOS)*FABCI
             IPOS=IPOS+1
             GINDXL=INDXF(INDI,INDB,MAXB)
             GINDXR=INDXF(INDA,INDC,MAXB)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX=GINDX0+GOFF(IRREPB,IRREPA)
             GAMMA(GINDX)=GAMMA(GINDX)-X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX=GINDX0+GOFF(IRREPB,IRREPA)
             GAMMA(GINDX)=GAMMA(GINDX)-X
195         CONTINUE
190        CONTINUE
          ELSE
C
C       21 21        21 21        12 21
C     G(BI,CA)  =  G(CA,BI) <-- g<AB,CI>
C
           IPOS=IFIRST
           DO 191 INDXB=1,NUMB
*VOCL LOOP,NOVREC
CDIR$ IVDEP
            DO 196 INDXA=1,NUMA
             INDB=INDXB+POP(IRREPA,ISPIN)
             INDA=INDXA+POP(IRREPB,ISPIN)
             X=BUF(IPOS)*FABCI
             IPOS=IPOS+1
             GINDXL=INDXF(INDB,INDI,MAXA)
             GINDXR=INDXF(INDC,INDA,MAXA)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX=GINDX0+GOFF(IRREPA,IRREPB)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX=GINDX0+GOFF(IRREPA,IRREPB)
             GAMMA(GINDX)=GAMMA(GINDX)+X
196         CONTINUE
191        CONTINUE
          ENDIF
185      CONTINUE
180     CONTINUE
170   CONTINUE
160   CONTINUE
      RETURN
      END
