      SUBROUTINE G3AB5B(GAMMA,BUF,GOFF,LENGAM,FABCI)
C
C THIS ROUTINE PROCESSES THE G(Ab,Ci) GAMMA LISTS FOR SPIN CASE
C  AB AND FILLS THE SYMMETRY CASE ABAB MULLIKEN ORDERED GAMMA LIST
C  FOR SPIN CASE AABB.  THIS CODE IS USED FOR UHF CALCULATIONS ONLY.
C
C NOTE THAT ABAB IN MULLIKEN CORRESPONDS TO AABB IN DIRAC.
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 
      DOUBLE PRECISION FABIJ,FAIBJ,FIJKL,FABCD,FABCI,FIJKA
      INTEGER GOFF(8,8),IOFFSL(8),IOFFSR(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
C
C FIRST DO LIST 130
C
C       21 21        22 11
C     G(AC,bi) <-- g<Ab,Ci>
C
C       21 21        11 22
C     G(CA,bi) <-- g<Ab,Ci>
C
C COMPUTE OFFSETS TO FIRST ELEMENT OF ALL G(XX,YY) IRREP POSITIONS
C  AND PUT IN FIRST CONTRIBUTION (THE PIECE FROM DPD IRREP = 1).
C
      LISTABCI=130
      IOFFR=1
      IOFFL=1
      DO 1000 IRREPR=1,NIRREP
       IOFFSR(IRREPR)=IOFFR
       IOFFSL(IRREPR)=IOFFL
       IOFFR=IOFFR+VRT(IRREPR,1)*POP(IRREPR,2)
       IOFFL=IOFFL+VRT(IRREPR,1)*VRT(IRREPR,2)
1000  CONTINUE
      DO 100 IRREPDO=2,NIRREP
       DO 110 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPL)
        NUMI=POP(IRREPR,2)
        NUMC=VRT(IRREPR,1)
        NUMB=VRT(IRREPL,2)
        NUMA=VRT(IRREPL,1)
        MAXL=MOPOP(IRREPL)*MOPOP(IRREPR)
        MAXA=MOPOP(IRREPL)
        MAXB=MOPOP(IRREPR)
        DO 120 INDXI=1,NUMI
         DO 130 INDXC=1,NUMC
          INDC=INDXC+POP(IRREPR,1)
          INDI=INDXI
          CALL GETLST(BUF,ILOGREC,1,1,1,LISTABCI)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          IF(IRREPL.GT.IRREPR)THEN
C
C       21 21        22 11
C     G(AC,bi) <-- g<Ab,Ci>
C
           DO 140 INDXB=1,NUMB
            DO 150 INDXA=1,NUMA
             X=BUF(IPOS)*FABCI
             IPOS=IPOS+1
             INDA=INDXA+POP(IRREPL,1)
             INDB=INDXB+POP(IRREPL,2)
             GINDXL=INDXF(INDA,INDC,MAXA)
             GINDXR=INDXF(INDB,INDI,MAXA)
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
150         CONTINUE
140        CONTINUE
          ELSE
C
C       21 21        11 22
C     G(CA,bi) <-- g<Ab,Ci>
C
           IPOS=IFIRST
           DO 141 INDXB=1,NUMB
            DO 151 INDXA=1,NUMA
             X=BUF(IPOS)*FABCI
             IPOS=IPOS+1
             INDA=INDXA+POP(IRREPL,1)
             INDB=INDXB+POP(IRREPL,2)
             GINDXL=INDXF(INDC,INDA,MAXB)
             GINDXR=INDXF(INDI,INDB,MAXB)
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X
151         CONTINUE
141        CONTINUE
          ENDIF
130      CONTINUE
120     CONTINUE
110    CONTINUE
100   CONTINUE
C
C NOW WE MUST LOOP OVER ALL OTHER IRREPS AND PUT THE OTHER PIECES IN
C
C       21 21        21 12
C     G(AC,ib) <-- g<Ab,Ci>
C
C       21 21        12 21
C     G(CA,ib) <-- g<Ab,Ci>
C
C
      DO 160 IRREPDO=2,NIRREP
C
C COMPUTE OFFSETS TO ALL BA AB POSITIONS
C
      IOFFR=1
      IOFFL=1
      DO 1200 IRREPR=1,NIRREP
       IRREPL=DIRPRD(IRREPR,IRREPDO)
       IOFFSR(IRREPR)=IOFFR
       IOFFSL(IRREPR)=IOFFL
       IOFFR=IOFFR+VRT(IRREPL,1)*POP(IRREPR,2)
       IOFFL=IOFFL+VRT(IRREPL,1)*VRT(IRREPR,2)
1200  CONTINUE
      DO 170 IRREPB=1,NIRREP
       IRREPA=DIRPRD(IRREPB,IRREPDO)
       ILOGREC=IOFFSR(IRREPB)
       IFIRST=IOFFSL(IRREPA)
       NUMI=POP(IRREPB,2)
       NUMC=VRT(IRREPA,1)
       NUMB=VRT(IRREPA,2)
       NUMA=VRT(IRREPB,1)
       MAXL=MOPOP(IRREPA)*MOPOP(IRREPB)
       MAXA=MOPOP(IRREPA)
       MAXB=MOPOP(IRREPB)
       DO 180 INDXI=1,NUMI
        DO 185 INDXC=1,NUMC
         INDC=INDXC+POP(IRREPA,1)
         INDI=INDXI
         CALL GETLST(BUF,ILOGREC,1,1,IRREPDO,LISTABCI)
         ILOGREC=ILOGREC+1
         IPOS=IFIRST
         IF(IRREPA.LT.IRREPB)THEN
C
C       21 21        21 12
C     G(AC,ib) <-- g<Ab,Ci>
C
          DO 190 INDXB=1,NUMB
           DO 195 INDXA=1,NUMA
            INDB=INDXB+POP(IRREPA,2)
            INDA=INDXA+POP(IRREPB,1)
            X=BUF(IPOS)*FABCI
            IPOS=IPOS+1
            GINDXL=INDXF(INDA,INDC,MAXB)
            GINDXR=INDXF(INDI,INDB,MAXB)
            GINDX0=INDXF(GINDXR,GINDXL,MAXL)
            GINDX=GINDX0+GOFF(IRREPB,IRREPA)
            GAMMA(GINDX)=GAMMA(GINDX)+X
195        CONTINUE
190       CONTINUE
         ELSE
C
C       21 21        12 21
C     G(CA,ib) <-- g<Ab,Ci>
C
          DO 191 INDXB=1,NUMB
           DO 196 INDXA=1,NUMA
            INDB=INDXB+POP(IRREPA,2)
            INDA=INDXA+POP(IRREPB,1)
            X=BUF(IPOS)*FABCI
            IPOS=IPOS+1
            GINDXL=INDXF(INDC,INDA,MAXA)
            GINDXR=INDXF(INDB,INDI,MAXA)
            GINDX0=INDXF(GINDXR,GINDXL,MAXL)
            GINDX=GINDX0+GOFF(IRREPA,IRREPB)
            GAMMA(GINDX)=GAMMA(GINDX)+X
196        CONTINUE
191       CONTINUE
         ENDIF
185     CONTINUE
180    CONTINUE
170   CONTINUE
160   CONTINUE
C
C NOW DO LIST 129
C
C       21 21        22 11
C     G(AI,bc) <-- g<Ab,Ic>
C
C       21 21        11 22
C     G(IA,cb) <-- g<Ab,Ic>
C
C COMPUTE OFFSETS TO FIRST ELEMENT OF ALL G(XX,YY) IRREP POSITIONS
C  AND PUT IN FIRST CONTRIBUTION (THE PIECE FROM DPD IRREP = 1).
C
      LISTABCI=129
      IOFFR=1
      IOFFL=1
      DO 2000 IRREPR=1,NIRREP
       IOFFSR(IRREPR)=IOFFR
       IOFFSL(IRREPR)=IOFFL
       IOFFR=IOFFR+POP(IRREPR,1)*VRT(IRREPR,2)
       IOFFL=IOFFL+VRT(IRREPR,1)*VRT(IRREPR,2)
2000  CONTINUE
      DO 200 IRREPDO=2,NIRREP
       DO 210 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPL)
        NUMC=VRT(IRREPR,2)
        NUMI=POP(IRREPR,1)
        NUMB=VRT(IRREPL,2)
        NUMA=VRT(IRREPL,1)
        MAXL=MOPOP(IRREPL)*MOPOP(IRREPR)
        MAXA=MOPOP(IRREPL)
        MAXB=MOPOP(IRREPR)
        DO 220 INDXC=1,NUMC
         DO 230 INDXI=1,NUMI
          INDC=INDXC+POP(IRREPR,2)
          INDI=INDXI
          CALL GETLST(BUF,ILOGREC,1,1,1,LISTABCI)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          IF(IRREPL.GT.IRREPR)THEN
C
C       21 21        22 11
C     G(AI,bc) <-- g<Ab,Ic>
C
           DO 240 INDXB=1,NUMB
            DO 250 INDXA=1,NUMA
             X=BUF(IPOS)*FABCI
             IPOS=IPOS+1
             INDB=INDXB+POP(IRREPL,2)
             INDA=INDXA+POP(IRREPL,1)
             GINDXL=INDXF(INDA,INDI,MAXA)
             GINDXR=INDXF(INDB,INDC,MAXA)
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
250         CONTINUE
240        CONTINUE
          ELSE
C
C       21 21        11 22
C     G(IA,cb) <-- g<Ab,Ic>
C
           DO 241 INDXB=1,NUMB
            DO 251 INDXA=1,NUMA
             X=BUF(IPOS)*FABCI
             IPOS=IPOS+1
             INDB=INDXB+POP(IRREPL,2)
             INDA=INDXA+POP(IRREPL,1)
             GINDXL=INDXF(INDI,INDA,MAXB)
             GINDXR=INDXF(INDC,INDB,MAXB)
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X
251         CONTINUE
241        CONTINUE
          ENDIF
230      CONTINUE
220     CONTINUE
210    CONTINUE
200   CONTINUE
C
C NOW WE MUST LOOP OVER ALL OTHER IRREPS AND PUT THE OTHER PIECES IN
C
C       21 21        21 12
C     G(AI,bc) <-- g<Ab,Ic>
C
C       21 21        12 21
C     G(IA,cb) <-- g<Ab,Ic>
C
C
      DO 260 IRREPDO=2,NIRREP
C
C COMPUTE OFFSETS TO ALL BA AB POSITIONS
C
      IOFFR=1
      IOFFL=1
      DO 2200 IRREPR=1,NIRREP
       IRREPL=DIRPRD(IRREPR,IRREPDO)
       IOFFSR(IRREPR)=IOFFR
       IOFFSL(IRREPR)=IOFFL
       IOFFR=IOFFR+POP(IRREPL,1)*VRT(IRREPR,2)
       IOFFL=IOFFL+VRT(IRREPL,1)*VRT(IRREPR,2)
2200  CONTINUE
      DO 270 IRREPB=1,NIRREP
       IRREPA=DIRPRD(IRREPB,IRREPDO)
       ILOGREC=IOFFSR(IRREPB)
       IFIRST=IOFFSL(IRREPA)
       NUMC=VRT(IRREPB,2)
       NUMI=POP(IRREPA,1)
       NUMB=VRT(IRREPA,2)
       NUMA=VRT(IRREPB,1)
       MAXL=MOPOP(IRREPA)*MOPOP(IRREPB)
       MAXA=MOPOP(IRREPA)
       MAXB=MOPOP(IRREPB)
       DO 280 INDXC=1,NUMC
        DO 285 INDXI=1,NUMI
         INDC=INDXC+POP(IRREPB,2)
         INDI=INDXI 
         CALL GETLST(BUF,ILOGREC,1,1,IRREPDO,LISTABCI)
         ILOGREC=ILOGREC+1
         IPOS=IFIRST
         IF(IRREPA.LT.IRREPB)THEN
C
C       21 21        21 12
C     G(AI,cb) <-- g<Ab,Ic>
C
          DO 290 INDXB=1,NUMB
           DO 295 INDXA=1,NUMA
            INDB=INDXB+POP(IRREPA,2)
            INDA=INDXA+POP(IRREPB,1)
            X=BUF(IPOS)*FABCI
            IPOS=IPOS+1
            GINDXL=INDXF(INDA,INDI,MAXB)
            GINDXR=INDXF(INDC,INDB,MAXB)
            GINDX0=INDXF(GINDXR,GINDXL,MAXL)
            GINDX=GINDX0+GOFF(IRREPB,IRREPA)
            GAMMA(GINDX)=GAMMA(GINDX)+X
295        CONTINUE
290       CONTINUE
         ELSE
C
C       21 21        12 21
C     G(IA,bc) <-- g<Ab,Ic>
C
          DO 291 INDXB=1,NUMB
           DO 296 INDXA=1,NUMA
            INDB=INDXB+POP(IRREPA,2)
            INDA=INDXA+POP(IRREPB,1)
            X=BUF(IPOS)*FABCI
            IPOS=IPOS+1
            GINDXL=INDXF(INDI,INDA,MAXA)
            GINDXR=INDXF(INDB,INDC,MAXA)
            GINDX0=INDXF(GINDXR,GINDXL,MAXL)
            GINDX=GINDX0+GOFF(IRREPA,IRREPB)
            GAMMA(GINDX)=GAMMA(GINDX)+X
296        CONTINUE
291       CONTINUE
         ENDIF
285     CONTINUE
280    CONTINUE
270   CONTINUE
260   CONTINUE
      RETURN
      END
