C
C THIS ROUTINE CONTROLS THE REMOVAL OF THE MOINTS FILE WHICH 
C  OCCURS BEFORE THE GAMMA BACKTRANSFORMATION
C
      SUBROUTINE ENDMOI(ICORE,MAXCOR,IUHF)
      IMPLICIT INTEGER (A-Z)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      LOGICAL GABCD,DABCD
      CHARACTER*4 SPCASE(3)
      DIMENSION ICORE(MAXCOR)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /ABCD/ GABCD,DABCD
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      DATA SPCASE /'AAAA','BBBB','AABB'/

C   o SPECIAL CODE FOR MBPT2 AND MBPT3 (mutually exclusive)
C     (COPY T AMPLITUDES TO GAMLAM FILE)
      IF (MBPT2.OR.MBPT3) THEN
         if (mbpt2) call aces_io_remove(51,'GAMLAM')
         IMODE = 0
         DO ISPIN = 3, 3-2*IUHF, -1
            LIST = 13 + ISPIN
            IF (MBPT2) THEN
               LIST1 = 43 + ISPIN
            ELSE
               LIST1 = 60 + ISPIN 
            END IF
            CALL INIPCK(1,ISYTYP(1,LIST),ISYTYP(2,LIST),LIST+100,
     &                  IMODE,0,0)
            IMODE = 0
            DO IRREP = 1, NIRREP
               NUMDIS = IRPDPD(IRREP,ISYTYP(2,43+ISPIN))
               DISSIZ = IRPDPD(IRREP,ISYTYP(1,43+ISPIN))
               I000 = 1
               I010 = I000 + NUMDIS*DISSIZ*IINTFP
               IF (I010.GE.MAXCOR) CALL INSMEM('ENDMOI',I010,MAXCOR)
               CALL GETLST(ICORE(I000),1,NUMDIS,1,IRREP,LIST1)
               CALL PUTLST(ICORE(I000),1,NUMDIS,1,IRREP,LIST+100)
            END DO
         END DO
c     END IF (MBPT2.OR.MBPT3)
      END IF

C   o SPECIAL CODE FOR DIRECT EVALUATION G(AB,CD)
      IF (DABCD) THEN
         IMODE = 0
         DO ISPIN = 3, 3-2*IUHF, -1
            LISTOLD =  43 + ISPIN
            LISTNEW = 153 + ISPIN
            CALL INIPCK(1,ISYTYP(1,LISTOLD),ISYTYP(2,LISTOLD),LISTNEW,
     &                  IMODE,0,0)
            DO IRREP = 1, NIRREP
               NUMDIS = IRPDPD(IRREP,ISYTYP(2,LISTOLD))
               DISSIZ = IRPDPD(IRREP,ISYTYP(1,LISTOLD))
               I000 = 1
               I010 = I000 + NUMDIS*DISSIZ*IINTFP
               IF (I010.GE.MAXCOR) CALL INSMEM('ENDMOI',I010,MAXCOR)
               CALL GETLST(ICORE(I000),1,NUMDIS,1,IRREP,LISTOLD)
               CALL PUTLST(ICORE(I000),1,NUMDIS,1,IRREP,LISTNEW)
            END DO
         END DO
         IF (.NOT.CCD) THEN
            CALL UPDMOI(1,NT(1),1,157,0,0)
            CALL GETLST(ICORE(I000),1,1,1,1,90)
            CALL PUTLST(ICORE(I000),1,1,1,1,157)
            IF (IUHF.EQ.1) THEN
            CALL UPDMOI(1,NT(2),2,157,0,0)
            CALL GETLST(ICORE(I000),1,1,1,2,90)
            CALL PUTLST(ICORE(I000),1,1,1,2,157)
            END IF
         END IF
c     END IF (DABCD)
      END IF

      CALL ACES_IO_REMOVE(50,'MOINTS')
      IF (GABCD) THEN
      CALL ACES_IO_REMOVE(52,'MOABCD')
      END IF

      RETURN
      END

