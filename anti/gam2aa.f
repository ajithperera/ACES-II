      SUBROUTINE GAM2AA(GAMMA,BUF,BUF1,BUF2,GOFF,LENGAM)
C
C THIS ROUTINE FORMS THE AABB SYMMETRY TYPE AO GAMMA LISTS
C  FOR SPIN CASES AAAA AND BBBB AND WRITES THEM TO THE
C  BACKTRANSFORMATION MOINTS FILE.
C
C NOTE THAT AABB IN MULLIKEN CORRESPONDS TO ABAB IN DIRAC.  CONSEQUENTLY,
C  ALL ELEMENTS OF GAMMA WHICH ARE PROCESSED HERE COME FROM DPD LISTS
C  FOR IRREPS 2-h, SINCE A.NE.B.
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(*),X,X1,X2,HALF
      DOUBLE PRECISION BUF1(*),BUF2(*)
      DOUBLE PRECISION FABIJ,FAIBJ,FABCD,FIJKL,FABCI,FIJKA
      INTEGER GOFF(8,8),IOFFSR(8),IOFFSL(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      LOGICAL ROHF
      LOGICAL TDA,EOM
      COMMON /EXCITE/ TDA,EOM
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
      COMMON /FACTORS/ FABIJ,FABCD,FIJKL,FAIBJ,FABCI,FIJKA
      COMMON /REF/ ROHF
C
      DATA HALF /0.5D0/
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
C
      DO 100 ISPIN=1,2
       CALL ZERO(GAMMA,LENGAM)
       IF(TDA)THEN
        CALL G2AA4(GAMMA,BUF,GOFF,LENGAM,ISPIN,FAIBJ)
       ELSE
        CALL G2AA3(GAMMA,BUF,GOFF,LENGAM,ISPIN,FABIJ)
        IF(MBPT2)GOTO 2000
        CALL G2AA1(GAMMA,BUF,GOFF,LENGAM,ISPIN,FIJKL)
        CALL G2AA6(GAMMA,BUF,BUF1,BUF2,GOFF,LENGAM,ISPIN,FABCD)
        CALL G2AA4(GAMMA,BUF,GOFF,LENGAM,ISPIN,FAIBJ)
        IF(CCD.OR.(MBPT3.AND.(.NOT.ROHF)))GOTO 2000
        CALL G2AA2(GAMMA,BUF,GOFF,LENGAM,ISPIN,FIJKA)
        CALL G2AA5(GAMMA,BUF,GOFF,LENGAM,ISPIN,FABCI)
2000    CONTINUE
       ENDIF
       LIST1=7
       LIST2=1+(ISPIN-1)*50
       IOFF=1
       CALL SSCAL(LENGAM,HALF,GAMMA,1)
       DO 5000 IRREPR=2,NIRREP
        DO 5100 IRREPL=1,IRREPR-1
         NUMDIS=NNP1O2(MOPOP(IRREPR))
         DISSIZ=NNP1O2(MOPOP(IRREPL))
         CALL PUTLST(GAMMA(IOFF),1,NUMDIS,1,LIST1,LIST2)
         IOFF=IOFF+DISSIZ*NUMDIS
         LIST2=LIST2+1
5100    CONTINUE
5000   CONTINUE
100   CONTINUE
      RETURN
      END
