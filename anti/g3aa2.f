      SUBROUTINE G3AA2(GAMMA,BUF,GOFF,LENGAM,ISPIN,FIJKA)
C
C THIS ROUTINE PROCESSES THE G(I<J,KA) GAMMA LISTS FOR SPIN CASES
C  AAAA AND BBBB AND FILLS THE SYMMETRY CASE ABAB MULLIKEN ORDERED 
C  GAMMA LIST.  THIS CODE IS USED FOR UHF CALCULATIONS ONLY.
C
C       21 21        22 11        22 11
C     G(JA,IK)  =  G(IK,JA) <-- g<IJ,KA>
C  
C       21 21        21 21        11 22
C     G(AJ,KI)  =  G(KI,AJ) <-- g<IJ,KA>
C
C IN ADDITION, THE FOLLOWING SYMMETRIZATION IS PERFORMED
C   
C      21 21      21 21      21 21
C    G(IK,JA) = G(IK,JA) + G(KI,JA) 
C
C VIA
C
C       21 21        21 21        12 21
C     G(JA,KI)  =  G(KI,JA) <-- g<IJ,KA>
C
C       21 21        21 21        21 12        12 12
C     G(AJ,IK)  =  G(IK,AJ) <-- g<IJ,KA> = - g<JI,KA>
C   
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 
      DOUBLE PRECISION FABIJ,FAIBJ,FIJKL,FABCD,FABCI,FIJKA
      INTEGER GOFF(8,8),IOFFSL(8),IOFFSR(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
      NNM1O2(I)   =(I*(I-1))/2
C
C COMPUTE OFFSETS TO FIRST ELEMENT OF ALL G(XX,YY) IRREP POSITIONS
C  AND PUT IN FIRST CONTRIBUTIONS.
C
      LISTIJKA=106+ISPIN
      IOFFR=1
      IOFFL=1
      DO 1000 IRREPR=1,NIRREP
       IOFFSR(IRREPR)=IOFFR
       IOFFSL(IRREPR)=IOFFL
       IOFFR=IOFFR+POP(IRREPR,ISPIN)*VRT(IRREPR,ISPIN)
       IOFFL=IOFFL+NNM1O2(POP(IRREPR,ISPIN))
1000  CONTINUE
      DO 100 IRREPDO=2,NIRREP
       DO 110 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPL)
        NUMA=VRT(IRREPR,ISPIN)
        NUMK=POP(IRREPR,ISPIN)
        NUMJ=POP(IRREPL,ISPIN)
        NUMI=POP(IRREPL,ISPIN)
        MAXL=MOPOP(IRREPL)*MOPOP(IRREPR)
        MAXA=MOPOP(IRREPR)
        MAXB=MOPOP(IRREPL)
        DO 120 INDXA=1,NUMA
         DO 130 INDXK=1,NUMK
          INDA=INDXA+POP(IRREPR,ISPIN)
          INDK=INDXK
          CALL GETLST(BUF,ILOGREC,1,1,1,LISTIJKA)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          IF(IRREPL.LT.IRREPR)THEN
C  
C       21 21        21 21        11 22
C     G(AJ,KI)  =  G(KI,AJ) <-- g<IJ,KA>
C
           DO 140 INDXJ=2,NUMJ
*VOCL LOOP,NOVREC
CDIR$ IVDEP
            DO 150 INDXI=1,INDXJ-1
             X=BUF(IPOS)*FIJKA
             IPOS=IPOS+1
             INDI=INDXI
             INDJ=INDXJ
             GINDXL=INDXF(INDK,INDI,MAXA)
             GINDXR=INDXF(INDA,INDJ,MAXA)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDXL=INDXF(INDK,INDJ,MAXA)
             GINDXR=INDXF(INDA,INDI,MAXA)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)-X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)-X
150         CONTINUE
140        CONTINUE
          ELSE
C
C       21 21        22 11        22 11
C     G(JA,IK)  =  G(IK,JA) <-- g<IJ,KA>
C  
           DO 141 INDXJ=2,NUMJ
*VOCL LOOP,NOVREC
CDIR$ IVDEP
            DO 151 INDXI=1,INDXJ-1
             X=BUF(IPOS)*FIJKA
             IPOS=IPOS+1
             INDI=INDXI
             INDJ=INDXJ
             GINDXL=INDXF(INDI,INDK,MAXB)
             GINDXR=INDXF(INDJ,INDA,MAXB)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDXL=INDXF(INDJ,INDK,MAXB)
             GINDXR=INDXF(INDI,INDA,MAXB)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)-X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)-X
151         CONTINUE
141        CONTINUE
          ENDIF
130      CONTINUE
120     CONTINUE
110    CONTINUE
100   CONTINUE
C
C NOW WE MUST LOOP OVER ALL OTHER IRREPS AND PUT THE OTHER PIECE IN
C
C       21 21        21 21        12 21
C     G(JA,KI)  =  G(KI,JA) <-- g<IJ,KA>
C
C       21 21        21 21        21 12        12 12
C     G(AJ,IK)  =  G(IK,AJ) <-- g<IJ,KA> = - g<JI,KA>
C   
      DO 160 IRREPDO=2,NIRREP
C
C COMPUTE OFFSETS TO ALL AB AB POSITIONS
C
      IOFFR=1
      IOFFL=1
      DO 1200 IRREPR=1,NIRREP
       IRREPL=DIRPRD(IRREPR,IRREPDO)
       IOFFSR(IRREPR)=IOFFR
       IOFFR=IOFFR+POP(IRREPL,ISPIN)*VRT(IRREPR,ISPIN)
       IF(IRREPL.GT.IRREPR)GOTO 1200
       IOFFSL(IRREPR)=IOFFL
       IOFFL=IOFFL+POP(IRREPL,ISPIN)*POP(IRREPR,ISPIN)
1200  CONTINUE
      DO 170 IRREPB=1,NIRREP
       IRREPA=DIRPRD(IRREPB,IRREPDO)
       ILOGREC=IOFFSR(IRREPB)
       IFIRST=IOFFSL(IRREPA)
       IFIRST2=IOFFSL(IRREPB)
       NUMA=VRT(IRREPB,ISPIN)
       NUMK=POP(IRREPA,ISPIN)
       NUMJ=POP(IRREPA,ISPIN)
       NUMI=POP(IRREPB,ISPIN)
       MAXL=MOPOP(IRREPA)*MOPOP(IRREPB)
       MAXA=MOPOP(IRREPA)
       MAXB=MOPOP(IRREPB)
       DO 180 INDXA=1,NUMA
        DO 185 INDXK=1,NUMK
         INDA=INDXA+POP(IRREPB,ISPIN)
         INDK=INDXK
         CALL GETLST(BUF,ILOGREC,1,1,IRREPDO,LISTIJKA)
         ILOGREC=ILOGREC+1
         IF(IRREPA.LT.IRREPB)THEN
C
C       21 21        21 21        21 12        12 12
C     G(AJ,IK)  =  G(IK,AJ) <-- g<IJ,KA> = - g<JI,KA>
C   
          IPOS=IFIRST2
          DO 190 INDXI=1,NUMI
*VOCL LOOP,NOVREC
CDIR$ IVDEP
            DO 195 INDXJ=1,NUMJ
             INDJ=INDXJ
             INDI=INDXI
             X=BUF(IPOS)*FIJKA
             IPOS=IPOS+1
             GINDXL=INDXF(INDA,INDJ,MAXB)
             GINDXR=INDXF(INDI,INDK,MAXB)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX=GINDX0+GOFF(IRREPB,IRREPA)
             GAMMA(GINDX)=GAMMA(GINDX)-X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX=GINDX0+GOFF(IRREPB,IRREPA)
             GAMMA(GINDX)=GAMMA(GINDX)-X
195         CONTINUE
190        CONTINUE
          ELSE
C
C       21 21        21 21        12 21
C     G(JA,KI)  =  G(KI,JA) <-- g<IJ,KA>
C
           IPOS=IFIRST
           DO 191 INDXJ=1,NUMJ
*VOCL LOOP,NOVREC
CDIR$ IVDEP
            DO 196 INDXI=1,NUMI
             INDJ=INDXJ
             INDI=INDXI
             X=BUF(IPOS)*FIJKA
             IPOS=IPOS+1
             GINDXL=INDXF(INDJ,INDA,MAXA)
             GINDXR=INDXF(INDK,INDI,MAXA)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX=GINDX0+GOFF(IRREPA,IRREPB)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX=GINDX0+GOFF(IRREPA,IRREPB)
             GAMMA(GINDX)=GAMMA(GINDX)+X
196         CONTINUE
191        CONTINUE
          ENDIF
185      CONTINUE
180     CONTINUE
170   CONTINUE
160   CONTINUE
      RETURN
      END
