      SUBROUTINE G2AA6(GAMMA,BUF,BUF1,BUF2,GOFF,LENGAM,ISPIN,FABCD)
C
C THIS ROUTINE PROCESSES THE G(A<C,B<D) GAMMAS AND AUGMENTS THE 
C  AABB MULLIKEN ORDERED MO GAMMA LIST FOR SPIN CASES AAAA AND BBBB.
C 
C              11 22        12 12
C            G(AC,BD) <-- g<AB,CD>
C
C              11 22        12 12
C            G(CA,BD) <-- g<AB,CD>
C
C IN ADDITION, THE FOLLOWING SYMMETRIZATION IS PERFORMED:
C 
C              11 22      11 22      11 22
C            G(AB,CD) = G(AB,CD) + G(BA,CD)
C
C NOTE THAT AABB IN MULLIKEN CORRESPONDS TO ABAB IN DIRAC.  CONSEQUENTLY,
C  ALL ELEMENTS OF GAMMA WHICH ARE PROCESSED HERE COME FROM DPD LISTS
C  FOR IRREPS 2-h, SINCE A.NE.B.
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(*),X,X1,X2
      DOUBLE PRECISION BUF1(*),BUF2(*),ONE,TWO 
      DOUBLE PRECISION FABIJ,FAIBJ,FABCD,FIJKL,FABCI,FIJKA
      INTEGER GOFF(8,8),IOFFSR(8),IOFFSL(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      LOGICAL MBPT4
      LOGICAL GABCD,DABCD
      LOGICAL TDA,EOM
C
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
      COMMON /IABCD/ IOFFABCD 
      COMMON /ABCD/  GABCD,DABCD
      COMMON /EXCITE/ TDA,EOM
      COMMON /STATSYM/IRREPX
C
      DATA ONE,TWO/1.D0,2.D0/
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
C
      NPASS=1
      IF(DABCD.AND.EOM) NPASS=2
C
      LISTABCD=IOFFABCD+130+ISPIN
      DO 100 IRREPGET=2,NIRREP
C
      DO 90 IPASS=1,NPASS
C
       IF(DABCD) THEN
C
        IF(IPASS.EQ.1) THEN
C
         NUMDSZ=IRPDPD(IRREPGET,ISYTYP(2,143+ISPIN))
         DISDSZ=IRPDPD(IRREPGET,ISYTYP(1,143+ISPIN))
         CALL GETLST(BUF1,1,NUMDSZ,1,IRREPGET,153+ISPIN)
         CALL GETLST(BUF2,1,NUMDSZ,1,IRREPGET,143+ISPIN)
         IF(CCSD) THEN
          I0TA=1
          CALL GETLST(BUF(I0TA),1,1,1,ISPIN,157)
          CALL FTAU(BUF1,BUF(I0TA),BUF(I0TA),DISDSZ,NUMDSZ,
     &              POP(1,ISPIN),POP(1,ISPIN),VRT(1,ISPIN),
     &              VRT(1,ISPIN),IRREPGET,ISPIN,ONE)
         ENDIF
C
         MBPT4=M4DQ.OR.M4SDQ.OR.M4SDTQ 
         IF(MBPT4) THEN
          CALL SSCAL(NUMDSZ*DISDSZ,TWO,BUF2,1) 
          CALL SAXPY(NUMDSZ*DISDSZ,ONE,BUF1,1,BUF2,1)
         ENDIF
C
        ELSE
C
C SECOND PASS, ONLY REQUIRED FOR EOM-CCSD
C
         IRREPGET2=DIRPRD(IRREPGET,IRREPX)
         NUMDSZ=IRPDPD(IRREPGET2,ISYTYP(2,460+ISPIN))
         DISDSZ=IRPDPD(IRREPGET,ISYTYP(1,460+ISPIN))
C
         CALL GETLST(BUF1,1,NUMDSZ,1,IRREPGET2,460+ISPIN)
         CALL GETLST(BUF2,1,NUMDSZ,1,IRREPGET2,443+ISPIN)
C
         I0TA=1
         I0RA=I0TA+NT(ISPIN)
         CALL GETLST(BUF(I0TA),1,1,1,ISPIN,157)
         CALL GETLST(BUF(I0RA),1,1,1,2+ISPIN,490)
C 
         CALL DTAU(IRREPGET,IRREPGET2,1,IRREPX,BUF1,
     &             BUF(I0TA),BUF(I0TA),BUF(I0RA),BUF(I0RA),
     &             ISPIN,ONE)
C
        ENDIF
C
       ENDIF

       IOFFR=1
       IOFFL=1
       DO 1100 IRREP1=1,NIRREP
        IRREP2=DIRPRD(IRREP1,IRREPGET)
        IF(IRREP2.GT.IRREP1)GOTO 1100
        IOFFSR(IRREP1)=IOFFR
        IOFFSL(IRREP1)=IOFFL
        IOFFR=IOFFR+VRT(IRREP2,ISPIN)*VRT(IRREP1,ISPIN)
        IOFFL=IOFFL+VRT(IRREP2,ISPIN)*VRT(IRREP1,ISPIN)
1100   CONTINUE
       DO 110 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPGET)
        IF(IRREPL.GT.IRREPR)GOTO 110
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPR)-1
        NUMD=VRT(IRREPR,ISPIN)
        NUMC=VRT(IRREPL,ISPIN)
        NUMB=VRT(IRREPR,ISPIN)
        NUMA=VRT(IRREPL,ISPIN)
        MAXL=NNP1O2(MOPOP(IRREPL))
        DO 120 INDXD=1,NUMD
         DO 130 INDXC=1,NUMC
          INDD=INDXD+POP(IRREPR,ISPIN)
          INDC=INDXC+POP(IRREPL,ISPIN)
C
          IF(.NOT.DABCD) THEN 
C
C READ G(AB,CD) FROM DISK
C
           CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTABCD)
C 
          ELSE
C
C CALCULATE G(AB,CD) ON THE FLY
C
           CALL DIRG2(BUF,BUF1,BUF2,BUF1,BUF2,NUMDSZ,DISDSZ,
     &                ILOGREC,IRREPGET,1)
C
          ENDIF
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          DO 140 INDXB=1,INDXD
           INDB=INDXB+POP(IRREPR,ISPIN)
           GINDXR=INDXT(INDB,INDD)
           DO 150 INDXA=1,INDXC
            IPOS=IFIRST+INDXF(INDXA,INDXB,NUMA)
            X=BUF(IPOS)*FABCD
            INDA=INDXA+POP(IRREPL,ISPIN)
            GINDXL=INDXT(INDA,INDC)
            GINDX0=INDXF(GINDXL,GINDXR,MAXL)
            GINDX =GINDX0+GOFF(IRREPL,IRREPR)
            GAMMA(GINDX)=GAMMA(GINDX)+X
150        CONTINUE
           DO 151 INDXA=INDXC,NUMA
            IPOS=IFIRST+INDXF(INDXA,INDXB,NUMA)
            X=BUF(IPOS)*FABCD
            INDA=INDXA+POP(IRREPL,ISPIN)
            GINDXL=INDXT(INDC,INDA)
            GINDX0=INDXF(GINDXL,GINDXR,MAXL)
            GINDX=GINDX0+GOFF(IRREPL,IRREPR)
            GAMMA(GINDX)=GAMMA(GINDX)+X
151        CONTINUE
140       CONTINUE
130      CONTINUE
120     CONTINUE
110    CONTINUE
101   CONTINUE
90    CONTINUE
100   CONTINUE
      RETURN
      END
