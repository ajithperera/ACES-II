      SUBROUTINE GAM3AB(GAMMA,BUF,BUF1,BUF2,SCR1,SCR2,
     &                  GOFF,LENGAM,IUHF,ALASKA)
C
C THIS ROUTINE DRIVES THE FORMATION OF THE ABAB SYMMETRY TYPE 
C GAMMA LISTS AND THEN WRITES THEM TO THE BACKTRANSFORMATION
C MOINTS FILE.  THIS IS THE INCORE VERSION!
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 
      DOUBLE PRECISION FABIJ,FAIBJ,FABCD,FIJKL,FABCI,FIJKA
      DOUBLE PRECISION BUF1(*),BUF2(*),SCR1(*),SCR2(*)
      INTEGER GOFF(8,8),IOFFSL(8),IOFFSR(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      LOGICAL ROHF,MOEQAO,ALASKA
      LOGICAL TDA,EOM
      COMMON /EXCITE/ TDA,EOM
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
      COMMON /SIZES/ MOEQAO
      COMMON /FACTORS/ FABIJ,FABCD,FIJKL,FAIBJ,FABCI,FIJKA
      COMMON /REF/ ROHF
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
C
      CALL ZERO(GAMMA,LENGAM)
      IF(TDA)THEN
       CALL G3AB4(GAMMA,BUF,GOFF,LENGAM,FAIBJ)
      ELSE
       CALL G3AB3(GAMMA,BUF,GOFF,LENGAM,FABIJ)
       IF(MBPT2)GOTO 2000
       CALL G3AB1(GAMMA,BUF,GOFF,LENGAM,FIJKL)
       CALL G3AB6(GAMMA,BUF,BUF1,BUF2,SCR1,SCR2,GOFF,LENGAM,FABCD,
     &            IUHF)
       CALL G3AB4(GAMMA,BUF,GOFF,LENGAM,FAIBJ)
       IF((MBPT3.AND.(.NOT.ROHF)).OR.CCD)GOTO 2000
       CALL G3AB2(GAMMA,BUF,GOFF,LENGAM,FIJKA)
       CALL G3AB5(GAMMA,BUF,GOFF,LENGAM,FABCI)
C      
2000   CONTINUE
      ENDIF
      LIST1=3
      LIST2=1
      IOFF=1
      DO 5000 IRREPDO=2,NIRREP
       DO 5100 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        IF(IRREPL.LT.IRREPR)GOTO 5100
        NUMDIS=MOPOP(IRREPR)*MOPOP(IRREPL)
        DISSIZ=NUMDIS
        IF(MOEQAO .OR. ALASKA)THEN
         CALL PUTLST(GAMMA(IOFF),1,NUMDIS,1,LIST1,LIST2)
         IOFF=IOFF+DISSIZ*NUMDIS
        ELSE
C
C CODE BELOW REQUIRED FOR CASES WHERE NMO<NAO, SINCE NAO GAMMAS
C EVENTUALLY OVERWRITE LIST (3,1), WHICH HAS THE PHYSICAL DIMENSIONS
C OF THE CORRESPONDING AO GAMMA
C
         DO 5101 IDIS=1,NUMDIS
          CALL PUTLST(GAMMA(IOFF),IDIS,1,1,LIST1,LIST2)
          IOFF=IOFF+DISSIZ
5101     CONTINUE
        ENDIF
        LIST2=LIST2+1
5100   CONTINUE
5000  CONTINUE
      RETURN
      END
