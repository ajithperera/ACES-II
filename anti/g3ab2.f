      SUBROUTINE G3AB2(GAMMA,BUF,GOFF,LENGAM,FIJKA)
C
C THIS ROUTINE PROCESSES THE G(Ij,Ka) GAMMA LISTS FOR SPIN CASE
C  AB AND FILLS THE SYMMETRY CASE ABAB MULLIKEN ORDERED GAMMA LIST.
C  THIS IS THE INCORE VERSION!
C
C NOTE THAT ABAB IN MULLIKEN CORRESPONDS TO AABB IN DIRAC.
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 
      DOUBLE PRECISION FABIJ,FAIBJ,FABCD,FIJKL,FABCI,FIJKA
      INTEGER GOFF(8,8),IOFFSL(8),IOFFSR(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
C
C COMPUTE OFFSETS TO FIRST ELEMENT OF ALL G(XX,YY) IRREP POSITIONS
C
      LISTIJKA=110
      IOFFR=1
      IOFFL=1
      DO 1000 IRREP=1,NIRREP
       IOFFSR(IRREP)=IOFFR
       IOFFSL(IRREP)=IOFFL
       IOFFR=IOFFR+POP(IRREP,1)*VRT(IRREP,2)
       IOFFL=IOFFL+POP(IRREP,1)*POP(IRREP,2)
1000  CONTINUE
      DO 100 IRREPDO=2,NIRREP
       DO 110 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPL)
        NUMA=VRT(IRREPR,2)
        NUMK=POP(IRREPR,1)
        NUMJ=POP(IRREPL,2)
        NUMI=POP(IRREPL,1)
        MAXL=MOPOP(IRREPL)*MOPOP(IRREPR)
        MAXIL=MOPOP(IRREPL)
        MAXIR=MOPOP(IRREPR)
        DO 120 INDXA=1,NUMA
         DO 130 INDXK=1,NUMK
          INDA=INDXA+POP(IRREPR,2)
          INDK=INDXK
          CALL GETLST(BUF,ILOGREC,1,1,1,LISTIJKA)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          IF(IRREPL.GT.IRREPR)THEN
           DO 140 INDXJ=1,NUMJ
            DO 150 INDXI=1,NUMI
             X=BUF(IPOS)*FIJKA
             IPOS=IPOS+1
             INDJ=INDXJ
             INDI=INDXI
             GINDXL=INDXF(INDI,INDK,MAXIL)
             GINDXR=INDXF(INDJ,INDA,MAXIL)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
150         CONTINUE
140        CONTINUE
          ELSE
           DO 141 INDXJ=1,NUMJ
            DO 151 INDXI=1,NUMI
             X=BUF(IPOS)*FIJKA
             IPOS=IPOS+1
             INDJ=INDXJ
             INDI=INDXI
             GINDXL=INDXF(INDK,INDI,MAXIR)
             GINDXR=INDXF(INDA,INDJ,MAXIR)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X
151         CONTINUE
141        CONTINUE
          ENDIF
130      CONTINUE
120     CONTINUE
110    CONTINUE
100   CONTINUE
C
C NOW WE MUST LOOP OVER ALL OTHER IRREPS AND PUT THE OTHER PIECE IN
C
      DO 160 IRREPDO=2,NIRREP
C
C COMPUTE OFFSETS TO ALL BA AB POSITIONS
C
       IOFFR=1
       IOFFL=1
       DO 1200 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        IOFFSR(IRREPR)=IOFFR
        IOFFSL(IRREPR)=IOFFL
        IOFFR=IOFFR+POP(IRREPL,1)*VRT(IRREPR,2)
        IOFFL=IOFFL+POP(IRREPL,1)*POP(IRREPR,2)
1200   CONTINUE
       DO 170 IRREPB=1,NIRREP
        IRREPA=DIRPRD(IRREPB,IRREPDO)
        ILOGREC=IOFFSR(IRREPB)
        IFIRST=IOFFSL(IRREPA)
        NUMA=VRT(IRREPB,2)
        NUMK=POP(IRREPA,1)
        NUMJ=POP(IRREPA,2)
        NUMI=POP(IRREPB,1)
        MAXL=MOPOP(IRREPA)*MOPOP(IRREPB)
        MAXA=MOPOP(IRREPA)
        MAXB=MOPOP(IRREPB)
        DO 180 INDXA=1,NUMA
         DO 185 INDXK=1,NUMK
          INDA=INDXA+POP(IRREPB,2)
          INDK=INDXK
          CALL GETLST(BUF,ILOGREC,1,1,IRREPDO,LISTIJKA)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          IF(IRREPA.GT.IRREPB)THEN
           DO 190 INDXJ=1,NUMJ
            DO 195 INDXI=1,NUMI
             INDI=INDXI
             INDJ=INDXJ
             X=BUF(IPOS)*FIJKA
             IPOS=IPOS+1
             GINDXL=INDXF(INDK,INDI,MAXA)
             GINDXR=INDXF(INDJ,INDA,MAXA)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX=GINDX0+GOFF(IRREPA,IRREPB)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX=GINDX0+GOFF(IRREPA,IRREPB)
             GAMMA(GINDX)=GAMMA(GINDX)+X
195         CONTINUE
190        CONTINUE
          ELSE
           DO 191 INDXJ=1,NUMJ
            DO 196 INDXI=1,NUMI
             INDI=INDXI
             INDJ=INDXJ
             X=BUF(IPOS)*FIJKA
             IPOS=IPOS+1
             GINDXL=INDXF(INDI,INDK,MAXB)
             GINDXR=INDXF(INDA,INDJ,MAXB)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX=GINDX0+GOFF(IRREPB,IRREPA)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX=GINDX0+GOFF(IRREPB,IRREPA)
             GAMMA(GINDX)=GAMMA(GINDX)+X
196         CONTINUE
191        CONTINUE
          ENDIF
185      CONTINUE
180     CONTINUE
170    CONTINUE
160   CONTINUE
      RETURN
      END
