      SUBROUTINE G3AA4(GAMMA,BUF,GOFF,LENGAM,ISPIN,FAIBJ)
C
C THIS ROUTINE PROCESSES THE G(AI,BJ) GAMMA LISTS FOR SPIN CASE
C  AAAA AND FILLS THE SYMMETRY CASE ABAB SYMMETRY TYPE
C  MULLIKEN ORDERED GAMMA LIST.
C
C NOTE ALSO THAT G ALPHA-ALPHA-ALPHA-ALPHA AND G BETA-BETA-BETA-BETA
C  ARE SYMMETRIC MATRICES FOR THIS SYMMETRY TYPE
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 
      DOUBLE PRECISION FABIJ,FAIBJ,FABCD,FIJKL,FABCI,FIJKA
      INTEGER GOFF(8,8),IOFFSL(8),IOFFSR(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
      NNM1O2(I)   =(I*(I-1))/2
C
C FIRST PROCESS THE IRREP 1 G(AI,BJ) LIST.
C
C
C     21 21      21 21        22 11
C   G(IJ,AB) = G(AB,IJ) <-- g<AI,BJ> 
C
C     21 21      21 21        22 11        22 11
C   G(AJ,IB) = G(IB,AJ) <-- g<IA,BJ> = - g<AI,BJ>
C
C
      LISTAIBJ=122+ISPIN
      IOFFR=1
      IOFFL=1
      DO 1000 IRREP=1,NIRREP
       IOFFSR(IRREP)=IOFFR
       IOFFSL(IRREP)=IOFFL
       IOFFL=IOFFL+VRT(IRREP,ISPIN)*POP(IRREP,ISPIN)
       IOFFR=IOFFR+VRT(IRREP,ISPIN)*POP(IRREP,ISPIN)
1000  CONTINUE
      DO 100 IRREPDO=2,NIRREP
       DO 110 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPL)
        NUMJ=POP(IRREPR,ISPIN)
        NUMB=VRT(IRREPR,ISPIN)
        NUMI=POP(IRREPL,ISPIN)
        NUMA=VRT(IRREPL,ISPIN)
        MAXL=MOPOP(IRREPL)*MOPOP(IRREPR)
        MAX1=MOPOP(IRREPL)
        MAX2=MOPOP(IRREPR)
        DO 120 INDXJ=1,NUMJ
         DO 130 INDXB=1,NUMB
          INDJ=INDXJ
          INDB=INDXB+POP(IRREPR,ISPIN)
          CALL GETLST(BUF,ILOGREC,1,1,1,LISTAIBJ)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          IF(IRREPL.GT.IRREPR)THEN
           DO 140 INDXI=1,NUMI
*VOCL LOOP,NOVREC
CDIR$ IVDEP
            DO 150 INDXA=1,NUMA
             X=BUF(IPOS)*FAIBJ
             IPOS=IPOS+1
             INDA=INDXA+POP(IRREPL,ISPIN)
             INDI=INDXI
             GINDXL=INDXF(INDA,INDB,MAX1)
             GINDXR=INDXF(INDI,INDJ,MAX1)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDXL=INDXF(INDA,INDJ,MAX1)
             GINDXR=INDXF(INDI,INDB,MAX1)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)-X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)-X
150         CONTINUE
140        CONTINUE
          ENDIF
130      CONTINUE
120     CONTINUE
110    CONTINUE
100   CONTINUE
C
C NOW PICK UP OTHER DPD IRREPS AND CONTINUE FILLING OUT THE GAMMAS.
C
C HERE WE WILL FILL OUT THE PIECES:
C
C     21 21      21 21      12 21
C   G(IJ,BA) = G(BA,IJ) = g<AI,BJ>
C
C     21 21      21 12        12 12
C   G(JA,IB) = g<IA,BJ> = - g<AI,BJ> 
C
C     21 21      12 21        21 21
C   G(AJ,BI) = g<IA,BJ> = - g<AI,BJ> 
C
      DO 160 IRREPDO=2,NIRREP
C
C COMPUTE OFFSETS TO ALL BA BA POSITIONS
C
       IOFFR=1
       IOFFL=1
       DO 1200 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        IOFFSR(IRREPR)=IOFFR
        IOFFSL(IRREPR)=IOFFL
        IOFFR=IOFFR+VRT(IRREPL,ISPIN)*POP(IRREPR,ISPIN)
        IOFFL=IOFFL+VRT(IRREPL,ISPIN)*POP(IRREPR,ISPIN)
1200   CONTINUE
       DO 170 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPR)
        IFIRST2=IOFFSL(IRREPL)
        NUMJ=POP(IRREPR,ISPIN)
        NUMB=VRT(IRREPL,ISPIN)
        NUMI=POP(IRREPR,ISPIN)
        NUMA=VRT(IRREPL,ISPIN)
        NUMI2=POP(IRREPL,ISPIN)
        NUMA2=VRT(IRREPR,ISPIN)
        MAXL=MOPOP(IRREPR)*MOPOP(IRREPL)
        MAXL1=MOPOP(IRREPL)
        MAXR1=MOPOP(IRREPR)
        DO 180 INDXJ=1,NUMJ
         DO 185 INDXB=1,NUMB
          INDB=INDXB+POP(IRREPL,ISPIN)
          INDJ=INDXJ
          CALL GETLST(BUF,ILOGREC,1,1,IRREPDO,LISTAIBJ)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          IPOS2=IFIRST2
          IF(IRREPL.LT.IRREPR)THEN
           DO 190 INDXI=1,NUMI
            DO 195 INDXA=1,NUMA
             X=BUF(IPOS)*FAIBJ
             IPOS=IPOS+1
             INDA=INDXA+POP(IRREPL,ISPIN)
             INDI=INDXI
             GINDXL=INDXF(INDJ,INDA,MAXR1)
             GINDXR=INDXF(INDI,INDB,MAXR1)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)-X
195         CONTINUE
190        CONTINUE
           DO 290 INDXI=1,NUMI2
*VOCL LOOP,NOVREC
CDIR$ IVDEP
            DO 295 INDXA=1,NUMA2
             X=BUF(IPOS2)*FAIBJ
             IPOS2=IPOS2+1
             INDA=INDXA+POP(IRREPR,ISPIN)
             INDI=INDXI
             GINDXL=INDXF(INDJ,INDI,MAXR1)
             GINDXR=INDXF(INDA,INDB,MAXR1)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX=GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X 
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX=GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X 
295         CONTINUE
290        CONTINUE
          ELSE
           DO 191 INDXI=1,NUMI
            DO 196 INDXA=1,NUMA
             X=BUF(IPOS)*FAIBJ
             IPOS=IPOS+1
             INDA=INDXA+POP(IRREPL,ISPIN)
             INDI=INDXI
             GINDXL=INDXF(INDA,INDJ,MAXL1)
             GINDXR=INDXF(INDB,INDI,MAXL1)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)-X
196         CONTINUE
191        CONTINUE
          ENDIF
185      CONTINUE
180     CONTINUE
170    CONTINUE
160   CONTINUE
      RETURN
      END
