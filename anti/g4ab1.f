      SUBROUTINE G4AB1(GAMMA,BUF,GOFF,DOIRR,LENGAM,FIJKL)
C
C THIS ROUTINE PROCESSES THE G(Ij,Kl) GAMMA LISTS AND FILLS
C  OUT THE ABCD SYMMETRY CASE MULLIKEN ORDERED GAMMA.
C
C FOR THIS, THE FOLLOWING SYMMETRIZATION IS REQUIRED:
C
C         12 34      12 34      21 34
C       G(IJ,KL) = g(IJ,KL) + g(JI,KL)
C
C         13 24      13 24      23 14
C       G<IK,JL> = g<IK,JL> + g(JK,IL>
C
C NOTE THAT ABCD IN MULLIKEN CORRESPONDS TO ACBD IN DIRAC.  NOTE ALSO THAT
C  ALL ELEMENTS OF GAMMA WHICH ARE PROCESSED HERE COME FROM DPD LISTS
C  FOR IRREPS 2-h, SINCE A.NE.C;B.NE.D.
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 
      DOUBLE PRECISION FABIJ,FAIBJ,FABCD,FIJKL,FABCI,FIJKA
      INTEGER GOFF(8,8,8,8),IDID(8),IOFFSL(8)
      INTEGER IOFFSR(8),DOIRR(42,4)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
C
C CALCULATE HOW MANY UNIQUE ABCD SYMMETRY SETS THERE ARE
C
      IF(NIRREP.EQ.8)THEN
       NUMABCD=42
      ELSEIF(NIRREP.EQ.4)THEN
       NUMABCD=3
      ENDIF
C     
      LISTIJKL=113
      DO 100 ICOUNT=1,NUMABCD
       IRREP1D=DOIRR(ICOUNT,1)
       IRREP2D=DOIRR(ICOUNT,3)
       IRREP3D=DOIRR(ICOUNT,2)
       IRREP4D=DOIRR(ICOUNT,4)
       IRREP1M=DOIRR(ICOUNT,1)
       IRREP2M=DOIRR(ICOUNT,2)
       IRREP3M=DOIRR(ICOUNT,3)
       IRREP4M=DOIRR(ICOUNT,4)
       IOFFSET=GOFF(IRREP1M,IRREP2M,IRREP3M,IRREP4M)
C
C FIRST PUT IN THE 1234 MULLIKEN PIECE
C
       IRREPGET=DIRPRD(IRREP3D,IRREP4D)
C
       IOFFR=1
       IOFFL=1
       DO 2000 IRREPA=1,NIRREP
        IRREPB=DIRPRD(IRREPA,IRREPGET)
        IOFFSR(IRREPA)=IOFFR
        IOFFSL(IRREPA)=IOFFL
        IOFFR=IOFFR+POP(IRREPB,1)*POP(IRREPA,2)
        IOFFL=IOFFL+POP(IRREPB,1)*POP(IRREPA,2)
2000   CONTINUE
C
       ILOGREC=IOFFSR(IRREP4D)
       IFIRST=IOFFSL(IRREP2D)
       NUML=POP(IRREP4D,2)
       NUMK=POP(IRREP3D,1)
       NUMJ=POP(IRREP2D,2)
       NUMI=POP(IRREP1D,1) 
       MAXL=MOPOP(IRREP1M)*MOPOP(IRREP2M)
       DO 120 INDXL=1,NUML
        DO 130 INDXK=1,NUMK
         INDL=INDXL
         INDK=INDXK
         CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTIJKL)
         ILOGREC=ILOGREC+1
         IPOS=IOFFSL(IRREP2D)
         DO 140 INDXJ=1,NUMJ
          DO 150 INDXI=1,NUMI
           X=BUF(IPOS)*FIJKL
           IPOS=IPOS+1
           INDI=INDXI
           INDJ=INDXJ
           GINDXL=INDXF(INDI,INDK,MOPOP(IRREP1M))
           GINDXR=INDXF(INDJ,INDL,MOPOP(IRREP3M))
           GINDX0=INDXF(GINDXL,GINDXR,MAXL)
           GINDX =GINDX0+IOFFSET
           GAMMA(GINDX)=GAMMA(GINDX)+X
150       CONTINUE
140      CONTINUE
130     CONTINUE
120    CONTINUE
C
C NOW PUT IN THE 2134 MULLIKEN PIECE 
C
       IRREPGET=DIRPRD(IRREP1D,IRREP4D)
       IOFFR=1
       IOFFL=1
C
       DO 1000 IRREPA=1,NIRREP
        IRREPB=DIRPRD(IRREPA,IRREPGET)
        IOFFSR(IRREPA)=IOFFR
        IOFFSL(IRREPA)=IOFFL
        IOFFR=IOFFR+POP(IRREPB,1)*POP(IRREPA,2)
        IOFFL=IOFFL+POP(IRREPB,1)*POP(IRREPA,2)
1000   CONTINUE
C
       ILOGREC=IOFFSR(IRREP4D)
       IFIRST=IOFFSL(IRREP2D)
       NUML=POP(IRREP4D,2)
       NUMK=POP(IRREP1D,1)
       NUMJ=POP(IRREP2D,2)
       NUMI=POP(IRREP3D,1) 
       MAXL=MOPOP(IRREP1M)*MOPOP(IRREP2M)
       DO 220 INDXL=1,NUML
        DO 230 INDXK=1,NUMK
         INDL=INDXL
         INDK=INDXK
         CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTIJKL)
         ILOGREC=ILOGREC+1
         IPOS=IOFFSL(IRREP2D)
         DO 240 INDXJ=1,NUMJ
          DO 250 INDXI=1,NUMI
           X=BUF(IPOS)*FIJKL
           IPOS=IPOS+1
           INDI=INDXI
           INDJ=INDXJ
           GINDXL=INDXF(INDK,INDI,MOPOP(IRREP1M))
           GINDXR=INDXF(INDJ,INDL,MOPOP(IRREP3M))
           GINDX0=INDXF(GINDXL,GINDXR,MAXL)
           GINDX =GINDX0+IOFFSET
           GAMMA(GINDX)=GAMMA(GINDX)+X
250       CONTINUE
240      CONTINUE
230     CONTINUE
220    CONTINUE
100   CONTINUE
      RETURN
      END
