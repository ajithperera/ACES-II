      SUBROUTINE G4AA3(GAMMA,BUF,GOFF,DOIRR,LENGAM,ISPIN,FABIJ)
C
C THIS ROUTINE PROCESSES THE G<I<J,K<L> GAMMA LISTS AND FILLS
C  OUT THE ABCD SYMMETRY CASE MULLIKEN ORDERED GAMMA FOR 
C  SPIN CASE AABB.  THIS IS FOR UHF CALCULATIONS ONLY!
C
C          12 34        13 24
C        G(AI,BJ) <-- g<AB,IJ>
C
C          12 34        24 13
C        G(IA,JB) <-- g<AB,IJ>
C
C          12 34        23 14
C        G(IA,BJ) <-- g<AB,IJ>
C
C          12 34        14 23
C        G(AI,JB) <-- g<AB,IJ>
C
C
C ONE MUST BE VERY CAREFUL HERE ABOUT SIGNS SINCE ONLY THOSE
C  IRREP PAIRS PQ ARE ON DISK SUCH THAT P<Q.  THIS MAKES THE
C  LOGIC FOR THIS ROUTINE SLIGHTLY COMPLICATED!  THIS IS HANDLED
C  BY SUBROUTINE GETFAC, WHICH DETERMINES THE SIGN AND REORDERS
C  EACH IRREP QUADRUPLET IN AN APPROPRIATE WAY.  COMPLICATED STUFF.
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 ,FACTOR,FACT
      DOUBLE PRECISION FABIJ,FIJKL,FABCD,FAIBJ,FABCI,FIJKA
      INTEGER GOFF(8,8,8,8),IDID(8),IOFFSL(8)
      INTEGER IOFFSR(8),DOIRR(42,4),IPTR(4),IND(4)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
      NNM1O2(I)   =(I*(I-1))/2
C
C CALCULATE HOW MANY UNIQUE ABCD SYMMETRY SETS THERE ARE
C
      IF(NIRREP.EQ.8)THEN
       NUMABCD=42
      ELSEIF(NIRREP.EQ.4)THEN
       NUMABCD=3
      ENDIF
C     
      LISTABIJ=113+ISPIN
      DO 100 ICOUNT=1,NUMABCD
       IRREP1M=DOIRR(ICOUNT,1)
       IRREP2M=DOIRR(ICOUNT,2)
       IRREP3M=DOIRR(ICOUNT,3)
       IRREP4M=DOIRR(ICOUNT,4)
C
       IOFFSET=GOFF(IRREP1M,IRREP2M,IRREP3M,IRREP4M)
       MAXL=MOPOP(IRREP1M)*MOPOP(IRREP2M)
       MAXL1=MOPOP(IRREP1M)
       MAXL2=MOPOP(IRREP3M)
C
       CALL GETFAC(IRREP1M,IRREP3M,IRREP2M,IRREP4M,IPTR,FACTOR)
       FACT=FABIJ*FACTOR
C
C         12 34        13 24
C       G(AI,BJ) <-- g<AB,IJ>
C
       IRREPGET=DIRPRD(IRREP2M,IRREP4M)
C
       IOFFR=1
       IOFFL=1
       DO 2000 IRREPA=1,NIRREP
        IRREPB=DIRPRD(IRREPA,IRREPGET)
        IF(IRREPB.GT.IRREPA)GOTO 2000
        IOFFSR(IRREPA)=IOFFR
        IOFFSL(IRREPA)=IOFFL
        IOFFR=IOFFR+POP(IRREPB,ISPIN)*POP(IRREPA,ISPIN)
        IOFFL=IOFFL+VRT(IRREPB,ISPIN)*VRT(IRREPA,ISPIN)
2000   CONTINUE
C
       ILOGREC=IOFFSR(IRREP4M)
       IFIRST=IOFFSL(IRREP3M)
       NUMJ=POP(IRREP4M,ISPIN)
       NUMI=POP(IRREP2M,ISPIN)
       NUMB=VRT(IRREP3M,ISPIN)
       NUMA=VRT(IRREP1M,ISPIN) 
       DO 120 INDXJ=1,NUMJ
        DO 130 INDXI=1,NUMI
         IND(4)=INDXJ
         IND(3)=INDXI
         CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTABIJ)
         ILOGREC=ILOGREC+1
         IPOS=IOFFSL(IRREP3M)
         DO 140 INDXB=1,NUMB
          DO 150 INDXA=1,NUMA
           X=BUF(IPOS)*FACT
           IPOS=IPOS+1
           IND(2)=INDXB+POP(IRREP3M,ISPIN)
           IND(1)=INDXA+POP(IRREP1M,ISPIN)
           GINDXL=INDXF(IND(IPTR(1)),IND(IPTR(3)),MAXL1)
           GINDXR=INDXF(IND(IPTR(2)),IND(IPTR(4)),MAXL2)
           GINDX0=INDXF(GINDXL,GINDXR,MAXL)
           GINDX =GINDX0+IOFFSET
           GAMMA(GINDX)=GAMMA(GINDX)+X
150       CONTINUE
140      CONTINUE
130     CONTINUE
120    CONTINUE
C
C         12 34        24 13
C       G(IA,JB) <-- g<AB,IJ>
C
C
       IRREP1M=DOIRR(ICOUNT,1)
       IRREP2M=DOIRR(ICOUNT,2)
       IRREP3M=DOIRR(ICOUNT,3)
       IRREP4M=DOIRR(ICOUNT,4)
       CALL GETFAC(IRREP2M,IRREP4M,IRREP1M,IRREP3M,IPTR,FACTOR)
       FACT=FABIJ*FACTOR
       ILOGREC=IOFFSR(IRREP3M)
       IFIRST=IOFFSL(IRREP4M)
       NUMJ=POP(IRREP3M,ISPIN)
       NUMI=POP(IRREP1M,ISPIN)
       NUMB=VRT(IRREP4M,ISPIN)
       NUMA=VRT(IRREP2M,ISPIN) 
       DO 160 INDXJ=1,NUMJ
        DO 170 INDXI=1,NUMI
         IND(4)=INDXJ
         IND(3)=INDXI
         CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTABIJ)
         ILOGREC=ILOGREC+1
         IPOS=IOFFSL(IRREP4M)
         DO 180 INDXB=1,NUMB
          DO 190 INDXA=1,NUMA
           X=BUF(IPOS)*FACT
           IPOS=IPOS+1
           IND(2)=INDXB+POP(IRREP4M,ISPIN)
           IND(1)=INDXA+POP(IRREP2M,ISPIN)
           GINDXL=INDXF(IND(IPTR(3)),IND(IPTR(1)),MAXL1)
           GINDXR=INDXF(IND(IPTR(4)),IND(IPTR(2)),MAXL2)
           GINDX0=INDXF(GINDXL,GINDXR,MAXL)
           GINDX =GINDX0+IOFFSET
           GAMMA(GINDX)=GAMMA(GINDX)+X
190       CONTINUE
180      CONTINUE
170     CONTINUE
160    CONTINUE
C
C         12 34        23 14
C       G(IA,BJ) <-- g<AB,IJ>
C
       IRREP1M=DOIRR(ICOUNT,1)
       IRREP2M=DOIRR(ICOUNT,2)
       IRREP3M=DOIRR(ICOUNT,3)
       IRREP4M=DOIRR(ICOUNT,4)
       CALL GETFAC(IRREP2M,IRREP3M,IRREP1M,IRREP4M,IPTR,FACTOR)
       FACT=FABIJ*FACTOR
       IRREPGET=DIRPRD(IRREP1M,IRREP4M)
C
       IOFFR=1
       IOFFL=1
       DO 3000 IRREPA=1,NIRREP
        IRREPB=DIRPRD(IRREPA,IRREPGET)
        IF(IRREPB.GT.IRREPA)GOTO 3000
        IOFFSR(IRREPA)=IOFFR
        IOFFSL(IRREPA)=IOFFL
        IOFFR=IOFFR+POP(IRREPB,ISPIN)*POP(IRREPA,ISPIN)
        IOFFL=IOFFL+VRT(IRREPB,ISPIN)*VRT(IRREPA,ISPIN)
3000   CONTINUE
C
       ILOGREC=IOFFSR(IRREP4M)
       IFIRST=IOFFSL(IRREP3M)
       NUMJ=POP(IRREP4M,ISPIN)
       NUMI=POP(IRREP1M,ISPIN)
       NUMB=VRT(IRREP3M,ISPIN)
       NUMA=VRT(IRREP2M,ISPIN) 
       DO 220 INDXJ=1,NUMJ
        DO 230 INDXI=1,NUMI
         IND(4)=INDXJ
         IND(3)=INDXI
         CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTABIJ)
         ILOGREC=ILOGREC+1
         IPOS=IOFFSL(IRREP3M)
         DO 240 INDXB=1,NUMB
          DO 250 INDXA=1,NUMA
           X=BUF(IPOS)*FACT
           IPOS=IPOS+1
           IND(2)=INDXB+POP(IRREP3M,ISPIN)
           IND(1)=INDXA+POP(IRREP2M,ISPIN)
           GINDXL=INDXF(IND(IPTR(3)),IND(IPTR(1)),MAXL1)
           GINDXR=INDXF(IND(IPTR(2)),IND(IPTR(4)),MAXL2)
           GINDX0=INDXF(GINDXL,GINDXR,MAXL)
           GINDX =GINDX0+IOFFSET
           GAMMA(GINDX)=GAMMA(GINDX)+X
250       CONTINUE
240      CONTINUE
230     CONTINUE
220    CONTINUE
C
C         12 34        14 23
C       G(AI,JB) <-- g<AB,IJ>
C
       IRREP1M=DOIRR(ICOUNT,1)
       IRREP2M=DOIRR(ICOUNT,2)
       IRREP3M=DOIRR(ICOUNT,3)
       IRREP4M=DOIRR(ICOUNT,4)
       CALL GETFAC(IRREP1M,IRREP4M,IRREP2M,IRREP3M,IPTR,FACTOR)
       FACT=FABIJ*FACTOR
       ILOGREC=IOFFSR(IRREP3M)
       IFIRST=IOFFSL(IRREP4M)
       NUMJ=POP(IRREP3M,ISPIN)
       NUMI=POP(IRREP2M,ISPIN)
       NUMB=VRT(IRREP4M,ISPIN)
       NUMA=VRT(IRREP1M,ISPIN) 
       DO 260 INDXJ=1,NUMJ
        DO 270 INDXI=1,NUMI
         IND(4)=INDXJ
         IND(3)=INDXI
         CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTABIJ)
         ILOGREC=ILOGREC+1
         IPOS=IOFFSL(IRREP4M)
         DO 280 INDXB=1,NUMB
          DO 290 INDXA=1,NUMA
           X=BUF(IPOS)*FACT
           IPOS=IPOS+1
           IND(2)=INDXB+POP(IRREP4M,ISPIN)
           IND(1)=INDXA+POP(IRREP1M,ISPIN)
           GINDXL=INDXF(IND(IPTR(1)),IND(IPTR(3)),MAXL1)
           GINDXR=INDXF(IND(IPTR(4)),IND(IPTR(2)),MAXL2)
           GINDX0=INDXF(GINDXL,GINDXR,MAXL)
           GINDX =GINDX0+IOFFSET
           GAMMA(GINDX)=GAMMA(GINDX)+X
290       CONTINUE
280      CONTINUE
270     CONTINUE
260    CONTINUE
C
100   CONTINUE
      RETURN
      END
