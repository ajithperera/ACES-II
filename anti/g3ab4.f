      SUBROUTINE G3AB4(GAMMA,BUF,GOFF,LENGAM,FAIBJ)
C
C THIS ROUTINE PROCESSES THE RING GAMMA LISTS FOR SPIN CASE
C  AB AND FILLS THE SYMMETRY CASE ABAB MULLIKEN ORDERED GAMMA LIST.
C
C NOTE THAT ABAB IN MULLIKEN CORRESPONDS TO AABB IN DIRAC.  EACH 
C  LOGICAL RECORD IS COMPOSED OF ABAB, BAAB, ABBA AND BABA PIECES.
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 
      DOUBLE PRECISION FABIJ,FAIBJ,FABCD,FIJKL,FABCI,FIJKA,FAIBJ2
      INTEGER GOFF(8,8),IOFFSL(8),IOFFSR(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
C
      LISTAIBJ=125
      LISTBIAJ=118
      FAIBJ2=FAIBJ
C
C STAGE 1 - DO THE G(Ai,Bj) (LIST 125) ELEMENTS
C
C FIRST PROCESS THE IRREP 1 G(Ai,Bj) LIST.  COMPUTE OFFSETS TO FIRST ELEMENT
C   OF ALL G(XX,YY) IRREP POSITIONS
C
      IOFFR=1
      IOFFL=1
      DO 1000 IRREPR=1,NIRREP
       IOFFSR(IRREPR)=IOFFR
       IOFFR=IOFFR+VRT(IRREPR,1)*POP(IRREPR,2)
1000  CONTINUE
      DO 1010 IRREPL=1,NIRREP
       IOFFSL(IRREPL)=IOFFL
       IOFFL=IOFFL+VRT(IRREPL,1)*POP(IRREPL,2)
1010  CONTINUE
      DO 100 IRREPDO=2,NIRREP
       DO 110 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPL)
        NUMJ=POP(IRREPR,2)
        NUMB=VRT(IRREPR,1)
        NUMI=POP(IRREPL,2)
        NUMA=VRT(IRREPL,1)
        MAXL=MOPOP(IRREPL)*MOPOP(IRREPR)
        MAX1=MOPOP(IRREPL)
        MAX2=MOPOP(IRREPR)
        DO 120 INDXJ=1,NUMJ
         DO 130 INDXB=1,NUMB
          INDB=INDXB+POP(IRREPR,1)
          INDJ=INDXJ
          CALL GETLST(BUF,ILOGREC,1,1,1,LISTAIBJ)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          IF(IRREPL.GT.IRREPR)THEN
C
C PUT IN THE VVOO AND OOVV PIECES IN ONE SHOT SINCE BOTH ARE AB-AB
C   SYMMETRY TYPE
C
           DO 140 INDXI=1,NUMI
            DO 150 INDXA=1,NUMA
             X=BUF(IPOS)*FAIBJ
             IPOS=IPOS+1
             INDA=INDXA+POP(IRREPL,1)
             INDI=INDXI
             GINDXL=INDXF(INDA,INDB,MAX1)
             GINDXR=INDXF(INDI,INDJ,MAX1)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
150         CONTINUE
140        CONTINUE
          ENDIF
130      CONTINUE
120     CONTINUE
110    CONTINUE
100   CONTINUE
C
C NOW PICK UP OTHER LOGICAL RECORDS AND CONTINUE TO FILL OUT GAMMA
C
      DO 160 IRREPDO=2,NIRREP
C
C COMPUTE OFFSETS TO ALL BA AB POSITIONS
C
       IOFFR=1
       IOFFL=1
       DO 1200 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        IOFFSR(IRREPR)=IOFFR
        IOFFSL(IRREPR)=IOFFL
        IOFFR=IOFFR+VRT(IRREPL,1)*POP(IRREPR,2)
        IOFFL=IOFFL+VRT(IRREPL,1)*POP(IRREPR,2)
1200   CONTINUE
       DO 170 IRREPB=1,NIRREP
        IRREPA=DIRPRD(IRREPB,IRREPDO)
        ILOGREC=IOFFSR(IRREPB)
        IFIRST =IOFFSL(IRREPA)
        NUMJ=POP(IRREPB,2)
        NUMB=VRT(IRREPA,1)
        NUMI=POP(IRREPA,2)
        NUMA=VRT(IRREPB,1)
        MAXL=MOPOP(IRREPA)*MOPOP(IRREPB)
        MAX1A=MOPOP(IRREPA)
        MAX1B=MOPOP(IRREPB)
        DO 180 INDXJ=1,NUMJ
         DO 185 INDXB=1,NUMB
          INDB=INDXB+POP(IRREPA,1)
          INDJ=INDXJ
          CALL GETLST(BUF,ILOGREC,1,1,IRREPDO,LISTAIBJ)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          IF(IRREPA.GT.IRREPB)THEN
C
C PUT IN THE VVOO AND OOVV PIECES IN ONE SHOT SINCE BOTH ARE AB-AB
C   SYMMETRY TYPE.  THIS IS SLIGHTLY MORE COMPLICATED THAN ABOVE
C   HOWEVER SINCE THESE ARE REALLY ABBA IN MULLIKEN.
C
           DO 190 INDXI=1,NUMI
            DO 195 INDXA=1,NUMA
             X=BUF(IPOS)*FAIBJ
             IPOS=IPOS+1
             INDA=INDXA+POP(IRREPB,1)
             INDI=INDXI
             GINDXL=INDXF(INDB,INDA,MAX1A)
             GINDXR=INDXF(INDI,INDJ,MAX1A)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPA,IRREPB)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPA,IRREPB)
             GAMMA(GINDX)=GAMMA(GINDX)+X
195         CONTINUE
190        CONTINUE
          ENDIF
185      CONTINUE
180     CONTINUE
170    CONTINUE
C
C NOW DO THE G(BI,aj) RING CONTRIBUTION.  REMEMBER THAT THIS IS
C  ALREADY STORED IN MULLIKEN ORDER SO ONLY THE IRREP 2->H LISTS
C  WILL CONTRIBUTE
C
       DO 270 IRREPB=1,NIRREP
        IRREPA=DIRPRD(IRREPB,IRREPDO)
        ILOGREC=IOFFSR(IRREPB)
        IFIRST1=IOFFSL(IRREPB)
        IFIRST2=IOFFSL(IRREPA)
        NUMJ=POP(IRREPB,2)
        NUMA=VRT(IRREPA,2)
        NUMI1=POP(IRREPB,1)
        NUMB1=VRT(IRREPA,1)
        NUMI2=POP(IRREPA,1)
        NUMB2=VRT(IRREPB,1)
        MAXL=MOPOP(IRREPA)*MOPOP(IRREPB)
        MAX1A=MOPOP(IRREPA)
        MAX1B=MOPOP(IRREPB)
        DO 280 INDXJ=1,NUMJ
         DO 285 INDXA=1,NUMA
          INDA=INDXA+POP(IRREPA,2)
          INDJ=INDXJ
          CALL GETLST(BUF,ILOGREC,1,1,IRREPDO,LISTBIAJ)
          ILOGREC=ILOGREC+1
          IPOS1=IFIRST1
          IPOS2=IFIRST2
          IF(IRREPA.GT.IRREPB)THEN
           DO 290 INDXI=1,NUMI1
            DO 295 INDXB=1,NUMB1
             X=BUF(IPOS1)*FAIBJ2
             IPOS1=IPOS1+1
             INDB=INDXB+POP(IRREPA,1)
             INDI=INDXI
             GINDXL=INDXF(INDB,INDI,MAX1A)
             GINDXR=INDXF(INDA,INDJ,MAX1A)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPA,IRREPB)
             GAMMA(GINDX)=GAMMA(GINDX)+X
295         CONTINUE
290        CONTINUE
           DO 390 INDXI=1,NUMI2
            DO 395 INDXB=1,NUMB2
             X=BUF(IPOS2)*FAIBJ2
             IPOS2=IPOS2+1
             INDB=INDXB+POP(IRREPB,1)
             INDI=INDXI
             GINDXL=INDXF(INDI,INDB,MAX1A)
             GINDXR=INDXF(INDA,INDJ,MAX1A)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPA,IRREPB)
             GAMMA(GINDX)=GAMMA(GINDX)+X
395         CONTINUE
390        CONTINUE
          ELSE
           DO 291 INDXI=1,NUMI1
            DO 296 INDXB=1,NUMB1
             X=BUF(IPOS1)*FAIBJ2
             IPOS1=IPOS1+1
             INDB=INDXB+POP(IRREPA,1)
             INDI=INDXI
             GINDXL=INDXF(INDI,INDB,MAX1B)
             GINDXR=INDXF(INDJ,INDA,MAX1B)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX=GINDX0+GOFF(IRREPB,IRREPA)
             GAMMA(GINDX)=GAMMA(GINDX)+X
296         CONTINUE
291        CONTINUE
           DO 391 INDXI=1,NUMI2
            DO 396 INDXB=1,NUMB2
             X=BUF(IPOS2)*FAIBJ2
             IPOS2=IPOS2+1
             INDB=INDXB+POP(IRREPB,1)
             INDI=INDXI
             GINDXL=INDXF(INDB,INDI,MAX1B)
             GINDXR=INDXF(INDJ,INDA,MAX1B)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX=GINDX0+GOFF(IRREPB,IRREPA)
             GAMMA(GINDX)=GAMMA(GINDX)+X
396         CONTINUE
391        CONTINUE
          ENDIF
285      CONTINUE
280     CONTINUE
270    CONTINUE
C
160   CONTINUE
C
      RETURN
      END
