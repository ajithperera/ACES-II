      SUBROUTINE G3AA3(GAMMA,BUF,GOFF,LENGAM,ISPIN,FABIJ)
C
C THIS ROUTINE PROCESSES THE G(A<B,I<J) GAMMA LISTS FOR SPIN CASE
C  AA AND FILLS THE SYMMETRY CASE ABAB SYMMETRY TYPE
C  MULLIKEN ORDERED GAMMA LIST.
C
C NOTE THAT ABAB IN MULLIKEN CORRESPONDS TO AABB IN DIRAC.  EACH 
C  LOGICAL RECORD IS COMPOSED OF ABAB, BAAB, ABBA AND BABA PIECES.
C
C NOTE ALSO THAT G ALPHA-ALPHA-ALPHA-ALPHA AND G BETA-BETA-BETA-BETA
C  ARE SYMMETRIC MATRICES FOR THIS SYMMETRY TYPE
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 
      DOUBLE PRECISION FABIJ,FAIBJ,FABCD,FIJKL,FABCI,FIJKA
      INTEGER GOFF(8,8),IOFFSL(8),IOFFSR(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
      NNM1O2(I)   =(I*(I-1))/2
C
C FIRST PROCESS THE IRREP 1 G(A<B,I<J) LIST, WHICH IS THE TRICKY
C  PART.  COMPUTE OFFSETS TO THE FIRST ELEMENT OF ALL G(XX,YY)
C  IRREP POSITIONS.
C
C
C          21 21        22 11 
C        G(AI,BJ) <-- g<AB,IJ> 
C
C          21 21        11 22
C        G(IA,JB) <-- g<AB,IJ>
C
      LISTABIJ=113+ISPIN
      IOFFR=1
      IOFFL=1
      DO 1000 IRREPR=1,NIRREP
       IOFFSR(IRREPR)=IOFFR
       IOFFR=IOFFR+NNM1O2(POP(IRREPR,ISPIN))
1000  CONTINUE
      DO 1010 IRREPL=1,NIRREP
       IOFFSL(IRREPL)=IOFFL
       IOFFL=IOFFL+NNM1O2(VRT(IRREPL,ISPIN))
1010  CONTINUE
      DO 100 IRREPDO=2,NIRREP
       DO 110 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPL)
        NUMJ=POP(IRREPR,ISPIN)
        NUMI=POP(IRREPR,ISPIN)
        NUMB=VRT(IRREPL,ISPIN)
        NUMA=VRT(IRREPL,ISPIN)
        MAXL=MOPOP(IRREPL)*MOPOP(IRREPR)
        OCC=POP(IRREPL,ISPIN)
        MAX1=MOPOP(IRREPL)
        MAX2=MOPOP(IRREPR)
        DO 120 INDXJ=2,NUMJ
         DO 130 INDXI=1,INDXJ-1
          INDI=INDXI
          INDJ=INDXJ
          CALL GETLST(BUF,ILOGREC,1,1,1,LISTABIJ)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          IF(IRREPL.GT.IRREPR)THEN
           DO 140 INDXB=2,NUMB
*VOCL LOOP,NOVREC
CDIR$ IVDEP
            DO 150 INDXA=1,INDXB-1
             X=BUF(IPOS)*FABIJ
             IPOS=IPOS+1
             INDA=INDXA+OCC
             INDB=INDXB+OCC
             GINDXL=INDXF(INDA,INDI,MAX1)
             GINDXR=INDXF(INDB,INDJ,MAX1)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=X
             GINDXL=INDXF(INDB,INDI,MAX1)
             GINDXR=INDXF(INDA,INDJ,MAX1)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=-X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=-X
150         CONTINUE
140        CONTINUE
          ELSE
           DO 141 INDXB=2,NUMB
*VOCL LOOP,NOVREC
CDIR$ IVDEP
            DO 151 INDXA=1,INDXB-1
             X=BUF(IPOS)*FABIJ
             IPOS=IPOS+1
             INDA=INDXA+OCC
             INDB=INDXB+OCC
             GINDXL=INDXF(INDI,INDA,MAX2)
             GINDXR=INDXF(INDJ,INDB,MAX2)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX=GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX=GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=X
             GINDXL=INDXF(INDI,INDB,MAX2)
             GINDXR=INDXF(INDJ,INDA,MAX2)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX=GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=-X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX=GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=-X
151         CONTINUE
141        CONTINUE
          ENDIF
130      CONTINUE
120     CONTINUE
110    CONTINUE
100   CONTINUE
C
C NOW PICK UP OTHER DPD IRREPS AND CONTINUE FILLING OUT THE GAMMAS.
C
C HERE WE WILL FILL OUT THE PIECES:
C
C          21 21        12 21       12 12
C        G(IA,BJ) <-- g<AB,IJ> = -g<AB,JI>
C
C          21 21        21 12       12 12
C        G(AI,JB) <-- g<AB,IJ> = -g<BA,IJ>
C
C
      DO 160 IRREPDO=2,NIRREP
C
C COMPUTE OFFSETS TO ALL BA BA POSITIONS
C
       IOFFR=1
       IOFFL=1
       DO 1200 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        IF(IRREPL.GT.IRREPR)GOTO 1200
        IOFFSR(IRREPR)=IOFFR
        IOFFSL(IRREPR)=IOFFL
        IOFFR=IOFFR+POP(IRREPL,ISPIN)*POP(IRREPR,ISPIN)
        IOFFL=IOFFL+VRT(IRREPL,ISPIN)*VRT(IRREPR,ISPIN)
1200   CONTINUE
       DO 170 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        IF(IRREPL.GT.IRREPR)GOTO 170
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPR)
        NUMJ=POP(IRREPR,ISPIN)
        NUMI=POP(IRREPL,ISPIN)
        NUMB=VRT(IRREPR,ISPIN)
        NUMA=VRT(IRREPL,ISPIN)
        MAXL=MOPOP(IRREPR)*MOPOP(IRREPL)
        OCCL=POP(IRREPL,ISPIN)
        OCCR=POP(IRREPR,ISPIN)
        MAXL1=MOPOP(IRREPL)
        MAXR1=MOPOP(IRREPR)
        DO 180 INDXJ=1,NUMJ
         DO 185 INDXI=1,NUMI
          INDI=INDXI
          INDJ=INDXJ
          CALL GETLST(BUF,ILOGREC,1,1,IRREPDO,LISTABIJ)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          DO 190 INDXB=1,NUMB
*VOCL LOOP,NOVREC
CDIR$ IVDEP
           DO 195 INDXA=1,NUMA
            X=BUF(IPOS)*FABIJ
            IPOS=IPOS+1
            INDA=INDXA+OCCL
            INDB=INDXB+OCCR
            GINDXL=INDXF(INDJ,INDA,MAXR1)
            GINDXR=INDXF(INDB,INDI,MAXR1)
            GINDX0=INDXF(GINDXL,GINDXR,MAXL)
            GINDX =GINDX0+GOFF(IRREPR,IRREPL)
            GAMMA(GINDX)=-X
            GINDX0=INDXF(GINDXR,GINDXL,MAXL)
            GINDX =GINDX0+GOFF(IRREPR,IRREPL)
            GAMMA(GINDX)=-X
195        CONTINUE
190       CONTINUE
185      CONTINUE
180     CONTINUE
170    CONTINUE
160   CONTINUE
      RETURN
      END
