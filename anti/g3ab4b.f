      SUBROUTINE G3AB4B(GAMMA,BUF,GOFF,LENGAM,FAIBJ)
C
C THIS ROUTINE PROCESSES RING TYPE GAMMAS NEEDED FOR THE
C  SPIN CASE BBAA MO GAMMA LIST FOR UHF CALCULATIONS.
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 
      DOUBLE PRECISION FABIJ,FAIBJ,FABCD,FIJKL,FABCI,FIJKA
      INTEGER GOFF(8,8),IOFFSL(8),IOFFSR(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
      NNM1O2(I)   =(I*(I-1))/2
C
C FIRST DO LIST 125
C
C         21 21        22 11
C       G(ij,AB) <-- g<Ai,Bj>
C
C         21 21        12 21
C       G(ij,BA) <-- g<Ai,Bj>
C
C WITH THE FOLLOWING SYMMETRIZATION:
C
C       G(ij,AB) = G(ij,AB) + G(ij,BA)
C
      LISTAIBJ=125
      IOFFR=1
      IOFFL=1
      DO 1000 IRREP=1,NIRREP
       IOFFSR(IRREP)=IOFFR
       IOFFSL(IRREP)=IOFFL
       IOFFL=IOFFL+VRT(IRREP,1)*POP(IRREP,2)
       IOFFR=IOFFR+VRT(IRREP,1)*POP(IRREP,2)
1000  CONTINUE
      DO 100 IRREPDO=2,NIRREP
       DO 110 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPL)
        NUMJ=POP(IRREPR,2)
        NUMB=VRT(IRREPR,1)
        NUMI=POP(IRREPL,2)
        NUMA=VRT(IRREPL,1)
        MAXL=MOPOP(IRREPL)*MOPOP(IRREPR)
        MAX1=MOPOP(IRREPL)
        DO 120 INDXJ=1,NUMJ
         DO 130 INDXB=1,NUMB
          INDJ=INDXJ
          INDB=INDXB+POP(IRREPR,1)
          CALL GETLST(BUF,ILOGREC,1,1,1,LISTAIBJ)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          IF(IRREPL.GT.IRREPR)THEN
           DO 140 INDXI=1,NUMI
            DO 150 INDXA=1,NUMA
             X=BUF(IPOS)*FAIBJ
             IPOS=IPOS+1
             INDA=INDXA+POP(IRREPL,1)
             INDI=INDXI
             GINDXL=INDXF(INDA,INDB,MAX1)
             GINDXR=INDXF(INDI,INDJ,MAX1)
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
150         CONTINUE
140        CONTINUE
          ENDIF
130      CONTINUE
120     CONTINUE
110    CONTINUE
100   CONTINUE
C
C NOW PICK UP OTHER DPD IRREPS TO FILL OUT GAMMA
C
      DO 160 IRREPDO=2,NIRREP
C
C COMPUTE OFFSETS TO ALL BA AB POSITIONS
C
C
       IOFFR=1
       IOFFL=1
       DO 1200 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        IOFFSR(IRREPR)=IOFFR
        IOFFSL(IRREPR)=IOFFL
        IOFFR=IOFFR+VRT(IRREPL,1)*POP(IRREPR,2)
        IOFFL=IOFFL+VRT(IRREPL,1)*POP(IRREPR,2)
1200   CONTINUE
       DO 170 IRREPB=1,NIRREP
        IRREPA=DIRPRD(IRREPB,IRREPDO)
        ILOGREC=IOFFSR(IRREPB)
        IFIRST =IOFFSL(IRREPA)
        NUMJ=POP(IRREPB,2)
        NUMB=VRT(IRREPA,1)
        NUMI=POP(IRREPA,2)
        NUMA=VRT(IRREPB,1)
        MAXL=MOPOP(IRREPA)*MOPOP(IRREPB)
        MAXA=MOPOP(IRREPA)
        MAXB=MOPOP(IRREPB)
        DO 180 INDXJ=1,NUMJ
         DO 185 INDXB=1,NUMB
          INDJ=INDXJ
          INDB=INDXB+POP(IRREPA,1)
          CALL GETLST(BUF,ILOGREC,1,1,IRREPDO,LISTAIBJ)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          IF(IRREPA.GT.IRREPB)THEN
           DO 190 INDXI=1,NUMI
            DO 195 INDXA=1,NUMA
             X=BUF(IPOS)*FAIBJ
             IPOS=IPOS+1
             INDI=INDXI
             INDA=INDXA+POP(IRREPB,1)
             GINDXL=INDXF(INDB,INDA,MAXA)
             GINDXR=INDXF(INDI,INDJ,MAXA)
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPA,IRREPB)
             GAMMA(GINDX)=GAMMA(GINDX)+X
195         CONTINUE
190        CONTINUE
          ENDIF
185      CONTINUE
180     CONTINUE
170    CONTINUE
160   CONTINUE
C
C NOW DO LIST 126
C
C         21 21        22 11
C       G(ab,IJ) <-- g<aI,bJ>
C
C         21 21        12 21
C       G(ba,IJ) <-- g<aI,bJ>
C
C WITH THE FOLLOWING SYMMETRIZATION:
C
C       G(ab,IJ) = G(ab,IJ) + G(ba,IJ)
C
      LISTAIBJ=126
      IOFFR=1
      IOFFL=1
      DO 2000 IRREP=1,NIRREP
       IOFFSR(IRREP)=IOFFR
       IOFFSL(IRREP)=IOFFL
       IOFFL=IOFFL+VRT(IRREP,2)*POP(IRREP,1)
       IOFFR=IOFFR+VRT(IRREP,2)*POP(IRREP,1)
2000  CONTINUE
      DO 200 IRREPDO=2,NIRREP
       DO 210 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPL)
        NUMJ=POP(IRREPR,1)
        NUMB=VRT(IRREPR,2)
        NUMI=POP(IRREPL,1)
        NUMA=VRT(IRREPL,2)
        MAXL=MOPOP(IRREPL)*MOPOP(IRREPR)
        MAX1=MOPOP(IRREPL)
        DO 220 INDXJ=1,NUMJ
         DO 230 INDXB=1,NUMB
          INDJ=INDXJ
          INDB=INDXB+POP(IRREPR,2)
          CALL GETLST(BUF,ILOGREC,1,1,1,LISTAIBJ)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          IF(IRREPL.GT.IRREPR)THEN
           DO 240 INDXI=1,NUMI
            DO 250 INDXA=1,NUMA
             X=BUF(IPOS)*FAIBJ
             IPOS=IPOS+1
             INDA=INDXA+POP(IRREPL,2)
             INDI=INDXI
             GINDXL=INDXF(INDA,INDB,MAX1)
             GINDXR=INDXF(INDI,INDJ,MAX1)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
250         CONTINUE
240        CONTINUE
          ENDIF
230      CONTINUE
220     CONTINUE
210    CONTINUE
200   CONTINUE
C
C NOW PICK UP OTHER DPD IRREPS TO FILL OUT GAMMA
C
      DO 260 IRREPDO=2,NIRREP
C
C COMPUTE OFFSETS TO ALL BA AB POSITIONS
C
C         21 21        12 21
C       G(ba,IJ) <-- g<aI,bJ>
C
       IOFFR=1
       IOFFL=1
       DO 2200 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        IOFFSR(IRREPR)=IOFFR
        IOFFSL(IRREPR)=IOFFL
        IOFFR=IOFFR+VRT(IRREPL,2)*POP(IRREPR,1)
        IOFFL=IOFFL+VRT(IRREPL,2)*POP(IRREPR,1)
2200   CONTINUE
       DO 270 IRREPB=1,NIRREP
        IRREPA=DIRPRD(IRREPB,IRREPDO)
        ILOGREC=IOFFSR(IRREPB)
        IFIRST =IOFFSL(IRREPA)
        NUMJ=POP(IRREPB,1)
        NUMB=VRT(IRREPA,2)
        NUMI=POP(IRREPA,1)
        NUMA=VRT(IRREPB,2)
        MAXL=MOPOP(IRREPA)*MOPOP(IRREPB)
        MAXA=MOPOP(IRREPA)
        MAXB=MOPOP(IRREPB)
        DO 280 INDXJ=1,NUMJ
         DO 285 INDXB=1,NUMB
          INDJ=INDXJ
          INDB=INDXB+POP(IRREPA,2)
          CALL GETLST(BUF,ILOGREC,1,1,IRREPDO,LISTAIBJ)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          IF(IRREPA.GT.IRREPB)THEN
           DO 290 INDXI=1,NUMI
            DO 295 INDXA=1,NUMA
             X=BUF(IPOS)*FAIBJ
             IPOS=IPOS+1
             INDI=INDXI
             INDA=INDXA+POP(IRREPB,2)
             GINDXL=INDXF(INDB,INDA,MAXA)
             GINDXR=INDXF(INDI,INDJ,MAXA)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPA,IRREPB)
             GAMMA(GINDX)=GAMMA(GINDX)+X
295         CONTINUE
290        CONTINUE
          ENDIF
285      CONTINUE
280     CONTINUE
270    CONTINUE
260   CONTINUE
C
C NOW DO LIST 118
C
C         21 21        21 21
C       G(aj,BI) <-- g<Bj,aI>
C
C         21 21        11 22      
C       G(aj,IB) <-- g<Bj,aI>
C
C         21 21        22 11
C       G(ja,BI) <-- g<Bj,aI>
C
C         21 21        12 12
C       G(ja,IB) <-- g<Bj,aI>
C
C
      LISTAIBJ=118
      IOFFR=1
      IOFFL=1
      DO 3000 IRREP=1,NIRREP
       IOFFSR(IRREP)=IOFFR
       IOFFSL(IRREP)=IOFFL
       IOFFL=IOFFL+VRT(IRREP,1)*POP(IRREP,2)
       IOFFR=IOFFR+VRT(IRREP,2)*POP(IRREP,1)
3000  CONTINUE
      DO 300 IRREPDO=2,NIRREP
       DO 310 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPL)
        NUMI=POP(IRREPR,1)
        NUMA=VRT(IRREPR,2)
        NUMJ=POP(IRREPL,2)
        NUMB=VRT(IRREPL,1)
        MAXL=MOPOP(IRREPL)*MOPOP(IRREPR)
        MAX1=MOPOP(IRREPL)
        MAX2=MOPOP(IRREPR)
        DO 320 INDXI=1,NUMI
         DO 330 INDXA=1,NUMA
          INDI=INDXI
          INDA=INDXA+POP(IRREPR,2)
          CALL GETLST(BUF,ILOGREC,1,1,1,LISTAIBJ)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          IF(IRREPL.GT.IRREPR)THEN
C
C         21 21        22 11
C       G(ja,BI) <-- g<Bj,aI>
C
           DO 340 INDXJ=1,NUMJ
            DO 350 INDXB=1,NUMB
             X=BUF(IPOS)*FAIBJ
             IPOS=IPOS+1
             INDB=INDXB+POP(IRREPL,1)
             INDJ=INDXJ
             GINDXL=INDXF(INDB,INDI,MAX1)
             GINDXR=INDXF(INDJ,INDA,MAX1)
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
350         CONTINUE
340        CONTINUE
          ELSE
C
C         21 21        11 22      
C       G(aj,IB) <-- g<Bj,aI>
C
           DO 341 INDXJ=1,NUMJ
            DO 351 INDXB=1,NUMB
             X=BUF(IPOS)*FAIBJ
             IPOS=IPOS+1
             INDB=INDXB+POP(IRREPL,1)
             INDJ=INDXJ
             GINDXL=INDXF(INDI,INDB,MAX2)
             GINDXR=INDXF(INDA,INDJ,MAX2)
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X
351         CONTINUE
341        CONTINUE
          ENDIF
330      CONTINUE
320     CONTINUE
310    CONTINUE
300   CONTINUE
C
C NOW PICK UP OTHER DPD IRREPS TO FILL OUT GAMMA
C
      DO 360 IRREPDO=2,NIRREP
C
C COMPUTE OFFSETS TO ALL BA AB POSITIONS
C
       IOFFR=1
       IOFFL=1
       DO 3200 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        IOFFSR(IRREPR)=IOFFR
        IOFFSL(IRREPR)=IOFFL
        IOFFR=IOFFR+VRT(IRREPL,2)*POP(IRREPR,1)
        IOFFL=IOFFL+VRT(IRREPL,1)*POP(IRREPR,2)
3200   CONTINUE
       DO 370 IRREPB=1,NIRREP
        IRREPA=DIRPRD(IRREPB,IRREPDO)
        ILOGREC=IOFFSR(IRREPB)
        IFIRST =IOFFSL(IRREPB)
        NUMI=POP(IRREPB,1)
        NUMA=VRT(IRREPA,2)
        NUMJ=POP(IRREPB,2)
        NUMB=VRT(IRREPA,1)
        MAXL=MOPOP(IRREPA)*MOPOP(IRREPB)
        MAXA=MOPOP(IRREPA)
        MAXB=MOPOP(IRREPB)
        DO 380 INDXI=1,NUMI
         DO 385 INDXA=1,NUMA
          INDI=INDXI
          INDA=INDXA+POP(IRREPA,2)
          CALL GETLST(BUF,ILOGREC,1,1,IRREPDO,LISTAIBJ)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          IF(IRREPA.GT.IRREPB)THEN
C
C         21 21        21 21
C       G(aj,BI) <-- g<Bj,aI>
C
           DO 390 INDXJ=1,NUMJ
            DO 395 INDXB=1,NUMB
             X=BUF(IPOS)*FAIBJ
             IPOS=IPOS+1
             INDJ=INDXJ
             INDB=INDXB+POP(IRREPA,1)
             GINDXL=INDXF(INDB,INDI,MAXA)
             GINDXR=INDXF(INDA,INDJ,MAXA)
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPA,IRREPB)
             GAMMA(GINDX)=GAMMA(GINDX)+X
395         CONTINUE
390        CONTINUE
          ELSE
C
C         21 21        12 12
C       G(ja,IB) <-- g<Bj,aI>
C
           DO 391 INDXJ=1,NUMJ
            DO 396 INDXB=1,NUMB
             X=BUF(IPOS)*FAIBJ
             IPOS=IPOS+1
             INDJ=INDXJ
             INDB=INDXB+POP(IRREPA,1)
             GINDXL=INDXF(INDI,INDB,MAXB)
             GINDXR=INDXF(INDJ,INDA,MAXB)
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPB,IRREPA)
             GAMMA(GINDX)=GAMMA(GINDX)+X
396         CONTINUE
391        CONTINUE
          ENDIF
385      CONTINUE
380     CONTINUE
370    CONTINUE
360   CONTINUE
C
      RETURN
      END
