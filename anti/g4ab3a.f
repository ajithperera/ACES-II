      SUBROUTINE G4AB3A(GAMMA,BUF,GOFF,DOIRR,LENGAM,FABIJ)
C
C THIS ROUTINE PROCESSES THE G<Ab,Ij> GAMMA LISTS AND FILLS
C  OUT THE ABCD SYMMETRY CASE MULLIKEN ORDERED GAMMA FOR 
C  SPIN CASE AABB.  THIS IS FOR UHF CALCULATIONS ONLY!
C
C          12 34        13 24
C        G(AI,bj) <-- g<Ab,Ij>
C
C          12 34        24 13
C        G(IA,jb) <-- g<Ab,Ij>
C
C          12 34        23 14
C        G(IA,bj) <-- g<Ab,Ij>
C
C          12 34        14 23
C        G(AI,jb) <-- g<Ab,Ij>
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 
      DOUBLE PRECISION FABIJ,FIJKL,FABCD,FAIBJ,FABCI,FIJKA
      INTEGER GOFF(8,8,8,8),IDID(8),IOFFSL(8)
      INTEGER IOFFSR(8),DOIRR(42,4)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
C
C CALCULATE HOW MANY UNIQUE ABCD SYMMETRY SETS THERE ARE
C
      IF(NIRREP.EQ.8)THEN
       NUMABCD=42
      ELSEIF(NIRREP.EQ.4)THEN
       NUMABCD=3
      ENDIF
C     
      LISTABIJ=116
      DO 100 ICOUNT=1,NUMABCD
       IRREP1M=DOIRR(ICOUNT,1)
       IRREP2M=DOIRR(ICOUNT,2)
       IRREP3M=DOIRR(ICOUNT,3)
       IRREP4M=DOIRR(ICOUNT,4)
       IOFFSET=GOFF(IRREP1M,IRREP2M,IRREP3M,IRREP4M)
C
C         12 34        13 24
C       G(AI,bj) <-- g<Ab,Ij>
C
       IRREPGET=DIRPRD(IRREP2M,IRREP4M)
C
       IOFFR=1
       IOFFL=1
       DO 2000 IRREPA=1,NIRREP
        IRREPB=DIRPRD(IRREPA,IRREPGET)
        IOFFSR(IRREPA)=IOFFR
        IOFFSL(IRREPA)=IOFFL
        IOFFR=IOFFR+POP(IRREPB,1)*POP(IRREPA,2)
        IOFFL=IOFFL+VRT(IRREPB,1)*VRT(IRREPA,2)
2000   CONTINUE
C
       ILOGREC=IOFFSR(IRREP4M)
       IFIRST=IOFFSL(IRREP3M)
       NUMJ=POP(IRREP4M,2)
       NUMI=POP(IRREP2M,1)
       NUMB=VRT(IRREP3M,2)
       NUMA=VRT(IRREP1M,1) 
       MAXL=MOPOP(IRREP1M)*MOPOP(IRREP2M)
       DO 120 INDXJ=1,NUMJ
        DO 130 INDXI=1,NUMI
         INDJ=INDXJ
         INDI=INDXI
         CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTABIJ)
         ILOGREC=ILOGREC+1
         IPOS=IOFFSL(IRREP3M)
         DO 140 INDXB=1,NUMB
          DO 150 INDXA=1,NUMA
           X=BUF(IPOS)*FABIJ
           IPOS=IPOS+1
           INDB=INDXB+POP(IRREP3M,2)
           INDA=INDXA+POP(IRREP1M,1)
           GINDXL=INDXF(INDA,INDI,MOPOP(IRREP1M))
           GINDXR=INDXF(INDB,INDJ,MOPOP(IRREP3M))
           GINDX0=INDXF(GINDXL,GINDXR,MAXL)
           GINDX =GINDX0+IOFFSET
           GAMMA(GINDX)=GAMMA(GINDX)+X
150       CONTINUE
140      CONTINUE
130     CONTINUE
120    CONTINUE
C
C       12 34        24 13
C     G(IA,jb) <-- g<Ab,Ij>
C
       ILOGREC=IOFFSR(IRREP3M)
       IFIRST=IOFFSL(IRREP4M)
       NUMJ=POP(IRREP3M,2)
       NUMI=POP(IRREP1M,1) 
       NUMB=VRT(IRREP4M,2)
       NUMA=VRT(IRREP2M,1)
       MAXL=MOPOP(IRREP1M)*MOPOP(IRREP2M)
       DO 420 INDXJ=1,NUMJ
        DO 430 INDXI=1,NUMI
         INDI=INDXI
         INDJ=INDXJ
         CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTABIJ)
         ILOGREC=ILOGREC+1
         IPOS=IOFFSL(IRREP4M)
         DO 440 INDXB=1,NUMB
          DO 450 INDXA=1,NUMA
           X=BUF(IPOS)*FABIJ
           IPOS=IPOS+1
           INDB=INDXB+POP(IRREP4M,2)
           INDA=INDXA+POP(IRREP2M,1)
           GINDXL=INDXF(INDI,INDA,MOPOP(IRREP1M))
           GINDXR=INDXF(INDJ,INDB,MOPOP(IRREP3M))
           GINDX0=INDXF(GINDXL,GINDXR,MAXL)
           GINDX =GINDX0+IOFFSET
           GAMMA(GINDX)=GAMMA(GINDX)+X
450       CONTINUE
440      CONTINUE
430     CONTINUE
420    CONTINUE
C
C       12 34        23 14
C     G(IA,bj) <-- g<Ab,Ij>
C
       IRREPGET=DIRPRD(IRREP1M,IRREP4M)
       IOFFR=1
       IOFFL=1
C
       DO 1000 IRREPA=1,NIRREP
        IRREPB=DIRPRD(IRREPA,IRREPGET)
        IOFFSR(IRREPA)=IOFFR
        IOFFSL(IRREPA)=IOFFL
        IOFFR=IOFFR+POP(IRREPB,1)*POP(IRREPA,2)
        IOFFL=IOFFL+VRT(IRREPB,1)*VRT(IRREPA,2)
1000   CONTINUE
C
       ILOGREC=IOFFSR(IRREP4M)
       IFIRST=IOFFSL(IRREP3M)
       NUMJ=POP(IRREP4M,2)
       NUMI=POP(IRREP1M,1) 
       NUMB=VRT(IRREP3M,2)
       NUMA=VRT(IRREP2M,1)
       MAXL=MOPOP(IRREP1M)*MOPOP(IRREP2M)
       DO 220 INDXJ=1,NUMJ
        DO 230 INDXI=1,NUMI
         INDI=INDXI
         INDJ=INDXJ
         CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTABIJ)
         ILOGREC=ILOGREC+1
         IPOS=IOFFSL(IRREP3M)
         DO 240 INDXB=1,NUMB
          DO 250 INDXA=1,NUMA
           X=BUF(IPOS)*FABIJ
           IPOS=IPOS+1
           INDB=INDXB+POP(IRREP3M,2)
           INDA=INDXA+POP(IRREP2M,1)
           GINDXL=INDXF(INDI,INDA,MOPOP(IRREP1M))
           GINDXR=INDXF(INDB,INDJ,MOPOP(IRREP3M))
           GINDX0=INDXF(GINDXL,GINDXR,MAXL)
           GINDX =GINDX0+IOFFSET
           GAMMA(GINDX)=GAMMA(GINDX)+X
250       CONTINUE
240      CONTINUE
230     CONTINUE
220    CONTINUE
C
C       12 34        14 23
C     G(AI,jb) <-- g<Ab,Ij>
C
       ILOGREC=IOFFSR(IRREP3M)
       IFIRST=IOFFSL(IRREP4M)
       NUMJ=POP(IRREP3M,2)
       NUMI=POP(IRREP2M,1) 
       NUMB=VRT(IRREP4M,2)
       NUMA=VRT(IRREP1M,1)
       MAXL=MOPOP(IRREP1M)*MOPOP(IRREP2M)
       DO 320 INDXJ=1,NUMJ
        DO 330 INDXI=1,NUMI
         INDI=INDXI
         INDJ=INDXJ
         CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTABIJ)
         ILOGREC=ILOGREC+1
         IPOS=IOFFSL(IRREP4M)
         DO 340 INDXB=1,NUMB
          DO 350 INDXA=1,NUMA
           X=BUF(IPOS)*FABIJ
           IPOS=IPOS+1
           INDB=INDXB+POP(IRREP4M,2)
           INDA=INDXA+POP(IRREP1M,1)
           GINDXL=INDXF(INDA,INDI,MOPOP(IRREP1M))
           GINDXR=INDXF(INDJ,INDB,MOPOP(IRREP3M))
           GINDX0=INDXF(GINDXL,GINDXR,MAXL)
           GINDX =GINDX0+IOFFSET
           GAMMA(GINDX)=GAMMA(GINDX)+X
350       CONTINUE
340      CONTINUE
330     CONTINUE
320    CONTINUE
100   CONTINUE
      RETURN
      END
