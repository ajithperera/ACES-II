      SUBROUTINE G3AB5(GAMMA,BUF,GOFF,LENGAM,FABCI)
C
C THIS ROUTINE PROCESSES THE G(Ab,Ci) GAMMA LISTS FOR SPIN CASE
C  AB AND FILLS THE SYMMETRY CASE ABAB MULLIKEN ORDERED GAMMA LIST.
C  THIS IS THE INCORE VERSION!
C
C NOTE THAT ABAB IN MULLIKEN CORRESPONDS TO AABB IN DIRAC.
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 
      DOUBLE PRECISION FABIJ,FAIBJ,FABCD,FIJKL,FABCI,FIJKA
      INTEGER GOFF(8,8),IOFFSL(8),IOFFSR(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
C
C COMPUTE OFFSETS TO FIRST ELEMENT OF ALL G(XX,YY) IRREP POSITIONS
C
      LISTABCI=130
      IOFFR=1
      IOFFL=1
      DO 1000 IRREP=1,NIRREP
       IOFFSR(IRREP)=IOFFR
       IOFFSL(IRREP)=IOFFL
       IOFFR=IOFFR+VRT(IRREP,1)*POP(IRREP,2)
       IOFFL=IOFFL+VRT(IRREP,1)*VRT(IRREP,2)
1000  CONTINUE
      DO 100 IRREPDO=2,NIRREP
       DO 110 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPL)
        NUMI=POP(IRREPR,2)
        NUMC=VRT(IRREPR,1)
        NUMB=VRT(IRREPL,2)
        NUMA=VRT(IRREPL,1)
        MAXL=MOPOP(IRREPL)*MOPOP(IRREPR)
        MAXIL=MOPOP(IRREPL)
        MAXIR=MOPOP(IRREPR)
        DO 120 INDXI=1,NUMI
         DO 130 INDXC=1,NUMC
          INDI=INDXI
          INDC=INDXC+POP(IRREPR,1) 
          CALL GETLST(BUF,ILOGREC,1,1,1,LISTABCI)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          IF(IRREPL.GT.IRREPR)THEN
           DO 140 INDXB=1,NUMB
            DO 150 INDXA=1,NUMA
             X=BUF(IPOS)*FABCI
             IPOS=IPOS+1
             INDA=INDXA+POP(IRREPL,1)
             INDB=INDXB+POP(IRREPL,1)
             GINDXL=INDXF(INDA,INDC,MAXIL)
             GINDXR=INDXF(INDB,INDI,MAXIL)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
150         CONTINUE
140        CONTINUE
          ELSE
           DO 141 INDXB=1,NUMB
            DO 151 INDXA=1,NUMA
             X=BUF(IPOS)*FABCI
             IPOS=IPOS+1
             INDB=INDXB+POP(IRREPL,1)
             INDA=INDXA+POP(IRREPL,1)
             GINDXL=INDXF(INDC,INDA,MAXIR)
             GINDXR=INDXF(INDI,INDB,MAXIR)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X
151         CONTINUE
141        CONTINUE
          ENDIF
130      CONTINUE
120     CONTINUE
110    CONTINUE
100   CONTINUE
C
C NOW WE MUST LOOP OVER ALL OTHER IRREPS AND PUT THE OTHER PIECE IN
C
      DO 160 IRREPDO=2,NIRREP
C
C COMPUTE OFFSETS TO ALL BA AB POSITIONS
C
       IOFFR=1
       IOFFL=1
       DO 1200 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        IOFFSR(IRREPR)=IOFFR
        IOFFSL(IRREPR)=IOFFL
        IOFFR=IOFFR+VRT(IRREPL,1)*POP(IRREPR,2)
        IOFFL=IOFFL+VRT(IRREPL,1)*VRT(IRREPR,2)
1200   CONTINUE
       DO 170 IRREPB=1,NIRREP
        IRREPA=DIRPRD(IRREPB,IRREPDO)
        ILOGREC=IOFFSR(IRREPB)
        IFIRST=IOFFSL(IRREPA)
        NUMI=POP(IRREPB,2)
        NUMC=VRT(IRREPA,1)
        NUMB=VRT(IRREPA,2)
        NUMA=VRT(IRREPB,1)
        MAXL=MOPOP(IRREPA)*MOPOP(IRREPB)
        MAXA=MOPOP(IRREPA)
        MAXB=MOPOP(IRREPB)
        DO 180 INDXI=1,NUMI
         DO 185 INDXC=1,NUMC
          INDI=INDXI
          INDC=INDXC+POP(IRREPA,1)
          CALL GETLST(BUF,ILOGREC,1,1,IRREPDO,LISTABCI)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          IF(IRREPA.GT.IRREPB)THEN
           DO 190 INDXB=1,NUMB
            DO 195 INDXA=1,NUMA
             INDB=INDXB+POP(IRREPA,2)
             INDA=INDXA+POP(IRREPB,1)
             X=BUF(IPOS)*FABCI
             IPOS=IPOS+1
             GINDXL=INDXF(INDC,INDA,MAXA)
             GINDXR=INDXF(INDB,INDI,MAXA)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX=GINDX0+GOFF(IRREPA,IRREPB)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX=GINDX0+GOFF(IRREPA,IRREPB)
             GAMMA(GINDX)=GAMMA(GINDX)+X
195         CONTINUE
190        CONTINUE
          ELSE
           DO 191 INDXB=1,NUMB
            DO 196 INDXA=1,NUMA
             INDB=INDXB+POP(IRREPA,2)
             INDA=INDXA+POP(IRREPB,1)
             X=BUF(IPOS)*FABCI
             IPOS=IPOS+1
             GINDXL=INDXF(INDA,INDC,MAXB)
             GINDXR=INDXF(INDI,INDB,MAXB)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX=GINDX0+GOFF(IRREPB,IRREPA)
             GAMMA(GINDX)=GAMMA(GINDX)+X
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX=GINDX0+GOFF(IRREPB,IRREPA)
             GAMMA(GINDX)=GAMMA(GINDX)+X
196         CONTINUE
191        CONTINUE
          ENDIF
185      CONTINUE
180     CONTINUE
170    CONTINUE
160   CONTINUE
      RETURN
      END
