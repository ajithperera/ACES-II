      SUBROUTINE G2AB5(GAMMA,BUF,GOFF,LENGAM,FABCI)
C
C THIS ROUTINE PROCESSES THE G(Ab,Ci) GAMMAS AND AUGMENTS THE 
C  AABB MULLIKEN ORDERED MO GAMMA LIST.
C
C NOTE THAT AABB IN MULLIKEN CORRESPONDS TO ABAB IN DIRAC.  CONSEQUENTLY,
C  ALL ELEMENTS OF GAMMA WHICH ARE PROCESSED HERE COME FROM DPD LISTS
C  FOR IRREPS 2-h, SINCE A.NE.B.
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 
      DOUBLE PRECISION FABIJ,FAIBJ,FABCD,FIJKL,FABCI,FIJKA
      INTEGER GOFF(8,8),IOFFSR(8),IOFFSL(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
C
      LISTABCI=130
      DO 100 IRREPGET=2,NIRREP
       IOFFR=1
       IOFFL=1
       DO 1100 IRREP1=1,NIRREP
        IRREP2=DIRPRD(IRREP1,IRREPGET)
        IOFFSR(IRREP1)=IOFFR
        IOFFSL(IRREP1)=IOFFL
        IOFFR=IOFFR+VRT(IRREP2,1)*POP(IRREP1,2)
        IOFFL=IOFFL+VRT(IRREP2,1)*VRT(IRREP1,2)
1100   CONTINUE
       DO 110 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPGET)
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPR)-1
        NUMI=POP(IRREPR,2)
        NUMC=VRT(IRREPL,1)
        NUMB=VRT(IRREPR,2)
        NUMA=VRT(IRREPL,1)
        MAXL=NNP1O2(MOPOP(IRREPL))
        MAXR=NNP1O2(MOPOP(IRREPR))
        DO 120 INDXI=1,NUMI
         DO 130 INDXC=1,NUMC
          INDI=INDXI
          INDC=INDXC+POP(IRREPL,1)
          CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTABCI)
          ILOGREC=ILOGREC+1
          DO 140 INDXB=1,NUMB 
           INDB=INDXB+POP(IRREPR,2)
           GINDXR=INDXT(INDI,INDB)
           IF(IRREPL.LT.IRREPR)THEN
            DO 150 INDXA=1,INDXC
             IPOS=IFIRST+INDXF(INDXA,INDXB,NUMA)
             X=BUF(IPOS)*FABCI
             INDA=INDXA+POP(IRREPL,1)
             GINDXL=INDXT(INDA,INDC)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
150         CONTINUE
            DO 151 INDXA=INDXC,NUMA
             IPOS=IFIRST+INDXF(INDXA,INDXB,NUMA)
             X=BUF(IPOS)*FABCI
             INDA=INDXA+POP(IRREPL,1)
             GINDXL=INDXT(INDC,INDA)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX=GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
151         CONTINUE
           ELSE
            DO 250 INDXA=1,INDXC
             IPOS=IFIRST+INDXF(INDXA,INDXB,NUMA)
             X=BUF(IPOS)*FABCI
             INDA=INDXA+POP(IRREPL,1)
             GINDXL=INDXT(INDA,INDC)
             GINDX0=INDXF(GINDXR,GINDXL,MAXR)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X
250         CONTINUE
            DO 251 INDXA=INDXC,NUMA
             IPOS=IFIRST+INDXF(INDXA,INDXB,NUMA)
             X=BUF(IPOS)*FABCI
             INDA=INDXA+POP(IRREPL,1)
             GINDXL=INDXT(INDC,INDA)
             GINDX0=INDXF(GINDXR,GINDXL,MAXR)
             GINDX=GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X
251         CONTINUE
           ENDIF
140       CONTINUE
130      CONTINUE
120     CONTINUE
110    CONTINUE
100   CONTINUE
      RETURN
      END
