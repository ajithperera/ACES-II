      SUBROUTINE G2AB4(GAMMA,BUF,GOFF,LENGAM,FAIBJ)
C
C THIS ROUTINE PROCESSES THE G(Ai,Bj) GAMMAS AND AUGMENTS THE 
C  AABB MULLIKEN ORDERED MO GAMMA LIST.
C
C NOTE THAT AABB IN MULLIKEN CORRESPONDS TO ABAB IN DIRAC.  CONSEQUENTLY,
C  ALL ELEMENTS OF GAMMA WHICH ARE PROCESSED HERE COME FROM DPD LISTS
C  FOR IRREPS 2-h, SINCE A.NE.B.
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 
      DOUBLE PRECISION FABIJ,FAIBJ,FABCD,FIJKL,FABCI,FIJKA,FAIBJ2
      INTEGER GOFF(8,8),IOFFSR(8),IOFFSL(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
C
      LISTAIBJ=125
      LISTBIAJ=118
      FAIBJ2=FAIBJ
      DO 100 IRREPGET=2,NIRREP
       IOFFR=1
       IOFFL=1
       DO 1100 IRREP1=1,NIRREP
        IRREP2=DIRPRD(IRREP1,IRREPGET)
        IOFFSR(IRREP1)=IOFFR
        IOFFSL(IRREP1)=IOFFL
        IOFFR=IOFFR+VRT(IRREP2,1)*POP(IRREP1,2)
        IOFFL=IOFFL+VRT(IRREP2,1)*POP(IRREP1,2)
1100   CONTINUE
       DO 110 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPGET)
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPR)-1
        NUMJ=POP(IRREPR,2)
        NUMB=VRT(IRREPL,1)
        NUMI=POP(IRREPR,2)
        NUMA=VRT(IRREPL,1)
        MAXL=NNP1O2(MOPOP(IRREPL))
        MAXR=NNP1O2(MOPOP(IRREPR))
        DO 120 INDXJ=1,NUMJ
         DO 130 INDXB=1,NUMB
          INDJ=INDXJ
          INDB=INDXB+POP(IRREPL,1)
          CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTAIBJ)
          ILOGREC=ILOGREC+1
          DO 140 INDXI=1,INDXJ
           INDI=INDXI
           GINDXR=INDXT(INDI,INDJ)
           IF(IRREPR.GT.IRREPL)THEN
            DO 150 INDXA=1,INDXB
             IPOS=IFIRST+INDXF(INDXA,INDXI,NUMA)
             X=BUF(IPOS)*FAIBJ
             INDA=INDXA+POP(IRREPL,1)
             GINDXL=INDXT(INDA,INDB)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
150         CONTINUE
            DO 151 INDXA=INDXB,NUMA
             IPOS=IFIRST+INDXF(INDXA,INDXI,NUMA)
             X=BUF(IPOS)*FAIBJ
             INDA=INDXA+POP(IRREPL,1)
             GINDXL=INDXT(INDB,INDA)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
151         CONTINUE
           ELSE
            DO 160 INDXA=1,INDXB
             IPOS=IFIRST+INDXF(INDXA,INDXI,NUMA)
             X=BUF(IPOS)*FAIBJ
             INDA=INDXA+POP(IRREPL,1)
             GINDXL=INDXT(INDA,INDB)
             GINDX0=INDXF(GINDXR,GINDXL,MAXR)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X
160         CONTINUE
            DO 161 INDXA=INDXB,NUMA
             IPOS=IFIRST+INDXF(INDXA,INDXI,NUMA)
             X=BUF(IPOS)*FAIBJ
             INDA=INDXA+POP(IRREPL,1)
             GINDXL=INDXT(INDB,INDA)
             GINDX0=INDXF(GINDXR,GINDXL,MAXR)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X
161         CONTINUE
           ENDIF 
140       CONTINUE
130      CONTINUE
120     CONTINUE
110    CONTINUE
100   CONTINUE
C
C NOW DO SECOND PART.  PROCESS THE G(BI,aj) LISTS WHICH ARE ALREADY
C  STORED IN MULLIKEN ORDER ON DISK.  CONSEQUENTLY, WE READ IRREP 1
C  TO GET THE AABB PIECES.
C
      IOFFR=1
      IOFFL=1
      DO 1000 IRREPR=1,NIRREP
       IOFFSR(IRREPR)=IOFFR
       IOFFSL(IRREPR)=IOFFL
       IOFFR=IOFFR+VRT(IRREPR,1)*POP(IRREPR,2)
       IOFFL=IOFFL+VRT(IRREPR,1)*POP(IRREPR,2)
1000  CONTINUE
1010  CONTINUE
      DO 200 IRREPDO=2,NIRREP
       DO 210 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPL)
        NUMJ=POP(IRREPR,2)
        NUMA=VRT(IRREPR,2)
        NUMI=POP(IRREPL,1)
        NUMB=VRT(IRREPL,1)
        MAXL=NNP1O2(MOPOP(IRREPL))
        DO 220 INDXJ=1,NUMJ
         DO 230 INDXA=1,NUMA
          INDJ=INDXJ
          INDA=INDXA+POP(IRREPR,2)
          CALL GETLST(BUF,ILOGREC,1,1,1,LISTBIAJ)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          IF(IRREPR.GT.IRREPL)THEN
           DO 240 INDXI=1,NUMI
            DO 250 INDXB=1,NUMB
             X=BUF(IPOS)*FAIBJ2
             IPOS=IPOS+1
             INDB=INDXB+POP(IRREPL,1)
             INDI=INDXI
             GINDXL=INDXT(INDI,INDB)
             GINDXR=INDXT(INDJ,INDA)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
250         CONTINUE
240        CONTINUE
          ENDIF
230      CONTINUE
220     CONTINUE
210    CONTINUE
200   CONTINUE
C
      RETURN
      END
