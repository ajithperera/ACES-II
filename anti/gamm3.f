      SUBROUTINE GAMM3(ICORE,MAXCOR,IUHF,ALASKA)
C
C THIS ROUTINE DRIVES THE PROCESSING OF THE ABAB GAMMAS
C
      IMPLICIT INTEGER (A-Z)
      LOGICAL GABCD,DABCD,ALASKA
      LOGICAL TDA,EOM
C
      DIMENSION ICORE(MAXCOR),GOFF(8,8)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
      COMMON /ABCD/  GABCD,DABCD
      COMMON /EXCITE/ TDA,EOM
      COMMON /STATSYM/IRREPX
C
C COMPUTE FULL SIZE OF THE ABAB GAMMA AND OFFSET FOR EACH IRREP
C PAIR
C
      LENGAM=0
      DO 10 IRREPDO=2,NIRREP
       DO 20 IRREP1=1,NIRREP
        IRREP2=DIRPRD(IRREPDO,IRREP1)
        IF(IRREP2.LT.IRREP1)GOTO 20
        GOFF(IRREP2,IRREP1)=LENGAM
        NSIZE=MOPOP(IRREP1)*MOPOP(IRREP2)
        LENGAM=LENGAM+NSIZE*NSIZE
20     CONTINUE
10    CONTINUE
C
C COMPUTE MAXIMUM SIZE REQUIRED FOR T1 AND T2 IN THE
C DIRECT EVALUATION OF G(AB,CD)
C
      IF(DABCD) THEN
C
       LENT=0
       LENSCR=0
C
       DO 1 ISPIN=3-2*IUHF,3
C
        DO 2 IRREP=1,NIRREP
C
         LENT=MAX(LENT,IRPDPD(IRREP,ISYTYP(1,143+ISPIN))*
     &                 IRPDPD(IRREP,ISYTYP(2,143+ISPIN)))
         LENSCR=MAX(LENSCR,IRPDPD(IRREP,ISYTYP(1,143+ISPIN)),
     &                     IRPDPD(IRREP,ISYTYP(2,143+ISPIN)))
C
2       CONTINUE
C
        IF(EOM) THEN
C
         DO 3 IRREPL=1,NIRREP
C
          IRREPR=DIRPRD(IRREPX,IRREPL)
COLD      IRREPR=DIRPRD(IRREPX,IRREPR)
C
          LENT=MAX(LENT,IRPDPD(IRREPL,ISYTYP(1,143+ISPIN))*
     &                  IRPDPD(IRREPR,ISYTYP(2,143+ISPIN)))
          LENSCR=MAX(LENSCR,IRPDPD(IRREPL,ISYTYP(1,143+ISPIN)),
     &                      IRPDPD(IRREPR,ISYTYP(2,143+ISPIN)))
C
3        CONTINUE
C
        ENDIF
C
1      CONTINUE
C
       LENTOTAL=LENGAM+2*(LENT+(1-IUHF)*LENSCR)
C
      ELSE
C
       LENTOTAL=LENGAM
C
      ENDIF
C
C DETERMINE IF THERE IS SUFFICIENT CORE TO DO THIS IN ONE SHOT
C
      IF(LENTOTAL.LE.MAXCOR/IINTFP)THEN
C
       ISTART=1
C
       IF(DABCD) THEN
        I001=ISTART
        I002=I001+LENT*IINTFP
        I003=I002+LENT*IINTFP
        I004=I003+LENSCR*IINTFP*(1-IUHF)
        ISTART=I004+LENSCR*IINTFP*(1-IUHF)
       ENDIF
C
       I000=ISTART
       I010=I000+LENGAM*IINTFP
       IF(IUHF.EQ.0)THEN
        CALL GAM3AB(ICORE(ISTART),ICORE(I010),
     &              ICORE(I001),ICORE(I002),
     &              ICORE(I003),ICORE(I004),GOFF,LENGAM,IUHF,ALASKA)
       ELSEIF(IUHF.NE.0)THEN
        CALL GAM3ABU(ICORE(ISTART),ICORE(I010),
     &               ICORE(I001),ICORE(I002),GOFF,LENGAM,ALASKA)
        CALL GAM3AA(ICORE(ISTART),ICORE(I010),
     &              ICORE(I001),ICORE(I002),GOFF,LENGAM)
       ENDIF
      ELSE
       CALL INSMEM('GAMM3   ',LENTOTAL*IINTFP,MAXCOR)
      ENDIF
      RETURN
      END
