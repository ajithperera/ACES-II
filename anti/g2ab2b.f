      SUBROUTINE G2AB2B(GAMMA,BUF,GOFF,LENGAM,FIJKA)
C
C THIS ROUTINE PROCESSES THE g<Ij,Ka> AND G<Ij,Ak> GAMMAS AND FILLS OUT 
C  THE AABB SYMMETRY TYPE MO GAMMA LIST FOR UHF CALCULATIONS
C      **** (FOR SPIN CASE BETA-BETA-ALPHA-ALPHA) ****
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 
      DOUBLE PRECISION FABIJ,FAIBJ,FABCD,FIJKL,FABCI,FIJKA
      INTEGER GOFF(8,8),IOFFSR(8),IOFFSL(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
C
C FIRST DO LIST 110
C
C         11 22        21 21
C       G(ja,IK) <-- g<Ij,Ka>     
C
C IN ADDITION, THE FOLLOWING SYMMETRIZATION IS NECESSARY
C
C       G(ja,IK) = G(ja,IK) + G(ja,KI)
C      
      LISTIJKA=110
      DO 100 IRREPGET=2,NIRREP
       IOFFR=1
       IOFFL=1
       DO 1100 IRREP1=1,NIRREP
        IRREP2=DIRPRD(IRREP1,IRREPGET)
        IOFFSR(IRREP1)=IOFFR
        IOFFSL(IRREP1)=IOFFL
        IOFFR=IOFFR+POP(IRREP2,1)*VRT(IRREP1,2)
        IOFFL=IOFFL+POP(IRREP2,1)*POP(IRREP1,2)
1100   CONTINUE
       DO 110 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPGET)
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPR)-1
        NUMA=VRT(IRREPR,2)
        NUMK=POP(IRREPL,1)
        NUMJ=POP(IRREPR,2)
        NUMI=POP(IRREPL,1)
        MAXL=NNP1O2(MOPOP(IRREPL))
        MAXR=NNP1O2(MOPOP(IRREPR))
        DO 120 INDXA=1,NUMA
         DO 130 INDXK=1,NUMK
          INDA=INDXA+POP(IRREPR,2)
          INDK=INDXK
          CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTIJKA)
          ILOGREC=ILOGREC+1
          DO 140 INDXJ=1,NUMJ 
           INDJ=INDXJ
           GINDXR=INDXT(INDJ,INDA)
           IF(IRREPL.GT.IRREPR)THEN
            DO 150 INDXI=1,INDXK
             IPOS=IFIRST+INDXF(INDXI,INDXJ,NUMI)
             X=BUF(IPOS)*FIJKA
             INDI=INDXI
             GINDXL=INDXT(INDI,INDK)
             GINDX0=INDXF(GINDXR,GINDXL,MAXR)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X
150         CONTINUE
            DO 151 INDXI=INDXK,NUMI
             IPOS=IFIRST+INDXF(INDXI,INDXJ,NUMI)
             X=BUF(IPOS)*FIJKA
             INDI=INDXI
             GINDXL=INDXT(INDK,INDI)
             GINDX0=INDXF(GINDXR,GINDXL,MAXR)
             GINDX=GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X
151         CONTINUE
           ENDIF
140       CONTINUE
130      CONTINUE
120     CONTINUE
110    CONTINUE
100   CONTINUE
C
C NOW DO LIST 109
C
C         11 22        21 21
C       G(jk,IA) <-- g<Ij,Ak>     
C
C IN ADDITION, THE FOLLOWING SYMMETRIZATION IS NECESSARY
C
C       G(jk,IA) = G(jk,IA) + G(kj,IA)
C      
      LISTIJKA=109
      DO 200 IRREPGET=2,NIRREP
       IOFFR=1
       IOFFL=1
       DO 2100 IRREP1=1,NIRREP
        IRREP2=DIRPRD(IRREP1,IRREPGET)
        IOFFSR(IRREP1)=IOFFR
        IOFFSL(IRREP1)=IOFFL
        IOFFR=IOFFR+VRT(IRREP2,1)*POP(IRREP1,2)
        IOFFL=IOFFL+POP(IRREP2,1)*POP(IRREP1,2)
2100   CONTINUE
       DO 210 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPGET)
        IF(IRREPL.LT.IRREPR)GOTO 210
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPR)-1
        NUMK=POP(IRREPR,2)
        NUMA=VRT(IRREPL,1)
        NUMJ=POP(IRREPR,2)
        NUMI=POP(IRREPL,1)
        MAXL=NNP1O2(MOPOP(IRREPL))
        MAXR=NNP1O2(MOPOP(IRREPR))
        DO 220 INDXK=1,NUMK
         DO 230 INDXA=1,NUMA
          INDA=INDXA+POP(IRREPL,1)
          INDK=INDXK
          CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTIJKA)
          ILOGREC=ILOGREC+1
          DO 240 INDXJ=1,INDXK
           INDJ=INDXJ
           GINDXR=INDXT(INDJ,INDK)
           DO 250 INDXI=1,NUMI
            IPOS=IFIRST+INDXF(INDXI,INDXJ,NUMI)
            X=BUF(IPOS)*FIJKA
            INDI=INDXI
            GINDXL=INDXT(INDI,INDA)
            GINDX0=INDXF(GINDXR,GINDXL,MAXR)
            GINDX =GINDX0+GOFF(IRREPR,IRREPL)
            GAMMA(GINDX)=GAMMA(GINDX)+X
250        CONTINUE
240       CONTINUE
          DO 241 INDXJ=INDXK,NUMJ
           INDJ=INDXJ
           GINDXR=INDXT(INDK,INDJ)
           DO 251 INDXI=1,NUMI
            IPOS=IFIRST+INDXF(INDXI,INDXJ,NUMI)
            X=BUF(IPOS)*FIJKA
            INDI=INDXI
            GINDXL=INDXT(INDI,INDA)
            GINDX0=INDXF(GINDXR,GINDXL,MAXR)
            GINDX =GINDX0+GOFF(IRREPR,IRREPL)
            GAMMA(GINDX)=GAMMA(GINDX)+X
251        CONTINUE
241       CONTINUE
230      CONTINUE
220     CONTINUE
210    CONTINUE
200   CONTINUE
      RETURN
      END
