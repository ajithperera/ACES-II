      SUBROUTINE G3AB3A(GAMMA,BUF,GOFF,LENGAM,FABIJ)
C
C THIS ROUTINE PROCESSES THE G(Ab,Ij) GAMMA LISTS FOR SPIN CASE
C  AABB AND FILLS THE SYMMETRY CASE ABAB MULLIKEN ORDERED GAMMA LIST.
C  THIS IS THE VERSION USED FOR UHF CALCULATIONS.
C
C NOTE THAT ABAB IN MULLIKEN CORRESPONDS TO AABB IN DIRAC.
C
C THIS ROUTINE FILLS OUT THE FOLLOWING PIECES
C
C       21 21        22 11
C     G(AI,bj) <-- g<Ab,Ij>
C
C       21 21        11 22
C     G(IA,jb) <-- g<Ab,Ij>
C
C       21 21        12 21 
C     G(IA,bj) <-- g<Ab,Ij>
C
C       21 21        21 12
C     G(AI,jb) <-- g<Ab,Ij>
C
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 
      DOUBLE PRECISION FABIJ,FAIBJ,FABCD,FIJKL,FABCI,FIJKA
      INTEGER GOFF(8,8),IOFFSL(8),IOFFSR(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
C
C FIRST PROCESS THE IRREP 1 G(Ab,Ij) LIST.  COMPUTE OFFSETS TO FIRST ELEMENT
C   OF ALL G(XX,YY) IRREP POSITIONS
C
      LISTABIJ=116
      IOFFR=1
      IOFFL=1
      DO 1000 IRREPR=1,NIRREP
       IOFFSR(IRREPR)=IOFFR
       IOFFR=IOFFR+POP(IRREPR,1)*POP(IRREPR,2)
1000  CONTINUE
      DO 1010 IRREPL=1,NIRREP
       IOFFSL(IRREPL)=IOFFL
       IOFFL=IOFFL+VRT(IRREPL,1)*VRT(IRREPL,2)
1010  CONTINUE
      DO 100 IRREPDO=2,NIRREP
       DO 110 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPL)
        NUMJ=POP(IRREPR,2)
        NUMI=POP(IRREPR,1)
        NUMB=VRT(IRREPL,2)
        NUMA=VRT(IRREPL,1)
        MAXL=MOPOP(IRREPL)*MOPOP(IRREPR)
        OCCLA=POP(IRREPL,1)
        OCCLB=POP(IRREPL,2)
        MAX1=MOPOP(IRREPL)
        MAX2=MOPOP(IRREPR)
        DO 120 INDXJ=1,NUMJ
         DO 130 INDXI=1,NUMI
          INDI=INDXI
          INDJ=INDXJ
          CALL GETLST(BUF,ILOGREC,1,1,1,LISTABIJ)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          IF(IRREPL.GT.IRREPR)THEN
           DO 140 INDXB=1,NUMB
            DO 150 INDXA=1,NUMA
             X=BUF(IPOS)*FABIJ
             IPOS=IPOS+1
             INDA=INDXA+OCCLA
             INDB=INDXB+OCCLB
             GINDXL=INDXF(INDA,INDI,MAX1)
             GINDXR=INDXF(INDB,INDJ,MAX1)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=X
150         CONTINUE
140        CONTINUE
          ELSE
           DO 141 INDXB=1,NUMB
            DO 151 INDXA=1,NUMA
             X=BUF(IPOS)*FABIJ
             IPOS=IPOS+1
             INDA=INDXA+OCCLA
             INDB=INDXB+OCCLB
             GINDXL=INDXF(INDI,INDA,MAX2)
             GINDXR=INDXF(INDJ,INDB,MAX2)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX=GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=X
151         CONTINUE
141        CONTINUE
          ENDIF
130      CONTINUE
120     CONTINUE
110    CONTINUE
100   CONTINUE
C
C NOW PICK UP OTHER LOGICAL RECORDS AND CONTINUE TO FILL OUT GAMMA
C
      DO 160 IRREPDO=2,NIRREP
C
C COMPUTE OFFSETS TO ALL BA AB POSITIONS
C
       IOFFR=1
       IOFFL=1
       DO 1200 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPDO)
        IOFFSR(IRREPR)=IOFFR
        IOFFSL(IRREPR)=IOFFL
        IOFFR=IOFFR+POP(IRREPL,1)*POP(IRREPR,2)
        IOFFL=IOFFL+VRT(IRREPL,1)*VRT(IRREPR,2)
1200   CONTINUE
       DO 170 IRREPB=1,NIRREP
        IRREPA=DIRPRD(IRREPB,IRREPDO)
        ILOGREC=IOFFSR(IRREPB)
        IFIRST =IOFFSL(IRREPA)
        NUMJ=POP(IRREPB,2)
        NUMI=POP(IRREPA,1)
        NUMB=VRT(IRREPA,2)
        NUMA=VRT(IRREPB,1)
        MAXL=MOPOP(IRREPA)*MOPOP(IRREPB)
        OCCLA=POP(IRREPA,1)
        OCCLB=POP(IRREPB,2)
        MAX1A=MOPOP(IRREPA)
        MAX1B=MOPOP(IRREPB)
        DO 180 INDXJ=1,NUMJ
         DO 185 INDXI=1,NUMI
          INDI=INDXI
          INDJ=INDXJ
          CALL GETLST(BUF,ILOGREC,1,1,IRREPDO,LISTABIJ)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          IF(IRREPA.GT.IRREPB)THEN
           DO 190 INDXB=1,NUMB
            DO 195 INDXA=1,NUMA
             X=BUF(IPOS)*FABIJ
             IPOS=IPOS+1
             INDA=INDXA+POP(IRREPB,1)
             INDB=INDXB+POP(IRREPA,2)
             GINDXL=INDXF(INDI,INDA,MAX1A)
             GINDXR=INDXF(INDB,INDJ,MAX1A)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPA,IRREPB)
             GAMMA(GINDX)=X
195         CONTINUE
190        CONTINUE
          ELSE
           DO 191 INDXB=1,NUMB
            DO 196 INDXA=1,NUMA
             X=BUF(IPOS)*FABIJ
             IPOS=IPOS+1
             INDA=INDXA+POP(IRREPB,1)
             INDB=INDXB+POP(IRREPA,2)
             GINDXL=INDXF(INDA,INDI,MAX1B)
             GINDXR=INDXF(INDJ,INDB,MAX1B)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX=GINDX0+GOFF(IRREPB,IRREPA)
             GAMMA(GINDX)=X
196         CONTINUE
191        CONTINUE
          ENDIF
185      CONTINUE
180     CONTINUE
170    CONTINUE
160   CONTINUE
      RETURN
      END
