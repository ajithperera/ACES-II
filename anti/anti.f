
C THIS PROGRAM DOES ONLY ONE TASK -  IT READS THE GAMLAM FILE AND
C  PRODUCES SYMMETRY BLOCKED MO GAMMA LISTS WHICH ARE THEN WRITTEN
C  TO A REINITIALIZED MOINTS FILE.  THE SYMMETRY BLOCKING DOES NOT
C  FOLLOW THE USUAL DPD PRESCRIPTION, BUT IS RATHER LIKE THAT USED
C  IN THE INTEGRAL TRANSFORMATION PROGRAM.  FOLLOWING EXECUTION OF
C  THIS PROGRAM, THE BACK-TRANSFORMATION OF THE GAMMAS CAN PROCEED
C  IN A HIGHLY EFFICIENT AND VECTORIZED FASHION
C
C CODED BY J.F. STANTON AND J. GAUSS, GAINESVILLE, NOVEMBER 1990.

      PROGRAM ANTI
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION FABIJ,FABCD,FIJKL,FAIBJ,FABCI,FIJKA
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      LOGICAL GABCD,DABCD,TDA,EOM,ALASKA
      COMMON / / ICORE(1)
      COMMON /ISTART/ I0,ICRSIZ
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /FLAGS/ IFLAGS(100)
      COMMON /INFO/  NOCCO(2),NVRTO(2)
      COMMON /ABCD/  GABCD,DABCD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
      COMMON /AOOFST/ INDOCC(8,2),INDVRT(8,2)
      COMMON /FACTORS/ FABIJ,FABCD,FIJKL,FAIBJ,FABCI,FIJKA
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /EXCITE/ TDA,EOM
C
      NNP1O2(I)=(I*(I+1))/2
C
      CALL CRAPSI(ICORE,IUHF,0)
      MAXCOR=ICRSIZ
      CALL SETMET
      CALL FMOPOP(POP,VRT,MOPOP,NIRREP)
C
C CALL ROUTINE TO COPY ANY USEFUL INFORMATION OFF EXISTING MOINTS
C  FILE AND THEN DELETE IT.
C
      CALL ENDMOI(ICORE(I0),MAXCOR,IUHF)
      IF(IUHF.EQ.0)CALL SPAD(ICORE(I0),MAXCOR,IUHF)
C
C SPIN ADAPT LISTS FOR RHF
C
C NOW CALL ROUTINE TO CREATE BRAND NEW MOINTS FILE AND CREATE LISTS
C Check to see whether we are doing a gradient calculation using ALASKA. For
C ALASKA gradient calculations, MO gammas need only to be backtransformed to AO
C basis (not to Cartesian AO basis), 02/2002 Ajith Perera.
C
      ALASKA = (IFLAGS(56).EQ.4)
      CALL SETLST(IUHF,ALASKA)
      IF (IFLAGS(35).NE.0) CALL INCOR(I0,ICRSIZ,IUHF)
      MAXCOR=ICRSIZ
C
C NOW THE REAL CODE BEGINS.  CALL ROUTINES TO WRITE SORTED MO
C  GAMMAS TO NEW MOINTS FILE
C
      FABIJ=1.0D0
      FIJKL=1.0D0
      FABCD=1.0D0
      FAIBJ=1.0D0
      FABCI=1.0D0
      FIJKA=1.0D0
      CALL GAMM1(ICORE(I0),MAXCOR,IUHF,ALASKA)
      IF(NIRREP.GE.2)THEN
       CALL GAMM2(ICORE(I0),MAXCOR,IUHF,ALASKA)
       CALL GAMM3(ICORE(I0),MAXCOR,IUHF,ALASKA)
       IF(NIRREP.GE.4)CALL GAMM4(ICORE(I0),MAXCOR,IUHF,ALASKA)
      ENDIF
C
      IF (IFLAGS(35).NE.0) THEN
         CALL ACES_AUXCACHE_FLUSH
         CALL ACES_AUXCACHE_RESET
      END IF
      IF(.NOT.GABCD)CALL ACES_IO_REMOVE(52,'MOABCD')
C SG 3/96
      IF (EOM) CALL ACES_IO_REMOVE(54,'DERGAM')
C
      CALL aces_fin
      STOP
      END
