      SUBROUTINE GAMM2(ICORE,MAXCOR,IUHF,ALASKA)
C
C THIS ROUTINE DRIVES THE PROCESSING OF THE AABB GAMMAS
C
      IMPLICIT INTEGER (A-Z)
      LOGICAL GABCD,DABCD,ALASKA
      LOGICAL TDA,EOM
C
      DIMENSION ICORE(MAXCOR),GOFF(8,8)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
      COMMON /ABCD/  GABCD,DABCD
      COMMON /EXCITE/ TDA,EOM
      COMMON /STATSYM/IRREPX
C
      NNP1O2(I)=(I*(I+1))/2
C
C COMPUTE FULL SIZE OF THE AABB GAMMA AND OFFSET FOR EACH IRREP
C  PAIR
C
      LENGAM=0
      DO 10 IRREP1=2,NIRREP
       DO 20 IRREP2=1,IRREP1-1
        GOFF(IRREP2,IRREP1)=LENGAM
        NSIZE=NNP1O2(MOPOP(IRREP1))*NNP1O2(MOPOP(IRREP2))
        LENGAM=LENGAM+NSIZE
20     CONTINUE
10    CONTINUE
C
C COMPUTE MAXIMUM SIZE REQUIRED FOR T1 AND T2 IN THE
C DIRECT EVALUATION OF G(Ab,CD)
C
      IF(DABCD) THEN
C
       LENT=0
       LENSCR=0
       DO 1 ISPIN=3-2*IUHF,3
C
        DO 2 IRREP=2,NIRREP
C
         LENT=MAX(LENT,IRPDPD(IRREP,ISYTYP(1,143+ISPIN))*
     &                 IRPDPD(IRREP,ISYTYP(2,143+ISPIN)))
         LENSCR=MAX(LENSCR,IRPDPD(IRREP,ISYTYP(1,143+ISPIN)),
     &                     IRPDPD(IRREP,ISYTYP(2,143+ISPIN)))
C
2       CONTINUE
C
        IF(EOM) THEN
C
         DO 3 IRREPL=2,NIRREP 
C
          IRREPR=DIRPRD(IRREPL,IRREPX)
          LENT=MAX(LENT,IRPDPD(IRREPL,ISYTYP(1,143+ISPIN))*
     &                  IRPDPD(IRREPR,ISYTYP(2,143+ISPIN)))
          LENSCR=MAX(LENSCR,IRPDPD(IRREPL,ISYTYP(1,143+ISPIN)),
     &                      IRPDPD(IRREPR,ISYTYP(2,143+ISPIN)))
C
3        CONTINUE
C
        ENDIF
C
1      CONTINUE
C
       LENTOTAL=LENGAM+2*(LENT+(1-IUHF)*LENSCR)
C
      ELSE
C
       LENTOTAL=LENGAM
C
      ENDIF
C
C DETERMINE IF THERE IS SUFFICIENT CORE TO DO THIS IN ONE SHOT
C
      IF(LENTOTAL.LE.MAXCOR/IINTFP)THEN
C
       ISTART=1
C
       IF(DABCD) THEN
        I001=ISTART
        I002=I001+IINTFP*LENT 
        I003=I002+IINTFP*LENT
        I004=I003+IINTFP*LENSCR*(1-IUHF)
        ISTART=I004+IINTFP*LENSCR*(1-IUHF)
       ENDIF
C
       I000=ISTART
       I010=I000+LENGAM*IINTFP
       IF(IUHF.EQ.0)THEN
        CALL GAM2AB(ICORE(ISTART),ICORE(I010),
     &              ICORE(I001),ICORE(I002),
     &              ICORE(I003),ICORE(I004),GOFF,LENGAM,IUHF,ALASKA)
       ELSEIF(IUHF.NE.0)THEN
        CALL GAM2ABU(ICORE(ISTART),ICORE(I010),
     &               ICORE(I001),ICORE(I002),GOFF,LENGAM,ALASKA)
        CALL GAM2AA(ICORE(ISTART),ICORE(I010),
     &              ICORE(I001),ICORE(I002),GOFF,LENGAM)
       ENDIF
      ELSE
       CALL INSMEM('GAMM2   ',I010,MAXCOR)
      ENDIF
      RETURN
      END
