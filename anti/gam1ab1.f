      SUBROUTINE GAM1AB1(GAMMA,BUF,BUF1,BUF2,SCR1,SCR2,
     &                  LENGAM,IRREPDO,LIST1,LIST2,IUHF)
C
C THIS ROUTINE FORMS THE ABAB SYMMETRY TYPE AO GAMMA LISTS
C  FOR SPIN CASE AB AND WRITES THEM TO THE BACKTRANSFORMATION
C  MOINTS FILE FOR A PARTICULAR IRREP. THIS IS THE INCORE VERSION!
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(*),X,ONE,TWO
      DOUBLE PRECISION BUF1(*),BUF2(*),SCR1(*),SCR2(*)
      DOUBLE PRECISION FABIJ,FAIBJ,FABCD,FIJKL,FABCI,FIJKA
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      LOGICAL MBPT4
      LOGICAL ROHF,MOEQAO
      LOGICAL GABCD,DABCD
      LOGICAL TDA,EOM
C
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
      COMMON /FACTORS/ FABIJ,FABCD,FIJKL,FAIBJ,FABCI,FIJKA
      COMMON /REF/ ROHF
      COMMON /SIZES/ MOEQAO
      COMMON /IABCD/ IOFFABCD
      COMMON /ABCD/ GABCD,DABCD
      COMMON /EXCITE/ TDA,EOM
      COMMON /STATSYM/IRREPX
C
      DATA ONE,TWO /1.D0,2.D0/
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
C
      IF(TDA)GOTO 3000
C
C FIRST PROCESS THE G(Ab,Ij) LIST
C
      CALL SYMPOS('F',POP(1,1),POP(1,2),IRREPDO,1,ILOGREC)
      CALL SYMPOS('F',VRT(1,1),VRT(1,2),IRREPDO,1,ITHRU1)
      LISTABIJ=116
      NUMJ=POP(IRREPDO,2)
      NUMI=POP(IRREPDO,1)
      NUMB=VRT(IRREPDO,2)
      NUMA=VRT(IRREPDO,1)
      NUMAO=MOPOP(IRREPDO)
      MAXL=NNP1O2(NUMAO)
      DO 101 INDXJ=1,NUMJ
       DO 102 INDXI=1,NUMI
        ILOGREC=ILOGREC+1
        INDI=INDXI
        INDJ=INDXJ
        CALL GETLST(BUF,ILOGREC,1,1,1,LISTABIJ)
        ITHRU=ITHRU1
        DO 103 INDXB=1,NUMB
         INDB=INDXB+NUMJ
         DO 104 INDXA=1,NUMA
          ITHRU=ITHRU+1
          X=BUF(ITHRU)*FABIJ
          INDA=INDXA+NUMI
          GINDXL=INDXT(INDI,INDA)
          GINDXR=INDXT(INDJ,INDB)
          GINDX0=INDXF(GINDXL,GINDXR,MAXL)
          GINDX =GINDX0
          GAMMA(GINDX)=X
104      CONTINUE
103     CONTINUE
102    CONTINUE
101   CONTINUE
      IF(MBPT2)GOTO 2000
C
 3000 continue
      IF(IUHF.EQ.0) THEN
C
C NOW PROCESS THE G(Ai,Bj) LIST.  VALUES ARE PUT
C  IN TWO PLACES IN SQUARE CANONICAL LIST [G(AB,ij) AND G(ij,AB)]
C  FOR RHF.
C
      CALL SYMPOS('F',VRT(1,1),POP(1,2),IRREPDO,1,ILOGREC)
      CALL SYMPOS('F',VRT(1,1),POP(1,2),IRREPDO,1,IOFFIR)
      NUMJ=POP(IRREPDO,2)
      NUMB=VRT(IRREPDO,1)
      NUMI=POP(IRREPDO,2)
      NUMA=VRT(IRREPDO,1)
      OCCA=POP(IRREPDO,1)
      OCCB=POP(IRREPDO,2)
      NUMAO=MOPOP(IRREPDO)
      MAXL=NNP1O2(NUMAO)
      DO 201 INDXJ=1,NUMJ
       DO 202 INDXB=1,NUMB
        ILOGREC=ILOGREC+1
        INDB=INDXB+OCCA
        INDJ=INDXJ
        CALL GETLST(BUF,ILOGREC,1,1,1,125)
        DO 203 INDXI=1,INDXJ
         INDI=INDXI
         GINDXR=INDXT(INDI,INDJ)
         DO 204 INDXA=1,INDXB
          IPOS=INDXF(INDXA,INDXI,NUMA)+IOFFIR
          X=BUF(IPOS)*FAIBJ
          INDA=INDXA+OCCA
          GINDXL=INDXT(INDA,INDB)
          GINDX1=INDXF(GINDXL,GINDXR,MAXL)
          GINDX =GINDX1
          GAMMA(GINDX)=GAMMA(GINDX)+X
          GINDX2=INDXF(GINDXR,GINDXL,MAXL)
          GINDX =GINDX2
          GAMMA(GINDX)=GAMMA(GINDX)+X
204      CONTINUE
         DO 205 INDXA=INDXB,NUMA
          IPOS=INDXF(INDXA,INDXI,NUMA)+IOFFIR
          X=BUF(IPOS)*FAIBJ
          INDA=INDXA+OCCA
          GINDXL=INDXT(INDB,INDA)
          GINDX1=INDXF(GINDXL,GINDXR,MAXL)
          GINDX =GINDX1
          GAMMA(GINDX)=GAMMA(GINDX)+X
          GINDX2=INDXF(GINDXR,GINDXL,MAXL)
          GINDX =GINDX2
          GAMMA(GINDX)=GAMMA(GINDX)+X
205      CONTINUE
203     CONTINUE
202    CONTINUE
201   CONTINUE
C
      ELSE
C
C NOW PROCESS THE G(Ai,Bj).  VALUES ARE PUT
C  IN G(AB,ij) 
C
C  UHF VERSION
C
      CALL SYMPOS('F',VRT(1,1),POP(1,2),IRREPDO,1,ILOGREC)
      CALL SYMPOS('F',VRT(1,1),POP(1,2),IRREPDO,1,IOFFIR)
       NUMJ=POP(IRREPDO,2)
       NUMB=VRT(IRREPDO,1)
       NUMI=POP(IRREPDO,2)
       NUMA=VRT(IRREPDO,1)
       OCCA=POP(IRREPDO,1)
       OCCB=POP(IRREPDO,2)
       NUMAO=MOPOP(IRREPDO)
       MAXL=NNP1O2(NUMAO)
       DO 1201 INDXJ=1,NUMJ
        DO 1202 INDXB=1,NUMB
         ILOGREC=ILOGREC+1
         INDB=INDXB+OCCA
         INDJ=INDXJ
         CALL GETLST(BUF,ILOGREC,1,1,1,125)
         DO 1203 INDXI=1,INDXJ
          INDI=INDXI
          GINDXR=INDXT(INDI,INDJ)
          DO 1204 INDXA=1,INDXB
           IPOS=INDXF(INDXA,INDXI,NUMA)+IOFFIR
           X=BUF(IPOS)*FAIBJ
           INDA=INDXA+OCCA
           GINDXL=INDXT(INDA,INDB)
           GINDX1=INDXF(GINDXL,GINDXR,MAXL)
           GINDX =GINDX1
           GAMMA(GINDX)=GAMMA(GINDX)+X
1204       CONTINUE
          DO 1205 INDXA=INDXB,NUMA
           IPOS=INDXF(INDXA,INDXI,NUMA)+IOFFIR
           X=BUF(IPOS)*FAIBJ
           INDA=INDXA+OCCA
           GINDXL=INDXT(INDB,INDA)
           GINDX1=INDXF(GINDXL,GINDXR,MAXL)
           GINDX =GINDX1
           GAMMA(GINDX)=GAMMA(GINDX)+X
1205       CONTINUE
1203      CONTINUE
1202     CONTINUE
1201    CONTINUE
C
C NOW PROCESS THE G(aI,bJ) LIST.  VALUES ARE PUT
C  IN G(IJ,ab).
C
C  UHF VERSION
C
      CALL SYMPOS('F',POP(1,1),VRT(1,2),IRREPDO,1,ILOGREC)
      CALL SYMPOS('F',POP(1,1),VRT(1,2),IRREPDO,1,IOFFIR)
       NUMJ=POP(IRREPDO,1)
       NUMB=VRT(IRREPDO,2)
       NUMI=POP(IRREPDO,1)
       NUMA=VRT(IRREPDO,2)
       OCCA=POP(IRREPDO,2)
       OCCB=POP(IRREPDO,1)
       NUMAO=MOPOP(IRREPDO)
       MAXL=NNP1O2(NUMAO)
       DO 2201 INDXJ=1,NUMJ
        DO 2202 INDXB=1,NUMB
         ILOGREC=ILOGREC+1
         INDB=INDXB+OCCA
         INDJ=INDXJ
         CALL GETLST(BUF,ILOGREC,1,1,1,126)
         DO 2203 INDXI=1,INDXJ
          INDI=INDXI
          GINDXR=INDXT(INDI,INDJ)
          DO 2204 INDXA=1,INDXB
           IPOS=INDXF(INDXA,INDXI,NUMA)+IOFFIR
           X=BUF(IPOS)*FAIBJ
           INDA=INDXA+OCCA
           GINDXL=INDXT(INDA,INDB)
           GINDX2=INDXF(GINDXR,GINDXL,MAXL)
           GINDX =GINDX2
           GAMMA(GINDX)=GAMMA(GINDX)+X
2204       CONTINUE
          DO 2205 INDXA=INDXB,NUMA
           IPOS=INDXF(INDXA,INDXI,NUMA)+IOFFIR
           X=BUF(IPOS)*FAIBJ
           INDA=INDXA+OCCA
           GINDXL=INDXT(INDB,INDA)
           GINDX2=INDXF(GINDXR,GINDXL,MAXL)
           GINDX =GINDX2
           GAMMA(GINDX)=GAMMA(GINDX)+X
2205       CONTINUE
2203      CONTINUE
2202     CONTINUE
2201    CONTINUE
C
      ENDIF
C
C NOW PROCESS THE G(BI,aj) LIST.  THESE ARE RESORTED ACCORDING
C  TO A LIST 21 TYPE STORAGE IN THE DENSITY CODE
C
      IF(IUHF.EQ.0) THEN
      CALL SYMPOS('F',POP(1,2),VRT(1,2),IRREPDO,1,ILOGREC)
      CALL SYMPOS('F',POP(1,1),VRT(1,1),IRREPDO,1,IOFFIR)
       NUMI=POP(IRREPDO,1)
       NUMB=VRT(IRREPDO,1)
       NUMJ=POP(IRREPDO,2)
       NUMA=VRT(IRREPDO,2)
       OCCA=POP(IRREPDO,1)
       OCCB=POP(IRREPDO,2)
       NUMAO=MOPOP(IRREPDO)
       MAXL=NNP1O2(NUMAO)
       DO 211 INDXJ=1,NUMJ
        DO 212 INDXA=1,NUMA
         ILOGREC=ILOGREC+1
         INDA=INDXA+OCCB
         INDJ=INDXJ
         CALL GETLST(BUF,ILOGREC,1,1,1,118)
         DO 213 INDXI=1,NUMI
          DO 214 INDXB=1,NUMB
           IPOS=INDXF(INDXB,INDXI,NUMB)+IOFFIR
           X=BUF(IPOS)*FAIBJ
           INDB=INDXB+OCCB
           INDI=INDXI
           GINDXL=INDXT(INDI,INDB)
           GINDXR=INDXT(INDJ,INDA)
           GINDX0=INDXF(GINDXL,GINDXR,MAXL)
           GINDX =GINDX0
           GAMMA(GINDX)=GAMMA(GINDX)+X
214       CONTINUE
213      CONTINUE
212     CONTINUE
211    CONTINUE
C
      ELSE
C
C  NOW THE UHF CASE, LIST 118 IS NOT RESORTED
C  WELL, ACTUALLY WE SEE AT THE MOMENT NOW REASON WHY TO DO 
C  IT AND FINALLY LIST 118 SHOULD BE KEPT IN THE ORIGINAL
C  ORDER FOR RHF TOO
C
      CALL SYMPOS('F',POP(1,1),VRT(1,2),IRREPDO,1,ILOGREC)
      CALL SYMPOS('F',POP(1,2),VRT(1,1),IRREPDO,1,IOFFIR)
       NUMI=POP(IRREPDO,1)
       NUMB=VRT(IRREPDO,1)
       NUMJ=POP(IRREPDO,2)
       NUMA=VRT(IRREPDO,2)
       OCCA=POP(IRREPDO,1)
       OCCB=POP(IRREPDO,2)
       NUMAO=MOPOP(IRREPDO)
       MAXL=NNP1O2(NUMAO)
       DO 221 INDXI=1,NUMI
        DO 222 INDXA=1,NUMA
         ILOGREC=ILOGREC+1
         INDA=INDXA+OCCB
         INDI=INDXI
         CALL GETLST(BUF,ILOGREC,1,1,1,118)
         DO 223 INDXJ=1,NUMJ
          INDJ=INDXJ
          DO 224 INDXB=1,NUMB
           IPOS=INDXF(INDXB,INDXJ,NUMB)+IOFFIR
           X=BUF(IPOS)*FAIBJ
           INDB=INDXB+OCCA
           GINDXL=INDXT(INDI,INDB)
           GINDXR=INDXT(INDJ,INDA)
           GINDX0=INDXF(GINDXL,GINDXR,MAXL)
           GINDX =GINDX0
           GAMMA(GINDX)=GAMMA(GINDX)+X
224       CONTINUE
223      CONTINUE
222     CONTINUE
221    CONTINUE
C
      ENDIF
C
      IF(TDA)GOTO 2000
C
C NOW PROCESS THE G(Ij,Kl) LIST 
C
      CALL SYMPOS('F',POP(1,1),POP(1,2),IRREPDO,1,ILOGREC)
      CALL SYMPOS('F',POP(1,1),POP(1,2),IRREPDO,1,IOFFIR)
       NUML=POP(IRREPDO,2)
       NUMK=POP(IRREPDO,1)
       NUMJ=POP(IRREPDO,2)
       NUMI=POP(IRREPDO,1)
       NUMAO=MOPOP(IRREPDO)
       MAXL=NNP1O2(NUMAO)
       DO 301 INDXL=1,NUML
        DO 302 INDXK=1,NUMK
         ILOGREC=ILOGREC+1
         INDL=INDXL
         INDK=INDXK
         CALL GETLST(BUF,ILOGREC,1,1,1,113)
         DO 303 INDXJ=1,INDXL
          INDJ=INDXJ
          GINDXR=INDXT(INDJ,INDL)
          DO 304 INDXI=1,INDXK
           IPOS=INDXF(INDXI,INDXJ,NUMI)+IOFFIR
           X=BUF(IPOS)*FIJKL
           INDI=INDXI
           GINDXL=INDXT(INDI,INDK)
           GINDX0=INDXF(GINDXL,GINDXR,MAXL)
           GINDX =GINDX0
           GAMMA(GINDX)=GAMMA(GINDX)+X
304       CONTINUE
          DO 305 INDXI=INDXK,NUMI
           IPOS=INDXF(INDXI,INDXJ,NUMI)+IOFFIR
           X=BUF(IPOS)*FIJKL
           INDI=INDXI
           GINDXL=INDXT(INDK,INDI)
           GINDX0=INDXF(GINDXL,GINDXR,MAXL)
           GINDX =GINDX0
           GAMMA(GINDX)=GAMMA(GINDX)+X 
305       CONTINUE
303      CONTINUE
302     CONTINUE
301    CONTINUE
C
C NOW PROCESS THE G(Ab,Cd) LIST
C
      NPASS=1
      IF(EOM.AND.DABCD) NPASS=2
C
      DO 410 IPASS=1,NPASS
C
       IF(DABCD) THEN
C
        IF(IPASS.EQ.1) THEN
C 
         NUMDSZ=IRPDPD(1,ISYTYP(2,146))
         DISDSZ=IRPDPD(1,ISYTYP(1,146))
C 
         I0TA=1
         I0TB=I0TA+NT(1)*IUHF
         CALL GETLST(BUF1,1,NUMDSZ,1,1,156)
         CALL GETLST(BUF2,1,NUMDSZ,1,1,146)
         IF(CCSD) THEN
          CALL GETLST(BUF(I0TA),1,1,1,1,157)
          IF(IUHF.NE.0 )CALL GETLST(BUF(I0TB),1,1,1,2,157)
          CALL FTAU(BUF1,BUF(I0TA),BUF(I0TB),DISDSZ,
     &              NUMDSZ,POP(1,1),POP(1,2),VRT(1,1),VRT(1,2),
     &              1,3,ONE)
         ENDIF
         MBPT4=M4DQ.OR.M4SDQ.OR.M4SDTQ
         IF(MBPT4) THEN
          CALL SSCAL(NUMDSZ*DISDSZ,TWO,BUF2,1)
          CALL SAXPY(NUMDSZ*DISDSZ,ONE,BUF1,1,BUf2,1)
         ENDIF
C
        ELSE
C
         NUMDSZ=IRPDPD(IRREPX,ISYTYP(2,146))
         DISDSZ=IRPDPD(1,ISYTYP(1,146))
C 
         I0TA=1
         I0TB=I0TA+NT(1)*IUHF
         I0RA=I0TB+NT(2)
         I0RB=I0RA+IRPDPD(IRREPX,9)*IUHF
C
         CALL GETLST(BUF1,1,NUMDSZ,1,IRREPX,463)
         CALL GETLST(BUF2,1,NUMDSZ,1,IRREPX,446)
C
         CALL GETLST(BUF(I0TA),1,1,1,1,157)
         CALL GETLST(BUF(I0RA),1,1,1,3,490)
         IF(IUHF.EQ.1) THEN
          CALL GETLST(BUF(I0TB),1,1,1,2,157)
          CALL GETLST(BUF(I0RB),1,1,1,4,490)
         ENDIF
C 
         CALL DTAU(1,IRREPX,1,IRREPX,BUF1,
     &             BUF(I0TA),BUF(I0TB),BUF(I0RA),BUF(I0RB),
     &             3,ONE)
C
        ENDIF
C
       ENDIF
C
       CALL SYMPOS('F',VRT(1,1),VRT(1,2),IRREPDO,1,ILOGREC)
       CALL SYMPOS('F',VRT(1,1),VRT(1,2),IRREPDO,1,IOFFIR)
C
       NUMD=VRT(IRREPDO,2)
       NUMC=VRT(IRREPDO,1)
       NUMB=VRT(IRREPDO,2)
       NUMA=VRT(IRREPDO,1)
       OCCA=POP(IRREPDO,1)
       OCCB=POP(IRREPDO,2)
       NUMAO=MOPOP(IRREPDO)
       MAXL=NNP1O2(NUMAO)
       DO 401 INDXD=1,NUMD
        DO 402 INDXC=1,NUMC
         ILOGREC=ILOGREC+1
         INDD=INDXD+OCCB
         INDC=INDXC+OCCA
         IF(.NOT.DABCD) THEN
C
C READ G(AB,CD) FROM DISK
C
          CALL GETLST(BUF,ILOGREC,1,1,1,133+IOFFABCD)
C
         ELSE
C
C CALCULATE G(AB,CD) ON THE FLY
C
          CALL DIRG2(BUF,BUF1,BUF2,SCR1,SCR2,NUMDSZ,DISDSZ,
     &               ILOGREC,1,IUHF)
C
         ENDIF
         DO 403 INDXB=1,INDXD
          INDB=INDXB+OCCB
          GINDXR=INDXT(INDB,INDD)
          DO 404 INDXA=1,INDXC
           IPOS=INDXF(INDXA,INDXB,NUMA)+IOFFIR
           X=BUF(IPOS)*FABCD
           INDA=INDXA+OCCA
           GINDXL=INDXT(INDA,INDC)
           GINDX0=INDXF(GINDXL,GINDXR,MAXL)
           GINDX =GINDX0
           GAMMA(GINDX)=GAMMA(GINDX)+X
404       CONTINUE
          DO 405 INDXA=INDXC,NUMA
           IPOS=INDXF(INDXA,INDXB,NUMA)+IOFFIR
           X=BUF(IPOS)*FABCD
           INDA=INDXA+OCCA
           GINDXL=INDXT(INDC,INDA)
           GINDX0=INDXF(GINDXL,GINDXR,MAXL)
           GINDX =GINDX0
           GAMMA(GINDX)=GAMMA(GINDX)+X
405       CONTINUE
403      CONTINUE
402     CONTINUE
401    CONTINUE
410   CONTINUE
C
 2000 continue
C     
      RETURN
      END
