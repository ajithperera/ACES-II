      SUBROUTINE G2AA3(GAMMA,BUF,GOFF,LENGAM,ISPIN,FABIJ)
C
C THIS ROUTINE PROCESSES THE G(A<B,I<J) GAMMAS AND AUGMENTS THE 
C  AABB SYMMETRY TYPE MULLIKEN ORDERED MO GAMMA LIST.
C
C NOTE THAT AABB IN MULLIKEN CORRESPONDS TO ABAB IN DIRAC.  CONSEQUENTLY,
C  ALL ELEMENTS OF GAMMA WHICH ARE PROCESSED HERE COME FROM DPD LISTS
C  FOR IRREPS 2-h, SINCE A.NE.B.  NOTE THAT THE LISTS FOR IRREPS 2-h
C  ARE NOT REALLY TRIANGULAR SINCE THE CASES I=J AND K=L DO NOT
C  OCCUR.
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 
      DOUBLE PRECISION FABIJ,FAIBJ,FABCD,FIJKL,FABCI,FIJKA
      INTEGER GOFF(8,8),IOFFSR(8),IOFFSL(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
C
      LISTABIJ=113+ISPIN
      DO 100 IRREPGET=2,NIRREP
       IOFFR=1
       IOFFL=1
       DO 1100 IRREP1=2,NIRREP
        IRREP2=DIRPRD(IRREP1,IRREPGET)
        IF(IRREP2.GT.IRREP1)GOTO 1100
        IOFFSR(IRREP1)=IOFFR
        IOFFSL(IRREP1)=IOFFL
        IOFFR=IOFFR+POP(IRREP2,ISPIN)*POP(IRREP1,ISPIN)
        IOFFL=IOFFL+VRT(IRREP2,ISPIN)*VRT(IRREP1,ISPIN)
1100   CONTINUE
       DO 110 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPGET)
        IF(IRREPL.GT.IRREPR)GOTO 110
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPR)
        NUMJ=POP(IRREPR,ISPIN)
        NUMI=POP(IRREPL,ISPIN)
        NUMB=VRT(IRREPR,ISPIN)
        NUMA=VRT(IRREPL,ISPIN)
        MAXL=NNP1O2(MOPOP(IRREPL))
        DO 120 INDXJ=1,NUMJ
         DO 130 INDXI=1,NUMI
          INDI=INDXI
          INDJ=INDXJ
          CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTABIJ)
          ILOGREC=ILOGREC+1
          IPOS=IFIRST
          DO 140 INDXB=1,NUMB
           DO 150 INDXA=1,NUMA
            X=BUF(IPOS)*FABIJ
            IPOS=IPOS+1
            INDA=INDXA+NUMI
            INDB=INDXB+NUMJ 
            GINDXL=INDXT(INDI,INDA)
            GINDXR=INDXT(INDJ,INDB)
            GINDX0=INDXF(GINDXL,GINDXR,MAXL)
            GINDX =GINDX0+GOFF(IRREPL,IRREPR)
            GAMMA(GINDX)=X
150        CONTINUE
140       CONTINUE
130      CONTINUE
120     CONTINUE
110    CONTINUE
100   CONTINUE
      RETURN
      END
