      SUBROUTINE SPAD(ICORE,MAXCOR,IUHF)
C
C FORM SPIN ADAPTED AB LISTS FOR RHF CALCULATIONS
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM
      DIMENSION ICORE(MAXCOR)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      LOGICAL ROHF
      LOGICAL TDA,EOM
      LOGICAL GABCD,DABCD
C
      COMMON /EXCITE/ TDA,EOM
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /REF/ROHF
      COMMON /IABCD/ IOFFABCD
      COMMON /ABCD/  GABCD,DABCD
      COMMON /FLAGS/ IFLAGS(100)
C
      DATA ONE  /1.0D0/
      DATA ONEM /-1.0D0/
C
C FIRST DO THE G(Ab,Ij) LISTS
C
      IF(.NOT.TDA)THEN
       LISTABIJ=116
       DO 10 IRREP=1,NIRREP
        NUMDIS=IRPDPD(IRREP,ISYTYP(2,16))
        DISSIZ=IRPDPD(IRREP,ISYTYP(1,16))
        I000=1
        I010=I000+NUMDIS*DISSIZ*IINTFP
        I020=I010+NUMDIS*IINTFP
        I030=I020+NUMDIS*IINTFP
        IF(I030.GE.MAXCOR) CALL INSMEM('SPAD1 ',I030,MAXCOR)
        CALL GETLST(ICORE(I000),1,NUMDIS,1,IRREP,LISTABIJ)
        CALL SPINAD3(IRREP,VRT(1,1),DISSIZ,NUMDIS,ICORE(I000),
     &               ICORE(I010),ICORE(I020))
        CALL PUTLST(ICORE(I000),1,NUMDIS,1,IRREP,LISTABIJ)
10     CONTINUE
      ENDIF
C
      IF(MBPT2)RETURN
C
C NOW DO THE G(Ai,Bj) ABAB LISTS
C
      DO 20 IRREP=1,NIRREP
       NUMDIS=IRPDPD(IRREP,ISYTYP(2,25))
       DISSIZ=IRPDPD(IRREP,ISYTYP(1,25))
       I000=1
       I010=I000+NUMDIS*DISSIZ*IINTFP
       I020=I010+NUMDIS*DISSIZ*IINTFP
       IF(I020.GE.MAXCOR) CALL INSMEM('SPAD2 ',I020,MAXCOR)
       CALL GETLST(ICORE(I000),1,NUMDIS,1,IRREP,125)
       CALL GETLST(ICORE(I010),1,NUMDIS,1,IRREP,123)
       CALL SAXPY(NUMDIS*DISSIZ,ONE,ICORE(I000),1,ICORE(I010),1)
       CALL PUTLST(ICORE(I010),1,NUMDIS,1,IRREP,125)
20    CONTINUE
C
C NOW DO THE G(Ia,Bj) ABBA LISTS - THIS IS FIRST SPIN ADAPTED AND
C   THEN RESORTED.
C
      DO 21 IRREP=1,NIRREP
       NUMDIS=IRPDPD(IRREP,ISYTYP(2,18))
       DISSIZ=IRPDPD(IRREP,ISYTYP(1,18))
       I000=1
       I010=I000+NUMDIS*DISSIZ*IINTFP
       I020=I010+NUMDIS*DISSIZ*IINTFP
       IF(I020.GE.MAXCOR) CALL INSMEM('SPAD3 ',I020,MAXCOR)
       CALL GETLST(ICORE(I000),1,NUMDIS,1,IRREP,118)
       CALL GETLST(ICORE(I010),1,NUMDIS,1,IRREP,123)
       CALL SAXPY(NUMDIS*DISSIZ,ONEM,ICORE(I010),1,ICORE(I000),1)
       CALL PUTLST(ICORE(I000),1,NUMDIS,1,IRREP,118)
21    CONTINUE
      NSIZE=ISYMSZ(ISYTYP(1,18),ISYTYP(2,18))
      I000=1
      I010=I000+NSIZE*IINTFP
      I020=I010+NSIZE*IINTFP
      IF(I020.GE.MAXCOR) CALL INSMEM('SPAD4 ',I020,MAXCOR)
      CALL GETALL(ICORE(I000),NSIZE,1,118)
      CALL SSTRNG(ICORE(I000),ICORE(I010),NSIZE,NSIZE,ICORE(I020),
     &            'ABBA')
      CALL PUTALL(ICORE(I010),NSIZE,1,118)
      IF(TDA)RETURN
C
C NOW DO THE G(Ij,Kl) LISTS
C
      DO 30 IRREP=1,NIRREP
       NUMDIS=IRPDPD(IRREP,ISYTYP(2,13))
       DISSIZ=IRPDPD(IRREP,ISYTYP(1,13))
       I000=1
       I010=I000+NUMDIS*DISSIZ*IINTFP
       I020=I010+NUMDIS*IINTFP
       I030=I020+NUMDIS*IINTFP
       IF(I030.GE.MAXCOR) CALL INSMEM('SPAD5 ',I020,MAXCOR)
       CALL GETLST(ICORE(I000),1,NUMDIS,1,IRREP,113)
       CALL SPINAD3(IRREP,POP(1,1),DISSIZ,NUMDIS,ICORE(I000),
     &              ICORE(I010),ICORE(I020))
       CALL PUTLST(ICORE(I000),1,NUMDIS,1,IRREP,113)
30    CONTINUE
C
C NOW DO THE G(Ab,Cd) LISTS
C
      IF(.NOT.DABCD) THEN
       DO 40 IRREP=1,NIRREP
        NUMDIS=IRPDPD(IRREP,ISYTYP(2,233))
        DISSIZ=IRPDPD(IRREP,ISYTYP(1,233))
        I000=1
        I010=I000+NUMDIS*DISSIZ*IINTFP
        I020=I010+NUMDIS*IINTFP
        I030=I020+NUMDIS*IINTFP
        IF(I030.LT.MAXCOR)THEN
         CALL GETLST(ICORE(I000),1,NUMDIS,1,IRREP,133+IOFFABCD)
         CALL SPINAD3(IRREP,VRT(1,1),DISSIZ,NUMDIS,ICORE(I000),
     &                ICORE(I010),ICORE(I020))
         CALL PUTLST(ICORE(I000),1,NUMDIS,1,IRREP,133+IOFFABCD)
        ELSE
C
C WRITE BETTER OUT-OF-CORE ALGORITHM FOR THIS!
C
         I000=1
         I010=I000+DISSIZ*IINTFP
         I020=I010+NUMDIS*IINTFP
         I030=I020+NUMDIS*IINTFP
         IF(I030.GE.MAXCOR) CALL INSMEM('SPAD6 ',I030,MAXCOR)
         DO 41 IDIS=1,NUMDIS
          CALL GETLST(ICORE(I000),IDIS,1,1,IRREP,133+IOFFABCD)
          CALL SPINAD3(IRREP,VRT(1,1),DISSIZ,1,ICORE(I000),
     &                 ICORE(I010),ICORE(I020))
          CALL PUTLST(ICORE(I000),IDIS,1,1,IRREP,133+IOFFABCD)
41       CONTINUE
        ENDIF
40     CONTINUE
      ENDIF
C
C NOW DO G(Ab,Ci) LISTS
C
      IF((MBPT3.AND.(.NOT.ROHF)).OR.CCD) RETURN
      DO 50 IRREP=1,NIRREP
       NUMDIS=IRPDPD(IRREP,ISYTYP(2,30))
       DISSIZ=IRPDPD(IRREP,ISYTYP(1,30))
       I000=1
       I010=I000+NUMDIS*DISSIZ*IINTFP
       I020=I010+NUMDIS*IINTFP
       I030=I020+NUMDIS*IINTFP
       IF(I030.LT.MAXCOR) THEN
C
C WE CAN DO IT INCORE
C
        CALL GETLST(ICORE(I000),1,NUMDIS,1,IRREP,130)
        CALL SPINAD3(IRREP,VRT(1,1),DISSIZ,NUMDIS,ICORE(I000),
     &               ICORE(I010),ICORE(I020))
        CALL PUTLST(ICORE(I000),1,NUMDIS,1,IRREP,130)
       ELSE
C
C PRIMITIVE OUT-OF-CORE ALGORITHM
C
        I000=1
        I010=I000+DISSIZ*IINTFP
        I020=I010+NUMDIS*IINTFP
        I030=I020+NUMDIS*IINTFP
        IF(I030.GE.MAXCOR) CALL INSMEM('SPAD7 ',I030,MAXCOR)
        DO 51 IDIS=1,NUMDIS
         CALL GETLST(ICORE(I000),IDIS,1,1,IRREP,130)
         CALL SPINAD3(IRREP,VRT(1,1),DISSIZ,1,ICORE(I000),
     &                ICORE(I010),ICORE(I020))
         CALL PUTLST(ICORE(I000),IDIS,1,1,IRREP,130)
51      CONTINUE
       ENDIF
50    CONTINUE
C
C NOW DO G(Ij,Ka) LISTS
C
      DO 60 IRREP=1,NIRREP
       NUMDIS=IRPDPD(IRREP,ISYTYP(2,10))
       DISSIZ=IRPDPD(IRREP,ISYTYP(1,10))
       I000=1
       I010=I000+NUMDIS*DISSIZ*IINTFP
       I020=I010+NUMDIS*IINTFP
       I030=I020+NUMDIS*IINTFP
       IF(I030.GE.MAXCOR) CALL INSMEM('SPAD8 ',I020,MAXCOR)
       CALL GETLST(ICORE(I000),1,NUMDIS,1,IRREP,110)
       CALL SPINAD3(IRREP,POP(1,1),DISSIZ,NUMDIS,ICORE(I000),
     &              ICORE(I010),ICORE(I020))
       CALL PUTLST(ICORE(I000),1,NUMDIS,1,IRREP,110)
60    CONTINUE
C
      RETURN
      END
