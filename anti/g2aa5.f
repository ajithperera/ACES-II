
      SUBROUTINE G2AA5(GAMMA,BUF,GOFF,LENGAM,ISPIN,FABCI)
C
C THIS ROUTINE PROCESSES THE g<IJ,KA> GAMMAS AND FILLS OUT 
C  THE AAAA SYMMETRY TYPE MO GAMMA LIST FOR UHF CALCULATIONS
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 
      DOUBLE PRECISION FABIJ,FAIBJ,FABCD,FIJKL,FIJKA,FABCI
      INTEGER GOFF(8,8),IOFFSR(8),IOFFSL(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
C
C
C         11 22        12 12
C       G(AC,BI) <-- g<AB,CI>
C
C         11 22        21 21        12 21 
C       G(BI,AC) <-- g<AB,CI> = - g<BA,CI>
C
C IN ADDITION, ALL GAMMA ELEMENTS ARE MADE SYMMETRIC IN THE I AND K 
C  INDICES.
C
C       G(AC,XX) = G(AC,XX) + G(CA,XX)
C      
      LISTABCI=126+ISPIN
      DO 100 IRREPGET=2,NIRREP
       IOFFR=1
       IOFFL=1
       DO 1100 IRREP1=1,NIRREP
        IRREP2=DIRPRD(IRREP1,IRREPGET)
        IOFFSR(IRREP1)=IOFFR
        IOFFR=IOFFR+VRT(IRREP2,ISPIN)*POP(IRREP1,ISPIN)
        IF(IRREP2.GT.IRREP1)GOTO 1100
        IOFFSL(IRREP1)=IOFFL
        IOFFL=IOFFL+VRT(IRREP2,ISPIN)*VRT(IRREP1,ISPIN)
1100   CONTINUE
       DO 110 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPGET)
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPR)-1
        IFIRST2=IOFFSL(IRREPL)-1
        NUMI=POP(IRREPR,ISPIN)
        NUMC=VRT(IRREPL,ISPIN)
        NUMB=VRT(IRREPR,ISPIN)
        NUMA=VRT(IRREPL,ISPIN)
        MAXL=NNP1O2(MOPOP(IRREPL))
        MAXR=NNP1O2(MOPOP(IRREPR))
        DO 120 INDXI=1,NUMI
         DO 130 INDXC=1,NUMC
          INDC=INDXC+POP(IRREPL,ISPIN)
          INDI=INDXI
          CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTABCI)
          ILOGREC=ILOGREC+1
          IF(IRREPL.LT.IRREPR)THEN
C
C         11 22        12 12
C       G(AC,BI) <-- g<AB,CI>
C
           DO 140 INDXB=1,NUMB
            INDB=INDXB+POP(IRREPR,ISPIN)
            GINDXR=INDXT(INDI,INDB)
            DO 150 INDXA=1,INDXC
             IPOS=IFIRST+INDXF(INDXA,INDXB,NUMA)
             X=BUF(IPOS)*FABCI
             INDA=INDXA+POP(IRREPL,ISPIN)
             GINDXL=INDXT(INDA,INDC)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
150         CONTINUE
            DO 151 INDXA=INDXC,NUMA
             IPOS=IFIRST+INDXF(INDXA,INDXB,NUMA)
             X=BUF(IPOS)*FABCI
             INDA=INDXA+POP(IRREPL,ISPIN)
             GINDXL=INDXT(INDC,INDA)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX=GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
151         CONTINUE
140        CONTINUE
C
C
          ELSE
C
C         11 22        21 21        12 21 
C       G(BI,AC) <-- g<AB,CI> = - g<BA,CI>
C
           DO 160 INDXA=1,INDXC
            INDA=INDXA+POP(IRREPL,ISPIN)
            DO 170 INDXB=1,NUMB
             IPOS=IFIRST2+INDXF(INDXB,INDXA,NUMB)
             X=BUF(IPOS)*FABCI
             INDB=INDXB+POP(IRREPR,ISPIN)
             GINDXL=INDXT(INDI,INDB)
             GINDXR=INDXT(INDA,INDC)
             GINDX0=INDXF(GINDXL,GINDXR,MAXR)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)-X
170         CONTINUE
160        CONTINUE
           DO 180 INDXA=INDXC,NUMA
            INDA=INDXA+POP(IRREPL,ISPIN)
            DO 190 INDXB=1,NUMB
             IPOS=IFIRST2+INDXF(INDXB,INDXA,NUMB)
             X=BUF(IPOS)*FABCI
             INDB=INDXB+POP(IRREPR,ISPIN)
             GINDXL=INDXT(INDI,INDB)
             GINDXR=INDXT(INDC,INDA)
             GINDX0=INDXF(GINDXL,GINDXR,MAXR)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)-X
190         CONTINUE
180        CONTINUE
          ENDIF
130      CONTINUE
120     CONTINUE
110    CONTINUE
100   CONTINUE
      RETURN
      END
