      SUBROUTINE CG1AAAA(W,BUF,IBUF,ICHAIN,DISSIZ,NDBCK,
     &                   NBKINT,NBUCK,IRECL,LIST1,LIST2,
     &                   IRREPDO, ALASKA)
C
C PICKS UP AAAA MO GAMMAS FROM SORT FILE AND WRITES THEM TO MOINTS.
C  THESE NEED TO BE SCALED BY A FACTOR OF 0.5
C
CEND
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (LUSRT = 20)
      INTEGER DISSIZ,DIRPRD,VRT,POP,DISFIRST,DISLEFT,DISWRITE
      INTEGER AND
      LOGICAL TDA,EOM, ALASKA, MOEQAO
      DIMENSION W(DISSIZ,NDBCK),BUF(NBKINT),IBUF(NBKINT)
      DIMENSION ICHAIN(NBUCK)
      COMMON /EXCITE/ TDA,EOM
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /FILES/ LUOUT,MOINTS
C SRG/JDW. 12/14/95. Eliminate /FILSPC/ from anti. Also variable BUFSIZ
C removed from INTEGER declaration.
C     COMMON /FILSPC/ ILNBUF,IPRCLN,IPRCWD
      COMMON /INFO/ NOCCO(2),NVRTO(2)
      COMMON/SYM/POP(8,2),VRT(8,2),NJUNK(6)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON /SIZES/ MOEQAO
C
      DATA HALF /0.5/
C
      IUPKI(INT)=AND(INT,IALONE)
      IUPKJ(INT)=AND(ISHFT(INT,-IBITWD),IALONE)
C
C LOOP OVER THE NUMBER OF BUCKETS
C
      NUMDIS=DISSIZ
      DISFIRST=1
      DO 10 IDIS=1,NBUCK
       NOFF=(IDIS-1)*NDBCK
       CALL ZERO(W,DISSIZ*NDBCK)
C
C CHAIN IN A BUCKETFULL OF GAMMAS HERE
C
1      READ(LUSRT,REC=ICHAIN(IDIS))BUF,IBUF,NUT,ICHAN
       ICHAIN(IDIS)=ICHAN
       DO 20 INT=1,NUT
        I=IUPKI(IBUF(INT))
        J=IUPKJ(IBUF(INT))-NOFF
        W(I,J)=W(I,J)+BUF(INT)
20     CONTINUE
       IF(ICHAIN(IDIS).NE.0)GOTO 1
C
C  IF WE REACH THIS POINT, THE ENTIRE BUCKET IS IN CORE.  DUMP THEM
C  TO THE MOINTS FILE.
C
       CALL SSCAL(DISSIZ*NDBCK,HALF,W,1)
       DISLEFT=NUMDIS+1-DISFIRST
       DISWRITE=MIN(NDBCK,DISLEFT)
       IF (MOEQAO .OR. ALASKA) THEN
          CALL PUTLST(W,DISFIRST,DISWRITE,1,LIST1,LIST2)
       ELSE
          DO  I = 1, DISWRITE
             IDISREAL = DISFIRST + I - 1 
             CALL PUTLST(W(1, I), IDISREAL, 1, 1, LIST1, LIST2)
          ENDDO
       ENDIF
       DISFIRST=DISFIRST+NDBCK
 10   CONTINUE
C
      CLOSE(UNIT=20,STATUS='DELETE')
      RETURN
      END
