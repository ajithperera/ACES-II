      SUBROUTINE G2AA4(GAMMA,BUF,GOFF,LENGAM,ISPIN,FAIBJ)
C
C THIS ROUTINE PROCESSES RING TYPE GAMMAS NEEDED FOR THE
C  SPIN CASE AAAA MO GAMMA LIST FOR UHF CALCULATIONS.
C
C          11 22        12 12
C        G(AB,IJ) <-- g<AI,BJ>
C
C          11 22        21 21
C        G(IJ,AB) <-- g<AI,BJ>
C
C          11 22        12 12        21 12       12 21
C        G(IB,AJ) <-- g<IA,BJ> = - g<AI,BJ> = -g<BJ,AI>
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 
      DOUBLE PRECISION FABIJ,FAIBJ,FABCD,FIJKL,FABCI,FIJKA
      INTEGER GOFF(8,8),IOFFSR(8),IOFFSL(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
      NNM1O2(I)   =(I*(I-1))/2
C 
      LISTAIBJ=122+ISPIN
      DO 100 IRREPGET=2,NIRREP
       IOFFR=1
       IOFFL=1
       DO 1100 IRREP1=1,NIRREP
        IRREP2=DIRPRD(IRREP1,IRREPGET)
        IOFFSR(IRREP1)=IOFFR
        IOFFSL(IRREP1)=IOFFL
        IOFFR=IOFFR+VRT(IRREP2,ISPIN)*POP(IRREP1,ISPIN)
        IOFFL=IOFFL+VRT(IRREP2,ISPIN)*POP(IRREP1,ISPIN)
1100   CONTINUE
       DO 110 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPGET)
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPR)-1
        IFIRST2=IOFFSL(IRREPL)-1
        NUMJ=POP(IRREPR,ISPIN)
        NUMB=VRT(IRREPL,ISPIN)
        NUMI=POP(IRREPR,ISPIN)
        NUMI2=POP(IRREPL,ISPIN)
        NUMA=VRT(IRREPL,ISPIN)
        NUMA2=VRT(IRREPR,ISPIN)
        IF(IRREPL.GT.IRREPR)THEN
         MAXL=NNP1O2(MOPOP(IRREPR))
         DO 120 INDXJ=1,NUMJ
          DO 130 INDXB=1,NUMB
           INDB=INDXB+POP(IRREPL,ISPIN)
           INDJ=INDXJ
           CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTAIBJ)
           ILOGREC=ILOGREC+1
           DO 140 INDXI=1,INDXJ
            INDI=INDXI
            GINDXR=INDXT(INDI,INDJ)
            DO 150 INDXA=1,INDXB
             IPOS=INDXF(INDXA,INDXI,NUMA)+IFIRST
             X=BUF(IPOS)*FAIBJ
             INDA=INDXA+POP(IRREPL,ISPIN)
             GINDXL=INDXT(INDA,INDB)
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X
150         CONTINUE
            DO 151 INDXA=INDXB,NUMA
             IPOS=INDXF(INDXA,INDXI,NUMA)+IFIRST
             X=BUF(IPOS)*FAIBJ
             INDA=INDXA+POP(IRREPL,ISPIN)
             GINDXL=INDXT(INDB,INDA)
             GINDX0=INDXF(GINDXR,GINDXL,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)+X
151         CONTINUE
140        CONTINUE
           DO 141 INDXI=1,NUMI2
            DO 152 INDXA=1,NUMA2
             IPOS=INDXF(INDXA,INDXI,NUMA2)+IFIRST2
             X=BUF(IPOS)*FAIBJ
             INDA=INDXA+POP(IRREPR,ISPIN)
             INDI=INDXI
             GINDXL=INDXT(INDJ,INDA)
             GINDXR=INDXT(INDI,INDB)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)-X 
152         CONTINUE
141        CONTINUE
130       CONTINUE
120      CONTINUE
        ELSE
         MAXL=NNP1O2(MOPOP(IRREPL))
         DO 220 INDXJ=1,NUMJ
          DO 230 INDXB=1,NUMB
           INDB=INDXB+POP(IRREPL,ISPIN)
           INDJ=INDXJ
           CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTAIBJ)
           ILOGREC=ILOGREC+1
           IPOS=IFIRST
           DO 240 INDXI=1,INDXJ
            INDI=INDXI
            GINDXR=INDXT(INDI,INDJ)
            DO 250 INDXA=1,INDXB
             IPOS=INDXF(INDXA,INDXI,NUMA)+IFIRST
             X=BUF(IPOS)*FAIBJ
             INDA=INDXA+POP(IRREPL,ISPIN)
             GINDXL=INDXT(INDA,INDB)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
250         CONTINUE
            DO 251 INDXA=INDXB,NUMA
             IPOS=INDXF(INDXA,INDXI,NUMA)+IFIRST
             X=BUF(IPOS)*FAIBJ
             INDA=INDXA+POP(IRREPL,ISPIN)
             GINDXL=INDXT(INDB,INDA)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
251         CONTINUE
240        CONTINUE
230       CONTINUE
220      CONTINUE
        ENDIF
110    CONTINUE
100   CONTINUE
      RETURN
      END
