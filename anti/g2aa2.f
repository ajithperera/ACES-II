      SUBROUTINE G2AA2(GAMMA,BUF,GOFF,LENGAM,ISPIN,FIJKA)
C
C THIS ROUTINE PROCESSES THE g<IJ,KA> GAMMAS AND FILLS OUT 
C  THE AAAA SYMMETRY TYPE MO GAMMA LIST FOR UHF CALCULATIONS
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 
      DOUBLE PRECISION FABIJ,FAIBJ,FABCD,FIJKL,FABCI,FIJKA
      INTEGER GOFF(8,8),IOFFSR(8),IOFFSL(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
C
C
C         11 22        12 12
C       G(IK,JA) <-- g<IJ,KA>
C
C         11 22        21 21        12 21 
C       G(JA,IK) <-- g<IJ,KA> = - g<JI,KA>
C
C IN ADDITION, ALL GAMMA ELEMENTS ARE MADE SYMMETRIC IN THE I AND K 
C  INDICES.
C
C       G(IK,XX) = G(IK,XX) + G(KI,XX)
C      
      LISTIJKA=106+ISPIN
      DO 100 IRREPGET=2,NIRREP
       IOFFR=1
       IOFFL=1
       DO 1100 IRREP1=1,NIRREP
        IRREP2=DIRPRD(IRREP1,IRREPGET)
        IOFFSR(IRREP1)=IOFFR
        IOFFR=IOFFR+POP(IRREP2,ISPIN)*VRT(IRREP1,ISPIN)
        IF(IRREP2.GT.IRREP1)GOTO 1100
        IOFFSL(IRREP1)=IOFFL
        IOFFL=IOFFL+POP(IRREP2,ISPIN)*POP(IRREP1,ISPIN)
1100   CONTINUE
       DO 110 IRREPR=1,NIRREP
        IRREPL=DIRPRD(IRREPR,IRREPGET)
        ILOGREC=IOFFSR(IRREPR)
        IFIRST =IOFFSL(IRREPR)-1
        IFIRST2=IOFFSL(IRREPL)-1
        NUMA=VRT(IRREPR,ISPIN)
        NUMK=POP(IRREPL,ISPIN)
        NUMJ=POP(IRREPR,ISPIN)
        NUMI=POP(IRREPL,ISPIN)
        MAXL=NNP1O2(MOPOP(IRREPL))
        MAXR=NNP1O2(MOPOP(IRREPR))
        DO 120 INDXA=1,NUMA
         DO 130 INDXK=1,NUMK
          INDA=INDXA+POP(IRREPR,ISPIN)
          INDK=INDXK
          CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTIJKA)
          ILOGREC=ILOGREC+1
          IF(IRREPL.LT.IRREPR)THEN
C
C         11 22        12 12
C       G(IK,JA) <-- g<IJ,KA>
C
           DO 140 INDXJ=1,NUMJ
            INDJ=INDXJ
            GINDXR=INDXT(INDJ,INDA)
            DO 150 INDXI=1,INDXK
             IPOS=IFIRST+INDXF(INDXI,INDXJ,NUMI)
             X=BUF(IPOS)*FIJKA
             INDI=INDXI
             GINDXL=INDXT(INDI,INDK)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX =GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
150         CONTINUE
            DO 151 INDXI=INDXK,NUMI
             IPOS=IFIRST+INDXF(INDXI,INDXJ,NUMI)
             X=BUF(IPOS)*FIJKA
             INDI=INDXI
             GINDXL=INDXT(INDK,INDI)
             GINDX0=INDXF(GINDXL,GINDXR,MAXL)
             GINDX=GINDX0+GOFF(IRREPL,IRREPR)
             GAMMA(GINDX)=GAMMA(GINDX)+X
151         CONTINUE
140        CONTINUE
C
C
          ELSE
C
C         11 22        21 21        12 21 
C       G(JA,IK) <-- g<IJ,KA> = - g<JI,KA>
C
           DO 160 INDXI=1,INDXK
            INDI=INDXI
            DO 170 INDXJ=1,NUMJ
             IPOS=IFIRST2+INDXF(INDXJ,INDXI,NUMJ)
             X=BUF(IPOS)*FIJKA
             INDJ=INDXJ
             GINDXL=INDXT(INDJ,INDA)
             GINDXR=INDXT(INDI,INDK)
             GINDX0=INDXF(GINDXL,GINDXR,MAXR)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)-X
170         CONTINUE
160        CONTINUE
           DO 180 INDXI=INDXK,NUMI
            INDI=INDXI
            DO 190 INDXJ=1,NUMJ
             IPOS=IFIRST2+INDXF(INDXJ,INDXI,NUMJ)
             X=BUF(IPOS)*FIJKA
             INDJ=INDXJ
             GINDXL=INDXT(INDJ,INDA)
             GINDXR=INDXT(INDK,INDI)
             GINDX0=INDXF(GINDXL,GINDXR,MAXR)
             GINDX =GINDX0+GOFF(IRREPR,IRREPL)
             GAMMA(GINDX)=GAMMA(GINDX)-X
190         CONTINUE
180        CONTINUE
          ENDIF
130      CONTINUE
120     CONTINUE
110    CONTINUE
100   CONTINUE
      RETURN
      END
