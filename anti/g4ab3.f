      SUBROUTINE G4AB3(GAMMA,BUF,GOFF,LENGAM,FABIJ)
C
C THIS ROUTINE PROCESSES THE G(Ab,Ij) GAMMA LISTS AND FILLS
C  OUT THE ABCD SYMMETRY CASE MULLIKEN ORDERED GAMMA.  THE
C  LOGIC IN THIS ROUTINE IS TERRIBLY COMPLICATED AND SHOULD
C  NOT BE STUDIED BY ANYONE WHO ISN'T SURE THAT QUANTUM CHEMISTRY
C  IS FOR THEM.  
C
C NOTE THAT ABCD IN MULLIKEN CORRESPONDS TO ACBD IN DIRAC.  NOTE ALSO THAT
C  ALL ELEMENTS OF GAMMA WHICH ARE PROCESSED HERE COME FROM DPD LISTS
C  FOR IRREPS 2-h, SINCE A.NE.C;B.NE.D.
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GAMMA(LENGAM),BUF(1),X,X1,X2 
      DOUBLE PRECISION FABIJ,FAIBJ,FABCD,FIJKL,FABCI,FIJKA
      INTEGER GOFF(8,8,8,8),IDID(8),IOFFSL(8)
      INTEGER IOFFSR(8)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /METH/ MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MOPOPS/ MOPOP(8)
C
      INDXF(I,J,N)=I+(J-1)*N
      INDXT(I,J)  =I+(J*(J-1))/2
      NNP1O2(I)   =(I*(I+1))/2
C
      LISTABIJ=116
      DO 100 IRREPGET=2,NIRREP
       IOFFR=1
       IOFFL=1
       DO 1000 IRREP1=1,NIRREP
        IRREP2=DIRPRD(IRREP1,IRREPGET)
        IOFFSR(IRREP1)=IOFFR
        IOFFSL(IRREP1)=IOFFL
        IOFFR=IOFFR+POP(IRREP2,1)*POP(IRREP1,2)
        IOFFL=IOFFL+VRT(IRREP2,1)*VRT(IRREP1,2)
1000   CONTINUE
       DO 105 IRREP1=1,NIRREP
        IRREP2=DIRPRD(IRREP1,IRREPGET)
        DO 110 IRREP3=1,NIRREP
         IRREP4=DIRPRD(IRREP3,IRREPGET)
         IRLOW1=MAX(IRREP1,IRREP3)
         IRLOW2=MAX(IRREP2,IRREP4)
         IF(IRLOW1.LE.IRLOW2.OR.IRREP1.EQ.IRREP3)GOTO 110
         ILOGREC=IOFFSR(IRREP4)
         IFIRST=IOFFSL(IRREP2)
         IRREPD=IRREP4
         IRREPC=IRREP2
         IRREPB=IRREP3
         IRREPA=IRREP1
         IRREPA0=MAX(IRREPA,IRREPB)
         IRREPB0=MIN(IRREPA,IRREPB)
         IRREPC0=MAX(IRREPC,IRREPD)
         IRREPD0=MIN(IRREPC,IRREPD)
         IOFFSET=GOFF(IRREPA0,IRREPB0,IRREPC0,IRREPD0)
         NUMJ=POP(IRREPD,2)
         NUMI=POP(IRREPB,1)
         NUMB=VRT(IRREPC,2)
         NUMA=VRT(IRREPA,1) 
         MAXL=MOPOP(IRREPA)*MOPOP(IRREPB)
         DO 120 INDXJ=1,NUMJ
          DO 130 INDXI=1,NUMI
           INDI=INDXI
           INDJ=INDXJ
           CALL GETLST(BUF,ILOGREC,1,1,IRREPGET,LISTABIJ)
           ILOGREC=ILOGREC+1
           IF(IRREPA.GT.IRREPB.AND.IRREPC.GT.IRREPD)THEN
            IPOS=IOFFSL(IRREPC)
            DO 140 INDXB=1,NUMB
             DO 150 INDXA=1,NUMA
              X=BUF(IPOS)*FABIJ
              IPOS=IPOS+1
              INDA=INDXA+POP(IRREPA,1)
              INDB=INDXB+POP(IRREPC,2)
              GINDXL=INDXF(INDA,INDI,MOPOP(IRREPA))
              GINDXR=INDXF(INDB,INDJ,MOPOP(IRREPC))
              GINDX0=INDXF(GINDXL,GINDXR,MAXL)
              GINDX =GINDX0+IOFFSET
              GAMMA(GINDX)=X
150          CONTINUE
140         CONTINUE
           ELSEIF(IRREPA.GT.IRREPB.AND.IRREPC.LT.IRREPD)THEN
            IPOS=IOFFSL(IRREPC)
            DO 141 INDXB=1,NUMB
             DO 151 INDXA=1,NUMA
              X=BUF(IPOS)*FABIJ
              IPOS=IPOS+1
              INDA=INDXA+POP(IRREPA,1)
              INDB=INDXB+POP(IRREPC,2)
              GINDXL=INDXF(INDA,INDI,MOPOP(IRREPA))
              GINDXR=INDXF(INDJ,INDB,MOPOP(IRREPD))
              GINDX0=INDXF(GINDXL,GINDXR,MAXL)
              GINDX =GINDX0+IOFFSET
              GAMMA(GINDX)=X
151          CONTINUE
141         CONTINUE
           ELSEIF(IRREPA.LT.IRREPB.AND.IRREPC.GT.IRREPD)THEN
            IPOS=IOFFSL(IRREPC)
            DO 142 INDXB=1,NUMB
             DO 152 INDXA=1,NUMA
              X=BUF(IPOS)*FABIJ
              IPOS=IPOS+1
              INDA=INDXA+POP(IRREPA,1)
              INDB=INDXB+POP(IRREPC,2)
              GINDXL=INDXF(INDI,INDA,MOPOP(IRREPB))
              GINDXR=INDXF(INDB,INDJ,MOPOP(IRREPC))
              GINDX0=INDXF(GINDXL,GINDXR,MAXL)
              GINDX =GINDX0+IOFFSET
              GAMMA(GINDX)=X
152          CONTINUE
142         CONTINUE
           ELSEIF(IRREPA.LT.IRREPB.AND.IRREPC.LT.IRREPD)THEN
            IPOS=IOFFSL(IRREPC)
            DO 143 INDXB=1,NUMB
             DO 153 INDXA=1,NUMA
              X=BUF(IPOS)*FABIJ
              IPOS=IPOS+1
              INDA=INDXA+POP(IRREPA,1)
              INDB=INDXB+POP(IRREPC,2)
              GINDXL=INDXF(INDI,INDA,MOPOP(IRREPB))
              GINDXR=INDXF(INDJ,INDB,MOPOP(IRREPD))
              GINDX0=INDXF(GINDXL,GINDXR,MAXL)
              GINDX =GINDX0+IOFFSET
              GAMMA(GINDX)=X
153          CONTINUE
143         CONTINUE
           ENDIF 
130       CONTINUE
120      CONTINUE
110     CONTINUE
105    CONTINUE
100   CONTINUE
      RETURN
      END
