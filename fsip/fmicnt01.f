      SUBROUTINE FMICNT01(ICORE,MAXCOR,IUHF) 
C
C THIS ROUTINE CALCULATES THE FOLLOWING CONTRIBUTION TO THE
C DOUBLES AMPLITUDES FOR THE (0,1) FOCK SPACE SECTOR
C
C
C    Z(ij,ka) = - P(ij) T(im,ka)*F(mj) 
C      NN AN              NN AN    NN 
C
C  
CEND
      IMPLICIT INTEGER (A-H,O-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH
      LOGICAL RHF
      DIMENSION ICORE(MAXCOR)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /FSSYMPOP/ FSDPDAN(8,22),FSDPDNA(8,22),FSDPDAA(8,22),
     &                  FSDPDIN(8,22),FSDPDNI(8,22),FSDPDII(8,22),
     &                  FSDPDAI(8,22),FSDPDIA(8,22)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /FSSYM/ POPA(8,2),POPI(8,2),VRTA(8,2),VRTI(8,2),
     &               NTAN(2),NTNA(2),NTAA(2),
     &               NTIN(2),NTNI(2),NTII(2),
     &               NTAI(2),NTIA(2),
     &               NF1AN(2),NF1AA(2),NF1IN(2),NF1II(2),NF1AI(2),
     &               NF2AN(2),NF2AA(2),NF2IN(2),NF2II(2),NF2AI(2)
C
      DATA ONE/1.0/, ONEM/-1.0/, ZILCH /0.0/
C
      RHF=IUHF.EQ.0
C
      DO 10 IRREP=1,NIRREP
C
C DO SPIN CASES AAAA AND BBBB
C
C UHF CODE
C
       IF(.NOT.RHF)THEN
        DO 100 ISPIN=1,2
         LISTT=95+ISPIN
         LISTZ=195+ISPIN
         NUMDIS=FSDPDAN(IRREP,ISYTYP(2,LISTT))
         DISSIZ=IRPDPD (IRREP,ISYTYP(1,LISTT))
         DISSIZF=IRPDPD(IRREP,20+ISPIN)
         I000=1
         I010=I000+IINTFP*NUMDIS*DISSIZF
         I020=I010+IINTFP*NUMDIS*DISSIZF
         I030=I020+IINTFP*NFMI(ISPIN)
C
C READ T(I<M,KA) INTO T(I<M,KA), TRANSPOSE AND EXPAND TO T(KA,IM)
C
         CALL FSGET(ICORE(I000),1,NUMDIS,1,IRREP,LISTT,'NNAN')
         CALL TRANSP(ICORE(I000),ICORE(I010),NUMDIS,DISSIZ)
         CALL SYMEXP(IRREP,POP(1,ISPIN),NUMDIS,ICORE(I010))
         CALL GETLST(ICORE(I020),1,1,1,ISPIN,91)
         IOFFT=I010
         IOFFF=I020
         IOFFZ=I000
C
C SET UP T(KAI,M)*F(MJ) MULTIPLICATION
C
         DO 110 IRREPM=1,NIRREP
          IRREPJ=IRREPM
          IRREPI=DIRPRD(IRREPM,IRREP)
          NSUM=POP(IRREPM,ISPIN)
          NCOL=POP(IRREPJ,ISPIN)
          NROW=NUMDIS*POP(IRREPI,ISPIN)
          CALL XGEMM('N','N',NROW,NCOL,NSUM,ONEM,ICORE(IOFFT),NROW,
     &               ICORE(IOFFF),NSUM,ZILCH,ICORE(IOFFZ),NROW)
          IOFFT=IOFFT+NROW*NCOL*IINTFP
          IOFFF=IOFFF+NSUM*NCOL*IINTFP
          IOFFZ=IOFFZ+NROW*NCOL*IINTFP
110      CONTINUE 
C
C TRANSPOSE RESULT, ANTISYMMETRIZE AND UPDATE INCREMENTS
C
         CALL ASSYM (IRREP,POP(1,ISPIN),NUMDIS,NUMDIS,ICORE(I010),
     &               ICORE(I000))
         CALL TRANSP(ICORE(I010),ICORE(I000),DISSIZ,NUMDIS)
         CALL FSGET(ICORE(I010),1,NUMDIS,1,IRREP,LISTZ,'NNAN')
         CALL SAXPY(NUMDIS*DISSIZ,ONE,ICORE(I000),1,ICORE(I010),1)
         CALL FSPUT(ICORE(I010),1,NUMDIS,1,IRREP,LISTZ,'NNAN')
100     CONTINUE
       ENDIF 
C
C DO SPIN CASE ABAB -> - T(Im,Ka) * f(mj) + T(Mj,Ka) * f(MI) [ISPIN=1]
C
C
       LISTT=99
       LISTZ=199
       NUMDIS=FSDPDAN(IRREP,ISYTYP(2,LISTT))
       DISSIZ=IRPDPD (IRREP,ISYTYP(1,LISTT))
       I000=1
       I010=I000+IINTFP*NUMDIS*DISSIZ
       I020=I010+IINTFP*NUMDIS*DISSIZ
       I030=I020+IINTFP*MAX(NFMI(2),NFMI(1),NUMDIS,DISSIZ)
       I040=I030+IINTFP*MAX(NUMDIS,DISSIZ)
       I050=I040+IINTFP*MAX(NUMDIS,DISSIZ)
C 
C READ T(Im,Ka) AND TRANSPOSE TO T(Ka,Im)
C        NN AN                     AN NN 
C
       CALL FSGET(ICORE(I000),1,NUMDIS,1,IRREP,LISTT,'NNAN')
       CALL TRANSP(ICORE(I000),ICORE(I010),NUMDIS,DISSIZ)
       CALL GETLST(ICORE(I020),1,1,1,1+IUHF,91)
       IOFFT=I010
       IOFFF=I020
       IOFFZ=I000
C
C SET UP T(KaI,m)*F(mj) MULTIPLICATION
C
       DO 120 IRREPM=1,NIRREP
        IRREPJ=IRREPM
        IRREPI=DIRPRD(IRREPM,IRREP)
        NSUM=POP(IRREPM,2)
        NCOL=POP(IRREPJ,2)
        NROW=NUMDIS*POP(IRREPI,1)
        CALL XGEMM('N','N',NROW,NCOL,NSUM,ONEM,ICORE(IOFFT),NROW,
     &             ICORE(IOFFF),NSUM,ZILCH,ICORE(IOFFZ),NROW)
        IOFFT=IOFFT+NROW*NCOL*IINTFP
        IOFFF=IOFFF+NSUM*NCOL*IINTFP
        IOFFZ=IOFFZ+NROW*NCOL*IINTFP
120    CONTINUE 
C
C NOW TRANSPOSE T(Ka,Im) AND Z(Ka,Im) TO (Ka,mI) 
C
       CALL SYMTR1(IRREP,POP(1,1),POP(1,2),NUMDIS,ICORE(I010),
     &             ICORE(I020),ICORE(I030),ICORE(I040))
       CALL SYMTR1(IRREP,POP(1,1),POP(1,2),NUMDIS,ICORE(I000),
     &             ICORE(I020),ICORE(I030),ICORE(I040))
       CALL GETLST(ICORE(I020),1,1,1,1,91)
       IOFFT=I010
       IOFFF=I020
       IOFFZ=I000
C
C SET UP T(Kam,I)*F(IJ) MULTIPLICATION
C
       DO 130 IRREPI=1,NIRREP
        IRREPJ=IRREPI
        IRREPM=DIRPRD(IRREPI,IRREP)
        NSUM=POP(IRREPI,1)
        NCOL=POP(IRREPJ,1)
        NROW=NUMDIS*POP(IRREPM,2)
        CALL XGEMM('N','N',NROW,NCOL,NSUM,ONEM,ICORE(IOFFT),NROW,
     &             ICORE(IOFFF),NSUM,ONE,ICORE(IOFFZ),NROW)
        IOFFT=IOFFT+NROW*NCOL*IINTFP
        IOFFF=IOFFF+NSUM*NCOL*IINTFP
        IOFFZ=IOFFZ+NROW*NCOL*IINTFP
130    CONTINUE 
C
C NOW TRANSPOSE TARGET BACK TO Z(Ij,Ka)
C
       CALL SYMTR1(IRREP,POP(1,2),POP(1,1),NUMDIS,ICORE(I000),
     &             ICORE(I020),ICORE(I030),ICORE(I040))
       CALL TRANSP(ICORE(I000),ICORE(I010),DISSIZ,NUMDIS)
C
       CALL FSGET(ICORE(I000),1,NUMDIS,1,IRREP,LISTZ,'NNAN') 
       CALL SAXPY(NUMDIS*DISSIZ,ONE,ICORE(I010),1,ICORE(I000),1)
       CALL FSPUT(ICORE(I000),1,NUMDIS,1,IRREP,LISTZ,'NNAN') 
C
C
C DO SPIN CASE BABA -> - T(Im,Ak) * f(mj) - T(Mj,Ak) * f(MI) [ISPIN=2]
C
C
       IF(.NOT.RHF)THEN
C 
        LISTT=98
        LISTZ=198
        NUMDIS=FSDPDNA(IRREP,ISYTYP(2,LISTT))
        DISSIZ=IRPDPD (IRREP,ISYTYP(1,LISTT))
        I000=1
        I010=I000+IINTFP*NUMDIS*DISSIZ
        I020=I010+IINTFP*NUMDIS*DISSIZ
        I030=I020+IINTFP*MAX(NFMI(2),NFMI(1),NUMDIS,DISSIZ)
        I040=I030+IINTFP*MAX(NUMDIS,DISSIZ)
        I050=I040+IINTFP*MAX(NUMDIS,DISSIZ)
C
C READ T(Im,Ak) AND TRANSPOSE TO T(Ak,Im)
C
        CALL FSGET(ICORE(I000),1,NUMDIS,1,IRREP,LISTT,'NNNA')
        CALL TRANSP(ICORE(I000),ICORE(I010),NUMDIS,DISSIZ)
        CALL GETLST(ICORE(I020),1,1,1,2,91)
        IOFFT=I010
        IOFFF=I020
        IOFFZ=I000
C
C SET UP T(AkI,m)*F(mj) MULTIPLICATION
C
        DO 140 IRREPM=1,NIRREP
         IRREPJ=IRREPM
         IRREPI=DIRPRD(IRREPM,IRREP)
         NSUM=POP(IRREPM,2)
         NCOL=POP(IRREPJ,2)
         NROW=NUMDIS*POP(IRREPI,1)
         CALL XGEMM('N','N',NROW,NCOL,NSUM,ONEM,ICORE(IOFFT),NROW,
     &              ICORE(IOFFF),NSUM,ZILCH,ICORE(IOFFZ),NROW)
         IOFFT=IOFFT+NROW*NCOL*IINTFP
         IOFFF=IOFFF+NSUM*NCOL*IINTFP
         IOFFZ=IOFFZ+NROW*NCOL*IINTFP
140     CONTINUE 
C
C NOW TRANSPOSE T(Ak,Ij) AND Z(Ak,Ij) TO (Ak,jI)
C
        CALL SYMTR1(IRREP,POP(1,1),POP(1,2),NUMDIS,ICORE(I010),
     &              ICORE(I020),ICORE(I030),ICORE(I040))
        CALL SYMTR1(IRREP,POP(1,1),POP(1,2),NUMDIS,ICORE(I000),
     &              ICORE(I020),ICORE(I030),ICORE(I040))
        CALL GETLST(ICORE(I020),1,1,1,1,91)
        IOFFT=I010
        IOFFF=I020
        IOFFZ=I000
C
C SET UP T(Akm,I)*F(IJ) MULTIPLICATION
C
        DO 150 IRREPI=1,NIRREP
         IRREPJ=IRREPI
         IRREPM=DIRPRD(IRREPI,IRREP)
         NSUM=POP(IRREPI,1)
         NCOL=POP(IRREPJ,1)
         NROW=NUMDIS*POP(IRREPM,2)
         CALL XGEMM('N','N',NROW,NCOL,NSUM,ONEM,ICORE(IOFFT),NROW,
     &              ICORE(IOFFF),NSUM,ONE,ICORE(IOFFZ),NROW)
         IOFFT=IOFFT+NROW*NCOL*IINTFP
         IOFFF=IOFFF+NSUM*NCOL*IINTFP
         IOFFZ=IOFFZ+NROW*NCOL*IINTFP
150     CONTINUE 
C
C NOW TRANSPOSE TARGET BACK TO Z(Ij,Ak)
C
        CALL SYMTR1(IRREP,POP(1,2),POP(1,1),NUMDIS,ICORE(I000),
     &              ICORE(I020),ICORE(I030),ICORE(I040))
        CALL TRANSP(ICORE(I000),ICORE(I010),DISSIZ,NUMDIS)
C
        CALL FSGET(ICORE(I000),1,NUMDIS,1,IRREP,LISTZ,'NNNA')
        CALL SAXPY(NUMDIS*DISSIZ,ONE,ICORE(I010),1,ICORE(I000),1)
        CALL FSPUT(ICORE(I000),1,NUMDIS,1,IRREP,LISTZ,'NNNA')
C
       ENDIF
10    CONTINUE
C
      RETURN
      END        
