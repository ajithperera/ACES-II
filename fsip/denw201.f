      SUBROUTINE DENW201(EVAL,T2,IUHF,NBAS)
C
C DIVIDE DOUBLE AMPLITUDES BY ENERGY DENOMINATORS AND
C WRITE TO WORKING AMPLITUDE LISTS
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL RHF
      INTEGER POP,VRT,DIRPRD,A,B,C,DISSYT
      DIMENSION T2(1),EVAL(2*NBAS),IOFFOCC(8,2),IOFFVRT(8,2)
      CHARACTER*8 LABEL(2)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /INFO/ NOCCO(2),NVRTO(2)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPX(255,2),DIRPRD(8,8)
      DATA LABEL /'SCFEVALA','SCFEVALB'/
      DATA ZILCH /0.0/
C
      RHF=IUHF.EQ.0
C
C CALCULATE OFFSETS FOR EIGENVALUES
C
      IOFF0=0
      DO 5 ISPIN=1,2
       IOFFOCC(1,ISPIN)=IOFF0
       IOFFVRT(1,ISPIN)=IOFF0+NOCCO(ISPIN)
       DO 6 IRREP=2,NIRREP
        IOFFOCC(IRREP,ISPIN)=IOFFOCC(IRREP-1,ISPIN)+
     &                       POP(IRREP-1,ISPIN)
        IOFFVRT(IRREP,ISPIN)=IOFFVRT(IRREP-1,ISPIN)+
     &                       VRT(IRREP-1,ISPIN)
6      CONTINUE
       IOFF0=IOFF0+NBAS
5     CONTINUE
C
C READ IN EIGENVALUES
C
      IA=1
      IB=1+NBAS
      CALL GETREC(20,'JOBARC',LABEL(1),IINTFP*NBAS,EVAL(IA))
      IF(IUHF.NE.0)THEN
       CALL GETREC(20,'JOBARC',LABEL(2),IINTFP*NBAS,EVAL(IB))
      ELSE
       CALL SCOPY (NBAS,EVAL(IA),1,EVAL(IB),1)
      ENDIF
C
C ABAB SPIN CASE
C
      DO 10 IRREP=1,NIRREP
       ITHRU=0
       LISTT=99
       NUMDST=IRPDPD(IRREP,ISYTYP(2,LISTT))
       DISSYT=IRPDPD(IRREP,ISYTYP(1,LISTT))
       CALL GETLST(T2,1,NUMDST,1,IRREP,LISTT+100)
       DO 20 IRREPR2=1,NIRREP
        IRREPL2=DIRPRD(IRREP,IRREPR2)
        NUMA=VRT(IRREPR2,2)
        NUMK=POP(IRREPL2,1)
        DO 21 A=1,NUMA
         DO 22 K=1,NUMK
          DO 30 IRREPR1=1,NIRREP
           IRREPL1=DIRPRD(IRREPR1,IRREP)
           NUMJ=POP(IRREPR1,2)
           NUMI=POP(IRREPL1,1)
           DO 31 J=1,NUMJ
            DO 32 I=1,NUMI
             ITHRU=ITHRU+1
             IOFFI=IOFFOCC(IRREPL1,1)+I
             IOFFJ=IOFFOCC(IRREPR1,2)+J
             IOFFK=IOFFOCC(IRREPL2,1)+K
             IOFFA=IOFFVRT(IRREPR2,2)+A
             Z=EVAL(IOFFI)+EVAL(IOFFJ)-EVAL(IOFFK)-EVAL(IOFFA)
             IF(Z.NE.ZILCH)T2(ITHRU)=T2(ITHRU)/Z
32          CONTINUE
31         CONTINUE
30        CONTINUE
22       CONTINUE
21      CONTINUE
20     CONTINUE
       CALL PUTLST(T2,1,NUMDST,1,IRREP,LISTT)
10    CONTINUE 
C
      IF(IUHF.EQ.0)RETURN
C
C BABA SPIN CASE (UHF ONLY)
C
      DO 110 IRREP=1,NIRREP
       ITHRU=0
       LISTT=98
       NUMDST=IRPDPD(IRREP,ISYTYP(2,LISTT))
       DISSYT=IRPDPD(IRREP,ISYTYP(1,LISTT))
       CALL GETLST(T2,1,NUMDST,1,IRREP,LISTT+100)
       DO 120 IRREPR2=1,NIRREP
        IRREPL2=DIRPRD(IRREP,IRREPR2)
        NUMK=POP(IRREPR2,2)
        NUMA=VRT(IRREPL2,1)
        DO 121 K=1,NUMK
         DO 122 A=1,NUMA
          DO 130 IRREPR1=1,NIRREP
           IRREPL1=DIRPRD(IRREPR1,IRREP)
           NUMJ=POP(IRREPR1,2)
           NUMI=POP(IRREPL1,1)
           DO 131 J=1,NUMJ
            DO 132 I=1,NUMI
             IOFFI=IOFFOCC(IRREPL1,1)+I
             IOFFJ=IOFFOCC(IRREPR1,2)+J
             IOFFK=IOFFOCC(IRREPR2,2)+K
             IOFFA=IOFFVRT(IRREPL2,1)+A
             Z=EVAL(IOFFI)+EVAL(IOFFJ)-EVAL(IOFFK)-EVAL(IOFFA)
             ITHRU=ITHRU+1
             IF(Z.NE.ZILCH)T2(ITHRU)=T2(ITHRU)/Z
132          CONTINUE
131         CONTINUE
130        CONTINUE
122       CONTINUE
121      CONTINUE
120     CONTINUE
        CALL PUTLST(T2,1,NUMDST,1,IRREP,LISTT)
110    CONTINUE 
C
C AAAA AND BBBB SPIN CASES
C
      DO 201 ISPIN=1,2
       DO 210 IRREP=1,NIRREP
        ITHRU=0
        LISTT=95+ISPIN
        NUMDST=IRPDPD(IRREP,ISYTYP(2,LISTT))
        DISSYT=IRPDPD(IRREP,ISYTYP(1,LISTT))
        CALL GETLST(T2,1,NUMDST,1,IRREP,LISTT+100)
        DO 220 IRREPR2=1,NIRREP
         IRREPL2=DIRPRD(IRREP,IRREPR2)
         NUMK=POP(IRREPL2,ISPIN)
         NUMA=VRT(IRREPR2,ISPIN)
         DO 221 A=1,NUMA
          DO 222 K=1,NUMK
           DO 230 IRREPR1=1,NIRREP
            IRREPL1=DIRPRD(IRREPR1,IRREP)
            IF(IRREPL1.LE.IRREPR1)THEN
             NUMJ=POP(IRREPR1,ISPIN)
             NUMI=POP(IRREPL1,ISPIN)
             DO 231 J=1,NUMJ
              IF(IRREPL1.EQ.IRREPR1)THEN
               ITOP=J-1
              ELSE
               ITOP=NUMI
              ENDIF
              DO 232 I=1,ITOP
               IOFFI=IOFFOCC(IRREPL1,ISPIN)+I
               IOFFJ=IOFFOCC(IRREPR1,ISPIN)+J
               IOFFK=IOFFOCC(IRREPL2,ISPIN)+K
               IOFFA=IOFFVRT(IRREPR2,ISPIN)+A
               Z=EVAL(IOFFI)+EVAL(IOFFJ)-EVAL(IOFFK)-EVAL(IOFFA)
               ITHRU=ITHRU+1
               IF(Z.NE.ZILCH)T2(ITHRU)=T2(ITHRU)/Z
232           CONTINUE
231          CONTINUE
            ENDIF 
230        CONTINUE
222       CONTINUE
221      CONTINUE
220     CONTINUE
        CALL PUTLST(T2,1,NUMDST,1,IRREP,LISTT)
210    CONTINUE 
201   CONTINUE
C
      RETURN
      END
