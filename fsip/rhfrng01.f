      
      SUBROUTINE RHFRNG01(ICORE,MAXCOR,IUHF)
C
C THIS ROUTINE CALCULATES THE FOLLOWING CONTRIBUTION TO THE
C DOUBLES AMPLITUDES FOR THE (0,1) FOCK SPACE SECTOR
C
C
C    Z(Ij,Ka) =  [2 T(Im,Ke) - T(Mi,Ke) ] * W(Ma,Ej) 
C      NN AN           NN AN      NN AN        NN NN 
C
C               - T(Im,Ke) * W(Ma,eJ)
C                   NN AN      NN NN 
C
C               - T(Mj,Ke) * W(Ma,eI)
C                   NN AN      NN NN 
C
C THIS CODE IS APPRORIATE ONLY FOR RHF REFERENCE CASES AND IS
C THE OPTIMAL SPIN ADAPTED IMPLEMENTATION OF THIS TERM.
C
CEND
      IMPLICIT INTEGER (A-H,O-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH
      LOGICAL RHF
      DIMENSION ICORE(MAXCOR)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /FSSYMPOP/ FSDPDAN(8,22),FSDPDNA(8,22),FSDPDAA(8,22),
     &                  FSDPDIN(8,22),FSDPDNI(8,22),FSDPDII(8,22),
     &                  FSDPDAI(8,22),FSDPDIA(8,22)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /FSSYM/ POPA(8,2),POPI(8,2),VRTA(8,2),VRTI(8,2),
     &               NTAN(2),NTNA(2),NTAA(2),
     &               NTIN(2),NTNI(2),NTII(2),
     &               NTAI(2),NTIA(2),
     &               NF1AN(2),NF1AA(2),NF1IN(2),NF1II(2),NF1AI(2),
     &               NF2AN(2),NF2AA(2),NF2IN(2),NF2II(2),NF2AI(2)
C
      DATA ONE/1.0/, ONEM/-1.0/, ZILCH /0.0/
C
C
C
C
      DO 10 IRREP=1,NIRREP
       LISTT=4
       LISTW=56
       LISTZ=4
       NUMDSW=IRPDPD (IRREP,ISYTYP(2,LISTW))
       DISSYW=IRPDPD (IRREP,ISYTYP(1,LISTW))
       NUMDST=IRPDPD (IRREP,ISYTYP(2,LISTT))
       DISSYT=FSDPDNA(IRREP,ISYTYP(1,LISTT))
       MAXT=MAX(DISSYT,NUMDST)
       I000=1
       I010=I000+IINTFP*NUMDSW*DISSYW
       I020=I010+IINTFP*NUMDST*DISSYT
       I030=I020+IINTFP*NUMDST*DISSYT
       I040=I030+IINTFP*MAXT
       I050=I040+IINTFP*MAXT
       I060=I050+IINTFP*MAXT
C
C READ THE W(Ma,Ej) ELEMENTS INTO W(EM,aj)
C
       CALL GETLST(ICORE(I000),1,NUMDSW,1,IRREP,LISTW)
C
C READ THE 2T(Im,Ke)-T(Mi,Ke) INTO T(IK,ME) [THESE ARE STORED IN
C SPIN ADAPTED FORM]
C
       CALL FSGET (ICORE(I010),1,NUMDST,1,IRREP,LISTT,'NANN')
C
C NOW TRANSPOSE KET INDICES  T(IK,ME) -> T(IK,EM)
C
       CALL SYMTR1(IRREP,POP(1,1),VRT(1,1),DISSYT,ICORE(I010),
     &             ICORE(I030),ICORE(I040),ICORE(I050))
C
C NOW DO MULTIPLICATION -   Z(IK,aj) = T(IK,EM) * W(EM,aj).
C                             NA NN      NA NN      NN NN
C
       NSUM=DISSYW
       NROW=DISSYT
       NCOL=NUMDSW
       CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(I010),NROW,
     &            ICORE(I000),NSUM,ZILCH,ICORE(I020),NROW)
C
C WRITE INCREMENTS TO ***LIST 4*** WHICH IS NO LONGER NEEDED AND
C WILL BE RESORTED.
C
       CALL SYMTR1(IRREP,VRT(1,1),POP(1,1),DISSYT,ICORE(I020),
     &             ICORE(I030),ICORE(I040),ICORE(I050))
       CALL FSPUT(ICORE(I020),1,NUMDST,1,IRREP,LISTZ,'NANN')
10    CONTINUE
C
C NOW RESORT INCREMENTS AND ADD TO NORMAL INCREMENT LIST
C
      NSIZE=ISYMSZ(ISYTYP(1,4),ISYTYP(2,4))
      I000=1
      I010=I000+IINTFP*NSIZE
      I020=I010+IINTFP*NSIZE
      CALL GETALL(ICORE(I000),NSIZE,1,4)
      CALL SSTGEN(ICORE(I000),ICORE(I010),NSIZE,POP(1,1),POP(1,1),
     &            POP(1,1),VRT(1,1),ICORE(I020),1,'1324')
      CALL GETALL(ICORE(I000),NSIZE,1,199)
      CALL SAXPY (NSIZE,ONE,ICORE(I010),1,ICORE(I000),1)
      CALL PUTALL(ICORE(I000),NSIZE,1,199)
C
C               - T(Im,Ke) * W(Ma,eJ)
C                   NN AN      NN NN 
C
      CALL GETALL(ICORE(I000),NSIZE,1,99)
      CALL SSTGEN(ICORE(I000),ICORE(I010),NSIZE,POP(1,1),POP(1,1),
     &            POP(1,1),VRT(1,1),ICORE(I020),1,'1324')
      CALL PUTALL(ICORE(I010),NSIZE,1,4)
      DO 11 IRREP=1,NIRREP
       LISTT=4
       LISTW=58
       LISTZ=4
       NUMDSW=IRPDPD (IRREP,ISYTYP(2,LISTW))
       DISSYW=IRPDPD (IRREP,ISYTYP(1,LISTW))
       NUMDST=IRPDPD (IRREP,ISYTYP(2,LISTT))
       DISSYT=FSDPDNA(IRREP,ISYTYP(1,LISTT))
       MAXT=MAX(DISSYT,NUMDST)
       I000=1
       I010=I000+IINTFP*NUMDSW*DISSYW
       I020=I010+IINTFP*NUMDST*DISSYT
       I030=I020+IINTFP*NUMDST*DISSYT
       I040=I030+IINTFP*MAXT
       I050=I040+IINTFP*MAXT
       I060=I050+IINTFP*MAXT
C
C READ THE W(Ma,Je) ELEMENTS INTO W(eM,aJ)
C
       CALL GETLST(ICORE(I000),1,NUMDSW,1,IRREP,LISTW)
C
C READ THE T(Im,Ke) INTO T(IK,ME)
C
       CALL FSGET (ICORE(I010),1,NUMDST,1,IRREP,LISTT,'NANN')
C
C NOW TRANSPOSE KET INDICES  T(IK,ME) -> T(IK,EM)
C
       CALL SYMTR1(IRREP,POP(1,1),VRT(1,1),DISSYT,ICORE(I010),
     &             ICORE(I030),ICORE(I040),ICORE(I050))
C
C NOW DO MULTIPLICATION -   Z(IK,aj) = T(IK,EM) * W(EM,aj).
C                             NA NN      NA NN      NN NN
C
       NSUM=DISSYW
       NROW=DISSYT
       NCOL=NUMDSW
       CALL XGEMM('N','N',NROW,NCOL,NSUM,ONEM,ICORE(I010),NROW,
     &            ICORE(I000),NSUM,ZILCH,ICORE(I020),NROW)
C
C WRITE INCREMENTS TO ***LIST 4*** WHICH IS NO LONGER NEEDED AND
C WILL BE RESORTED.
C
       CALL SYMTR1(IRREP,VRT(1,1),POP(1,1),DISSYT,ICORE(I020),
     &             ICORE(I030),ICORE(I040),ICORE(I050))
       CALL FSPUT(ICORE(I020),1,NUMDST,1,IRREP,LISTZ,'NANN')
11    CONTINUE
C
C NOW RESORT INCREMENTS AND ADD TO NORMAL INCREMENT LIST
C
      NSIZE=ISYMSZ(ISYTYP(1,4),ISYTYP(2,4))
      I000=1
      I010=I000+IINTFP*NSIZE
      I020=I010+IINTFP*NSIZE
      CALL GETALL(ICORE(I000),NSIZE,1,4)
      CALL SSTGEN(ICORE(I000),ICORE(I010),NSIZE,POP(1,1),POP(1,1),
     &            POP(1,1),VRT(1,1),ICORE(I020),1,'1324')
      CALL GETALL(ICORE(I000),NSIZE,1,199)
      CALL SAXPY (NSIZE,ONE,ICORE(I010),1,ICORE(I000),1)
      CALL PUTALL(ICORE(I000),NSIZE,1,199)
C
C OTHER TERMS - FIRST GENERATE NECESSARY RESORTED AMPLITUDES
C      
C              T(Ij,Ka) -> T(Ia,Kj)
C
      CALL NEWTYP(4,18,14,.TRUE.)
      CALL GETALL(ICORE(I000),NSIZE,1,99)
      CALL SSTGEN(ICORE(I000),ICORE(I010),NSIZE,POP(1,1),POP(1,1),
     &            POP(1,1),VRT(1,1),ICORE(I020),1,'1432')
      CALL PUTALL(ICORE(I010),NSIZE,1,4)
C
      DO 12 IRREP=1,NIRREP
C
C           - T(Mj,Ke) * W(Ma,eI)
C               NN AN      NN NN 
C
       LISTT=4
       LISTW=58
       LISTZ=4
       NUMDSW=IRPDPD (IRREP,ISYTYP(2,LISTW))
       DISSYW=IRPDPD (IRREP,ISYTYP(1,LISTW))
       NUMDST=FSDPDAN(IRREP,ISYTYP(2,LISTT))
       DISSYT=IRPDPD (IRREP,ISYTYP(1,LISTT))
       MAXT=MAX(DISSYT,NUMDST,NUMDSW,DISSYW)
       I000=1
       I010=I000+IINTFP*NUMDSW*DISSYW
       I020=I010+IINTFP*NUMDST*DISSYT
       I030=I020+IINTFP*NUMDST*DISSYT
       I040=I030+IINTFP*MAXT
       I050=I040+IINTFP*MAXT
       I060=I050+IINTFP*MAXT
C
C READ THE W(Ma,eI) INTO W(eM,aI)
C
       CALL GETLST(ICORE(I000),1,NUMDSW,1,IRREP,LISTW)
C
C READ THE T(Mj,Ke) ELEMENTS INTO T(Me,Kj)
C
       CALL FSGET(ICORE(I010),1,NUMDST,1,IRREP,LISTT,'NNAN')
C
C NOW TRANSPOSE BRA INDICES  T(Me,Kj) -> T(eM,Kj)
C
       CALL SYMTR3(IRREP,POP(1,1),VRT(1,1),DISSYT,NUMDST,
     &             ICORE(I010),ICORE(I030),ICORE(I040),ICORE(I050))
C
C                                             +
C NOW DO MULTIPLICATION -   Z(aI,Kj) = W(eM,aI) * T(eM,Kj)
C                             NN AN      NN NN      NN AN
C
       NSUM=DISSYW
       NROW=NUMDSW
       NCOL=NUMDST
       CALL XGEMM('T','N',NROW,NCOL,NSUM,ONEM,ICORE(I000),NROW,
     &            ICORE(I010),NSUM,ZILCH,ICORE(I020),NROW)
C
C NOW REORDER THE INCREMENTS Z(aI,Kj)->Z(Ia,Kj)
C
       CALL SYMTR3(IRREP,VRT(1,1),POP(1,1),DISSYT,NUMDST,
     &             ICORE(I020),ICORE(I030),ICORE(I040),ICORE(I050))
C
C WRITE INCREMENTS TO ***LIST 4***.  THEY WILL BE RESORTED AND
C SYMMETRIZED LATER.
C
       CALL FSPUT(ICORE(I020),1,NUMDST,1,IRREP,LISTZ,'NNAN')
C
12    CONTINUE
C
C NOW RESORT INCREMENTS AND ADD TO NORMAL INCREMENT LIST
C
      NSIZE=ISYMSZ(ISYTYP(1,4),ISYTYP(2,4))
      I000=1
      I010=I000+IINTFP*NSIZE
      I020=I010+IINTFP*NSIZE
      CALL GETALL(ICORE(I000),NSIZE,1,4)
      CALL SSTGEN(ICORE(I000),ICORE(I010),NSIZE,POP(1,1),VRT(1,1),
     &            POP(1,1),POP(1,1),ICORE(I020),1,'1432')
      CALL GETALL(ICORE(I000),NSIZE,1,199)
      CALL SAXPY (NSIZE,ONE,ICORE(I010),1,ICORE(I000),1)
      CALL PUTALL(ICORE(I000),NSIZE,1,199)
      CALL NEWTYP(4,21,17,.TRUE.)
C
      RETURN
      END
