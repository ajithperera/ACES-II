
        
      SUBROUTINE RST01(ICORE,MAXCOR,IUHF)
C
C THIS ROUTINE RESORTS THE T2 AMPLITUDES FOR THE 0,1
C SECTOR.  THESE ARE STORED AS FOLLOWS:
C
C LIST 96:    T2(IJ,ZA)       I<J ; Z,A    AAAA
C      97:    T2(ij,za)       i<j ; z,a    BBBB
C      98:    T2(Ij,Az)       I,j ; A,z    BABA
C      99:    T2(Ij,Za)       I,j ; Z,a    ABAB
C
C FROM THESE, THE FOLLOWING RESORTED LISTS ARE GENERATED
C
C      1 :    T2(IJ,ZA)       I,Z ; J,A    AAAA
C      2 :    T2(ij,za)       i,z ; j,a    BBBB
C      3 :    T2(Ij,Az)       I,A ; j,z    BABA
C      4 :    T2(Ij,Za)       I,Z ; j,a    ABAB
C
C Z IS AN OCCUPIED INDEX.  FOR THE (0,1) AMPLITUDES, THE LISTS
C ARE ACTUALLY STORED FOR ALL OCCUPIED ORBITALS.  RESTRICTION TO
C ACTIVE ORBITALS ONLY IS HANDLED IMPLICITLY THROUGH FS IO ROUTINES.
C
CEND
      IMPLICIT INTEGER (A-Z)
      LOGICAL RHF,SPINAD
      DIMENSION ICORE(MAXCOR)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
C
      RHF=IUHF.EQ.0
C
C DEAL WITH THE AAAA, BBBB AND BABA SPIN CASES FOR UHF REFERENCES
C
      IF(.NOT.RHF)THEN
C
C LOOP OVER SPIN CASES
C
       DO 10 ISPIN=1,2
        LISTW1=95+ISPIN
        LISTW2=ISPIN
C
C SIZE OF FULL EXPANDED I,J ; Z,A LIST
C
        ISIZE=ISYMSZ(20+ISPIN,ISYTYP(2,LISTW1))
        I000=1
        I010=I000+IINTFP*ISIZE
        I020=I010+IINTFP*ISIZE
        IOFFW=I000
        IF(I020.GT.MAXCOR)CALL INSMEM('RST01',I020,MAXCOR)
        DO 100 IRREP=1,NIRREP
         NUMDIS= IRPDPD(IRREP,ISYTYP(2,LISTW1))
         DISSIZ= IRPDPD(IRREP,ISYTYP(1,LISTW1))
         DISSIZF=IRPDPD(IRREP,20+ISPIN)
C
C READ LIST AND EXPAND BRA INDICES.  COLLECT ALL IRREPS IN ICORE. 
C
         CALL GETLST (ICORE(IOFFW),1,NUMDIS,1,IRREP,LISTW1)
         CALL SYMEXP2(IRREP,POP(1,ISPIN),DISSIZF,DISSIZ,NUMDIS,
     &                ICORE(IOFFW),ICORE(IOFFW))
         IOFFW=IOFFW+NUMDIS*DISSIZF*IINTFP
100     CONTINUE
C
C RESORT AND DUMP.
C
        CALL SSTGEN(ICORE(I000),ICORE(I010),ISIZE,POP(1,ISPIN),
     &              POP(1,ISPIN),POP(1,ISPIN),VRT(1,ISPIN),
     &              ICORE(I020),1,'1324') 
        CALL PUTALL(ICORE(I010),ISIZE,1,LISTW2)
10     CONTINUE
C
C DO BABA SPIN CASE (ONLY FOR UHF REFERENCE)
C
C                  I,j ; A,z -> I,A ; j,z
C
       LISTW1=98
       LISTW2=3
       ISIZE=ISYMSZ(ISYTYP(1,LISTW1),ISYTYP(2,LISTW1))
       I000=1
       I010=I000+IINTFP*ISIZE
       I020=I010+IINTFP*ISIZE
       IF(I020.GT.MAXCOR)CALL INSMEM('RST01',I020,MAXCOR)
       CALL GETALL(ICORE(I000),ISIZE,1,LISTW1)
       CALL SSTGEN(ICORE(I000),ICORE(I010),ISIZE,POP(1,1),
     &             POP(1,2),VRT(1,1),POP(1,2),ICORE(I020),1,'1324')
       CALL PUTALL(ICORE(I010),ISIZE,1,LISTW2)
      ENDIF
C
C DO ABAB SPIN CASE FOR BOTH UHF AND RHF REFERENCES.  SPIN ADAPT BEFORE
C DEPOSITING FOR NOW
C
C                  I,j ; Z,a -> I,Z ; j,a
C
      LISTW1=99
      LISTW2=4
      ISIZE=ISYMSZ(ISYTYP(1,LISTW1),ISYTYP(2,LISTW1))
      I000=1
      I010=I000+IINTFP*ISIZE
      I020=I010+IINTFP*ISIZE
      IF(I020.GT.MAXCOR)CALL INSMEM('RST01',I020,MAXCOR)
      CALL GETALL (ICORE(I000),ISIZE,1,LISTW1)
      spinad=rhf
      IF(SPINAD)THEN
       IOFF=I000
       DO 20 IRREP=1,NIRREP
        NUM=IRPDPD(IRREP,ISYTYP(2,LISTW1))
        DIS=IRPDPD(IRREP,ISYTYP(1,LISTW1))
        I030=I020+MAX(NUM,DIS)*IINTFP
        I040=I030+MAX(NUM,DIS)*IINTFP
        I050=I040+MAX(NUM,DIS)*IINTFP
        CALL SPINAD3(IRREP,POP(1,1),DIS,NUM,ICORE(IOFF),
     &               ICORE(I020),ICORE(I030))
        IOFF=IOFF+NUM*DIS*IINTFP
20     CONTINUE
      ENDIF
      CALL SSTGEN(ICORE(I000),ICORE(I010),ISIZE,POP(1,1),
     &            POP(1,2),POP(1,1),VRT(1,2),ICORE(I020),1,'1324')
      CALL PUTALL(ICORE(I010),ISIZE,1,LISTW2)
C
C ALL DONE, RETURN
C
      RETURN
      END
