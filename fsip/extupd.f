      SUBROUTINE EXTUPD(ICORE,MAXCOR,IUHF,TAU,IORDER,SINGLE,E,
     &                  RLETYP,MAXORD)
C
C THIS ROUTINE FORMS AN EXTRAPOLATED T VECTOR ACCORDING TO THE
C TAU VECTOR.
C
C   T(new) = [1 - SUM TAU(k)] T(1) + SUM TAU(k) * T(k)
C                   k                  k
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION Z,COEFF,TAU,E,SDOT,Z0
      LOGICAL SINGLE,NONHF
      DIMENSION ICORE(MAXCOR),TAU(IORDER)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &                DIRPRD(8,8)
      COMMON /FSSYMPOP/ FSDPDAN(8,22),FSDPDNA(8,22),FSDPDAA(8,22),
     &                  FSDPDIN(8,22),FSDPDNI(8,22),FSDPDII(8,22),
     &                  FSDPDAI(8,22),FSDPDIA(8,22)
      COMMON /FSSYM/ POPA(8,2),POPI(8,2),VRTA(8,2),VRTI(8,2),
     &               NTAN(2),NTNA(2),NTAA(2),
     &               NTIN(2),NTNI(2),NTII(2),
     &               NTAI(2),NTIA(2),
     &               NF1AN(2),NF1AA(2),NF1IN(2),NF1II(2),NF1AI(2),
     &               NF2AN(2),NF2AA(2),NF2IN(2),NF2II(2),NF2AI(2)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON /EXTRAP/ NSIZET(4)
      COMMON /FLAGS/ IFLAGS(100)
      COMMON /EXTIO/ IOLDEST
C
      IGET(I)=1+MOD(IOLDEST+MAXORD-I,MAXORD+1)
C
C
C FORM  Z= 1 - SUM TAU(k)
C                k
C
      Z=1.0
      DO 10 I=1,IORDER
       Z=Z-TAU(I)
10    CONTINUE
      DO 100 ISPIN=4,4-3*IUHF,-1
       SIZET=NSIZET(ISPIN)
       I000=1
       I010=I000+IINTFP*SIZET
       I020=I010+IINTFP*SIZET
       NLIST=95+ISPIN
       CALL IZERO(ICORE(I000),SIZET*IINTFP)
       DO 20 I=1,IORDER
        COEFF=TAU(I)
        CALL GETLST(ICORE(I010),IGET(I),1,1,ISPIN,77)
        CALL SAXPY(SIZET,COEFF,ICORE(I010),1,ICORE(I000),1)
20     CONTINUE
       IF(RLETYP.NE.2)THEN
        CALL PUTLST(ICORE(I000),IGET(1),1,1,ISPIN,77)
       ELSE
        CALL PUTLST(ICORE(I000),IGET(1),1,1,ISPIN+4,77)
       ENDIF
C
C NOW OVERWRITE LISTS 95-99 WITH THE UPDATED T2 VECTOR, AND THE
C   T1 LISTS AS WELL.
C
       IOFF=1
       DO 30 IRREP=1,NIRREP
        DISSIZ=IRPDPD (IRREP,ISYTYP(1,NLIST))
        IF(ISPIN.NE.3)THEN
         NUMDIS=FSDPDAN(IRREP,ISYTYP(2,NLIST))
         CALL FSPUT(ICORE(IOFF),1,NUMDIS,1,IRREP,NLIST,'NNAN')
        ELSE
         NUMDIS=FSDPDNA(IRREP,ISYTYP(2,NLIST))
         CALL FSPUT(ICORE(IOFF),1,NUMDIS,1,IRREP,NLIST,'NNNA')
        ENDIF 
        IOFF=IOFF+NUMDIS*DISSIZ*IINTFP
30     CONTINUE
       IF(ISPIN.LT.3.OR.IUHF.EQ.0)THEN
        CALL FSPUTT1(ICORE(IOFF),MIN(1+IUHF,ISPIN),94,
     &               'AI',20+MIN(1+IUHF,ISPIN))
       ENDIF
100   CONTINUE
      RETURN
      END 
