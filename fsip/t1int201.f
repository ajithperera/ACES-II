      SUBROUTINE T1INT201(ICORE,MAXCOR,IUHF)
C
C THIS ROUTINE CALCULATES THE FOLLOWING CONTRIBUTIONS TO THE
C DOUBLES AMPLITUDES FOR THE (0,1) FOCK SPACE SECTOR
C
C
C    Z(ij,ka) = - W(ij,ma) * T1(mk)
C      NN AN        NN AI       IA
C
CEND
      IMPLICIT INTEGER (A-H,O-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH
      LOGICAL RHF
      DIMENSION ICORE(MAXCOR)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /FSSYMPOP/ FSDPDAN(8,22),FSDPDNA(8,22),FSDPDAA(8,22),
     &                  FSDPDIN(8,22),FSDPDNI(8,22),FSDPDII(8,22),
     &                  FSDPDAI(8,22),FSDPDIA(8,22)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /FSSYM/ POPA(8,2),POPI(8,2),VRTA(8,2),VRTI(8,2),
     &               NTAN(2),NTNA(2),NTAA(2),
     &               NTIN(2),NTNI(2),NTII(2),
     &               NTAI(2),NTIA(2),
     &               NF1AN(2),NF1AA(2),NF1IN(2),NF1II(2),NF1AI(2),
     &               NF2AN(2),NF2AA(2),NF2IN(2),NF2II(2),NF2AI(2)
C
      DATA ONE/1.0/, ONEM/-1.0/, ZILCH /0.0/
C
      RHF=IUHF.EQ.0
C
      DO 10 IRREP=1,NIRREP
C
C SPIN CASE ABAB -> W(Ij,Ma)*T1(KM)
C                     NN IN     AI
C
       LISTW =110
       LISTZ =199
       NUMDSW=FSDPDIN(IRREP,ISYTYP(2,LISTW))
       DISSYW=IRPDPD (IRREP,ISYTYP(1,LISTW))
       NUMDSZ=FSDPDAN(IRREP,ISYTYP(2,LISTZ))
       DISSYZ=IRPDPD (IRREP,ISYTYP(1,LISTZ))
       I000=1
       I010=I000+IINTFP*MAX(NUMDSW*DISSYW,NUMDSZ*DISSYZ)
       I020=I010+IINTFP*NUMDSZ*DISSYZ
       MAXW=MAX(NUMDSW,DISSYW,NUMDSZ,DISSYZ,NF1AI(1))
       I030=I020+IINTFP*MAXW
       I040=I030+IINTFP*MAXW
       I050=I040+IINTFP*MAXW
       CALL FSGET (ICORE(I000),1,NUMDSW,1,IRREP,LISTW,'NNIN')
       CALL SYMTR1(IRREP,POPI(1,1),VRT(1,2),DISSYW,ICORE(I000),
     &             ICORE(I020),ICORE(I030),ICORE(I040))
       CALL FSGETT1(ICORE(I020),1,94,'AI',21)
C
C                                  +
C DO MULTIPLICATION W(Ija,M)*T1(KM)  = Z(IjaK)
C                     NNN I     AI       NNNA
C
       IOFFT1=I020
       IOFFW =I000
       IOFFZ =I010
       DO 20 IRREPM=1,NIRREP
        IRREPK=IRREPM
        IRREPA=DIRPRD(IRREPM,IRREP)
        NROW=DISSYW*VRT(IRREPA,2)
        NCOL=POPA(IRREPK,1)
        NSUM=POPI(IRREPM,1)
        CALL XGEMM('N','T',NROW,NCOL,NSUM,ONEM,ICORE(IOFFW),NROW,
     &             ICORE(IOFFT1),NCOL,ZILCH,ICORE(IOFFZ),NROW)
        IOFFT1=IOFFT1+NCOL*NSUM*IINTFP
        IOFFW =IOFFW +NROW*NSUM*IINTFP
        IOFFZ =IOFFZ +NROW*NCOL*IINTFP
20     CONTINUE
       CALL SYMTR1(IRREP,VRT(1,2),POPA(1,1),DISSYZ,ICORE(I010),
     &             ICORE(I020),ICORE(I030),ICORE(I040))
       CALL FSGET(ICORE(I000),1,NUMDSZ,1,IRREP,LISTZ,'NNAN')
       CALL SAXPY(NUMDSZ*DISSYZ,ONE,ICORE(I010),1,ICORE(I000),1)
       CALL FSPUT(ICORE(I000),1,NUMDSZ,1,IRREP,LISTZ,'NNAN')
C
C SKIP REST FOR RHF
C
       IF(RHF)GOTO 10
C
C SPIN CASE BABA -> W(Ij,Am)*T1(km)
C                     NN NI     AI
C
       LISTW =109
       LISTZ =198
       NUMDSW=FSDPDNI(IRREP,ISYTYP(2,LISTW))
       DISSYW=IRPDPD (IRREP,ISYTYP(1,LISTW))
       NUMDSZ=FSDPDNA(IRREP,ISYTYP(2,LISTZ))
       DISSYZ=IRPDPD (IRREP,ISYTYP(1,LISTZ))
       I000=1
       I010=I000+IINTFP*MAX(NUMDSW*DISSYW,NUMDSZ*DISSYZ)
       I020=I010+IINTFP*NUMDSZ*DISSYZ
       I030=I020+IINTFP*NF1AI(2)
       CALL FSGET (ICORE(I000),1,NUMDSW,1,IRREP,LISTW,'NNNI')
       CALL FSGETT1(ICORE(I020),2,94,'AI',22)
C
C                                  +
C DO MULTIPLICATION W(IjA,m)*T1(km)  = Z(IjAk)
C                     NNN I     AI       NNNA
C
       IOFFT1=I020
       IOFFW =I000
       IOFFZ =I010
       DO 30 IRREPM=1,NIRREP
        IRREPK=IRREPM
        IRREPA=DIRPRD(IRREPM,IRREP)
        NROW=DISSYW*VRT(IRREPA,1)
        NCOL=POPA(IRREPK,2)
        NSUM=POPI(IRREPM,2)
        CALL XGEMM('N','T',NROW,NCOL,NSUM,ONEM,ICORE(IOFFW),NROW,
     &             ICORE(IOFFT1),NCOL,ZILCH,ICORE(IOFFZ),NROW)
        IOFFT1=IOFFT1+NCOL*NSUM*IINTFP
        IOFFW =IOFFW +NROW*NSUM*IINTFP
        IOFFZ =IOFFZ +NROW*NCOL*IINTFP
30     CONTINUE
       CALL FSGET(ICORE(I000),1,NUMDSZ,1,IRREP,LISTZ,'NNNA')
       CALL SAXPY(NUMDSZ*DISSYZ,ONE,ICORE(I010),1,ICORE(I000),1)
       CALL FSPUT(ICORE(I000),1,NUMDSZ,1,IRREP,LISTZ,'NNNA')
C
C SPIN CASES AAAA AND BBBB -> W(I<J,MA)*T1(KM)
C                               N N IN     AI
C
       DO 100 ISPIN=1,2
        LISTW =106+ISPIN
        LISTZ =195+ISPIN
        NUMDSW=FSDPDIN(IRREP,ISYTYP(2,LISTW))
        DISSYW=IRPDPD (IRREP,ISYTYP(1,LISTW))
        NUMDSZ=FSDPDAN(IRREP,ISYTYP(2,LISTZ))
        DISSYZ=IRPDPD (IRREP,ISYTYP(1,LISTZ))
        I000=1
        I010=I000+IINTFP*MAX(NUMDSW*DISSYW,NUMDSZ*DISSYZ)
        I020=I010+IINTFP*NUMDSZ*DISSYZ
        MAXW=MAX(NUMDSW,DISSYW,NUMDSZ,DISSYZ,NF1AI(ISPIN))
        I030=I020+IINTFP*MAXW
        I040=I030+IINTFP*MAXW
        I050=I040+IINTFP*MAXW
        CALL FSGET (ICORE(I000),1,NUMDSW,1,IRREP,LISTW,'NNIN')
        CALL SYMTR1(IRREP,POPI(1,ISPIN),VRT(1,ISPIN),DISSYW,ICORE(I000),
     &              ICORE(I020),ICORE(I030),ICORE(I040))
        CALL FSGETT1(ICORE(I020),ISPIN,94,'AI',20+ISPIN)
C
C                                  +
C DO MULTIPLICATION W(I<JA,M)*T1(KM)  = Z(I<JA,K)
C                     N NN I     AI       N NN A
C
        IOFFT1=I020
        IOFFW =I000
        IOFFZ =I010
        DO 40 IRREPM=1,NIRREP
         IRREPK=IRREPM
         IRREPA=DIRPRD(IRREPM,IRREP)
         NROW=DISSYW*VRT(IRREPA,ISPIN)
         NCOL=POPA(IRREPK,ISPIN)
         NSUM=POPI(IRREPM,ISPIN)
         CALL XGEMM('N','T',NROW,NCOL,NSUM,ONEM,ICORE(IOFFW),NROW,
     &              ICORE(IOFFT1),NCOL,ZILCH,ICORE(IOFFZ),NROW)
         IOFFT1=IOFFT1+NCOL*NSUM*IINTFP
         IOFFW =IOFFW +NROW*NSUM*IINTFP
         IOFFZ =IOFFZ +NROW*NCOL*IINTFP
40      CONTINUE
        CALL SYMTR1(IRREP,VRT(1,ISPIN),POPA(1,ISPIN),DISSYZ,
     &              ICORE(I010),
     &              ICORE(I020),ICORE(I030),ICORE(I040))
        CALL FSGET(ICORE(I000),1,NUMDSZ,1,IRREP,LISTZ,'NNAN')
        CALL SAXPY(NUMDSZ*DISSYZ,ONE,ICORE(I010),1,ICORE(I000),1)
        CALL FSPUT(ICORE(I000),1,NUMDSZ,1,IRREP,LISTZ,'NNAN')
C
100    CONTINUE
C
10    CONTINUE
C
      RETURN
      END
