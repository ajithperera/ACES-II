      SUBROUTINE FEACNT01(ICORE,MAXCOR,IUHF)
C
C THIS ROUTINE CALCULATES THE FOLLOWING CONTRIBUTION TO THE
C DOUBLES AMPLITUDES FOR THE (0,1) FOCK SPACE SECTOR
C
C
C    Z(ij,ka) = T(ij,ke) * F(ea)
C      NN AN      NN AN      NN       
C
C  
CEND
      IMPLICIT INTEGER (A-H,O-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH
      LOGICAL RHF
      DIMENSION ICORE(MAXCOR)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /FSSYMPOP/ FSDPDAN(8,22),FSDPDNA(8,22),FSDPDAA(8,22),
     &                  FSDPDIN(8,22),FSDPDNI(8,22),FSDPDII(8,22),
     &                  FSDPDAI(8,22),FSDPDIA(8,22)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /FSSYM/ POPA(8,2),POPI(8,2),VRTA(8,2),VRTI(8,2),
     &               NTAN(2),NTNA(2),NTAA(2),
     &               NTIN(2),NTNI(2),NTII(2),
     &               NTAI(2),NTIA(2),
     &               NF1AN(2),NF1AA(2),NF1IN(2),NF1II(2),NF1AI(2),
     &               NF2AN(2),NF2AA(2),NF2IN(2),NF2II(2),NF2AI(2)
C
      DATA ONE/1.0/, ONEM/-1.0/, ZILCH /0.0/
C
      RHF=IUHF.EQ.0
C
      DO 10 IRREP=1,NIRREP
C
       IF(RHF)THEN
C 
C CODE FOR RHF.  
C
        LISTT=99
        LISTZ=199
        NUMDIS=FSDPDAN(IRREP,ISYTYP(2,LISTT))
        DISSIZ=IRPDPD (IRREP,ISYTYP(1,LISTT))
        I000=1
        I010=I000+IINTFP*NUMDIS*DISSIZ
        I020=I010+IINTFP*NUMDIS*DISSIZ
        I030=I020+IINTFP*NFEA(1)
C
C READ T(ij,ke) AND F(ea)
C
        CALL FSGET(ICORE(I010),1,NUMDIS,1,IRREP,LISTT,'NNAN')
        CALL GETLST(ICORE(I020),1,1,1,1,92)
        IOFFT=I010
        IOFFF=I020
        IOFFZ=I000
C
C SET UP T(ijk,e)*F(ea) MULTIPLICATION
C
        DO 110 IRREPE=1,NIRREP 
         IRREPA=IRREPE
         IRREPK=DIRPRD(IRREPE,IRREP)
         NSUM=VRT(IRREPE,1)
         NCOL=VRT(IRREPA,1)
         NROW=DISSIZ*POPA(IRREPK,1)  
         CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFT),NROW,
     &             ICORE(IOFFF),NSUM,ZILCH,ICORE(IOFFZ),NROW)
         IOFFT=IOFFT+NROW*NCOL*IINTFP
         IOFFF=IOFFF+NSUM*NCOL*IINTFP
         IOFFZ=IOFFZ+NROW*NCOL*IINTFP
110     CONTINUE 
        CALL FSGET(ICORE(I010),1,NUMDIS,1,IRREP,LISTZ,'NNAN')
        CALL SAXPY(NUMDIS*DISSIZ,ONE,ICORE(I000),1,ICORE(I010),1)
        CALL FSPUT(ICORE(I010),1,NUMDIS,1,IRREP,LISTZ,'NNAN')
C
       ELSE
C 
C UHF CODE...
C
C
C DO SPIN CASES AAAA AND BBBB
C
        DO 100 ISPIN=1,2
         LISTT=95+ISPIN
         LISTZ=195+ISPIN
         NUMDIS=FSDPDAN(IRREP,ISYTYP(2,LISTT))
         DISSIZ=IRPDPD (IRREP,ISYTYP(1,LISTT))
         I000=1
         I010=I000+IINTFP*NUMDIS*DISSIZ
         I020=I010+IINTFP*NUMDIS*DISSIZ
         I030=I020+IINTFP*NFEA(ISPIN)
C
C READ T(I<J,KE) INTO T(I<J,KE) AND F(EA)
C
         CALL FSGET(ICORE(I010),1,NUMDIS,1,IRREP,LISTT,'NNAN')
         CALL GETLST(ICORE(I020),1,1,1,ISPIN,92)
         IOFFT=I010
         IOFFF=I020
         IOFFZ=I000
C
C SET UP T(I<JK,E)*F(EA) MULTIPLICATION
C
         DO 1100 IRREPE=1,NIRREP
          IRREPA=IRREPE
          IRREPK=DIRPRD(IRREPE,IRREP)
          NSUM=VRT(IRREPE,ISPIN)
          NCOL=VRT(IRREPA,ISPIN)
          NROW=DISSIZ*POPA(IRREPK,ISPIN)
          CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFT),NROW,
     &               ICORE(IOFFF),NSUM,ZILCH,ICORE(IOFFZ),NROW)
          IOFFT=IOFFT+NROW*NCOL*IINTFP
          IOFFF=IOFFF+NSUM*NCOL*IINTFP
          IOFFZ=IOFFZ+NROW*NCOL*IINTFP
1100     CONTINUE 
         CALL FSGET(ICORE(I010),1,NUMDIS,1,IRREP,LISTZ,'NNAN')
         CALL SAXPY(NUMDIS*DISSIZ,ONE,ICORE(I000),1,ICORE(I010),1)
         CALL FSPUT(ICORE(I010),1,NUMDIS,1,IRREP,LISTZ,'NNAN')
100     CONTINUE
C
C DO SPIN CASE ABAB ->  T(Ij,Ke) * f(ea)
C
        LISTT=99
        LISTZ=199
        NUMDIS=FSDPDAN(IRREP,ISYTYP(2,LISTT))
        DISSIZ=IRPDPD (IRREP,ISYTYP(1,LISTT))
        I000=1
        I010=I000+IINTFP*NUMDIS*DISSIZ
        I020=I010+IINTFP*NUMDIS*DISSIZ
        I030=I020+IINTFP*NFEA(2)
C
C READ T(Ij,Ke) AND F(eb)
C
        CALL FSGET(ICORE(I010),1,NUMDIS,1,IRREP,LISTT,'NNAN')
        CALL GETLST(ICORE(I020),1,1,1,2,92)
        IOFFT=I010
        IOFFF=I020
        IOFFZ=I000
C
C SET UP T(IjK,e)*F(eb) MULTIPLICATION
C
        DO 120 IRREPE=1,NIRREP
         IRREPA=IRREPE
         IRREPK=DIRPRD(IRREPE,IRREP)
         NSUM=VRT(IRREPE,2)
         NCOL=VRT(IRREPA,2)
         NROW=DISSIZ*POPA(IRREPK,1)
         CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFT),NROW,
     &              ICORE(IOFFF),NSUM,ZILCH,ICORE(IOFFZ),NROW)
         IOFFT=IOFFT+NROW*NCOL*IINTFP
         IOFFF=IOFFF+NSUM*NCOL*IINTFP
         IOFFZ=IOFFZ+NROW*NCOL*IINTFP
120     CONTINUE 
        CALL FSGET(ICORE(I010),1,NUMDIS,1,IRREP,LISTZ,'NNAN')
        CALL SAXPY(NUMDIS*DISSIZ,ONE,ICORE(I000),1,ICORE(I010),1)
        CALL FSPUT(ICORE(I010),1,NUMDIS,1,IRREP,LISTZ,'NNAN')
C
C DO SPIN CASE BABA ->  T(Ij,Ek) * f(EA)
C
        LISTT=98
        LISTZ=198
        NUMDIS=FSDPDNA(IRREP,ISYTYP(2,LISTT))
        DISSIZ=IRPDPD (IRREP,ISYTYP(1,LISTT))
        I000=1
        I010=I000+IINTFP*NUMDIS*DISSIZ
        I020=I010+IINTFP*NUMDIS*DISSIZ
        I030=I020+IINTFP*MAX(NFMI(2),NFMI(1),NUMDIS,DISSIZ)
        I040=I030+IINTFP*MAX(NUMDIS,DISSIZ)
        I050=I040+IINTFP*MAX(NUMDIS,DISSIZ)
C
C READ T(Ij,Ek), TRANSPOSE TO T(Ij,kE) AND READ F(EA)
C
        CALL FSGET(ICORE(I010),1,NUMDIS,1,IRREP,LISTT,'NNNA')
        CALL SYMTR1(IRREP,VRT(1,1),POPA(1,2),DISSIZ,ICORE(I010),
     &              ICORE(I020),ICORE(I030),ICORE(I040))
        CALL GETLST(ICORE(I020),1,1,1,1,92)
        IOFFT=I010
        IOFFF=I020
        IOFFZ=I000
C
C SET UP T(Ijk,E)*F(EA) MULTIPLICATION
C
        DO 140 IRREPE=1,NIRREP
         IRREPA=IRREPE
         IRREPK=DIRPRD(IRREPE,IRREP)
         NSUM=VRT(IRREPE,1)
         NCOL=VRT(IRREPA,1)
         NROW=DISSIZ*POPA(IRREPK,2) 
         CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFT),NROW,
     &              ICORE(IOFFF),NSUM,ZILCH,ICORE(IOFFZ),NROW)
         IOFFT=IOFFT+NROW*NCOL*IINTFP
         IOFFF=IOFFF+NSUM*NCOL*IINTFP
         IOFFZ=IOFFZ+NROW*NCOL*IINTFP
140     CONTINUE 
        CALL SYMTR1(IRREP,POPA(1,2),VRT(1,1),DISSIZ,ICORE(I000),
     &              ICORE(I020),ICORE(I030),ICORE(I040))
        CALL FSGET(ICORE(I010),1,NUMDIS,1,IRREP,LISTZ,'NNNA')
        CALL SAXPY(NUMDIS*DISSIZ,ONE,ICORE(I000),1,ICORE(I010),1)
        CALL FSPUT(ICORE(I010),1,NUMDIS,1,IRREP,LISTZ,'NNNA')
C
       ENDIF
10    CONTINUE
C
      RETURN
      END        
