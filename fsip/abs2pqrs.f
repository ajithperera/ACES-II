      SUBROUTINE ABS2PQRS(IABS,NUM,DSZ,PQTYPE,RSTYPE,
     &                    POPP,POPQ,POPR,POPS,P,Q,R,S)
C
      IMPLICIT INTEGER (A-Z)
      CHARACTER*3 PQTYPE,RSTYPE
      LOGICAL RHF
      DOUBLE PRECISION T2(1)
      DIMENSION IRROFF(8),NUM(8),DSZ(8),POPP(8),POPQ(8),
     &          POPR(8),POPS(8),IOFFP(8),IOFFQ(8),IOFFR(8),IOFFS(8)
      COMMON /FLAGS/ IFLAGS(100)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /INFO/ NOCCO(2),NVRTO(2)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPXX(255,2),DIRPRD(8,8)
      COMMON /FSSYMPOP/ FSDPDAN(8,22),FSDPDNA(8,22),FSDPDAA(8,22),
     &                  FSDPDIN(8,22),FSDPDNI(8,22),FSDPDII(8,22),
     &                  FSDPDAI(8,22),FSDPDIA(8,22)
      DATA ZILCH /0.0/
C   
      NNM1O2(I)=(I*(I-1))/2
      IGETLT(IX)=INT(0.5*(1.0+SQRT(8.0*IX-7))+1.D-8)+1
      IGETRT(IX)=IX-NNM1O2(IGETLT(IX)-1)
      IGETR(LSTELM,NUML)=1+(LSTELM-1)/NUML
      IGETL(LSTELM,NUML)=LSTELM-(IGETR(LSTELM,NUML)-1)*NUML
C
      IOFF0=1
      IOFF1=0
      IOFF2=0
      IOFF3=0
      IOFF4=0
      DO 10 IRREP=1,NIRREP
       IRROFF(IRREP)=IOFF0
       IOFFP(IRREP) =IOFF1 
       IOFFQ(IRREP) =IOFF2 
       IOFFR(IRREP) =IOFF3 
       IOFFS(IRREP) =IOFF4 
       IOFF0=IOFF0+NUM(IRREP)*DSZ(IRREP)
       IOFF1=IOFF1+POPP(IRREP)
       IOFF2=IOFF2+POPQ(IRREP)
       IOFF3=IOFF3+POPR(IRREP)
       IOFF4=IOFF4+POPS(IRREP)
10    CONTINUE
C
      DO 11 IRREP=1,NIRREP
       IF(IABS.GE.IRROFF(IRREP))IRREPX=IRREP
11    CONTINUE
      IOFFSET=IABS-IRROFF(IRREPX)+1
      IDISOFF=1+(IOFFSET-1)/DSZ(IRREPX)
      IDSZOFF=IOFFSET-(IDISOFF-1)*DSZ(IRREPX)
C
C NOW WE KNOW THE IRREP, THE DISTRIBUTION NUMBER WITHIN THE IRREP 
C AND THE DISTRIBUTION OFFSET.  PROCEED TO GET SYMMETRIES OF P,Q
C R AND S
C
C R AND S FIRST
C
      IF(RSTYPE.EQ.'FUL')THEN
       IOFF=1
       DO 12 IRREPS=1,NIRREP
        IRREPR=DIRPRD(IRREPS,IRREPX)
        IRROFF(IRREPS)=IOFF 
        IF(IDISOFF.GE.IRROFF(IRREPS))THEN
         ISYMS=IRREPS
         ISYMR=IRREPR
        ENDIF
        IOFF=IOFF+POPR(IRREPR)*POPS(IRREPS)
12     CONTINUE
       IOFFSET=IDISOFF-IRROFF(ISYMS)+1
       IRELS  =IGETR(IOFFSET,POPR(ISYMR))
       IRELR  =IGETL(IOFFSET,POPR(ISYMR))
       R      =IRELR+IOFFR(ISYMR)
       S      =IRELS+IOFFS(ISYMS)
      ELSEIF(RSTYPE.EQ.'PCK')THEN
       IOFF=1
       DO 22 IRREPS=1,NIRREP
        IRREPR=DIRPRD(IRREPS,IRREPX)
        IF(IRREPR.LE.IRREPS)THEN
         IRROFF(IRREPS)=IOFF 
         IF(IDISOFF.GE.IRROFF(IRREPS))THEN
          ISYMS=IRREPS
          ISYMR=IRREPR
         ENDIF
         IF(IRREPR.EQ.IRREPS)THEN
          IOFF=IOFF+NNM1O2(POPR(IRREPR))
         ELSE
          IOFF=IOFF+POPR(IRREPR)*POPS(IRREPS)
         ENDIF
        ENDIF
22     CONTINUE
       IOFFSET=IDISOFF-IRROFF(ISYMS)+1
       IRELS  =IGETRT(IOFFSET)
       IRELR  =IGETLT(IOFFSET)
       R      =IRELR+IOFFR(ISYMR)
       S      =IRELS+IOFFS(ISYMS)
      ENDIF
C
C P AND Q
C
      IF(PQTYPE.EQ.'FUL')THEN
       IOFF=1
       DO 13 IRREPQ=1,NIRREP
        IRREPP=DIRPRD(IRREPQ,IRREPX)
        IRROFF(IRREPQ)=IOFF 
        IF(IDSZOFF.GE.IRROFF(IRREPQ))THEN
         ISYMQ=IRREPQ
         ISYMP=IRREPP
        ENDIF
        IOFF=IOFF+POPP(IRREPP)*POPQ(IRREPQ)
13     CONTINUE
       IOFFSET=IDSZOFF-IRROFF(ISYMQ)+1
       IRELQ  =IGETR(IOFFSET,POPP(ISYMP))
       IRELP  =IGETL(IOFFSET,POPP(ISYMP))
       P      =IRELP+IOFFP(ISYMP)
       Q      =IRELQ+IOFFQ(ISYMQ)
      ELSEIF(PQTYPE.EQ.'PCK')THEN
       IOFF=1
       DO 23 IRREPQ=1,NIRREP
        IRREPP=DIRPRD(IRREPQ,IRREPX)
        IF(IRREPP.LE.IRREPQ)THEN
         IRROFF(IRREPQ)=IOFF 
         IF(IDISOFF.GE.IRROFF(IRREPQ))THEN
          ISYMQ=IRREPQ
          ISYMP=IRREPP
         ENDIF
         IF(IRREPP.EQ.IRREPQ)THEN
          IOFF=IOFF+NNM1O2(POPP(IRREPP))
         ELSE
          IOFF=IOFF+POPP(IRREPP)*POPQ(IRREPQ)
         ENDIF
        ENDIF
23     CONTINUE
       IOFFSET=IDISOFF-IRROFF(ISYMQ)+1
       IRELQ  =IGETRT(IOFFSET)
       IRELP  =IGETLT(IOFFSET)
       P      =IRELP+IOFFP(ISYMP)
       Q      =IRELQ+IOFFQ(ISYMQ)
      ENDIF
C
      RETURN
      END
