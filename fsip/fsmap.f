      SUBROUTINE FSMAP(XTYP,YTYP,XSPIN,YSPIN,TYPE,POPX,POPY,IDROPX,
     &                 IDROPY,NBAS,IFULL,IPART,IMAP,DPDVEC,IFSPOS,
     &                 IOFF)
C
C THIS ROUTINE CONSTRUCTS A GATHER-SCATTER VECTOR WHICH RELATES
C POSITIONS IN A SYMMETRY PACKED PQ DISTRIBUTION TO THOSE IN THE
C SAME DISTRIBUTION WHERE CERTAIN ELEMENTS OF P OR Q ARE EXCLUDED.
C
C THIS VECTORS PRODUCED BY THIS ROUTINE ARE USEFUL FOR READING QUANTITIES
C INVOLVING ONLY ACTIVE OR INACTIVE INDICES FROM THE STANDARD LISTS.
C
CEND
      IMPLICIT INTEGER (A-Z)
C
      CHARACTER*4 TYPE
      CHARACTER*3 XTYP,YTYP
      DIMENSION POPX(8),POPY(8),IDROPX(NBAS),IDROPY(NBAS),IMAP(1)
      DIMENSION IOFFX(8),IOFFY(8),DPDVEC(8),IFSPOS(8)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),DIRPRD(8,8)
      COMMON /INFO  / NOCCO(2),NVRTO(2)
C
      IFULL=0
      IPART=0
C
C CALCULATE OFFSETS FOR STARTS OF IRREPS
C
      IF(XTYP.EQ.'VRT')THEN
       IOFFX(1)=NOCCO(XSPIN)
      ELSE
       IOFFX(1)=0
      ENDIF
      IF(YTYP.EQ.'VRT')THEN
       IOFFY(1)=NOCCO(YSPIN)
      ELSE
       IOFFY(1)=0
      ENDIF
      DO 5 IRREP=2,NIRREP
       IOFFX(IRREP)=IOFFX(IRREP-1)+POPX(IRREP-1)
       IOFFY(IRREP)=IOFFY(IRREP-1)+POPY(IRREP-1) 
5     CONTINUE
      IABSX=0
      IABSY=0
C
      IF(TYPE.EQ.'FULL')THEN
C
C X,Y (FULL) DISTRIBUTION.  
C
       DO 10 IRREPXY=1,NIRREP 
        INUMIRR=0
        IFULL0=0
        DO 20 IRREPY=1,NIRREP
         IRREPX=DIRPRD(IRREPY,IRREPXY)
         NUMX=POPX(IRREPX)
         NUMY=POPY(IRREPY)
         DO 30 INDEXY=1,NUMY
          DO 40 INDEXX=1,NUMX
           IABSX=IOFFX(IRREPX)+INDEXX
           IABSY=IOFFY(IRREPY)+INDEXY
           IFULL0=IFULL0+1
           IF(IDROPX(IABSX)*IDROPY(IABSY).NE.0)THEN
            IPART=IPART+1
            INUMIRR=INUMIRR+1
            IMAP(IPART)=IFULL0
           ENDIF
40        CONTINUE
30       CONTINUE 
20      CONTINUE
        IFULL=IFULL+IFULL0
        DPDVEC(IRREPXY)=INUMIRR
        IFSPOS(IRREPXY)=IOFF
        IOFF=IOFF+INUMIRR
10     CONTINUE
C
      ELSEIF(TYPE.EQ.'PACK'.OR.TYPE.EQ.'PCK2')THEN
C
C X<Y OR X<=Y (PACKED) DISTRIBUTION 
C
       IF(TYPE.EQ.'PACK')THEN
        IOFFR=1
       ELSE
        IOFFR=0
       ENDIF
       DO 110 IRREPXY=1,NIRREP
        INUMIRR=0
        IFULL0 =0
        DO 120 IRREPY=1,NIRREP
         IRREPX=DIRPRD(IRREPY,IRREPXY)
C
         IF(IRREPX.EQ.IRREPY)THEN
C
          NUMX=POPX(IRREPX)
          NUMY=POPY(IRREPY)
          DO 130 INDEXY=1,NUMX
           DO 140 INDEXX=1,INDEXY-IOFFR
            IABSX=IOFFX(IRREPX)+INDEXX
            IABSY=IOFFY(IRREPY)+INDEXY
            IFULL0=IFULL0+1
            IF(IDROPX(IABSX)*IDROPY(IABSY).NE.0)THEN
             IPART=IPART+1
             INUMIRR=INUMIRR+1
             IMAP(IPART)=IFULL0
            ENDIF
140        CONTINUE
130       CONTINUE
C
         ELSEIF(IRREPX.LT.IRREPY)THEN
C          
          NUMX=POPX(IRREPX)
          NUMY=POPY(IRREPY)
          DO 135 INDEXY=1,NUMY
           DO 145 INDEXX=1,NUMX
            IABSX=IOFFX(IRREPX)+INDEXX
            IABSY=IOFFY(IRREPY)+INDEXY
            IFULL0=IFULL0+1
            IF(IDROPX(IABSX)*IDROPY(IABSY).NE.0)THEN
             IPART=IPART+1
             INUMIRR=INUMIRR+1
             IMAP(IPART)=IFULL0
            ENDIF
145        CONTINUE
135       CONTINUE 
C
         ENDIF
C
120     CONTINUE
        DPDVEC(IRREPXY)=INUMIRR
        IFSPOS(IRREPXY)=IOFF
        IOFF=IOFF+INUMIRR
        IFULL=IFULL+IFULL0
110    CONTINUE
C
      ENDIF
C
c      write(6,'((15I5))')(IMAP(I),I=1,IPART)
C
      RETURN
      END
