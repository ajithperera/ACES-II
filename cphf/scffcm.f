      SUBROUTINE SCFFCM(IRREP,NPERT,UAIA,UAIB,BAIA,BAIB,
     &                  SIJA,SIJB,BIJA,BIJB,FDA,FDB,
     &                  HESS,HESS2,SCR1,SCR2,ISCR,NCOORD,
     &                  IPERT,NTPERT,ITRANS)
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD
      LOGICAL SCF,NONHF,TRAINV
C
      DIMENSION UAIA(1),UAIB(1),BAIA(1),BAIB(1),
     &          SIJA(1),SIJB(1),BIJA(1),BIJB(1),
     &          FDA(1),FDB(1),HESS(NCOORD,NCOORD),
     &          HESS2(NCOORD,NCOORD),SCR1(1),SCR2(1),
     &          ISCR(NCOORD),ITRANS(1)      
C
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),NJUNK(18)
      COMMON/METHOD/IUHF,SCF,NONHF
      COMMON/PERT/NTTPERT,NNPERT(8),IIPERT(8),IXPERT,IYPERT,IZPERT,
     &            IYZPERT,IXZPERT,IXYPERT,ITRANSX,ITRANSY,ITRANSZ,
     &            NUCIND
      COMMON/INVAR/TRAINV
C
      DATA HALFM,ONE /-0.5D0,1.0D0/
      DATA TWO,FOUR /2.D0,4.D0/
C
C DETERMINE LENGTHS OF UAI AND BAI VECTORS
C
      NAA=IRPDPD(IRREP,9)
      NAB=IRPDPD(IRREP,10)
      NIA=IRPDPD(IRREP,21)
      NIB=IRPDPD(IRREP,22)
C 
      IND=0
      ISTART1=1
C
C  LOOP OVER ALL PERTURBATIONS IN THIS IRREP
C
      DO 1000 IPERT1=1,NPERT
C
       IND1=ISCR(IPERT+IPERT1)
C
       IOFF1=1+(IPERT1-1)*NAA
       IOFF2=1+(IPERT1-1)*NIA
       IOFF1B=1+(IPERT1-1)*NAB
       IOFF2B=1+(IPERT1-1)*NIB
C
       DO 1000 IPERT2=1,NPERT
C
        IND2=ISCR(IPERT+IPERT2)
C
        IOFF4=1+(IPERT2-1)*NAA
        IOFF5=1+(IPERT2-1)*NIA
        IOFF4B=1+(IPERT2-1)*NAB
        IOFF5B=1+(IPERT2-1)*NIB
C
        HESS(IND1,IND2)=
     &  SDOT(NAA,BAIA(IOFF1),1,UAIA(IOFF4),1)
     &   +HALFM*SDOT(NIA,BIJA(IOFF2),1,SIJA(IOFF5),1)
     &   +HALFM*SDOT(NIA,FDA(IOFF5),1,SIJA(IOFF2),1)
C
        IF(IUHF.EQ.0) THEN
         HESS(IND1,IND2)=HESS(IND1,IND2)*TWO
        ELSE
        HESS(IND1,IND2)=HESS(IND1,IND2)+
     &        SDOT(NAB,BAIB(IOFF1B),1,UAIB(IOFF4B),1)
     &          +HALFM*SDOT(NIB,BIJB(IOFF2B),1,SIJB(IOFF5B),1)
     &                 +HALFM*SDOT(NIB,FDB(IOFF5B),1,SIJB(IOFF2B),1)
C
        ENDIF
        HESS(IND1,IND2)=TWO*HESS(IND1,IND2)
1000  CONTINUE
C
C UPDATE THE FCM FILE
C
      IF(IND1.EQ.NCOORD)THEN
C
      IF(TRAINV) THEN
C
C USE TRANSLATIONAL INVARIANCE TO CREATE FULL FCM FILE
C
       CALL EXPFCM(HESS,NCOORD,ITRANS,NUCIND)
C
      ENDIF
C
C FOR CASES WITH SYMMETRY, TRANSFORM THE HESSIAN BACK TO
C NON-SYMMETRIC REPRESENTATION
C
       IF(NIRREP.NE.1) THEN
C
        CALL ZERO(HESS2,NCOORD*NCOORD)
        CALL TRAHES(HESS,HESS2,SCR1,SCR2,NCOORD)
c YAU : old
c       CALL ICOPY(NCOORD*NCOORD*IINTFP,HESS2,1,HESS,1)
c YAU : new
        CALL DCOPY(NCOORD*NCOORD,HESS2,1,HESS,1)
c YAU : end
C
       ENDIF
       IF(SCF) THEN
C
C FOR HF WAVE FUNCTION, THE FCM FILE HAS BEEN ALREADY WRIITEN
C BY ABACUS, GET THOSE PIECES AND ADD THEM TO THE HESSIAN
C
        OPEN(UNIT=81,FILE='FCM',STATUS='OLD',FORM='FORMATTED')
        READ(81,3000)NATOMS,NATOMS6
        DO 2000 ICOORD=1,NCOORD
         READ(81,3001)(HESS2(ICOORD,J),J=1,NCOORD)
2000    CONTINUE
3000    FORMAT(2I5)
3001    FORMAT(3F20.10)
        CALL SAXPY(NCOORD*NCOORD,ONE,HESS2,1,HESS,1)
        
       ELSE
C
C FOR CORRELATED WAVE FUNCTION, THIS IS THE FIRST TIME THE FCM
C FILE IS WRITTEN 
C
        OPEN(UNIT=81,FILE='FCM',STATUS='UNKNOWN',FORM='FORMATTED')
        NATOMS=0
        NATOMS6=0
       ENDIF
       REWIND(81) 
       WRITE(81,3000)NATOMS,NATOMS6
       DO 2100 ICOORD=1,NCOORD 
        WRITE(81,3001)(HESS(ICOORD,J),J=1,NCOORD)
2100   CONTINUE 
       CLOSE(UNIT=81,STATUS='KEEP')
       CALL PUTREC(20,'JOBARC','HESSIANM',NCOORD*NCOORD*IINTFP,HESS)
C
      ENDIF
C
      RETURN
      END
