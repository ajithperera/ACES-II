      SUBROUTINE SCFHYP(SCR,MAXCOR,IXYZSYM)
C
C  THIS ROUTINE CALCULATES THE SCF HYPERPPOLARIZABILITIES
C
C  Beta(f,g,h) = 2 SUM a,b,i  f(a,b)^f U(a,i)^g U(b,i)^h 
C
C                + 2 SUM i,j,a  f(i,j)^f U(a,i)^g U(a,j)^h
C
C                + all possible non equivalent permutations of f,g,h
C
C  RHF AND UHF SPIN CASES ARE CONSIDERED
C
CEND
C
C CODED JG JAN/91
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD,POP,VRT
      LOGICAL FIELD,GEOM,MAGN,SCF,NONHF,SPIN,THIRD
      DIMENSION SCR(MAXCOR)
      DIMENSION JXYZ(3),IXYZSYM(3)
      DIMENSION IUAIX(3,2),IUAI(2)
      DIMENSION IOO(3,2),IVV(3,2)
      DIMENSION IOFFOV2(8,2),IOFFOV3(8,2)
C
      COMMON/HYPER/BETA(3,3,3)
      COMMON/BASSYM/NBAS(8),NBASIS,NBASSQ,NBASTT
      COMMON/LSYM/NLENQ(8),NLENT(8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),NJUNK(18) 
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/METHOD/IUHF,SCF,NONHF
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/PERT/NTPERT,NPERT(8),IPERT(8),IFPERT(3),IJUNK(7)
      COMMON/PERT2/FIELD,GEOM,THIRD,MAGN,SPIN
C
      DATA AZERO,ONE,TWO,FOUR /0.D0,1.D0,2.D0,4.D0/
C
      CALL ZERO(BETA,27)
C
C  ALLOCATE MEMORY FOR SCRATCH, MO COEFFICIENTS, AND
C  DIPOLE INTEGRALS
C
C
      LENOOA=0
      LENOOB=0
      LENVVA=0
      LENVVB=0
      LENUAIA=0
      LENUAIB=0
      DO 1 IXYZ=1,3
       LENOOA=MAX(LENOOA,IRPDPD(IXYZSYM(IXYZ)+1,21))
       LENOOB=MAX(LENOOB,IRPDPD(IXYZSYM(IXYZ)+1,22))
       LENVVA=MAX(LENVVA,IRPDPD(IXYZSYM(IXYZ)+1,19))
       LENVVB=MAX(LENVVB,IRPDPD(IXYZSYM(IXYZ)+1,20))
       LENUAIA=LENUAIA+IRPDPD(IXYZSYM(IXYZ)+1,9)
       LENUAIB=LENUAIB+IRPDPD(IXYZSYM(IXYZ)+1,10)
1     CONTINUE
C
      ISCR=1
      IUAI(1)=ISCR+NBASSQ
      IUAIX(1,1)=IUAI(1)
      IUAIX(2,1)=IUAIX(1,1)+IRPDPD(IXYZSYM(1)+1,9)
      IUAIX(3,1)=IUAIX(2,1)+IRPDPD(IXYZSYM(2)+1,9)
      IUAI(2)=IUAI(1)+LENUAIA*IUHF
      IUAIX(1,2)=IUAI(2)
      IUAIX(2,2)=IUAIX(1,2)+IRPDPD(IXYZSYM(1)+1,10)
      IUAIX(3,2)=IUAIX(2,2)+IRPDPD(IXYZSYM(2)+1,10)
      IOO(1,1)=IUAI(2)+LENUAIB
      IOO(2,1)=IOO(1,1)+LENOOA
      IOO(3,1)=IOO(2,1)+LENOOA
      IOO(1,2)=IOO(1,1)+3*IUHF*LENOOA
      IOO(2,2)=IOO(1,2)+LENOOB
      IOO(3,2)=IOO(2,2)+LENOOB
      IVV(1,1)=IOO(1,2)+3*LENOOB
      IVV(2,1)=IVV(1,1)+LENVVA
      IVV(3,1)=IVV(2,1)+LENVVA
      IVV(1,2)=IVV(1,1)+3*IUHF*LENVVA
      IVV(2,2)=IVV(1,2)+LENVVB
      IVV(3,2)=IVV(2,2)+LENVVB
      IEND=IVV(3,2)+LENVVB
      IF(IEND.GT.MAXCOR) CALL INSMEM('SCFHYP',IINTFP*IEND,IINTFP*MAXCOR)
C
C  GET CPHF COEFFICIENTS 
C
      JXYZ(1)=NPERT(IFPERT(1))+1
      IF(FIELD) JXYZ(1)=1
      JXYZ(2)=NPERT(IFPERT(2))+1
      IF(FIELD) JXYZ(2)=1
      IF(IFPERT(1).EQ.IFPERT(2)) JXYZ(2)=JXYZ(2)+1
      JXYZ(3)=NPERT(IFPERT(3))+1
      IF(FIELD) JXYZ(3)=1
      IF(IFPERT(1).EQ.IFPERT(3)) JXYZ(3)=JXYZ(3)+1
      IF(IFPERT(2).EQ.IFPERT(3)) JXYZ(3)=JXYZ(3)+1

      DO 2 IXYZ=1,3
       CALL GETLST(SCR(IUAIX(IXYZ,1)),JXYZ(IXYZ),1,1,IFPERT(IXYZ),182)
       CALL GETLST(SCR(IOO(IXYZ,1)),JXYZ(IXYZ),1,1,IFPERT(IXYZ),176)
       CALL GETLST(SCR(IVV(IXYZ,1)),JXYZ(IXYZ),1,1,IFPERT(IXYZ),178)
       IF(IUHF.EQ.1) THEN
        CALL GETLST(SCR(IUAIX(IXYZ,2)),JXYZ(IXYZ),1,1,IFPERT(IXYZ),183)
        CALL GETLST(SCR(IOO(IXYZ,2)),JXYZ(IXYZ),1,1,IFPERT(IXYZ),177)
        CALL GETLST(SCR(IVV(IXYZ,2)),JXYZ(IXYZ),1,1,IFPERT(IXYZ),179)
       ENDIF
2     CONTINUE
C
C  LOOP OVER ALL CARTESIAN COORDINATES X,Y,Z
C
      DO 100 IXYZ1=1,3
C
       IRREP1=IFPERT(IXYZ1)
C
C LOOP OVER ALL CARTESIAN COORDINATES X,Y,Z
C
       DO 200 IXYZ2=1,3
C
        IRREP2=IFPERT(IXYZ2)
C
        IOFFOV2(1,1)=IUAIX(IXYZ2,1)
        IOFFOV2(1,2)=IUAIX(IXYZ2,2)
        DO 210 IRREPR=1,NIRREP-1
         IRREPL=DIRPRD(IRREP2,IRREPR)
         IOFFOV2(IRREPR+1,1)=IOFFOV2(IRREPR,1)
     &                     +POP(IRREPR,1)*VRT(IRREPL,1)
         IOFFOV2(IRREPR+1,2)=IOFFOV2(IRREPR,2) 
     &                     +POP(IRREPR,2)*VRT(IRREPL,2) 
210    CONTINUE
C
        DO 300 IXYZ3=1,3
C
         IRREP3=IFPERT(IXYZ3)
C
         IF(IRREP3.NE.DIRPRD(IRREP1,IRREP2)) GO TO 300
C
         IOFFOV3(1,1)=IUAIX(IXYZ3,1)
         IOFFOV3(1,2)=IUAIX(IXYZ3,2)
         DO 310 IRREPR=1,NIRREP-1
          IRREPL=DIRPRD(IRREP3,IRREPR) 
          IOFFOV3(IRREPR+1,1)=IOFFOV3(IRREPR,1)
     &                     +POP(IRREPR,1)*VRT(IRREPL,1)
          IOFFOV3(IRREPR+1,2)=IOFFOV3(IRREPR,2) 
     &                     +POP(IRREPR,2)*VRT(IRREPL,2) 
310     CONTINUE
        
        DO 320 ISPIN=1,IUHF+1
C
         IOFF23=ISCR
         DO 220 IRREPL2=1,NIRREP
C
          IRREPR=DIRPRD(IRREP2,IRREPL2)
          IRREPL3=DIRPRD(IRREP3,IRREPR)
          NOCC=POP(IRREPR,ISPIN)
          NVRT2=VRT(IRREPL2,ISPIN)
          NVRT3=VRT(IRREPL3,ISPIN)
C

C   MULTIPLICATION  SUM A U(AI) U(BI)
C
          CALL XGEMM('N','T',NVRT3,NVRT2,NOCC,ONE,
     &               SCR(IOFFOV3(IRREPR,ISPIN)),NVRT3,
     &               SCR(IOFFOV2(IRREPR,ISPIN)),NVRT2,
     &               AZERO,SCR(IOFF23),NVRT3)
C
        IOFF23=IOFF23+NVRT2*NVRT3
220      CONTINUE
C
C    CONTRACTION WITH THE TRANSFORMED DIPOLE INTEGRALS
C
         TERM=SDOT(IRPDPD(IRREP1,18+ISPIN),
     &            SCR(IVV(IXYZ1,ISPIN)),1,SCR(ISCR),1)
         BETA(IXYZ1,IXYZ2,IXYZ3)=BETA(IXYZ1,IXYZ2,IXYZ3)+TERM
C
         IOFF23=ISCR
         DO 230 IRREPR2=1,NIRREP
C
          IRREPL=DIRPRD(IRREP2,IRREPR2)
          IRREPR3=DIRPRD(IRREPL,IRREP3)
          NOCC2=POP(IRREPR2,ISPIN)
          NVRT=VRT(IRREPL,ISPIN)
          NOCC3=POP(IRREPR3,ISPIN)
C
          CALL XGEMM('T','N',NOCC3,NOCC2,NVRT,ONE,
     &               SCR(IOFFOV3(IRREPR3,ISPIN)),
     &               NVRT,SCR(IOFFOV2(IRREPR2,ISPIN)),NVRT,
     &               AZERO,SCR(IOFF23),NOCC3)
C
          IOFF23=IOFF23+NOCC2*NOCC3
230      CONTINUE
C
C    CONTRACTION WITH THE TRANSFORMED DIPOLE INTEGRALS
C
         TERM=SDOT(IRPDPD(IRREP1,20+ISPIN),
     &            SCR(IOO(IXYZ1,ISPIN)),1,SCR(ISCR),1)
         BETA(IXYZ1,IXYZ2,IXYZ3)=BETA(IXYZ1,IXYZ2,IXYZ3)-TERM
C
320     CONTINUE
C
300    CONTINUE           
C 
200   CONTINUE
C
100   CONTINUE
C
       FACT=TWO
       IF(IUHF.EQ.0) FACT=FOUR
       DO 900 IXYZ1=1,3
       DO 900 IXYZ2=1,IXYZ1
       DO 900 IXYZ3=1,IXYZ2
        TERM=FACT*(BETA(IXYZ1,IXYZ2,IXYZ3)+BETA(IXYZ2,IXYZ3,IXYZ1)
     &             + BETA(IXYZ3,IXYZ1,IXYZ2))
       BETA(IXYZ1,IXYZ2,IXYZ3)=TERM
       BETA(IXYZ1,IXYZ3,IXYZ2)=TERM
       BETA(IXYZ2,IXYZ1,IXYZ3)=TERM
       BETA(IXYZ2,IXYZ3,IXYZ1)=TERM
       BETA(IXYZ3,IXYZ2,IXYZ1)=TERM
       BETA(IXYZ3,IXYZ1,IXYZ2)=TERM
900    CONTINUE
       RETURN
       END
