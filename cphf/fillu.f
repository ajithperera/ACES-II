
      SUBROUTINE FILLU(SCR,MAXSCR)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD,POP,VRT,XYZSYM 
      LOGICAL FIELD,GEOM,THIRD,MAGN,SPIN
C 
      DIMENSION SCR(MAXSCR)
C
      COMMON/PERT/NTPERT,NPERT(8),JPERT(8),XYZSYM(3),LJUNK(7) 
      COMMON/PERT2/FIELD,GEOM,THIRD,MAGN,SPIN
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),NJUNK(18)
C
      DATA HALFM,TWO /-0.5D0,2.D0/
C
C LOOP OVER ALL IRREPS
C
      DO 1000 IRREP=1,NIRREP
C
C DETERMINE THE NUMBER OF PERTURBATIONS
C
       IOFFF=0
       NFX=0
       IF(.NOT.FIELD) THEN
C
C GEOMETRICAL PERTURBATIONS
C
        NFX=NFX+NPERT(IRREP)
       ENDIF
C
C ELECTRICAL FIELD PERTURBATIONS
C
       DO 10 IXYZ=1,3
        IF(XYZSYM(IXYZ).EQ.IRREP) NFX=NFX+1
10     CONTINUE
C
C ALLOCATE MEMORY
C
       IUIJA=1
       IUABB=IUIJA+IRPDPD(IRREP,21)
       IUAIA=IUABB+IRPDPD(IRREP,20)
       IUAIB=IUAIA+IRPDPD(IRREP,9)
       IEND=IUAIB+IRPDPD(IRREP,10)
C
C LOOP OVER ALL PERTURBATIONS
C
       DO 100 IPERT=1+IOFFF,NFX
        IF(IPERT.LE.NPERT(IRREP).AND.(.NOT.FIELD)) THEN
         CALL GETLST(SCR(IUIJA),IPERT,1,1,IRREP,170)
         CALL SSCAL(IRPDPD(IRREP,21),HALFM,SCR(IUIJA),1)
         CALL GETLST(SCR(IUABB),IPERT,1,1,IRREP,173)
         CALL SSCAL(IRPDPD(IRREP,20),HALFM,SCR(IUABB),1)
        ELSE
         CALL ZERO(SCR(IUIJA),IRPDPD(IRREP,21))
         CALL ZERO(SCR(IUABB),IRPDPD(IRREP,20))
        ENDIF
        CALL GETLST(SCR(IUAIA),IPERT,1,1,IRREP,182)
        CALL GETLST(SCR(IUAIB),IPERT,1,1,IRREP,183)
C
C LOOP OVER ALL IRREP
C
        IOFFOO=IUIJA-1
        IOFFVV=IUABB-1
        IOFFVOA=IUAIA-1
        IOFFVOB=IUAIB-1
C
        DO 200 IRREPR=1,NIRREP
C
         IRREPL=DIRPRD(IRREPR,IRREP)
C
         IOFFVOA2=IUAIA-1
         IOFFVOB2=IUAIB-1
         DO 201 IRREP1=1,IRREPL-1
          IRREP2=DIRPRD(IRREP1,IRREP)
          IOFFVOA2=IOFFVOA2+POP(IRREP1,1)*VRT(IRREP2,2)
          IOFFVOB2=IOFFVOB2+POP(IRREP1,2)*VRT(IRREP2,2)
201      CONTINUE
C
         NOCCLA=POP(IRREPL,1)
         NOCCRA=POP(IRREPR,1)
         NVRTLA=VRT(IRREPL,1)
         NVRTRA=VRT(IRREPR,1)
         NOCCLB=POP(IRREPL,2)
         NOCCRB=POP(IRREPR,2)
         NVRTLB=VRT(IRREPL,2)
         NVRTRB=VRT(IRREPR,2)
         NOPR=NOCCRA-NOCCRB
         NOPL=NOCCLA-NOCCLB
C
C LOOP OVER ALL OPEN SHELL ORBITALS
C
          DO 250 IOPR=1,NOPR
           DO 251 IOCCL=1,NOCCLB
C
C U(I,1) = - S(1,I) - U(1,i)
C
            IOFFO=IOFFOO+IOCCL+(NOCCRB+IOPR-1)*NOCCLA
            IOFFI=IOFFVOB2+IOPR+(IOCCL-1)*NVRTRB
            SCR(IOFFO)=TWO*SCR(IOFFO)-SCR(IOFFI)
C
251        CONTINUE
C
           DO 252 IVRTL=1+NOPL,NVRTLB
C
C U(a,1) = U(A,1)
C
            IOFFO=IOFFVV+IVRTL+(IOPR-1)*NVRTLB
            IOFFI=IOFFVOA+(IVRTL-NOPL)+(IOPR+NOCCRB-1)*NVRTLA
            SCR(IOFFO)=SCR(IOFFI)
C
252        CONTINUE
C
250       CONTINUE
C
          DO 260 IOPL=1,NOPL
C
           DO 261 IOCCR=1,NOCCRB
C
C U(1,I) = U(1,i)
C
            IOFFO=IOFFOO+IOPL+NOCCLB+(IOCCR-1)*NOCCLA
            IOFFI=IOFFVOB+IOPL+(IOCCR-1)*NVRTLB
            SCR(IOFFO)=SCR(IOFFI)
C
261        CONTINUE
C
           DO 262 IVRTR=1+NOPR,NVRTRB
C
C U(1,a) = - S(1,a) - U(A,1)
C
            IOFFO=IOFFVV+IOPL+(IVRTR-1)*NVRTLB
            IOFFI=IOFFVOA2+(IVRTR-NOPR)+(IOPL+NOCCLB-1)*NVRTRA
            SCR(IOFFO)=TWO*SCR(IOFFO)-SCR(IOFFI)
C
262        CONTINUE
260       CONTINUE
C
         IOFFOO=IOFFOO+NOCCRA*NOCCLA
         IOFFVV=IOFFVV+NVRTRA*NVRTLA
         IOFFVOA=IOFFVOA+NVRTLA*NOCCRA
         IOFFVOB=IOFFVOB+NVRTLB*NOCCRB
200     CONTINUE
        CALL PUTLST(SCR(IUIJA),IPERT,1,1,IRREP,198)
        CALL PUTLST(SCR(IUABB),IPERT,1,1,IRREP,199)
100    CONTINUE
1000  CONTINUE
C
      RETURN 
      END
