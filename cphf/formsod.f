

      
      SUBROUTINE FORMSOD(ICORE,MAXCOR)
C
C  THIS ROUTINE TRANSFORMS THE FOCK MATRIX DERIVATIVES
C  FROM THE AO TO THE MO BASIS
C
CEND
C
C CODED JAN/91 JG
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,POP,VRT
      LOGICAL SCF,NONHF,ROHF,SEMI,GEOM,FIELD,THIRD,MAGN,SPIN
C
      DIMENSION ICORE(MAXCOR)
      DIMENSION ICMO(2)
C
      COMMON/INFO/NOCCO(2),NVRTO(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),NJUNK(18)
      COMMON/BASSYM/NBAS(8),NBASIS,NBASSQ,NBASTT
      COMMON/METHOD/IUHF,SCF,NONHF
      COMMON/OPENSH/ROHF,SEMI
      COMMON/OFFSET/IOFFC(8)
      COMMON/PERT/NTPERT,NPERT(8),IPERT(8),IXPERT,IYPERT,IZPERT,
     &            IXYPERT,IXZPERT,IYZPERT,ITRANSX,ITRANSY,ITRANSZ,
     &            NUCIND
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/LSYM/NLENQ(8),NLENT(8)
      COMMON/PERT2/FIELD,GEOM,THIRD,MAGN,SPIN
C
      IANTI=1
C
C  ALLOCATE MEMORY FOR MO COEFFICIENTS
C
      ICMO(1)=1
      ICMO(2)=ICMO(1)+NBASSQ*IUHF*IINTFP
      ISCR=ICMO(2)+NBASIS*NBASIS*IINTFP
      IEVA=ISCR+NBASIS*NBASIS*IINTFP
      IEVB=IEVA+IINTFP*IUHF*NBASIS
      IFD=IEVB+IINTFP*NBASIS
      MXSCR=NBASIS*NBASIS
C
      LENFD=0
      LENFD2=0
      DO 10 IRREP=1,NIRREP
       LENFD=MAX(LENFD,NLENT(IRREP))
       LENFD2=MAX(LENFD2,NLENQ(IRREP))
10    CONTINUE
      IEND=IFD+IINTFP*LENFD*(IUHF+1)
C
      IF(IEND.GE.MAXCOR) CALL ERREX
C
C GET EIGEN VECTORS AND SYMMETRY PACK THEM
C
      CALL GETREC(20,'JOBARC','SCFEVCA0',NBASIS*NBASIS*IINTFP,
     &            ICORE(ISCR))
      CALL SYMC(ICORE(ISCR),ICORE(ICMO(1)),NBASIS,NBAS,.FALSE.,1)
      IF(IUHF.EQ.1) THEN
       CALL GETREC(20,'JOBARC','SCFEVCB0',
     &             NBASIS*NBASIS*IINTFP,ICORE(ISCR))
       CALL SYMC(ICORE(ISCR),ICORE(ICMO(2)),NBASIS,NBAS,.FALSE.,2)
      ENDIF
C
C FILL IOFFC
C
      IOFFC(1)=1
      DO 1 IRREP=1,NIRREP-1
       IOFFC(IRREP+1)=IOFFC(IRREP)+NBAS(IRREP)*NBAS(IRREP)
1     CONTINUE 
C
C READ ORBITAL ENERGIES
C
      CALL GETREC(20,'JOBARC','SCFEVLA0',IINTFP*NBASIS,
     &            ICORE(IEVA))
      IF(IUHF.EQ.1) THEN
       CALL GETREC(20,'JOBARC','SCFEVLB0',IINTFP*NBASIS,
     &             ICORE(IEVB))
      ENDIF
C
C ALLOCATE MEMORY FOR THE TRANSFORMED OVERLAP MATRIX DERIVATIVES
C
      IFDEXP=IEND
      IFDMO=IFDEXP+IINTFP*(IUHF+1)*LENFD2
      IEND2=IFDMO+IINTFP*LENFD2*(IUHF+1)
      MXCOR2=MAXCOR-IEND2
      
C
C  LOOP OVER ALL IRREPS
C
      DO 100 IRREPP=1,NIRREP
C
C  CHECK IF THERE ANY PERTURBATIONS BELONGING TO THIS IRREP
C 
       NP=NPERT(IRREPP)
       NSP=NP
       NSP=0 
       IF(IYZPERT.EQ.IRREPP) THEN
        NP=NP+1
        NSP=NSP+1
       ENDIF
       IF(IXZPERT.EQ.IRREPP) THEN
        NP=NP+1
        NSP=NSP+1
       ENDIF
       IF(IXYPERT.EQ.IRREPP) THEN
        NP=NP+1
        NSP=NSP+1
       ENDIF
C
       IF(NP.EQ.0) GO TO 100
C
C  LOOP OVER ALL PERTURBATIONS IN THIS IRREP
C
       DO 150 IP=1+NSP,NP
C
C READ IN THE FOCK MATRIX DERIVATIVE INTEGRALS
C
       CALL GETLST(ICORE(IFD),IP,1,1,IRREPP,102)
       IF(IUHF.EQ.1) THEN
        IFD2=IFD+IINTFP*NLENT(IRREPP)
        CALL GETLST(ICORE(IFD2),IP,1,1,IRREPP,103)
       ENDIF
C
       IFDEXP2=IFDEXP+IINTFP*NLENQ(IRREPP)*IUHF
       CALL MATEXP(IRREPP,NBAS,ICORE(IFD),ICORE(IFDEXP),IANTI)
       IF(IUHF.EQ.1) CALL MATEXP(IRREPP,NBAS,ICORE(IFD2),
     &                           ICORE(IFDEXP2),IANTI)
C
       NOCCSQ=IRPDPD(IRREPP,21)
       NVRTSQ=IRPDPD(IRREPP,19)
       NOVSQ=IRPDPD(IRREPP,9)
C
C  COMPUTE THE OCCUPIED-OCCUPIED BLOCK
C
       IOFF=IFDMO
       CALL FORMIJ(IRREPP,ICORE(IFDEXP),ICORE(ICMO(1)),NBASSQ,
     &             ICORE(ISCR),MXSCR,IUHF,ICORE(IOFF),NOCCSQ,
     &             .TRUE.)
C
       CALL PUTLST(ICORE(IOFF),IP-NSP,1,1,IRREPP,176)
       IF(.NOT.SCF) CALL PUTLST(ICORE(IOFF),IP-NSP,1,1,IRREPP,186)
C
       IF(IUHF.EQ.1) THEN
C
        CALL PUTLST(ICORE(IOFF+IINTFP*NOCCSQ),IP-NSP,1,1,IRREPP,177)
        IF(.NOT.SCF) CALL PUTLST(ICORE(IOFF+IINTFP*NOCCSQ),IP-NSP,1,
     &                           1,IRREPP,187)
C
       ENDIF
C
C  COMPUTE THE VIRTUAL-VIRTUAL BLOCK
C
       IOFF=IOFF+IINTFP*(IUHF*NOCCSQ+IRPDPD(IRREPP,22))
       CALL FORMAB(IRREPP,ICORE(IFDEXP),ICORE(ICMO(1)),NBASSQ,
     &             ICORE(ISCR),MXSCR,IUHF,ICORE(IOFF),NVRTSQ,
     &             .TRUE.)
C
       CALL PUTLST(ICORE(IOFF),IP-NSP,1,1,IRREPP,178)
       IF(.NOT.SCF) CALL PUTLST(ICORE(IOFF),IP-NSP,1,1,IRREPP,188)
C
       IF(IUHF.EQ.1) THEN
C
        CALL PUTLST(ICORE(IOFF+IINTFP*NVRTSQ),IP-NSP,1,1,IRREPP,179)
        IF(.NOT.SCF) CALL PUTLST(ICORE(IOFF+IINTFP*NVRTSQ),
     &                           IP-NSP,1,1,IRREPP,189)
C
       ENDIF 
C
C  COMPUTE THE VIRTUAL-OCCUPIED BLOCK
C
       IOFF=IOFF+IINTFP*(IUHF*NVRTSQ+IRPDPD(IRREPP,20))
       CALL FORMAI(IRREPP,ICORE(IFDEXP),ICORE(ICMO(1)),NBASSQ,
     &             ICORE(ISCR),MXSCR,IUHF,ICORE(IOFF),NOVSQ,
     &             .TRUE.)
C
       CALL PUTLST(ICORE(IOFF),IP-NSP,1,1,IRREPP,180)
C
       IF(IUHF.EQ.1) THEN
C
        CALL PUTLST(ICORE(IOFF+IINTFP*NOVSQ),IP-NSP,1,1,IRREPP,181)
C
       ENDIF
C
150    CONTINUE
C
100   CONTINUE
C
C ALL DONE, RETURN 
C
      RETURN
      END
