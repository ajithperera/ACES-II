
      SUBROUTINE FORMDD(ICORE,MAXCOR)
C
C  THIS ROUTINE TRANSFORMS THE DIPOLE MATRICES 
C  FROM THE AO TO THE MO BASIS
C
CEND
C
C CODED JAN/91 JG
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,POP,VRT
      LOGICAL SCF,NONHF,GEOM,FIELD,THIRD,MAGN,SPIN
C
      DIMENSION ICORE(MAXCOR)
      DIMENSION ICMO(2),IXYZ(3),JXYZ(3),ID(3)
C
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),NJUNK(18)
      COMMON/BASSYM/NBAS(8),NBASIS,NBASSQ,NBASTT
      COMMON/METHOD/IUHF,SCF,NONHF
      COMMON/PERT2/FIELD,GEOM,THIRD,MAGN,SPIN
      COMMON/OFFSET/IOFFC(8)
      COMMON/PERT/NTPERT,NPERT(8),IPERT(8),IXPERT,IYPERT,IZPERT,
     &            IYZPERT,IXZPERT,IXYPERT,ITRANSX,ITRANSY,ITRANSZ,
     &            NUCIND
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/LSYM/NLENQ(8),NLENT(8)
C
C  ALLOCATE MEMORY FOR MO COEFFICIENTS
C
      MXSCR=MAXCOR
      IXYZ(1)=IXPERT
      IXYZ(2)=IYPERT
      IXYZ(3)=IZPERT
      JXYZ(1)=1
      JXYZ(2)=1
      IF(IXPERT.EQ.IYPERT) JXYZ(2)=2
      JXYZ(3)=1
      IF(IXPERT.EQ.IZPERT) JXYZ(3)=2
      IF(IYPERT.EQ.IZPERT) JXYZ(3)=JXYZ(3)+1
      ICMO(1)=1
      ICMO(2)=ICMO(1)+NBASSQ*IUHF*IINTFP
      ISCR=ICMO(2)+NBASIS*NBASIS*IINTFP
      ID(1)=ISCR+NBASSQ*IINTFP
      ID(2)=ID(1)+NLENT(IXYZ(1))*IINTFP
      ID(3)=ID(2)+NLENT(IXYZ(2))*IINTFP
      IEND=ID(3)+NLENT(IXYZ(3))*IINTFP
C
      IF(IEND.GE.MAXCOR) CALL INSMEM('FORMDD',IEND,MAXCOR)
C
C GET EIGEN VECTORS AND SYMMETRY PACK THEM
C
      CALL GETREC(20,'JOBARC','SCFEVCA0',NBASIS*NBASIS*IINTFP,
     &            ICORE(ISCR))
      CALL SYMC(ICORE(ISCR),ICORE(ICMO(1)),NBASIS,NBAS,.FALSE.,1)
      IF(IUHF.EQ.1) THEN
       CALL GETREC(20,'JOBARC','SCFEVCB0',
     &             NBASIS*NBASIS*IINTFP,ICORE(ISCR))
       CALL SYMC(ICORE(ISCR),ICORE(ICMO(2)),NBASIS,NBAS,.FALSE.,2)
      ENDIF
C
C FILL IOFFC
C
      IOFFC(1)=1
      DO 1 IRREP=1,NIRREP-1
       IOFFC(IRREP+1)=IOFFC(IRREP)+NBAS(IRREP)*NBAS(IRREP)
1     CONTINUE 
C
C READ IN THE DIPOLE INTEGRALS
C
      DO 2 ICART=1,3
       CALL GETLST(ICORE(ID(ICART)),1,1,1,ICART,104)
2     CONTINUE
C
C ALLOCATE MEMORY FOR THE TRANSFORMED DIPOLE INTEGRALS
C
      IDDEXP=IEND
      IDDMO=IDDEXP+NBASIS*NBASIS*IINTFP
C
C  LOOP OVER ALL XYZ
C
      DO 100 ICART=1,3
C
       IRREPX=IXYZ(ICART)
C
C  OFFSETS IN EXPANDED DIPOLE MATRIX
C
C  LOOP NOW OVER ALL PERTURBATIONS IN THIS IRREP
C
       CALL MATEXP(IRREPX,NBAS,ICORE(ID(ICART)),ICORE(IDDEXP),0)
C
       NOCCSQ=IRPDPD(IRREPX,21)
       NVRTSQ=IRPDPD(IRREPX,19)
       NOVSQ=IRPDPD(IRREPX,9)
C
C  COMPUTE THE OCCUPIED-OCCUPIED BLOCK
C
       IOFF=IDDMO
       CALL FORMIJ(IRREPX,ICORE(IDDEXP),ICORE(ICMO(1)),NBASSQ,
     &             ICORE(ISCR),MXSCR,IUHF,ICORE(IOFF),NOCCSQ,
     &             .FALSE.)
C
       NOFFSET=NPERT(IRREPX)+JXYZ(ICART)
       IF(FIELD) NOFFSET=JXYZ(ICART)
C
       CALL PUTLST(ICORE(IOFF),NOFFSET,1,1,IRREPX,176)
       IF(.NOT.SCF) 
     &     CALL PUTLST(ICORE(IOFF),JXYZ(ICART),1,1,IRREPX,186)
       IF(IUHF.EQ.1) THEN
        IOFF2=IOFF+NOCCSQ*IINTFP
        CALL PUTLST(ICORE(IOFF2),NOFFSET,1,1,IRREPX,177)
        IF(.NOT.SCF) 
     &      CALL PUTLST(ICORE(IOFF2),JXYZ(ICART),1,1,IRREPX,187)
       ENDIF

C
C  COMPUTE THE VIRTUAL-VIRTUAL BLOCK
C
       IOFF=IOFF+IINTFP*(IUHF*NOCCSQ+IRPDPD(IRREPX,22))
       CALL FORMAB(IRREPX,ICORE(IDDEXP),ICORE(ICMO(1)),NBASSQ,
     &             ICORE(ISCR),MXSCR,IUHF,ICORE(IOFF),NVRTSQ,
     &             .FALSE.)
C
       CALL PUTLST(ICORE(IOFF),NOFFSET,1,1,IRREPX,178)
       IF(.NOT.SCF) 
     &     CALL PUTLST(ICORE(IOFF),JXYZ(ICART),1,1,IRREPX,188)
       IF(IUHF.EQ.1) THEN
        IOFF2=IOFF+NVRTSQ*IINTFP
        CALL PUTLST(ICORE(IOFF2),NOFFSET,1,1,IRREPX,179)
        IF(.NOT.SCF) 
     &      CALL PUTLST(ICORE(IOFF2),JXYZ(ICART),1,1,IRREPX,189)
       ENDIF
C
C  COMPUTE THE VIRTUAL-OCCUPIED BLOCK
C
       IOFF=IOFF+IINTFP*(IUHF*NVRTSQ+IRPDPD(IRREPX,20))
       CALL FORMAI(IRREPX,ICORE(IDDEXP),ICORE(ICMO(1)),NBASSQ,
     &             ICORE(ISCR),MXSCR,IUHF,ICORE(IOFF),NOVSQ,
     &             .FALSE.)
C
       CALL PUTLST(ICORE(IOFF),NOFFSET,1,1,IRREPX,180)
       IF(IUHF.EQ.1) THEN
        IOFF2=IOFF+NOVSQ*IINTFP
        CALL PUTLST(ICORE(IOFF2),NOFFSET,1,1,IRREPX,181)
       ENDIF
C
150    CONTINUE
C
100   CONTINUE
C
C ALL DONE, RETURN 
C
      RETURN
      END
