         
      SUBROUTINE UAIINVO(IRREPX,IOFFX,NX,FAIA,UAIA,ICORE,
     &                   MXCOR,IUHF)
C
C THIS ROUTINE COMPUTES THE CONTRIBUTIONS:
C
C  FD(A,I) = 2 SUM U(M,E) * (<MA||EI>+<MI||EA>) + U(m,e) * <Am|Ie> (ISPIN=1)
C
C  FD(a,i) = 2 SUM U(m,e) * (<ma||ei>+<mi||ea>) + U(M,E) * <aM|iE> (ISPIN=2)
C
CEND
C

      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,TWO,UAIA,FAIA
      DIMENSION FAIA(1),UAIA(1),ICORE(MXCOR)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      DATA ONE,ONEM,TWO/1.0D0,-1.D0,2.D0/
C
      DO 10 ISPIN=1,2
       LISTW1A=22+ISPIN
       LISTW1B=18+ISPIN
       LISTW2=19-ISPIN
       IRREP=IRREPX
       NDSZ1=IRPDPD(IRREP,ISYTYP(1,LISTW1A))
       NDIS1=IRPDPD(IRREP,ISYTYP(2,LISTW1A))
       NDSZ2=IRPDPD(IRREP,ISYTYP(1,LISTW2))
       NDIS2=IRPDPD(IRREP,ISYTYP(2,LISTW2))
C
       I000=1
       I010=I000+IINTFP*NDSZ1*NDIS1
       I020=I010+IINTFP*NDSZ1*NDIS1
       IF(I020.GE.MXCOR) CALL INSMEM('UAIINVO',I020,MXCOR)
       CALL GETLST(ICORE(I000),1,NDIS1,2,IRREP,LISTW1B)
       CALL GETLST(ICORE(I010),1,NDIS1,2,IRREP,LISTW1A)
       CALL SAXPY(NDSZ1*NDIS1,ONEM,ICORE(I010),1,ICORE(I000),1)
C
       DO 21 IPERT=IOFFX+1,NX
        CALL GETLST(UAIA,IPERT,1,1,IRREPX,181+ISPIN)
        CALL GETLST(FAIA,IPERT,1,1,IRREPX,179+ISPIN)
        CALL XGEMM('N','N',NDSZ1,1,NDIS1,ONE,ICORE(I000),NDSZ1,
     &             UAIA,NDIS1,ONE,FAIA,NDSZ1)
        CALL PUTLST(FAIA,IPERT,1,1,IRREPX,179+ISPIN)
21     CONTINUE
C
C NOW DO SECOND PART 
C
C  2* U(e,m) <Ae||Im>
C 
       I000=1
       I010=I000+IINTFP*NDSZ2*NDIS2
       IF(I010.GE.MXCOR) CALL INSMEM('UAIINVO',I010,MXCOR)
       CALL GETLST(ICORE(I000),1,NDIS2,2,IRREP,LISTW2)
       DO 22 IPERT=IOFFX+1,NX
        CALL GETLST(UAIA,IPERT,1,1,IRREPX,184-ISPIN)
        CALL GETLST(FAIA,IPERT,1,1,IRREPX,179+ISPIN)
        CALL XGEMM('N','N',NDSZ2,1,NDIS2,TWO,ICORE(I000),
     &             NDSZ2,UAIA,NDIS2,ONE,FAIA,NDSZ2)
        CALL PUTLST(FAIA,IPERT,1,1,IRREPX,179+ISPIN)
22     CONTINUE
10    CONTINUE
      RETURN
      END
