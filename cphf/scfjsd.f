      SUBROUTINE SCFJSD(IRREP,NPERTI,UAIA,UAIB,
     &                  BAIA,BAIB,JSD,JSD2,SCR1,
     &                  SCR2,NCOORD,IPERT)
C
C THIS ROUTINE CALCULATES THE SPIN-DIPOLAR
C CONTRIBUTION TO THE INDIRECT SPIN-SPIN COUPLING CONSTANTS
C
CEND
C
C JG 4/93
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL SCF,NONHF,LAST
      INTEGER DIRPRD
      DOUBLE PRECISION JSD,JSD2,MPROTON
C
      DIMENSION UAIA(1),UAIB(1),BAIA(1),BAIB(1),
     &          JSD(NCOORD,NCOORD),JSD2(NCOORD/2,NCOORD/2),
     &          SCR1(1),SCR2(1)
C
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),NJUNK(18)
      COMMON/METHOD/IUHF,SCF,NONHF
      COMMON/PERT/NTPERT,NNPERT(8),IIPERT(8),IXPERT,IYPERT,IZPERT,
     &            IYZPERT,IXZPERT,IXYPERT,ITRANSX,ITRANSY,ITRANSZ,
     &            NUCIND
      COMMON/FLAGS/IFLAGS(100)
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
C
      DATA IONE/1/
      DATA HALFM,ONE,ONEM /-0.5D0,1.0D0,-1.D0/
      DATA TWO,FOUR,EIGHT /2.D0,4.D0,8.D0/
C
C C: VELOCITY OF LINGHT IN A.U.
C
      DATA C /137.035987D0/
C
C MPROTON: MASS OF PROTON IN A.U.
C
      DATA MPROTON /1836.152736D0/
C
C CONVERT: CONVERSION FROM A.U. TO SEC  (FOR TIME UNITS)
C
      DATA  CONVERT /2.4188843D-17/
C
      DATA PI /3.141592654D0/
C
      DATA NINE/9.D0/
C
C CONVERSION OF CALCULATED COUPLING TENSOR TO THE USUAL UNITS (HZ)
C
      FACT=ONEM/(EIGHT*(MPROTON**2)*(C**4)*CONVERT*TWO*NINE*PI)
   
C
C DETERMINE LENGTHS OF UAI AND BAI VECTORS
C
      NAA=IRPDPD(IRREP,9)
      NAB=IRPDPD(IRREP,10)
C 
C  LOOP OVER ALL NUCLEAR SPINS WITHIN THIS IRREP
C
      DO 1000 IPERT1=1,NPERTI
C
       IND1=IPERT+IPERT1
C
       IOFF1=1+(IPERT1-1)*NAA
       IOFF1B=1+(IPERT1-1)*NAB
C
C  LOOP OVER ALL PERTURBATIONS IN THIS IRREP
C
      DO 1000 IPERT2=1,NPERTI
C
       IND2=IPERT+IPERT2
C
       IOFF4=1+(IPERT2-1)*NAA
       IOFF4B=1+(IPERT2-1)*NAB
C
       JSD(IND1,IND2)=
     &        -FACT*SDOT(NAA,UAIA(IOFF4),1,BAIA(IOFF1),1)
C
       IF(IUHF.EQ.0) THEN
        JSD(IND1,IND2)=JSD(IND1,IND2)*TWO
       ELSE
        JSD(IND1,IND2)=JSD(IND1,IND2)
     &        -FACT*SDOT(NAB,UAIB(IOFF4B),1,BAIB(IOFF1B),1)
C
       ENDIF
1000  CONTINUE
C
      IF(IND1.EQ.NCOORD) THEN
C
C FOR CASES WITH SYMMETRY, TRANSFORM THE COUPLING TENSOR BACK TO
C THE NON-SYMMETRIC REPRESENTATION
C
       IF(NIRREP.NE.1) THEN
C
        CALL ZERO(JSD2,NCOORD*NCOORD)
        CALL TRAHES(JSD,JSD2,SCR1,SCR2,NCOORD)
c YAU : old
c       CALL ICOPY(NCOORD*NCOORD*IINTFP,JSD2,1,JSD,1)
c YAU : new
        CALL DCOPY(NCOORD*NCOORD,JSD2,1,JSD,1)
c YAU : end
       ENDIF
C
C REDUCE JSD MATRIX TO FULL COUPLING CONSTANT MATRIX
C
       CALL REDJSD(JSD,JSD2,NCOORD)
C
C PRINT FC CONTRIBUTION TO J
C
       CALL GETREC(20,'JOBARC','NINDATOM',IONE,NUCIND)
       CALL GETREC(20,'JOBARC','MULTATOM',NUCIND,SCR1)
       CALL GETREC(20,'JOBARC','NAMCOORD',IINTFP*3*NUCIND,
     &             SCR1(1+NUCIND))
C
C 10/13 Ajith change the print level
C
       IF (IFLAGS(2) .GE. 20) THEN
          IF(SCF) THEN
        CALL HEADER('Spin-dipolar contribution to J (in Hz)',-1)
          ELSE
         CALL HEADER(
     &         'SCF spin-dipolar contribution to J (in Hz)',-1)
         ENDIF
C
C JPRI ALSO CONVERTS THE COUPLINGS TO THE REAL J (GIVEN IN HZ)
C
 
         CALL JPRI(JSD2,NCOORD/2,NUCIND,SCR1,SCR1(1+NUCIND),
     &             SCR2,.TRUE.)
         ENDIF
C
C SAVE JSD ON THE FILE `JSD' FOR LATER USE
C
      OPEN(UNIT=82,FILE='JSD',STATUS='UNKNOWN',FORM='FORMATTED')
C
3001  FORMAT(3F20.10)
      REWIND(82)
      DO 2100 ICOORD=1,NCOORD/2
       WRITE(82,3001) (JSD2(ICOORD,J),J=1,NCOORD/2)
2100  CONTINUE
      CLOSE(UNIT=82,STATUS='KEEP')
C
      IF(.NOT.SCF) THEN
C
C SAVE JSD ON THE FILE `JSD' FOR LATER USE
C
       OPEN(UNIT=82,FILE='JSDSCF',STATUS='UNKNOWN',FORM='FORMATTED')
C
       REWIND(82)
       DO 2101 ICOORD=1,NCOORD/2
        WRITE(82,3001) (JSD2(ICOORD,J),J=1,NCOORD/2)
2101   CONTINUE
       CLOSE(UNIT=82,STATUS='KEEP')
C
      ENDIF
      ENDIF
C
      RETURN
      END
