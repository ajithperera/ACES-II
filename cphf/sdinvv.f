         
      SUBROUTINE SDINVV(IRREPX,IVV,DOO,ICORE,MXCOR,IUHF)
C
C THIS ROUTINE COMPUTES THE CONTRIBUTIONS:
C
C      FD(A,B) = - SUM SD(M,N) * <MA||NB> + SD(m,n) * <Am|Bn> (ISPIN=1)
C
C      FD(a,b) = - SUM SD(m,n) * <ma||nb> + SD(M,N) * <aM|bN> (ISPIN=2)
C
CEND
C

      IMPLICIT INTEGER (A-Z)
      CHARACTER*4 TYPE
      DOUBLE PRECISION IVV,DOO,ONE
      DIMENSION DOO(1),IVV(1),ICORE(MXCOR)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      DATA ONE /1.0D0/
C
      IF(IUHF.EQ.0)THEN
C
C SPIN ADAPTED RHF CODE
C
       LISTW1=25
       LISTW2=23
       DO 120 IRREP=1,NIRREP
        DISSIZ=IRPDPD(IRREP,ISYTYP(1,LISTW1)) 
        NUMDIS=IRPDPD(IRREP,ISYTYP(2,LISTW1)) 
        I000=1
        I010=I000+IINTFP*NUMDIS*DISSIZ
        I020=I010+IINTFP*DISSIZ*DISSIZ
        CALL GETLST(ICORE(I000),1,NUMDIS,1,IRREP,LISTW1)
        CALL GETLST(ICORE(I010),1,NUMDIS,2,IRREP,LISTW2)
        CALL SAXPY(NUMDIS*DISSIZ,ONE,ICORE(I010),1,ICORE(I000),1)
        CALL VMINUS(ICORE(I000),NUMDIS*DISSIZ)
        CALL DDOT24(IRREPX,IRREP,IVV,DOO,ICORE(I000),ICORE(I010),DISSIZ,
     &             VRT(1,1),VRT(1,1),VRT(1,1),POP(1,1),VRT(1,1),
     &             POP(1,1),'TSTS')
120    CONTINUE
      ELSEIF(IUHF.NE.0)THEN
       DO 10 ISPIN=1,2
        LISTW1=22+ISPIN
        LISTW2=24+ISPIN
        DO 20 IRREP=1,NIRREP
         NDSZ1=IRPDPD(IRREP,ISYTYP(1,LISTW1))
         NDIS1=IRPDPD(IRREP,ISYTYP(2,LISTW1))
         NDSZ2=IRPDPD(IRREP,ISYTYP(1,LISTW2))
         NDIS2=IRPDPD(IRREP,ISYTYP(2,LISTW2))
         IOFFI=1+(ISPIN-1)*IRPDPD(IRREPX,19)
C
C DO FIRST PART - SD(m,n) * I(ma,nb)
C
         I000=1
         I010=I000+IINTFP*NDSZ1*NDIS1
         I020=I010+IINTFP*IRPDPD(IRREPX,19)
         IOFFD=1+(ISPIN-1)*IRPDPD(IRREPX,21)
         CALL GETLST(ICORE(I000),1,NDIS1,2,IRREP,LISTW1)
         CALL VMINUS(ICORE(I000),NDSZ1*NDIS1)
         CALL DDOT24(IRREPX,IRREP,IVV(IOFFI),DOO(IOFFD),ICORE(I000),
     &              ICORE(I010),NDSZ1,VRT(1,ISPIN),VRT(1,ISPIN),
     &              VRT(1,ISPIN),POP(1,ISPIN),VRT(1,ISPIN),POP(1,ISPIN),
     &             'TSTS')
C
C NOW DO SECOND PART 
C
C        SD(m,n) * <Am|Bn> [ISPIN=1]
C
C        SD(M,N) * <aM|bN> [ISPIN=2]
C
         I000=1
         I010=I000+IINTFP*NDSZ2*NDIS2
         I020=I010+IINTFP*IRPDPD(IRREPX,18+ISPIN)
         IOFFD=1+(2-ISPIN)*IRPDPD(IRREPX,21)
         CALL GETLST(ICORE(I000),1,NDIS2,2,IRREP,LISTW2)
         CALL VMINUS(ICORE(I000),NDSZ2*NDIS2)
         TYPE='TSTS'
         CALL DDOT24(IRREPX,IRREP,IVV(IOFFI),DOO(IOFFD),ICORE(I000),
     &              ICORE(I010),NDSZ2,VRT(1,ISPIN),VRT(1,ISPIN),
     &              VRT(1,ISPIN),POP(1,3-ISPIN),VRT(1,ISPIN),
     &              POP(1,3-ISPIN),TYPE)
20      CONTINUE
10     CONTINUE
      ENDIF
      RETURN
      END
