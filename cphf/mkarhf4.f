      SUBROUTINE MKARHF4(IRREP,AMAT,SCRATCH)
C
C   THIS ROUTINE CALCULATES THE A-MATRIX USED IN THE
C   SOLUTION OF THE CPHF EQUATION FOR RHF WAVE FUNCTIONS
C   IN CASES WHERE THE PERTURBATION BREAKS THE SPIN SYMMETREY.
C   NOTE, ONLY THE BLOCK OF THE A MATRIX CORRESPONDING TO
C   IRREP OF THE PERTURBATION IS REQUIRED. FURTHER,
C   FOR RHF WE ARE USING A SPIN-ADAPTED VERSION.
C   THE FORMULA IS GIVEN BY
C
C   A(AI,BJ) = <AB//IJ> + <AJ//IB> - <Ab//Ij> - <Aj//Ib>
C
C            = <AB//IJ> - <JA//IB> - 2 <Ab//Ij>
C
C            =  - <Ab//Ji> - <Aj//Bi>
C
C   IRREP : THE IRREP OF THE CONSIDERED PERTURBATION
C   AMAT : WILL RETURN THE CORRESPONDING PART OF THE A MATRIX
C   SCRATCH : SCRATCH VECTOR REQUIRED TO CONSTRUCT THE A MATRIX
C
CEND
C
C   JG 4/93, A MODIFICATION OF MKARHF2
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD,DISSYW,POP,VRT
      DIMENSION AMAT(1),SCRATCH(1)
      COMMON/INFO/NOCCO(2),NVRTO(2)
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NJUNK(6)
C
      DATA ONEM/-1.0D+0/
C
C   READ IN FIRST THE INTEGRALS - <Ab//Ji> (ORDERING Ai,bJ) 
C   THEY ARE STORED ON LIST 21
C
      LISTW=21          
      NUMSYW=IRPDPD(IRREP,ISYTYP(2,LISTW))
      DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
C
      CALL GETLST(AMAT,1,NUMSYW,2,IRREP,LISTW)
C
      CALL VMINUS(AMAT,NUMSYW*DISSYW)
C
C   NOW ADD THE INTEGRALS - <Aj//Bi> (ORDERING Ai,Bj)
C   THEY ARE STORED ON LIST 25
C
      LISTW=25
      NUMSYW=IRPDPD(IRREP,ISYTYP(2,LISTW))
      DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
C
      CALL GETLST(SCRATCH,1,NUMSYW,2,IRREP,LISTW)
C
      CALL SAXPY(NUMSYW*DISSYW,ONEM,SCRATCH,1,AMAT,1)
C
C  ALL DONE
C
      RETURN
      END      
