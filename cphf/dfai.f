         
      SUBROUTINE DFAI(IRREPX,IOFFX,NFX,DFAIA,UAIA,SIJA,SABA,
     &                EVAL,FAIA,FIJA,FABA,SCR)
C
C THIS ROUTINE CALCULATES THE FINAL CONTRIBUTIONS TO THE
C VIRTUAL-OCCUPIED BLOCK OF THE TOTAL DERIVATIVES OF
C THE FOCK MATRIX
C
C   d f(A,I)         x        x                           x
C   -------- = f(A,I)  + SUM U (R,A) f(R,I) + SUM f(A,R) U (R,I)
C     d x                 R                    R
C
C HOWEVER, THE FOLLOWING TERMS ARE ALREADY INCLUDED IN - B(A,I)^x
C
C         x
C  - SUM S (M,A) f(M,I)
C     M
C
C                x
C  - SUM f(A,M) S (M,I)
C     M
C
C IT REMAINS TO CALCULATE THE FOLLOWING TERMS
C
C         x
C  - SUM U (A,M) f(M,I)
C     M
C
C                x
C  - SUM f(A,M) U (I,M)
C     M
C
C         x
C    SUM U (E,A) f(E,I)
C     E
C  
C                x
C    SUM f(A,E) U (E,I)
C     E
C
CEND
C
C CODED MAY/91 JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER POP,VRT,DIRPRD
      CHARACTER*1 SPCASE(2)
      LOGICAL GEOM,FIELD,THIRD,MAGN,STERM,SPIN
C
      DIMENSION DFAIA(100),UAIA(100),SIJA(100),SABA(100),
     &          EVAL(100),FAIA(100),FIJA(100),FABA(100),SCR(100)
C
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/INFO/NOCCO(2),NVRTO(2)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),NJUNK(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/PERT/NTPERT,NPERT(8),IIPERT(8),IXPERT,IYPERT,IZPERT,
     &            IYZPERT,IXZPERT,IXYPERT,ITRANSX,ITRANSY,ITRANSZ,
     &            NUCIND
      COMMON/PERT2/FIELD,GEOM,THIRD,MAGN,SPIN
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
C
      DATA SPCASE /'A','B'/
      DATA ONE,ONEM,HALF,HALFM,TWOM /1.D0,-1.D0,0.5D0,-0.5D0,-2.D0/
C
      DO 10 ISPIN=1,2
C
       CALL GETLST(FIJA,1,1,1,2+ISPIN,91)
       CALL GETLST(FABA,1,1,1,2+ISPIN,92)
       CALL GETLST(FAIA,1,1,1,2+ISPIN,93)
       CALL GETREC(20,'JOBARC','SCFEVAL'//SPCASE(ISPIN),
     &             IINTFP*(NOCCO(1)+NVRTO(1)),EVAL)
C
       DO 12 IPERT=IOFFX+1,NFX
C
        STERM=(.NOT.FIELD).AND.(IPERT.LE.NPERT(IRREPX))
C
        CALL GETLST(UAIA,IPERT,1,1,IRREPX,181+ISPIN)
c         IF(STERM) THEN
         IF(ISPIN.EQ.1) THEN
          CALL GETLST(SIJA,IPERT,1,1,IRREPX,198)
          CALL SSCAL(IRPDPD(IRREPX,21),TWOM,SIJA,1)
          IF(STERM) THEN
           CALL GETLST(SABA,IPERT,1,1,IRREPX,171+ISPIN)
          ELSE
           CALL ZERO(SABA,IRPDPD(IRREPX,19))
          ENDIF
         ELSE
          IF(STERM) THEN
           CALL GETLST(SIJA,IPERT,1,1,IRREPX,169+ISPIN)
          ELSE
           CALL ZERO(SIJA,IRPDPD(IRREPX,22))
          ENDIF
          CALL GETLST(SABA,IPERT,1,1,IRREPX,199)
          CALL SSCAL(IRPDPD(IRREPX,20),TWOM,SABA,1)
         ENDIF
c        ENDIF
C
        CALL ZERO(DFAIA,IRPDPD(IRREPX,8+ISPIN))
C
C FIRST TERM: - SUM M U^x(A,M) f(M,I)
C
        IOFFD=1
        IOFFF=1
        IOFFU=1
C
        DO 20 IRREPR=1,NIRREP
C
         IRREPL=DIRPRD(IRREPR,IRREPX)
C
         NOCCR=POP(IRREPR,ISPIN)
         NVRTL=VRT(IRREPL,ISPIN)
C
         CALL XGEMM('N','N',NVRTL,NOCCR,NOCCR,ONEM,UAIA(IOFFU),
     &              NVRTL,FIJA(IOFFF),NOCCR,ONE,DFAIA(IOFFD),
     &              NVRTL)
C
C UPDATE OFFSETS
C
         IOFFF=IOFFF+NOCCR*NOCCR
         IOFFU=IOFFU+NOCCR*NVRTL
         IOFFD=IOFFD+NOCCR*NVRTL
C
20      CONTINUE
C
C SECOND TERM: - SUM M f(A,M) U^x(I,M)
C
c        IF(STERM) THEN
C
         IOFFD=1
         IOFFS=1
C
         DO 30 IRREPR=1,NIRREP
C
          IRREPL=DIRPRD(IRREPR,IRREPX)
C
          NOCCR=POP(IRREPR,ISPIN)
          NVRTL=VRT(IRREPL,ISPIN)
          NOCCL=POP(IRREPL,ISPIN)
C
          IOFFF=1
          IOFFS=1
          DO 89 IRREP=1,IRREPL-1
           IRREP1=DIRPRD(IRREP,IRREPX)
           IOFFF=IOFFF+VRT(IRREP,ISPIN)*POP(IRREP,ISPIN)
           IOFFS=IOFFS+POP(IRREP1,ISPIN)*POP(IRREP,ISPIN)
89        CONTINUE
C
          CALL XGEMM('N','T',NVRTL,NOCCR,NOCCL,HALF,FAIA(IOFFF),
     &               NVRTL,SIJA(IOFFS),NOCCR,ONE,DFAIA(IOFFD),
     &               NVRTL)
C
C UPDATE OFFSETS
C 
          IOFFD=IOFFD+NVRTL*NOCCR
          IOFFS=IOFFS+NOCCR*NOCCL
C
30       CONTINUE
C 
C THIRD TERM: SUM E U^x(E,A) f(E,I)
C
         IOFFD=1
         IOFFF=1
C
         DO 40 IRREPR=1,NIRREP
C
          IRREPL=DIRPRD(IRREPR,IRREPX)
C
          IOFFS=1
C 07/96 bug fix: Ajith 
          DO 41 IRREP1=1, IRREPL-1
           IRREP2=DIRPRD(IRREP1,IRREPX)
           IOFFS=IOFFS+VRT(IRREP1,ISPIN)*VRT(IRREP2,ISPIN)
41        CONTINUE
C
          NVRTL=VRT(IRREPL,ISPIN)
          NOCCR=POP(IRREPR,ISPIN)
          NVRTR=VRT(IRREPR,ISPIN)
C
          CALL XGEMM('T','N',NVRTL,NOCCR,NVRTR,HALFM,SABA(IOFFS),
     &               NVRTR,FAIA(IOFFF),NVRTR,ONE,DFAIA(IOFFD),
     &               NVRTL)
C
C UPDATE OFFSETS
C 
          IOFFF=IOFFF+NVRTR*NOCCR
          IOFFD=IOFFD+NVRTL*NOCCR
C
40       CONTINUE
C
c        ENDIF
C
C FOURTH TERM: SUM E F(A,E) U^x(E,I)
C
        IOFFD=1
        IOFFU=1
C
        DO 50 IRREPR=1,NIRREP
C
         IRREPL=DIRPRD(IRREPR,IRREPX)
C
         NVRTL=VRT(IRREPL,ISPIN)
         NOCCR=POP(IRREPR,ISPIN)
C
         IOFFF=1
         DO 87 IRREP=1,IRREPL-1
          IOFFF=IOFFF+VRT(IRREP,ISPIN)*VRT(IRREP,ISPIN)
87       CONTINUE
C
         CALL XGEMM('N','N',NVRTL,NOCCR,NVRTL,ONE,FABA(IOFFF),
     &              NVRTL,UAIA(IOFFU),NVRTL,ONE,DFAIA(IOFFD),
     &              NVRTL)
C
C UPDATE OFFSETS
C
         IOFFD=IOFFD+NVRTL*NOCCR
         IOFFU=IOFFU+NVRTL*NOCCR
C
50      CONTINUE
C
C ADD NOW DIAGONAL PIECES
C
C
C FIRST TERM - U(A,I) f(I,I)
C
        IOFFE=1
        IOFFU=1
        IOFFD=1
C
        DO 60 IRREPR=1,NIRREP
C
         IRREPL=DIRPRD(IRREPR,IRREPX)
C
         NVRTL=VRT(IRREPL,ISPIN)
         NOCCR=POP(IRREPR,ISPIN)
C
         DO 65 IOCC=1,NOCCR
C
          CALL SAXPY(NVRTL,-EVAL(IOFFE),UAIA(IOFFU),1,DFAIA(IOFFD),1)
C
          IOFFE=IOFFE+1
          IOFFU=IOFFU+NVRTL
          IOFFD=IOFFD+NVRTL
C
65       CONTINUE
C
60      CONTINUE
C
C SECOND TERM:  f(A,A) U(A,I)
C
        IOFFE=NOCCO(ISPIN)+1
        IOFFU=1
        IOFFD=1
C
        DO 70 IRREPR=1,NIRREP
C
         IRREPL=DIRPRD(IRREPR,IRREPX)
C
         NVRTL=VRT(IRREPL,ISPIN)
         NOCCR=POP(IRREPR,ISPIN)
C
         IOFFE1=IOFFE
         DO 88 IRREP=1,IRREPL-1
          IOFFE1=IOFFE1+VRT(IRREP,ISPIN)
88       CONTINUE
C
         IOFFU1=IOFFU
         IOFFD1=IOFFD
C
         DO 75 IVRT=1,NVRTL
C
          CALL SAXPY(NOCCR,EVAL(IOFFE1),UAIA(IOFFU1),NVRTL,
     &               DFAIA(IOFFD1),NVRTL)
C
          IOFFE1=IOFFE1+1
          IOFFU1=IOFFU1+1
          IOFFD1=IOFFD1+1
C
75       CONTINUE
C
         IOFFU=IOFFU+NVRTL*NOCCR
         IOFFD=IOFFD+NVRTL*NOCCR
C
70      CONTINUE
C
C ALL DONE, UPDATE NOW DFAI ON GAMLAM
C
        CALL GETLST(SCR,IPERT,1,1,IRREPX,179+ISPIN)
C
        CALL SAXPY(IRPDPD(IRREPX,8+ISPIN),ONE,SCR,1,DFAIA,1)
C
        CALL PUTLST(DFAIA,IPERT,1,1,IRREPX,179+ISPIN)
c        call checksum('dfai',dfaia,irpdpd(irrepx,8+ispin))
C
12     CONTINUE
10    CONTINUE
C
      RETURN
      END 
