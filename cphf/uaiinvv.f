         
      SUBROUTINE UAIINVV(IRREPX,IOFFX,NX,FABA,UAIA,ICORE,
     &                   MXCOR,IUHF)
C
C THIS ROUTINE COMPUTES THE CONTRIBUTIONS:
C
C      FD(A,B) =  SUM U(M,E) * <MA||EB> + U(m,e) * <Am|Be> (ISPIN=1)
C
C      FD(a,b) =  SUM U(m,e) * <ma||eb> + U(M,E) * <aM|be> (ISPIN=2)
C
CEND
C

      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,UAIA,FABA
      DIMENSION FABA(1),UAIA(1),ICORE(MXCOR)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      DATA ONE/1.0D0/
C
      IF(IUHF.EQ.0)THEN
C
C SPIN ADAPTED RHF CODE
C
       LISTW=30
       DO 120 IRREP=1,NIRREP
        DISSIZ=IRPDPD(IRREP,ISYTYP(1,LISTW)) 
        NUMDIS=IRPDPD(IRREP,ISYTYP(2,LISTW)) 
        I000=1
        I010=I000+IINTFP*NUMDIS*DISSIZ
        I020=I010+IINTFP*NUMDIS
        I030=I020+IINTFP*NUMDIS
        IF(I030.LT.MXCOR) THEN
         CALL GETLST(ICORE(I000),1,NUMDIS,1,IRREP,LISTW)
         CALL SPINAD3(IRREP,VRT(1,1),DISSIZ,NUMDIS,ICORE(I000),
     &                ICORE(I010),ICORE(I020))
         DO 121 IPERT=IOFFX+1,NX
         CALL GETLST(UAIA,IPERT,1,1,IRREPX,182)
         CALL ZERO(FABA,IRPDPD(IRREPX,19))
         CALL DDOT24(IRREPX,IRREP,FABA,UAIA,ICORE(I000),ICORE(I010),
     &               DISSIZ,
     &               VRT(1,1),VRT(1,1),VRT(1,1),VRT(1,1),VRT(1,1),
     &               POP(1,1),'TSTS')
         CALL SYMMET6(IRREPX,VRT(1,1),FABA,ICORE(I010),
     &                IRPDPD(IRREPX,19))
         CALL GETLST(ICORE(I010),IPERT,1,1,IRREPX,178)
         CALL SAXPY(IRPDPD(IRREPX,19),ONE,FABA,1,ICORE(I010),1)
         CALL PUTLST(ICORE(I010),IPERT,1,1,IRREPX,178)
121      CONTINUE  
        ELSE
         call errex
         IOFFSET=1
         IOFFU=1
         DO 123 IRREP1=1,NIRREP
          NOCC=POP(IRREP1,1)
          IRREP2=DIRPRD(IRREP1,IRREP)
          NVRT=VRT(IRREP2,1)
          I010=I000+IINTFP*DISSIZ*NVRT
          I020=I010+IINTFP*NVRT
          I030=I020+IINTFP*NVRT
          IF(I030.GE.MXCOR) CALL INSMEM('UAIINVV',I030,MXCOR)
          DO 123 IOCC=1,NOCC
           CALL GETLST(ICORE(I000),IOFFSET,NVRT,1,IRREP,LISTW)
           IOFFSET=IOFFSET+NVRT
           CALL SPINAD3(IRREP,VRT,DISSIZ,NVRT,ICORE(I000),
     &                  ICORE(I010),ICORE(I020))
           DO 124 IPERT=IOFFX+1,NX
            CALL GETLST(UAIA,IPERT,1,1,IRREPX,182)
            CALL ZERO(FABA,IRPDPD(IRREPX,19))
            CALL DDOT24(IRREPX,IRREP,FABA,UAIA(IOFFU),ICORE(I000),
     &                  ICORE(I010),DISSIZ,VRT(1,1),VRT(1,1),VRT(1,1),
C
C Added the missing argument (02/17/97)--AP. 
C Not a problem since this piece is never get called
C
     &                  VRT(1,1),VRT(1,1),POP(1,1),'TSTS')
            CALL SYMMET6(IRREPX,VRT(1,1),FABA,ICORE(I010),
     &                   IRPDPD(IRREPX,19))
            CALL GETLST(ICORE(I010),IPERT,1,1,IRREPX,178)
            CALL SAXPY(IRPDPD(IRREPX,19),ONE,FABA,1,ICORE(I010),1)
            CALL PUTLST(ICORE(I010),IPERT,1,1,IRREPX,178)
124        CONTINUE
123      CONTINUE
        ENDIF 
120    CONTINUE
      ELSEIF(IUHF.NE.0)THEN
       DO 10 ISPIN=1,2
        LISTW1=26+ISPIN
        LISTW2=31-ISPIN
        DO 20 IRREP=1,NIRREP
         NDSZ1=IRPDPD(IRREP,ISYTYP(1,LISTW1))
         NDSZF=IRPDPD(IRREP,18+ISPIN)
         NDIS1=IRPDPD(IRREP,ISYTYP(2,LISTW1))
         NDSZ2=IRPDPD(IRREP,ISYTYP(1,LISTW2))
         NDIS2=IRPDPD(IRREP,ISYTYP(2,LISTW2))
C
C DO FIRST PART - SD(m,n) * I(mi,nj)
C
         I000=1
         I010=I000+IINTFP*NDSZF*NDIS1
         CALL GETLST(ICORE(I000),1,NDIS1,2,IRREP,LISTW1)
         CALL SYMEXP2(IRREP,VRT(1,ISPIN),NDSZF,NDSZ1,NDIS1,
     &                ICORE(I000),ICORE(I000))
         DO 21 IPERT=IOFFX+1,NX
         CALL GETLST(UAIA,IPERT,1,1,IRREPX,181+ISPIN)
         CALL ZERO(FABA,IRPDPD(IRREPX,18+ISPIN))
         CALL DDOT24(IRREPX,IRREP,FABA,UAIA,ICORE(I000),
     &              ICORE(I010),NDSZF,VRT(1,ISPIN),VRT(1,ISPIN),
     &              VRT(1,ISPIN),VRT(1,ISPIN),VRT(1,ISPIN),
     &              POP(1,ISPIN),'TSTS')
         CALL SYMMET6(IRREPX,VRT(1,ISPIN),FABA,ICORE(I010),
     &                IRPDPD(IRREPX,18+ISPIN))
         CALL GETLST(ICORE(I010),IPERT,1,1,IRREPX,177+ISPIN)
         CALL SAXPY(IRPDPD(IRREPX,18+ISPIN),ONE,FABA,1,ICORE(I010),1)
         CALL PUTLST(ICORE(I010),IPERT,1,1,IRREPX,177+ISPIN)
21       CONTINUE
C
C NOW DO SECOND PART 
C
C        SD(m,n) * <Im|Jn> [ISPIN=1]
C 
C        SD(M,N) * <Mi|Nj> [ISPIN=2]
C
         I000=1
         I010=I000+IINTFP*NDSZ2*NDIS2
         CALL GETLST(ICORE(I000),1,NDIS2,2,IRREP,LISTW2)
         DO 22 IPERT=IOFFX+1,NX
         CALL GETLST(UAIA,IPERT,1,1,IRREPX,184-ISPIN)
         CALL ZERO(FABA,IRPDPD(IRREPX,18+ISPIN))
         IF(ISPIN.EQ.1) THEN
         CALL DDOT24(IRREPX,IRREP,FABA,UAIA,ICORE(I000),
     &              ICORE(I010),NDSZ2,VRT(1,ISPIN),VRT(1,ISPIN),
     &              VRT(1,ISPIN),VRT(1,3-ISPIN),VRT(1,ISPIN),
     &              POP(1,3-ISPIN),'TSTS')
         ELSE
         CALL DDOT24(IRREPX,IRREP,FABA,UAIA,ICORE(I000),
     &              ICORE(I010),NDSZ2,VRT(1,ISPIN),VRT(1,ISPIN),
     &              VRT(1,3-ISPIN),VRT(1,ISPIN),POP(1,3-ISPIN),
     &              VRT(1,ISPIN),'STST')
         ENDIF
         CALL SYMMET6(IRREPX,VRT(1,ISPIN),FABA,ICORE(I010),
     &                IRPDPD(IRREPX,18+ISPIN))
         CALL GETLST(ICORE(I010),IPERT,1,1,IRREPX,177+ISPIN)
         CALL SAXPY(IRPDPD(IRREPX,18+ISPIN),ONE,FABA,1,ICORE(I010),
     &              1)
         CALL PUTLST(ICORE(I010),IPERT,1,1,IRREPX,177+ISPIN)
22       CONTINUE
20      CONTINUE
10     CONTINUE
      ENDIF
      RETURN
      END
