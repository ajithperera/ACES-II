         

 
      SUBROUTINE SETBAS
C
C  THIS SUBROUTINE FILLS THE COMMON BLOCK /BASSYM/
C
C  ITS PARAMETER ARE
C 
C      NBAS ..... NUMBER OF BASIS FUNCTION PER IRREP
C      NBASIS ... TOTAL NUMBER OF BASIS FUNCTION
C      NBASSQ ... SIZE OF A SYMMETRY PACKED SQUARE MATRIX
C      NBASTT ... SIZE OF A SYMMETRY PACKED TRIANGULAR MATRIX
C
CEND            
C
      IMPLICIT INTEGER(A-Z)
      COMMON/BASSYM/NBAS(8),NBASIS,NBASSQ,NBASTT
      COMMON/LSYM/NLENQ(8),NLENT(8)
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/METHOD/IUHF,SCF,NONHF
C
      DO 100 IRREP=1,NIRREP
C
       NBAS(IRREP)=POP(IRREP,1)+VRT(IRREP,1)
C
100   CONTINUE
C
      NBASIS=0
      NBASSQ=0
      NBASTT=0
C
      DO 200 IRREP=1,NIRREP
C
       NBASIS=NBASIS+NBAS(IRREP)
C
       NBASSQ=NBASSQ+NBAS(IRREP)*NBAS(IRREP)
C
       NBASTT=NBASTT+(NBAS(IRREP)*(NBAS(IRREP)+1))/2
C
200   CONTINUE
C
      NLENQ(1)=NBASSQ
      NLENT(1)=NBASTT
C
      DO 300 IRREP=2,NIRREP
C
       NLENQ(IRREP)=0
       NLENT(IRREP)=0
C
       DO 400 IRREP1=1,NIRREP
C
        IRREP2=DIRPRD(IRREP,IRREP1)
C
        IF(IRREP2.GT.IRREP1) THEN
C
         NLENT(IRREP)=NLENT(IRREP)+NBAS(IRREP1)*NBAS(IRREP2)
C
        ENDIF
C
        NLENQ(IRREP)=NLENQ(IRREP)+NBAS(IRREP1)*NBAS(IRREP2)
C
400    CONTINUE
C
300   CONTINUE


      RETURN
      END
