      SUBROUTINE SDEXP(IRREP,IPERT,SDCOMP,SDFULL,ICOORD,NLEN,
     &                 IXYZ)
C
C THIS ROUTINE EXTRACTS FROM THE COMPRESSED MATRIX OF THE
C OVERLAP INTEGRAL DERIVATIVES THE ELEMENTS WHICH CORRESPOND
C TO ONE PARTICULAR PERTURBATION.
C
CEND
C
C CODED JAN/91 JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD 
C
      DIMENSION SDCOMP(1),SDFULL(1),ICOORD(NLEN,3,1)
C
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/BASSYM/NBAS(8),NBASIS,NBASSQ,NBASTT
C
      IF(IRREP.EQ.1) THEN
C
      IOFFSC=0
      IOFFSF=0
      IOFFB=0
C
      DO 100 IRREPR=1,NIRREP
C
       N=NBAS(IRREPR)
C
       CALL ZERO(SDFULL(IOFFSF+1),N*N)
C
       DO 150 J=1,N
        DO 140 I=1,J
         INDOLD=J*(J-1)/2+I
         INDNEW1=I+(J-1)*N
         INDNEW2=J+(I-1)*N
         IF(ICOORD(I+IOFFB,IXYZ,IRREP).EQ.IPERT)  THEN
          SDFULL(IOFFSF+INDNEW1)=-SDCOMP(IOFFSC+INDOLD)
          SDFULL(IOFFSF+INDNEW2)=-SDCOMP(IOFFSC+INDOLD)
         ENDIF
         IF(ICOORD(J+IOFFB,IXYZ,IRREP).EQ.IPERT) THEN
          SDFULL(IOFFSF+INDNEW1)=SDCOMP(IOFFSC+INDOLD)
          SDFULL(IOFFSF+INDNEW2)=SDCOMP(IOFFSC+INDOLD)
         ENDIF
140     CONTINUE
150    CONTINUE
C
       IOFFSC=IOFFSC+N*(N+1)/2
       IOFFSF=IOFFSF+N*N
       IOFFB=IOFFB+N
       
100   CONTINUE       
C
      ELSE
C
      IOFF=0
      IOFF1=0
      IOFFBR=0
C
      DO 200 IRREPR=1,NIRREP
C
       IRREPL=DIRPRD(IRREP,IRREPR)
C
        NR=NBAS(IRREPR)
        NL=NBAS(IRREPL)
C
        CALL ZERO(SDFULL(1+IOFF),NR*NL)
C
C GET OFFSET FOR BASIS FUNCTIONS WITHIN IRREPL
C
        IOFFBL=0
        IOFF2=0
        DO 201 IRREPL1=1,IRREPL-1
         IOFFBL=IOFFBL+NBAS(IRREPL1)
         IF(IRREPL1.GT.DIRPRD(IRREPL1,IRREP)) THEN
         IOFF2=IOFF2+NBAS(IRREPL1)*NBAS(DIRPRD(IRREPL1,IRREP))
         ENDIF
201     CONTINUE
C
       IF(IRREPL.LT.IRREPR) THEN
C
C CASE 1 IRREPL LT IRREPR
C
c        write(*,*)'i,1,l,r', ioff,ioff1,ioffbl,ioffbr
        DO 240 IR=1,NR
        DO 250 IL=1,NL
         IND=IL+(IR-1)*NL
         IF(ICOORD(IR+IOFFBR,IXYZ,IRREP).EQ.IPERT) THEN
          SDFULL(IOFF+IND)=-SDCOMP(IOFF1+IND)
         ENDIF
         IF(ICOORD(IL+IOFFBL,IXYZ,IRREP).EQ.IPERT) THEN
          SDFULL(IOFF+IND)=SDCOMP(IOFF1+IND)
         ENDIF
250     CONTINUE
240     CONTINUE
C
        IOFF1=IOFF1+NR*NL
C
       ELSE
C
C CASE 2 IRREPL GT IRREPR
C
c        write(*,*) 'i,2,r,',ioff,ioff2,ioffbr,ioffbl
        DO 340 IR=1,NR
        DO 350 IL=1,NL
         IND1=IL+(IR-1)*NL
         IND2=IR+(IL-1)*NR
         IF(ICOORD(IR+IOFFBR,IXYZ,IRREP).EQ.IPERT) THEN
          SDFULL(IOFF+IND1)=SDCOMP(IOFF2+IND2)
         ENDIF
         IF(ICOORD(IL+IOFFBL,IXYZ,IRREP).EQ.IPERT) THEN
          SDFULL(IOFF+IND1)=-SDCOMP(IOFF2+IND2)
         ENDIF
350     CONTINUE
340     CONTINUE    
C
       ENDIF
C
C UPDATE OFFSETS
C
       IOFF=IOFF+NL*NR
       IOFFBR=IOFFBR+NR
C
200   CONTINUE
      ENDIF
C
      RETURN
      END
