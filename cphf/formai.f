
      SUBROUTINE FORMAI(IRREPP,DAO,CMO,NBSSQ,SCR,MXSCR,IUHF,
     &                  DMOAI,NOVSQ,FLAG)
C
C THIS SUBROUTINE TAKES ONE-ELECTRON INTEGRAL DERIVATIVES 
C GIVEN IN THE AO REPRESENTATION AND CALCULATES THEIR
C OCCUPIED-VIRTUAL BLOCK IN THE MO REPRESENTATION.
C
C INPUT : IRREPP ..... IRREP OF PERTURBATION
C         DAO ........ INTEGRAL DERIVATIVES IN AO BASIS
C         CMO ........ MO COEFFICIENT MATRIX
C         NBSSQ ...... LENGTH OF CMO   
C         SCR ........ SCRACTH VECTOR
C         MXSCR ...... LENGTH OF SCRACTH VECTOR IN DOUBLE PRECISION WORDS
C         IUHF ....... UHF FLAG
C         NOVSQ ...... LENGTH OF THE ALPHA OCCUPIED -VIRTUAL BLOCK
C         FLAG ....... TRUE IF DAO DEPENDS ON SPIN ( I.E. FOCK MATRICES)
C
C OUTPUT : DMOAI ..... MO INTEGRAL DERIVATIVES
C
CEND
       IMPLICIT DOUBLE PRECISION(A-H,O-Z)
       INTEGER POP,VRT,DIRPRD
       LOGICAL FLAG
C
       DIMENSION DAO(1),CMO(NBSSQ,1),SCR(MXSCR),DMOAI(NOVSQ,1)
C
       COMMON/OFFSET/IOFF1(8)
       COMMON/LSYM/NLENQ(8),NLENT(8)
       COMMON/BASSYM/NBAS(8),NBASIS,NBASSQ,NBASTT
       COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
       COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
C
       DATA AZERO,ONE /0.D0,1.D0/
C
C LOOP OVER ALL POSSIBLE SPIN CASES
C
       DO 1000 ISPIN=1,IUHF+1
C
C   LOOP OVER ALL IRREPS ON THE RIGHT HAND SIDE OF DAO
C
        IOFFCR=1
        IOFFAO=1
        IF(FLAG) IOFFAO=1+(ISPIN-1)*NLENQ(IRREPP)
        IOFFMO=1
        DO 900 IRREPR=1,NIRREP
C
C   IRREP OF THE LEFT HAND SIDE OF DAO IS NOW
C
         IRREPL=DIRPRD(IRREPP,IRREPR)
C
         IOFFCL=IOFF1(IRREPL)+POP(IRREPL,ISPIN)*NBAS(IRREPL)
C
C   PERFORM MULTIPLICATION
C
         CALL XGEMM('N','N',NBAS(IRREPL),POP(IRREPR,ISPIN),
     &              NBAS(IRREPR),ONE,
     &              DAO(IOFFAO),NBAS(IRREPL),CMO(IOFFCR,ISPIN),
     &              NBAS(IRREPR),AZERO,SCR,NBAS(IRREPL))
C 
         CALL XGEMM('T','N',VRT(IRREPL,ISPIN),POP(IRREPR,ISPIN),
     &              NBAS(IRREPL),ONE,
     &              CMO(IOFFCL,ISPIN),NBAS(IRREPL),SCR,NBAS(IRREPL),
     &              AZERO,DMOAI(IOFFMO,ISPIN),VRT(IRREPL,ISPIN))
C
         IOFFMO=IOFFMO+POP(IRREPR,ISPIN)*VRT(IRREPL,ISPIN)
         IOFFAO=IOFFAO+NBAS(IRREPR)*NBAS(IRREPL)
         IOFFCR=IOFFCR+NBAS(IRREPR)*NBAS(IRREPR)
C
900     CONTINUE
C
c        call checksum('formai',dmoai(1,ispin),ioffmo-1)
1000   CONTINUE
C
       RETURN
       END
