      SUBROUTINE SCFDDP(IRREP,NPERT,UAIA,UAIB,SIJA,SIJB,
     &                  BAIA,BAIB,BIJA,BIJB,
     &                  DDP,DDP2,SCR1,SCR2,ISCR,CHARGE,
     &                  NCOORD,IPERT,NFPERT,IXYZSYM,LAST,
     &                  ITRANS)
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL SCF,NONHF,LAST,TRAINV
      INTEGER DIRPRD
C
      DIMENSION UAIA(1),UAIB(1),BAIA(1),BAIB(1),
     &          SIJA(1),SIJB(1),BIJA(1),BIJB(1),
     &          DDP(3,NCOORD),DDP2(3,NCOORD),IXYZSYM(3),
     &          CHARGE(NCOORD),SCR1(1),SCR2(1),ISCR(9),
     &          ITRANS(1) 
C
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),NJUNK(18)
      COMMON/METHOD/IUHF,SCF,NONHF
      COMMON/PERT/NTPERT,NNPERT(8),IIPERT(8),IXPERT,IYPERT,IZPERT,
     &            IYZPERT,IXZPERT,IXYPERT,ITRANSX,ITRANSY,ITRANSZ,
     &            NUCIND
      COMMON/INVAR/TRAINV
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
C
      DATA HALFM,ONE /-0.5D0,1.0D0/
      DATA TWO,FOUR /2.D0,4.D0/
C
C DETERMINE LENGTHS OF UAI AND BAI VECTORS
C
      NAA=IRPDPD(IRREP,9)
      NAB=IRPDPD(IRREP,10)
      NIA=IRPDPD(IRREP,21)
      NIB=IRPDPD(IRREP,22)
C 
      IND=0
      ISTART1=1
C
C  LOOP OVER ALL ELECTRICAL FIELD COMPONENTS WITHIN
C  THIS IRREP
C
      DO 1000 IFIELD=1,NFPERT
C
       IOFF1=1+(IFIELD-1)*NAA
       IOFF2=1+(IFIELD-1)*NIA
       IOFF1B=1+(IFIELD-1)*NAB
       IOFF2B=1+(IFIELD-1)*NIB
C
       DO 10 IXYZ1=ISTART1,3
        IF(IXYZSYM(IXYZ1).EQ.IRREP-1) THEN
         IND1=IXYZ1
         GO TO 15
        ENDIF
10     CONTINUE
       CALL ERREX
15     CONTINUE      
       ISTART1=IND1+1
C
C  LOOP OVER ALL PERTURBATIONS IN THIS IRREP
C
      DO 1000 IPERT1=1,NPERT
C
       IND2=ISCR(IPERT+IPERT1)
C
       IOFF4=1+(IPERT1-1)*NAA
       IOFF5=1+(IPERT1-1)*NIA
       IOFF4B=1+(IPERT1-1)*NAB
       IOFF5B=1+(IPERT1-1)*NIB
C
       DDP(IND1,IND2)=
     &        SDOT(NAA,BAIA(IOFF1),1,UAIA(IOFF4),1)
     &        +HALFM*SDOT(NIA,BIJA(IOFF2),1,SIJA(IOFF5),1)
C
       IF(IUHF.EQ.0) THEN
        DDP(IND1,IND2)=DDP(IND1,IND2)*TWO
       ELSE
        DDP(IND1,IND2)=DDP(IND1,IND2)+
     &        SDOT(NAB,BAIB(IOFF1B),1,UAIB(IOFF4B),1)
     &        +HALFM*SDOT(NIB,BIJB(IOFF2B),1,SIJB(IOFF5B),1)
C
       ENDIF
       DDP(IND1,IND2)=TWO*DDP(IND1,IND2)
1000  CONTINUE
C
C UPDATE THE DIPDER FILE
C
       IF(IND2.EQ.NCOORD.OR.LAST) THEN
C
C FOR CASES WITH SYMMETRY, TRANSFORM THE DIPOLE DERIVATIVES BACK TO
C THE NON-SYMMETRIC REPRESENTATION
C
        IF(TRAINV) THEN
C
C CREATE FULL DIPDER CONTRIBUTION USING TRANSLATIONAL INVARIANCE 
C
         CALL EXPDDP(DDP,NCOORD,ITRANS,NUCIND)
C
        ENDIF 
C
        IF(NIRREP.NE.1) THEN
C
         CALL ZERO(DDP2,NCOORD*3)
         CALL TRADIP(DDP,DDP2,SCR1,NCOORD)
c YAU : old
c        CALL ICOPY(NCOORD*3*IINTFP,DDP2,1,DDP,1)
c YAU : new
         CALL DCOPY(NCOORD*3,DDP2,1,DDP,1)
c YAU : end
C
        ENDIF
C
        IF(SCF) THEN
         OPEN(UNIT=82,FILE='DIPDER',STATUS='OLD',FORM='FORMATTED')
         DO 2000 IFIELD=1,3
          READ(82,3000) IJUNK
          DO 2000 IATOM=1,NCOORD/3
           READ(82,3001) CHARGE(IATOM),DDP2(IFIELD,3*IATOM-2),
     &                   DDP2(IFIELD,3*IATOM-1),
     &                   DDP2(IFIELD,3*IATOM)
2000     CONTINUE
C
3000     FORMAT(I5)
3001     FORMAT(4F20.10)
C
         CALL SAXPY(NCOORD*3,ONE,DDP2,1,DDP,1)
C
        ELSE
         OPEN(UNIT=82,FILE='DIPDER',STATUS='UNKNOWN',FORM='FORMATTED')
        ENDIF
        REWIND(82)
C
        DO 2100 IFIELD=1,3
         WRITE(82,3000) IFIELD
         DO 2100 IATOM=1,NCOORD/3
           WRITE(82,3001) CHARGE(IATOM),DDP(IFIELD,3*IATOM-2),
     &                    DDP(IFIELD,3*IATOM-1),
     &                    DDP(IFIELD,3*IATOM)
2100    CONTINUE
C
        CLOSE(UNIT=82,STATUS='KEEP')
C
       ENDIF
      RETURN
      END
