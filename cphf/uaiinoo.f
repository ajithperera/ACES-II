      SUBROUTINE UAIINOO(IRREPX,IOFFX,NX,FIJA,UAIA,ICORE,
     &                   MXCOR,IUHF)
C
C THIS ROUTINE COMPUTES THE CONTRIBUTIONS:
C
C      FD(I,J) = 2 SUM U(M,E) * <MI||EJ> + U(m,e) * <Im|Je> (ISPIN=1)
C
C      FD(i,j) = 2 SUM SD(m,e) * <mi||ej> + U(M,E) * <iM|je> (ISPIN=2)
C
CEND
C

      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,UAIA,FIJA
      DIMENSION FIJA(1),UAIA(1),ICORE(MXCOR)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      DATA ONE /1.0D0/
C
      IF(IUHF.EQ.0)THEN
C
C SPIN ADAPTED RHF CODE
C
       LISTW=10
       DO 120 IRREP=1,NIRREP
        DISSIZ=IRPDPD(IRREP,ISYTYP(1,LISTW)) 
        NUMDIS=IRPDPD(IRREP,ISYTYP(2,LISTW)) 
        I000=1
        I010=I000+IINTFP*NUMDIS*DISSIZ
        I020=I010+IINTFP*NUMDIS*DISSIZ
        I030=I020+IINTFP*NUMDIS
        CALL GETLST(ICORE(I000),1,NUMDIS,1,IRREP,LISTW)
        CALL SPINAD3(IRREP,POP(1,1),DISSIZ,NUMDIS,ICORE(I000),
     &               ICORE(I010),ICORE(I020))
        CALL TRANSP(ICORE(I000),ICORE(I010),NUMDIS,DISSIZ)
c YAU : old
c       CALL ICOPY(NUMDIS*DISSIZ*IINTFP,ICORE(I010),1,ICORE(I000),1)
c YAU : new
        CALL DCOPY(NUMDIS*DISSIZ,ICORE(I010),1,ICORE(I000),1)
c YAU : end
        DO 121 IPERT=IOFFX+1,NX
        CALL GETLST(UAIA,IPERT,1,1,IRREPX,182)
        CALL ZERO(FIJA,IRPDPD(IRREPX,21))
        CALL DDOT24(IRREPX,IRREP,FIJA,UAIA,ICORE(I000),ICORE(I010),
     &              NUMDIS,
     &             POP(1,1),POP(1,1),POP(1,1),VRT(1,1),POP(1,1),
     &             POP(1,1),'TSTS')
        CALL SYMMET6(IRREPX,POP(1,1),FIJA,ICORE(I010),
     &               IRPDPD(IRREPX,21))
        CALL GETLST(ICORE(I010),IPERT,1,1,IRREPX,176)
        CALL SAXPY(IRPDPD(IRREPX,21),ONE,FIJA,1,ICORE(I010),1)
        CALL PUTLST(ICORE(I010),IPERT,1,1,IRREPX,176)
121    CONTINUE
120    CONTINUE
      ELSEIF(IUHF.NE.0)THEN
       DO 10 ISPIN=1,2
        LISTW1=6+ISPIN
        LISTW2=11-ISPIN
        DO 20 IRREP=1,NIRREP
         NDSZ1=IRPDPD(IRREP,ISYTYP(1,LISTW1))
         NDSZF=IRPDPD(IRREP,20+ISPIN)
         NDIS1=IRPDPD(IRREP,ISYTYP(2,LISTW1))
         NDSZ2=IRPDPD(IRREP,ISYTYP(1,LISTW2))
         NDIS2=IRPDPD(IRREP,ISYTYP(2,LISTW2))
C
C DO FIRST PART 2 U(E,M) I(IE,JM)
C
C THE FACTOR OF TWO IS OBTAINED IN THE SYMMET6 CALL
C
         I000=1
         I001=1+IINTFP*NDSZ1*NDIS1
         I010=I000+IINTFP*NDSZF*NDIS1
         I020=I010+IINTFP*NDIS1*NDSZ1
         CALL GETLST(ICORE(I001),1,NDIS1,2,IRREP,LISTW1)
         CALL TRANSP(ICORE(I001),ICORE(I000),NDIS1,NDSZ1)
         CALL SYMEXP(IRREP,POP(1,ISPIN),NDIS1,ICORE(I000))
         DO 21 IPERT=IOFFX+1,NX
         CALL GETLST(UAIA,IPERT,1,1,IRREPX,181+ISPIN)
         CALL ZERO(FIJA,IRPDPD(IRREPX,20+ISPIN))
         CALL DDOT24(IRREPX,IRREP,FIJA,UAIA,ICORE(I000),
     &              ICORE(I010),NDIS1,POP(1,ISPIN),POP(1,ISPIN),
     &              POP(1,ISPIN),VRT(1,ISPIN),POP(1,ISPIN),
     &              POP(1,ISPIN),'TSTS')
         CALL SYMMET6(IRREPX,POP(1,ISPIN),FIJA,ICORE(I010), 
     &                IRPDPD(IRREPX,20+ISPIN))
         CALL GETLST(ICORE(I010),IPERT,1,1,IRREPX,175+ISPIN)
         CALL SAXPY(IRPDPD(IRREPX,20+ISPIN),ONE,FIJA,1,ICORE(I010),1)
         CALL PUTLST(ICORE(I010),IPERT,1,1,IRREPX,175+ISPIN)
21       CONTINUE
C
C NOW DO SECOND PART 
C
C      2* U(e,m) * <Ie|Jm> [ISPIN=1]
C 
C      2*U(E,M) * <Ei|Mj> [ISPIN=2]
C
         I000=1
         I010=I000+IINTFP*NDSZ2*NDIS2
         I020=I010+IINTFP*NDSZ2*NDIS2
         CALL GETLST(ICORE(I010),1,NDIS2,2,IRREP,LISTW2)
         CALL TRANSP(ICORE(I010),ICORE(I000),NDIS2,NDSZ2)
         DO 22 IPERT=IOFFX+1,NX
         CALL GETLST(UAIA,IPERT,1,1,IRREPX,184-ISPIN)
         CALL ZERO(FIJA,IRPDPD(IRREPX,20+ISPIN))
         IF(ISPIN.EQ.1) THEN
         CALL DDOT24(IRREPX,IRREP,FIJA,UAIA,ICORE(I000),
     &              ICORE(I010),NDIS2,POP(1,ISPIN),POP(1,ISPIN),
     &              POP(1,ISPIN),VRT(1,3-ISPIN),POP(1,ISPIN),
     &              POP(1,3-ISPIN),'TSTS')
         ELSE
         CALL DDOT24(IRREPX,IRREP,FIJA,UAIA,ICORE(I000),
     &              ICORE(I010),NDIS2,POP(1,ISPIN),POP(1,ISPIN),
     &              VRT(1,3-ISPIN),POP(1,ISPIN),
     &              POP(1,3-ISPIN),POP(1,ISPIN),'STST')
         ENDIF
         CALL SYMMET6(IRREPX,POP(1,ISPIN),FIJA,ICORE(I010),
     &                IRPDPD(IRREPX,20+ISPIN))
         CALL GETLST(ICORE(I010),IPERT,1,1,IRREPX,175+ISPIN)
         CALL SAXPY(IRPDPD(IRREPX,20+ISPIN),ONE,FIJA,1,ICORE(I010),1)
         CALL PUTLST(ICORE(I010),IPERT,1,1,IRREPX,175+ISPIN)
22       CONTINUE
20      CONTINUE
10     CONTINUE
      ENDIF
      RETURN
      END
