      SUBROUTINE TDEE_DETNCALC(SCR,IUHF)
C
C THE NUMBER OF DIFFERENT EXCITATION SPECTRA TO BE CALCULATED
C IS DETERMINED. AT PRESENT NCALC = 1, UNLESS FOR CORE-EXCITATION
C SPECTRA. THEN IT EQUALS THE NUMBER OF DIFFERENT 'CORE-SITES'
C IN THE SYSTEM
C
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION SCR(*), EMIN, EMINPREV, THRESH
C
      COMMON/INFO/NOCCO(2),NVRTO(2)
      COMMON/PROJECT/IPROJECT, IPATTERN, NCALC, ICALC
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      IF (IPATTERN .NE. 1) THEN
        NCALC = 1
      ELSE
C      
C  GET ORBITAL EIGENVALUES TO DETERMINE RELEVANT ORBITALS
C
        NBASA = NOCCO(1) + NVRTO(1)
        IF (IUHF.NE.0) THEN
          NBASB = NOCCO(2)  + NVRTO(2)
        ELSE
          NBASB = 0
        ENDIF
        I000 = 1
        I010 = I000 + NBASA
        I020 = I010 + NBASB
        CALL GETREC(20, 'JOBARC', 'SCFEVALA', IINTFP*NBASA, SCR(I000))
        IF (IUHF. NE. 0) THEN
          CALL GETREC(20, 'JOBARC', 'SCFEVALB', IINTFP*NBASB, SCR(I010))
        ENDIF
C  
C  DETERMINE NUMBER OF DIFFERENT CORE-SITES IN MOLECULE
C
        THRESH = 2.0D0 / 27.2113957D0
C
C  CORE ENERGIES CORRESPOND TO SAME 'SITE' IF THEY LY WITHIN THRESH (2 EV)
C
        NCALC = 0
        EMINPREV = - 1.0D8
  100   EMIN = 100.0D0
        DO I = 1, NOCCO(1)
          IF (SCR(I) .GT. EMINPREV .AND. SCR(I) .LT. EMIN)
     &       EMIN = SCR(I)
        ENDDO
        IF (EMIN .LT. -4.0) THEN
C
C   CORE-LEVEL ENERGY IS FOUND
C
          NCALC = NCALC + 1
          EMINPREV = EMIN + THRESH
C
        ENDIF
C
C  FIND NEXT CORE-SITE
C
        IF (EMIN .LT. -4.0) GOTO 100
C
        WRITE(6,*) ' Number of different core-sites: ', NCALC
C
      ENDIF
C
      RETURN
      END
