      SUBROUTINE T32IJKA14(GOOOV,OOOVAD,LNOOOV,W,CORE,
     1                     IADW,
     1                     IRPI,IRPJ,IRPK,IRPIJ,IRPIK,IRPJK,
     1                     I,J,K,ISPIN,LAMBDA)
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION TOL
      DOUBLE PRECISION W(1),CORE(1),GOOOV(LNOOOV)
      LOGICAL LAMBDA
      DIMENSION IADW(8)
      DIMENSION IADT2(8),LENT2(8),IADGI(8),LENGI(8)
      DIMENSION OOOVAD(8)
C
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     1                DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /SYM/    POP(8,2),VRT(8,2),NTAA,NTBB,NF1AA,NF2AA,
     1                NF1BB,NF2BB
C
      COMMON /T3OFF/  IOFFVV(8,8,10),IOFFOO(8,8,10),IOFFVO(8,8,4)
C
      INDEX(I) = I*(I-1)/2
C     List offset
      IF (LAMBDA) THEN
         LISTOFF=100
      ELSE
         LISTOFF=0
      ENDIF
C
      IF(IRPI.EQ.IRPJ)THEN
      IJ = IOFFOO(IRPJ,IRPIJ,ISPIN) + INDEX(J-1) + I
      ELSE
      IJ = IOFFOO(IRPJ,IRPIJ,ISPIN) + (J-1)*POP(IRPI,ISPIN) + I
      ENDIF
C
      IF(IRPI.EQ.IRPK)THEN
      IK = IOFFOO(IRPK,IRPIK,ISPIN) + INDEX(K-1) + I
      ELSE
      IK = IOFFOO(IRPK,IRPIK,ISPIN) + (K-1)*POP(IRPI,ISPIN) + I
      ENDIF
C
      IF(IRPJ.EQ.IRPK)THEN
      JK = IOFFOO(IRPK,IRPJK,ISPIN) + INDEX(K-1) + J
      ELSE
      JK = IOFFOO(IRPK,IRPJK,ISPIN) + (K-1)*POP(IRPJ,ISPIN) + J
      ENDIF
C
C     ROUTINE TO COMPUTE CONTRIBUTION OF T3 AAA OR T3 BBB IN W(IJ,LA) AAAA
C     OR BBBB. 
C
C
C                        ABC  BC
C     G(IJ,LA) = - SUM  T    W
C                  B<C   IJK  KL
C                   K
C       AA AA            AAA  AA
C       BB BB            BBB  BB
C
C     T IS (B<C,A) REPRESENTATION OF T3 (A<B<C).
C
C     SET ADDRESSES FOR W INCREMENTS (L,A), LABELLED BY IRPA.
C
      DO   20 IRPA=1,NIRREP
      IRPL = DIRPRD(IRPA,IRPIJ)
      LENGI(IRPA) = POP(IRPL,ISPIN) * VRT(IRPA,ISPIN)
      IF(IRPA.EQ.1)THEN
      IADGI(IRPA) = 1
      ELSE
      IADGI(IRPA) = IADGI(IRPA-1) + LENGI(IRPA-1)
      ENDIF
   20 CONTINUE
C
C     SET ADDRESSES FOR T2.
C
      DO   30 IRPL=1,NIRREP
      IRPKL = DIRPRD(IRPL,IRPK)
      IRPBC = IRPKL
      LENT2(IRPL) = IRPDPD(IRPBC,ISPIN) * POP(IRPL,ISPIN)
      IF(IRPL.EQ.1)THEN
      IADT2(IRPL) = 1
      ELSE
      IADT2(IRPL) = IADT2(IRPL-1) + LENT2(IRPL-1)
      ENDIF
   30 CONTINUE
C
C     READ W2(B<C,L).
C
      DO   70 IRPL=1,NIRREP
      IF(POP(IRPL,ISPIN).EQ.0) GOTO 70
      IRPKL = DIRPRD(IRPK,IRPL)
C
      IF(IRPK.LT.IRPL)THEN
      DO   40    L=1,POP(IRPL,ISPIN)
      KL = IOFFOO(IRPL,IRPKL,ISPIN) + (L-1)*POP(IRPK,ISPIN) + K
      CALL GETLST(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPKL,ISPIN)),
     1            KL,1,1,IRPKL,13+ISPIN+LISTOFF)
   40 CONTINUE
      ENDIF
C
      IF(IRPK.GT.IRPL)THEN
      KL = IOFFOO(IRPK,IRPKL,ISPIN) + (K-1)*POP(IRPL,ISPIN) + 1
      CALL GETLST(CORE(IADT2(IRPL)),
     1            KL,POP(IRPL,ISPIN),1,IRPKL,13+ISPIN+LISTOFF)
      CALL VMINUS(CORE(IADT2(IRPL)),LENT2(IRPL))
      ENDIF
C
      IF(IRPK.EQ.IRPL)THEN
      DO   60    L=1,POP(IRPL,ISPIN)
C
      IF(K.LT.L)THEN
      KL = IOFFOO(IRPL,IRPKL,ISPIN) + INDEX(L-1) + K
      CALL GETLST(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPKL,ISPIN)),
     1            KL,1,1,IRPKL,13+ISPIN+LISTOFF)
      ENDIF
C
      IF(K.GT.L)THEN
      KL = IOFFOO(IRPK,IRPKL,ISPIN) + INDEX(K-1) + L
      CALL GETLST(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPKL,ISPIN)),
     1            KL,1,1,IRPKL,13+ISPIN+LISTOFF)
      CALL VMINUS(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPKL,ISPIN)),
     1            IRPDPD(IRPKL,ISPIN))
      ENDIF
C
      IF(K.EQ.L)THEN
      CALL ZERO(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPKL,ISPIN)),
     1          IRPDPD(IRPKL,ISPIN))
      ENDIF
   60 CONTINUE
C
      ENDIF
C
C     WE NOW HAVE W IN THE FORM T2(B<C,L) AND T3 IN THE FORM
C     T3(B<C,A). CONTRACT TOGETHER TO FORM W(L,A).
C
      IRPA = DIRPRD(IRPIJ,IRPL)
      CALL XGEMM('T','N',
     1           POP(IRPL,ISPIN),VRT(IRPA,ISPIN),IRPDPD(IRPKL,ISPIN),
     1           -1.0D+00,CORE(IADT2(IRPL)),IRPDPD(IRPKL,ISPIN),
     1                    W(IADW(IRPA)),IRPDPD(IRPKL,ISPIN),
     1            1.0D+00,
     1           GOOOV(OOOVAD(IRPIJ) + (IJ-1)*IRPDPD(IRPIJ,15+ISPIN)
     1                 + IADGI(IRPA) - 1),POP(IRPL,ISPIN))
   70 CONTINUE
C
C                        ABC  BC
C     G(IK,LA) =   SUM  T    W
C                  B<C   IJK  JL
C                   J
C       AA AA            AAA  AA
C       BB BB            BBB  BB
C
C     W IS (B<C,A) REPRESENTATION OF T3 (A<B<C).
C
C     SET ADDRESSES FOR W INCREMENTS (L,A), LABELLED BY IRPA.
C
      DO  120 IRPA=1,NIRREP
      IRPL = DIRPRD(IRPA,IRPIK)
      LENGI(IRPA) = POP(IRPL,ISPIN) * VRT(IRPA,ISPIN)
      IF(IRPA.EQ.1)THEN
      IADGI(IRPA) = 1
      ELSE
      IADGI(IRPA) = IADGI(IRPA-1) + LENGI(IRPA-1)
      ENDIF
  120 CONTINUE
C
C     SET ADDRESSES FOR W2.
C
      DO  130 IRPL=1,NIRREP
      IRPJL = DIRPRD(IRPL,IRPJ)
      IRPBC = IRPJL
      LENT2(IRPL) = IRPDPD(IRPBC,ISPIN) * POP(IRPL,ISPIN)
      IF(IRPL.EQ.1)THEN
      IADT2(IRPL) = IADGI(NIRREP) + LENGI(NIRREP)
      ELSE
      IADT2(IRPL) = IADT2(IRPL-1) + LENT2(IRPL-1)
      ENDIF
  130 CONTINUE
C
C     READ W(B<C,L).
C
      DO  170 IRPL=1,NIRREP
      IF(POP(IRPL,ISPIN).EQ.0) GOTO 170
      IRPJL = DIRPRD(IRPJ,IRPL)
C
      IF(IRPJ.LT.IRPL)THEN
      DO  140    L=1,POP(IRPL,ISPIN)
      JL = IOFFOO(IRPL,IRPJL,ISPIN) + (L-1)*POP(IRPJ,ISPIN) + J
      CALL GETLST(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPJL,ISPIN)),
     1            JL,1,1,IRPJL,13+ISPIN+LISTOFF)
  140 CONTINUE
      ENDIF
C
      IF(IRPJ.GT.IRPL)THEN
C     DO  150    L=1,POP(IRPL,ISPIN)
C     JL = IOFFOO(IRPJ,IRPJL,ISPIN) + (J-1)*POP(IRPL,ISPIN) + L
C     CALL GETLST(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPJL,ISPIN)),
C    1            JL,1,1,IRPJL,13+ISPIN+LISTOFF)
C 150 CONTINUE
      JL = IOFFOO(IRPJ,IRPJL,ISPIN) + (J-1)*POP(IRPL,ISPIN) + 1
      CALL GETLST(CORE(IADT2(IRPL)),
     1            JL,POP(IRPL,ISPIN),1,IRPJL,13+ISPIN+LISTOFF)
      CALL VMINUS(CORE(IADT2(IRPL)),LENT2(IRPL))
      ENDIF
C
      IF(IRPJ.EQ.IRPL)THEN
      DO  160    L=1,POP(IRPL,ISPIN)
C
      IF(J.LT.L)THEN
      JL = IOFFOO(IRPL,IRPJL,ISPIN) + INDEX(L-1) + J
      CALL GETLST(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPJL,ISPIN)),
     1            JL,1,1,IRPJL,13+ISPIN+LISTOFF)
      ENDIF
C
      IF(J.GT.L)THEN
      JL = IOFFOO(IRPJ,IRPJL,ISPIN) + INDEX(J-1) + L
      CALL GETLST(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPJL,ISPIN)),
     1            JL,1,1,IRPJL,13+ISPIN+LISTOFF)
      CALL VMINUS(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPJL,ISPIN)),
     1            IRPDPD(IRPJL,ISPIN))
      ENDIF
C
      IF(J.EQ.L)THEN
      CALL ZERO(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPJL,ISPIN)),
     1          IRPDPD(IRPJL,ISPIN))
      ENDIF
  160 CONTINUE
C
      ENDIF
C
C     WE NOW HAVE W IN THE FORM W(B<C,L) AND T3 IN THE FORM
C     T3(B<C,A). CONTRACT TOGETHER TO FORM W(L,A).
C
      IRPA = DIRPRD(IRPIK,IRPL)
      CALL XGEMM('T','N',
     1           POP(IRPL,ISPIN),VRT(IRPA,ISPIN),IRPDPD(IRPJL,ISPIN),
     1            1.0D+00,CORE(IADT2(IRPL)),IRPDPD(IRPJL,ISPIN),
     1                    W(IADW(IRPA)),IRPDPD(IRPJL,ISPIN),
     1            1.0D+00,
     1           GOOOV(OOOVAD(IRPIK) + (IK-1)*IRPDPD(IRPIK,15+ISPIN)
     1                 + IADGI(IRPA) - 1),POP(IRPL,ISPIN))
  170 CONTINUE
C
C                        ABC  BC
C     G(JK,LA) = - SUM  T    T
C                  B<C   IJK  IL
C                   K
C       AA AA            AAA  AA
C       BB BB            BBB  BB
C
C     T IS (B<C,A) REPRESENTATION OF T3 (A<B<C).
C
C     SET ADDRESSES FOR W INCREMENTS (L,A), LABELLED BY IRPA.
C
      DO  220 IRPA=1,NIRREP
      IRPL = DIRPRD(IRPA,IRPJK)
      LENGI(IRPA) = POP(IRPL,ISPIN) * VRT(IRPA,ISPIN)
      IF(IRPA.EQ.1)THEN
      IADGI(IRPA) = 1
      ELSE
      IADGI(IRPA) = IADGI(IRPA-1) + LENGI(IRPA-1)
      ENDIF
  220 CONTINUE
C
C     SET ADDRESSES FOR W2.
C
      DO  230 IRPL=1,NIRREP
      IRPIL = DIRPRD(IRPL,IRPI)
      IRPBC = IRPIL
      LENT2(IRPL) = IRPDPD(IRPBC,ISPIN) * POP(IRPL,ISPIN)
      IF(IRPL.EQ.1)THEN
      IADT2(IRPL) = IADGI(NIRREP) + LENGI(NIRREP)
      ELSE
      IADT2(IRPL) = IADT2(IRPL-1) + LENT2(IRPL-1)
      ENDIF
  230 CONTINUE
C
C     READ W(B<C,L).
C
      DO  270 IRPL=1,NIRREP
      IF(POP(IRPL,ISPIN).EQ.0) GOTO 270
      IRPIL = DIRPRD(IRPI,IRPL)
C
      IF(IRPI.LT.IRPL)THEN
      DO  240    L=1,POP(IRPL,ISPIN)
      IL = IOFFOO(IRPL,IRPIL,ISPIN) + (L-1)*POP(IRPI,ISPIN) + I
      CALL GETLST(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPIL,ISPIN)),
     1            IL,1,1,IRPIL,13+ISPIN+LISTOFF)
  240 CONTINUE
      ENDIF
C
      IF(IRPI.GT.IRPL)THEN
C     DO  250    L=1,POP(IRPL,ISPIN)
C     IL = IOFFOO(IRPI,IRPIL,ISPIN) + (I-1)*POP(IRPL,ISPIN) + L
C     CALL GETLST(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPIL,ISPIN)),
C    1            IL,1,1,IRPIL,13+ISPIN+LISTOFF)
C 250 CONTINUE
      IL = IOFFOO(IRPI,IRPIL,ISPIN) + (I-1)*POP(IRPL,ISPIN) + 1
      CALL GETLST(CORE(IADT2(IRPL)),
     1            IL,POP(IRPL,ISPIN),1,IRPIL,13+ISPIN+LISTOFF)
      CALL VMINUS(CORE(IADT2(IRPL)),LENT2(IRPL))
      ENDIF
C
      IF(IRPI.EQ.IRPL)THEN
      DO  260    L=1,POP(IRPL,ISPIN)
C
      IF(I.LT.L)THEN
      IL = IOFFOO(IRPL,IRPIL,ISPIN) + INDEX(L-1) + I
C     WRITE(6,*) I,L,IL,IRPI,IRPL,IRPIL,POP(IRPI,ISPIN)
      CALL GETLST(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPIL,ISPIN)),
     1            IL,1,1,IRPIL,13+ISPIN+LISTOFF)
      ENDIF
C
      IF(I.GT.L)THEN
      IL = IOFFOO(IRPI,IRPIL,ISPIN) + INDEX(I-1) + L
      CALL GETLST(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPIL,ISPIN)),
     1            IL,1,1,IRPIL,13+ISPIN+LISTOFF)
      CALL VMINUS(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPIL,ISPIN)),
     1            IRPDPD(IRPIL,ISPIN))
      ENDIF
C
      IF(I.EQ.L)THEN
      CALL ZERO(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPIL,ISPIN)),
     1          IRPDPD(IRPIL,ISPIN))
      ENDIF
  260 CONTINUE
C
      ENDIF
C
C     WE NOW HAVE W IN THE FORM W(B<C,L) AND T3 IN THE FORM
C     T3(B<C,A). CONTRACT TOGETHER TO FORM W(L,A).
C
      IRPA = DIRPRD(IRPJK,IRPL)
      CALL XGEMM('T','N',
     1           POP(IRPL,ISPIN),VRT(IRPA,ISPIN),IRPDPD(IRPIL,ISPIN),
     1           -1.0D+00,CORE(IADT2(IRPL)),IRPDPD(IRPIL,ISPIN),
     1                    W(IADW(IRPA)),IRPDPD(IRPIL,ISPIN),
     1            1.0D+00,
     1           GOOOV(OOOVAD(IRPJK) + (JK-1)*IRPDPD(IRPJK,15+ISPIN)
     1                 + IADGI(IRPA) - 1),POP(IRPL,ISPIN))
  270 CONTINUE
      RETURN
      END
