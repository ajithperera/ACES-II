      SUBROUTINE DVT32(DVVA,DVVB,T3C,WC,T3D,WD,
     1                  IADT3,IADW,IRPIJK,NVRTA,NVRTB,JFLAG)
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION DVVA(NVRTA),DVVB(NVRTB),T3C(1),WC(1),
     1                                         T3D(1),WD(1)
#include "maxbasfn.par"
      DIMENSION IADT3(8),IADW(8),COFF(8)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     1                DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /SYM/    POP(8,2),VRT(8,2),NTAA,NTBB,NF1AA,NF2AA,
     1                NF1BB,NF2BB
      COMMON /INFO/   NOCCO(2),NVRTO(2)
C
      COMMON /ACTORB/ ABSVRT(MAXBASFN,8,2),ABSOCC(MAXBASFN,8,2)
C
      ISPIN1 = 1
      ISPIN2 = 2
C
C     ROUTINE TO COMPUTE CONTRIBUTION OF T3 AAB TO DIAGONAL
C     ELEMENTS OF VIRTUAL-VIRTUAL BLOCK OF RELAXED DENSITY MATRIX.
C
C                           ABC   ABC
C     DVV(C,C) =    SUM    T     T
C                   A<B     IJK   IJK
C                  I<J<K
C
C         B B       A A     AAB   AAB
C                  A A B    AAB   AAB
C
C     T3 IS (A<B,C) (AAB) REPRESENTATION OF AAB TRIPLES.
C
      DO   30 IRPC=1,NIRREP
      IF(VRT(IRPC,ISPIN2).EQ.0) GOTO 30
      IRPAB = DIRPRD(IRPC,IRPIJK)
      DO   20    C=1,VRT(IRPC,ISPIN2)
      CACT = ABSVRT(C,IRPC,ISPIN2)
      DO   10   AB=1,IRPDPD(IRPAB,ISPIN1)
      DVVB(CACT) = DVVB(CACT) + 
     1       T3C(IADT3(IRPC) + (C-1)*IRPDPD(IRPAB,ISPIN1) + AB - 1) *
     1       T3D(IADT3(IRPC) + (C-1)*IRPDPD(IRPAB,ISPIN1) + AB - 1)
   10 CONTINUE
   20 CONTINUE
   30 CONTINUE
C
      IF(JFLAG.NE.0) RETURN
C
      ITEST = 0
      IF(ITEST.GT.0)THEN
C
C     A LIKELY FASTER ALGORITHM. REPLACE INNER LOOPS BY SUBROUTINE
C     CALL FOR IMPROVEMENT.
C
      DO  130 IRPC=1,NIRREP
C
      IF(IRPC.EQ.1)THEN
      COFF(IRPC)  = 0
      ELSE
      COFF(IRPC)  = COFF(IRPC-1) + VRT(IRPC-1,ISPIN2)
      ENDIF
C
      IF(VRT(IRPC,ISPIN2).EQ.0) GOTO 130
C
      IRPAB = DIRPRD(IRPC,IRPIJK)
      DO  120    C=1,VRT(IRPC,ISPIN2)
      DO  110   AB=1,IRPDPD(IRPAB,ISPIN1)
      DVVB(C + COFF(IRPC)) = DVVB(C + COFF(IRPC)) + 
     1       WC(IADT3(IRPC) + (C-1)*IRPDPD(IRPAB,ISPIN1) + AB - 1) *
     1       WD(IADT3(IRPC) + (C-1)*IRPDPD(IRPAB,ISPIN1) + AB - 1)
  110 CONTINUE
  120 CONTINUE
  130 CONTINUE
C
      ENDIF
C
C                           ABC   ABC
C     DVV(A,A) =    SUM    T     T
C                   B<C     IJK   IJK
C                  I<J<K
C
C         A A       A B     AAB   AAB
C                  A A B    AAB   AAB
C
C     W IS (B,C,A) REPRESENTATION OF T3 (A<B,C) (AAB) TRIPLES.
C
      DO  230 IRPA=1,NIRREP
C
      IF(IRPA.EQ.1)THEN
      COFF(IRPA)  = 0
      ELSE
      COFF(IRPA)  = COFF(IRPA-1) + VRT(IRPA-1,ISPIN1)
      ENDIF
C
      IRPBC = DIRPRD(IRPA,IRPIJK)
C
      IF(VRT(IRPA,ISPIN1).EQ.0) GOTO 230
C
      DO  220    A=1,VRT(IRPA,ISPIN1)
      DO  210   BC=1,IRPDPD(IRPBC,13)
C
      DVVA(A+COFF(IRPA)) = DVVA(A+COFF(IRPA)) +
     1     WC(IADW(IRPA) + (A-1)*IRPDPD(IRPBC,13) + BC - 1) *
     1     WD(IADW(IRPA) + (A-1)*IRPDPD(IRPBC,13) + BC - 1)
  210 CONTINUE
  220 CONTINUE
  230 CONTINUE
      RETURN
      END
