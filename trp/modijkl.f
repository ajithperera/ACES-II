      SUBROUTINE MODIJKL(CORE,MAXCOR,IUHF,FACT)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER POP,VRT,DIRPRD,DISSIZZ,DISSIZW,DISSIZT
      DIMENSION CORE(1)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFEA(2),NFMI(2)
      COMMON/SYMINF/NSTART,NIRREP,IRREPY(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYMLOC/ISYMOFF(8,8,25)
C
      DATA ONE,ONEM,ZILCH /1.0D+00,-1.0D+00,0.0D+00/
C
      IF(IUHF.EQ.0)THEN
      IBOT = 3
      ELSE
      IBOT = 1
      ENDIF
C
      DO 100 ISPIN=IBOT,3
C
      LISTZ = 50 + ISPIN
      LISTW = 13 + ISPIN
      LISTT = 43 + ISPIN
C
      DO  10 IRREP=1,NIRREP
C
      IF(ISPIN.EQ.1.OR.ISPIN.EQ.2)THEN
        DISSIZZ = IRPDPD(IRREP, 2 + ISPIN)
        NDISZ   = IRPDPD(IRREP, 2 + ISPIN)
        DISSIZW = IRPDPD(IRREP,     ISPIN)
        NDISW   = IRPDPD(IRREP, 2 + ISPIN)
        DISSIZT = IRPDPD(IRREP,     ISPIN)
        NDIST   = IRPDPD(IRREP, 2 + ISPIN)
      ELSE
        DISSIZZ = IRPDPD(IRREP,14)
        NDISZ   = IRPDPD(IRREP,14)
        DISSIZW = IRPDPD(IRREP,13)
        NDISW   = IRPDPD(IRREP,14)
        DISSIZT = IRPDPD(IRREP,13)
        NDIST   = IRPDPD(IRREP,14)
      ENDIF
C
      I000 = 1
      I010 = I000 + DISSIZZ * NDISZ
      I020 = I010 + DISSIZW * NDISW
      I030 = I020 + DISSIZT * NDIST
      NEED = I030 * IINTFP
      IF(NEED.GT.MAXCOR) CALL INSMEM('MODIJKL',NEED,MAXCOR)
C
      CALL GETLST(CORE(I000),1,NDISZ,2,IRREP,LISTZ)
      CALL GETLST(CORE(I010),1,NDISW,2,IRREP,LISTW)
      CALL GETLST(CORE(I020),1,NDIST,2,IRREP,LISTT)
C
      IOFFZ = I000
      IOFFW = I010
      IOFFT = I020
C
C     W^T * T
C
      CALL XGEMM('T','N',DISSIZZ,NDISZ,DISSIZW,FACT,
     &               CORE(IOFFW),DISSIZW,
     &               CORE(IOFFT),DISSIZT,
     &           ONE,CORE(IOFFZ),DISSIZZ)
C
C     T^T * W
C
c      CALL XGEMM('T','N',DISSIZZ,NDISZ,DISSIZT,FACT,
c     &               CORE(IOFFT),DISSIZT,
c     &               CORE(IOFFW),DISSIZW,
c     &           ONE,CORE(IOFFZ),DISSIZZ)
C
      CALL PUTLST(CORE(IOFFZ),1,NDISZ,2,IRREP,LISTZ)
   10 CONTINUE
  100 CONTINUE
      RETURN
      END
