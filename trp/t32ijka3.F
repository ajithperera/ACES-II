      SUBROUTINE T32IJKA3(GOOOV,LNOOOV,OOOVAD,T3,W,CORE,
     1                    IADT3,IADW,
     1                    IRPI,IRPJ,IRPK,IRPIJ,IRPIK,IRPJK,
     1                    I,J,K,LAMBDA)
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION GOOOV(LNOOOV,4)
      DOUBLE PRECISION T3(1),W(1),CORE(1)
      LOGICAL LAMBDA
      DIMENSION IADT3(8),IADW(8)
      DIMENSION IADT2(8),LENT2(8),IADGI(8),LENGI(8)
      DIMENSION OOOVAD(8,4)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     1                DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /SYM/    POP(8,2),VRT(8,2),NTAA,NTBB,NF1AA,NF2AA,
     1                NF1BB,NF2BB
      COMMON /INFO/   NOCCO(2),NVRTO(2)
      COMMON /FLAGS/  IFLAGS(100)
      EQUIVALENCE(ICLLVL,IFLAGS( 2))
      EQUIVALENCE(IDRLVL,IFLAGS( 3))
      EQUIVALENCE(IREFNC,IFLAGS(11))
C
      COMMON /T3OFF/  IOFFVV(8,8,10),IOFFOO(8,8,10),IOFFVO(8,8,4)
C
      INDEX(I) = I*(I-1)/2
C     List offset
      IF (LAMBDA) THEN
         LISTOFF=100
      ELSE
         LISTOFF=0
      ENDIF
C
      ISPIN1 = 2
      ISPIN2 = 1
C
      IF(IRPI.EQ.IRPJ)THEN
      IJ = IOFFOO(IRPJ,IRPIJ,ISPIN1) + INDEX(J-1) + I
      ELSE
      IJ = IOFFOO(IRPJ,IRPIJ,ISPIN1) + (J-1)*POP(IRPI,ISPIN1) + I
      ENDIF
      IK = IOFFOO(IRPI,IRPIK,5) + (I-1)*POP(IRPK,ISPIN2) + K
      JK = IOFFOO(IRPJ,IRPJK,5) + (J-1)*POP(IRPK,ISPIN2) + K
C
C     ROUTINE TO COMPUTE CONTRIBUTION OF T3 BBA IN G(IJ,LA) BBBB.
C
C
C                        ABC  BC
C     G(IJ,LA) =   SUM  T    T
C                  B<C   IJK  LK
C                   K
C       BB BB            BBA  BA
C
C     W IS (B,C,A) REPRESENTATION OF T3 (A<B,C).
C
C     SET ADDRESSES FOR GAMMA INCREMENTS (L,A), LABELLED BY IRPA.
C
      DO   20 IRPA=1,NIRREP
      IRPL = DIRPRD(IRPA,IRPIJ)
      LENGI(IRPA) = POP(IRPL,ISPIN1) * VRT(IRPA,ISPIN1)
      IF(IRPA.EQ.1)THEN
      IADGI(IRPA) = 1
      ELSE
      IADGI(IRPA) = IADGI(IRPA-1) + LENGI(IRPA-1)
      ENDIF
   20 CONTINUE
C
C     SET ADDRESSES FOR T2.
C
      DO   30 IRPL=1,NIRREP
      IRPKL = DIRPRD(IRPL,IRPK)
      IRPBC = IRPKL
      LENT2(IRPL) = IRPDPD(IRPBC,13) * POP(IRPL,ISPIN1)
      IF(IRPL.EQ.1)THEN
      IADT2(IRPL) = 1
      ELSE
      IADT2(IRPL) = IADT2(IRPL-1) + LENT2(IRPL-1)
      ENDIF
   30 CONTINUE
C
C     READ T2(BC,L).
C
      DO   70 IRPL=1,NIRREP
      IF(POP(IRPL,ISPIN1).EQ.0) GOTO 70
      IRPKL = DIRPRD(IRPK,IRPL)
C
      DO   40    L=1,POP(IRPL,ISPIN1)
C
      KL = IOFFOO(IRPL,IRPKL,5) + (L-1)*POP(IRPK,ISPIN2) + K
      CALL GETLST(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPKL,13)),
     1            KL,1,1,IRPKL,16+LISTOFF)
   40 CONTINUE
C
C     WE NOW HAVE T2 IN THE FORM T2(BC,L) AND T3 IN THE FORM
C     T3(BC,A). CONTRACT TOGETHER TO FORM G(L,A), WHICH CAN
C     BE SUMMED INTO GAMMA(LA | IJ).
C
      IRPA = DIRPRD(IRPIJ,IRPL)
      CALL XGEMM('T','N',
     1           POP(IRPL,ISPIN1),VRT(IRPA,ISPIN1),IRPDPD(IRPKL,13),
     1            1.0D+00,CORE(IADT2(IRPL)),IRPDPD(IRPKL,13),
     1                    W(IADW(IRPA)),IRPDPD(IRPKL,13),
     1            1.0D+00,
     1 GOOOV(OOOVAD(IRPIJ,2) + (IJ-1)*IRPDPD(IRPIJ,15+ISPIN1) + 
     1       IADGI(IRPA) - 1,2),
     1            POP(IRPL,ISPIN1))
   70 CONTINUE
C
C                        ABC  AB
C     G(KI,CL) = - SUM  T    T
C                  A<B   IJK  JL
C                   J
C       AB AB            BBA  BB
C
C
C     NOTE : THERE IS A PHASE DILEMMA HERE. THUS, WHILE
C
C                     <IJ//KA>  = - <IJ/KA>
C                      AB  BA        AB AB
C
C            IT IS <IJ/KA> WHICH IS STORED. HENCE, TO BE CONSISTENT,
C            WE INTRODUCE A MINUS SIGN WHICH DOES NOT APPEAR IN THE
C            GAMMA EQUATIONS.
C
C     SET ADDRESSES FOR GAMMA INCREMENTS (L,C), LABELLED BY IRPC.
C
      DO  120 IRPC=1,NIRREP
      IRPL = DIRPRD(IRPC,IRPIK)
      LENGI(IRPC) = POP(IRPL,ISPIN1) * VRT(IRPC,ISPIN2)
      IF(IRPC.EQ.1)THEN
      IADGI(IRPC) = 1
      ELSE
      IADGI(IRPC) = IADGI(IRPC-1) + LENGI(IRPC-1)
      ENDIF
  120 CONTINUE
C
C     SET ADDRESSES FOR T2.
C
      DO  130 IRPL=1,NIRREP
      IRPJL = DIRPRD(IRPL,IRPJ)
      IRPAB = IRPJL
      LENT2(IRPL) = IRPDPD(IRPAB,ISPIN1) * POP(IRPL,ISPIN1)
      IF(IRPL.EQ.1)THEN
      IADT2(IRPL) = 1
      ELSE
      IADT2(IRPL) = IADT2(IRPL-1) + LENT2(IRPL-1)
      ENDIF
  130 CONTINUE
C
C     READ T2(A<B,L).
C
      DO  170 IRPL=1,NIRREP
      IF(POP(IRPL,ISPIN1).EQ.0) GOTO 170
      IRPJL = DIRPRD(IRPJ,IRPL)
C
      IF(IRPJ.LT.IRPL)THEN
      DO  140    L=1,POP(IRPL,ISPIN1)
      JL = IOFFOO(IRPL,IRPJL,ISPIN1) + (L-1)*POP(IRPJ,ISPIN1) + J
      CALL GETLST(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPJL,ISPIN1)),
     1            JL,1,1,IRPJL,13+ISPIN1+LISTOFF)
  140 CONTINUE
      ENDIF
C
      IF(IRPJ.GT.IRPL)THEN
      JL = IOFFOO(IRPJ,IRPJL,ISPIN1) + (J-1)*POP(IRPL,ISPIN1) + 1
      CALL GETLST(CORE(IADT2(IRPL)),
     1            JL,POP(IRPL,ISPIN1),1,IRPJL,13+ISPIN1+LISTOFF)
      CALL VMINUS(CORE(IADT2(IRPL)),LENT2(IRPL))
      ENDIF
C
      IF(IRPJ.EQ.IRPL)THEN
      DO  160    L=1,POP(IRPL,ISPIN1)
C
      IF(J.LT.L)THEN
      JL = IOFFOO(IRPL,IRPJL,ISPIN1) + INDEX(L-1) + J
      CALL GETLST(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPJL,ISPIN1)),
     1            JL,1,1,IRPJL,13+ISPIN1+LISTOFF)
      ENDIF
C
      IF(J.GT.L)THEN
      JL = IOFFOO(IRPJ,IRPJL,ISPIN1) + INDEX(J-1) + L
      CALL GETLST(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPJL,ISPIN1)),
     1            JL,1,1,IRPJL,13+ISPIN1+LISTOFF)
      CALL VMINUS(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPJL,ISPIN1)),
     1            IRPDPD(IRPJL,ISPIN1))
      ENDIF
C
      IF(J.EQ.L)THEN
      CALL ZERO(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPJL,ISPIN1)),
     1          IRPDPD(IRPJL,ISPIN1))
      ENDIF
  160 CONTINUE
C
      ENDIF
C
C     WE NOW HAVE T2 IN THE FORM T2(A<B,L) AND T3 IN THE FORM
C     T3(A<B,C). CONTRACT TOGETHER TO FORM G(L,C), WHICH CAN
C     BE SUMMED INTO GAMMA(LC | IK).
C
      IRPC = DIRPRD(IRPIK,IRPL)
      CALL XGEMM('T','N',
     1           POP(IRPL,ISPIN1),VRT(IRPC,ISPIN2),IRPDPD(IRPJL,ISPIN1),
     1            1.0D+00,CORE(IADT2(IRPL)),IRPDPD(IRPJL,ISPIN1),
     1                    T3(IADT3(IRPC)),IRPDPD(IRPJL,ISPIN1),
     1            1.0D+00,
     1 GOOOV(OOOVAD(IRPIK,3) + (IK-1)*IRPDPD(IRPIK,11) +
     1 IADGI(IRPC) - 1,3),
     1            POP(IRPL,ISPIN1))
  170 CONTINUE
C
C
C                        ABC  AB
C     G(KJ,CL) =   SUM  T    T
C                  A<B   IJK  IL
C                   I
C       AB AB            BBA  BB
C
      DO  220 IRPC=1,NIRREP
      IRPL = DIRPRD(IRPC,IRPJK)
      LENGI(IRPC) = POP(IRPL,ISPIN1) * VRT(IRPC,ISPIN2)
      IF(IRPC.EQ.1)THEN
      IADGI(IRPC) = 1
      ELSE
      IADGI(IRPC) = IADGI(IRPC-1) + LENGI(IRPC-1)
      ENDIF
  220 CONTINUE
C
C     SET ADDRESSES FOR T2.
C
      DO  230 IRPL=1,NIRREP
      IRPIL = DIRPRD(IRPL,IRPI)
      IRPAB = IRPIL
      LENT2(IRPL) = IRPDPD(IRPAB,ISPIN1) * POP(IRPL,ISPIN1)
      IF(IRPL.EQ.1)THEN
      IADT2(IRPL) = 1
      ELSE
      IADT2(IRPL) = IADT2(IRPL-1) + LENT2(IRPL-1)
      ENDIF
  230 CONTINUE
C
C     READ T2(A<B,L).
C
      DO  270 IRPL=1,NIRREP
      IF(POP(IRPL,ISPIN1).EQ.0) GOTO 270
      IRPIL = DIRPRD(IRPI,IRPL)
C
      IF(IRPI.LT.IRPL)THEN
      DO  240    L=1,POP(IRPL,ISPIN1)
      IL = IOFFOO(IRPL,IRPIL,ISPIN1) + (L-1)*POP(IRPI,ISPIN1) + I
      CALL GETLST(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPIL,ISPIN1)),
     1            IL,1,1,IRPIL,13+ISPIN1+LISTOFF)
  240 CONTINUE
      ENDIF
C
      IF(IRPI.GT.IRPL)THEN
      IL = IOFFOO(IRPI,IRPIL,ISPIN1) + (I-1)*POP(IRPL,ISPIN1) + 1
      CALL GETLST(CORE(IADT2(IRPL)),
     1            IL,POP(IRPL,ISPIN1),1,IRPIL,13+ISPIN1+LISTOFF)
      CALL VMINUS(CORE(IADT2(IRPL)),LENT2(IRPL))
      ENDIF
C
      IF(IRPI.EQ.IRPL)THEN
      DO  260    L=1,POP(IRPL,ISPIN1)
C
      IF(I.LT.L)THEN
      IL = IOFFOO(IRPL,IRPIL,ISPIN1) + INDEX(L-1) + I
      CALL GETLST(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPIL,ISPIN1)),
     1            IL,1,1,IRPIL,13+ISPIN1+LISTOFF)
      ENDIF
C
      IF(I.GT.L)THEN
      IL = IOFFOO(IRPI,IRPIL,ISPIN1) + INDEX(I-1) + L
      CALL GETLST(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPIL,ISPIN1)),
     1            IL,1,1,IRPIL,13+ISPIN1+LISTOFF)
      CALL VMINUS(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPIL,ISPIN1)),
     1            IRPDPD(IRPIL,ISPIN1))
      ENDIF
C
      IF(I.EQ.L)THEN
      CALL ZERO(CORE(IADT2(IRPL) + (L-1)*IRPDPD(IRPIL,ISPIN1)),
     1          IRPDPD(IRPIL,ISPIN1))
      ENDIF
  260 CONTINUE
C
      ENDIF
C
C     WE NOW HAVE T2 IN THE FORM T2(A<B,L) AND T3 IN THE FORM
C     T3(A<B,C). CONTRACT TOGETHER TO FORM G(L,C), WHICH CAN
C     BE SUMMED INTO GAMMA(LC | JK).
C
      IRPC = DIRPRD(IRPJK,IRPL)
      CALL XGEMM('T','N',
     1           POP(IRPL,ISPIN1),VRT(IRPC,ISPIN2),IRPDPD(IRPIL,ISPIN1),
     1           -1.0D+00,CORE(IADT2(IRPL)),IRPDPD(IRPIL,ISPIN1),
     1                    T3(IADT3(IRPC)),IRPDPD(IRPIL,ISPIN1),
     1            1.0D+00,
     1 GOOOV(OOOVAD(IRPJK,3) + (JK-1)*IRPDPD(IRPJK,11) +
     1 IADGI(IRPC) - 1,3),
     1            POP(IRPL,ISPIN1))
  270 CONTINUE
C
C                        ABC  BC
C     G(KI,LA) = - SUM  T    T
C                  B,C   IJK  JL
C                   J
C       AB AB            AAB  AB
C
C     READ GAMMA(KI,LA). READ THE WHOLE LIST FOR THIS IRPIK AND
C     TRANSPOSE SO THAT ORDER IS GAMMA(LA,KI).
C
C     SET ADDRESSES FOR GAMMA INCREMENTS (L,A), LABELLED BY IRPA.
C
      DO  320 IRPA=1,NIRREP
      IRPL = DIRPRD(IRPA,IRPIK)
      LENGI(IRPA) = POP(IRPL,ISPIN2) * VRT(IRPA,ISPIN1)
      IF(IRPA.EQ.1)THEN
      IADGI(IRPA) = 1
      ELSE
      IADGI(IRPA) = IADGI(IRPA-1) + LENGI(IRPA-1)
      ENDIF
  320 CONTINUE
C
C     SET ADDRESSES FOR T2.
C
      DO  330 IRPL=1,NIRREP
      IRPJL = DIRPRD(IRPL,IRPJ)
      IRPBC = IRPJL
      LENT2(IRPL) = IRPDPD(IRPBC,13) * POP(IRPL,ISPIN2)
      IF(IRPL.EQ.1)THEN
      IADT2(IRPL) = 1
      ELSE
      IADT2(IRPL) = IADT2(IRPL-1) + LENT2(IRPL-1)
      ENDIF
  330 CONTINUE
C
C     READ T2(BC,L).
C
      DO  370 IRPL=1,NIRREP
      IF(POP(IRPL,ISPIN2).EQ.0) GOTO 370
      IRPJL = DIRPRD(IRPJ,IRPL)
C
      JL = IOFFOO(IRPJ,IRPJL,5) + (J-1)*POP(IRPL,ISPIN2) + 1
      CALL GETLST(CORE(IADT2(IRPL)),
     1            JL,POP(IRPL,ISPIN2),1,IRPJL,16+LISTOFF)
C
C     WE NOW HAVE T2 IN THE FORM T2(BC,L) AND T3 IN THE FORM
C     W(BC,A). CONTRACT TOGETHER TO FORM G(L,A), WHICH CAN
C     BE SUMMED INTO GAMMA(LA | KI).
C
      IRPA = DIRPRD(IRPIK,IRPL)
      CALL XGEMM('T','N',
     1           POP(IRPL,ISPIN2),VRT(IRPA,ISPIN1),IRPDPD(IRPJL,13),
     1           -1.0D+00,CORE(IADT2(IRPL)),IRPDPD(IRPJL,13),
     1                    W(IADW(IRPA)),IRPDPD(IRPJL,13),
     1            1.0D+00,
     1 GOOOV(OOOVAD(IRPIK,4) + (IK-1)*IRPDPD(IRPIK,18) +
     1       IADGI(IRPA) - 1,4),
     1            POP(IRPL,ISPIN2))
  370 CONTINUE
C
C                        ABC  BC
C     G(KJ,LA) =   SUM  T    T
C                  B,C   IJK  IL
C                   I
C       AB AB            AAB  AB
C
C     SET ADDRESSES FOR GAMMA INCREMENTS (L,A), LABELLED BY IRPA.
C
      DO  420 IRPA=1,NIRREP
      IRPL = DIRPRD(IRPA,IRPJK)
      LENGI(IRPA) = POP(IRPL,ISPIN2) * VRT(IRPA,ISPIN1)
      IF(IRPA.EQ.1)THEN
      IADGI(IRPA) = 1
      ELSE
      IADGI(IRPA) = IADGI(IRPA-1) + LENGI(IRPA-1)
      ENDIF
  420 CONTINUE
C
C     SET ADDRESSES FOR T2.
C
      DO  430 IRPL=1,NIRREP
      IRPIL = DIRPRD(IRPL,IRPI)
      IRPBC = IRPIL
      LENT2(IRPL) = IRPDPD(IRPBC,13) * POP(IRPL,ISPIN2)
      IF(IRPL.EQ.1)THEN
      IADT2(IRPL) = 1
      ELSE
      IADT2(IRPL) = IADT2(IRPL-1) + LENT2(IRPL-1)
      ENDIF
  430 CONTINUE
C
C     READ T2(BC,L).
C
      DO  470 IRPL=1,NIRREP
      IF(POP(IRPL,ISPIN2).EQ.0) GOTO 470
      IRPIL = DIRPRD(IRPI,IRPL)
C
      IL = IOFFOO(IRPI,IRPIL,5) + (I-1)*POP(IRPL,ISPIN2) + 1
      CALL GETLST(CORE(IADT2(IRPL)),
     1            IL,POP(IRPL,ISPIN2),1,IRPIL,16+LISTOFF)
C
C     WE NOW HAVE T2 IN THE FORM T2(BC,L) AND T3 IN THE FORM
C     W(BC,A). CONTRACT TOGETHER TO FORM G(L,A), WHICH CAN
C     BE SUMMED INTO GAMMA(LA | KJ).
C
      IRPA = DIRPRD(IRPJK,IRPL)
      CALL XGEMM('T','N',
     1           POP(IRPL,ISPIN2),VRT(IRPA,ISPIN1),IRPDPD(IRPIL,13),
     1            1.0D+00,CORE(IADT2(IRPL)),IRPDPD(IRPIL,13),
     1                    W(IADW(IRPA)),IRPDPD(IRPIL,13),
     1            1.0D+00,
     1 GOOOV(OOOVAD(IRPJK,4) + (JK-1)*IRPDPD(IRPJK,18) +
     1       IADGI(IRPA) - 1,4),
     1            POP(IRPL,ISPIN2))
  470 CONTINUE
C
      RETURN
      END
