      
      SUBROUTINE CCENRG(CORE,IUHF,ECCTOT,NONHF)
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ECCAA,ECCBB,ECCAB,ECCTOT,FACT,EREF
      DOUBLE PRECISION CORE(1),SDOT
      LOGICAL NONHF
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     1                DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /SYM/    POP(8,2),VRT(8,2),NTAA,NTBB,NF1AA,NF2AA,
     1                NF1BB,NF2BB
      COMMON /INFO/   NOCCO(2),NVRTO(2)
      COMMON /FLAGS/  IFLAGS(100)
      EQUIVALENCE(ICLLVL,IFLAGS( 2))
      EQUIVALENCE(IDRLVL,IFLAGS( 3))
      EQUIVALENCE(IREFNC,IFLAGS(11))
      EQUIVALENCE(IQRHFP,IFLAGS(32))
      EQUIVALENCE(IQRHFM,IFLAGS(33))
C
      WRITE(6,1000)
 1000 FORMAT(' @CCENRG-I, CCSD/QCISD energy will now be evaluated. ')
C
C     NOTE WE ARE LAZY AND DO NOT CHECK TO SEE IF WE HAVE ENOUGH
C     MEMORY (SO DON'T TRY RHODOPSIN WITH THIS CODE).
C
      ECCAA  = 0.0D+00
      ECCBB  = 0.0D+00
      ECCAB  = 0.0D+00
      ECCTOT = 0.0D+00
      EREF   = 0.0D+00
C
C     I000     T1  ALPHA   ALL OF IT
C     I010     T1  BETA    ALL OF IT
C     I020     FIA ALPHA   ALL OF IT
C     I030     FIA BETA    ALL OF IT
C     I040     <AB||IJ>   ONE SYMMETRY BLOCK
C     I050     T2(AB,IJ)  ONE SYMMETRY BLOCK (REPLACED BY TAU)
C
      I000   = 1
      I010   = I000 + NTAA
      I020   = I010 + NTBB
      I030   = I020 + NTAA
      I040   = I030 + NTBB
C
      CALL GETLST(CORE(I000),1,1,1,1,90)
      IF(IUHF.NE.0)THEN
      CALL GETLST(CORE(I010),1,1,1,2,90)
      ELSE
      CALL GETLST(CORE(I010),1,1,1,1,90)
      ENDIF
C
C     READ FOV.
C
C     IF(IREFNC.EQ.2.OR.IQRHFP.NE.0.OR.IQRHFM.NE.0)THEN
      IF(NONHF)THEN
      CALL GETLST(CORE(I020),1,1,2,3,93)
      IF(IUHF.GT.0)THEN
      CALL GETLST(CORE(I030),1,1,2,4,93)
      ENDIF
      ENDIF
C
      FACT   = 1.0D+00
      DO   40 IRPIJ=1,NIRREP
      IF(IRPDPD(IRPIJ,3).EQ.0) GOTO 10
C
      ISPIN  = 1
C
      I050 = I040 + IRPDPD(IRPIJ,1) * IRPDPD(IRPIJ,3)
      I060 = I050 + IRPDPD(IRPIJ,1) * IRPDPD(IRPIJ,3)

      CALL GETLST(CORE(I040),1,IRPDPD(IRPIJ,3),2,IRPIJ,14)
      CALL GETLST(CORE(I050),1,IRPDPD(IRPIJ,3),1,IRPIJ,44)
C
      IF(ICLLVL.EQ.11.OR.ICLLVL.EQ.22.OR.ICLLVL.EQ.50.OR.
     1   ICLLVL.EQ.51)THEN
      CALL FTAU(CORE(I050),CORE(I000),CORE(I000),
     1          IRPDPD(IRPIJ,1),IRPDPD(IRPIJ,3),POP(1,1),POP(1,1),
     1          VRT(1,1),VRT(1,1),IRPIJ,ISPIN,FACT)
      ENDIF
C
C     CALL DOTPRD(CORE(I040),CORE(I050),IRPDPD(IRPIJ,1)*
C    1                                  IRPDPD(IRPIJ,3),ECCAA)
C
      ECCAA = ECCAA + SDOT(IRPDPD(IRPIJ,1)*IRPDPD(IRPIJ,3),
     1                     CORE(I040),1,CORE(I050),1)
   10 CONTINUE
C
      IF(IUHF.EQ.0.OR.IRPDPD(IRPIJ,4).EQ.0) GOTO 20
C
      ISPIN  = 2
C
      I050 = I040 + IRPDPD(IRPIJ,2) * IRPDPD(IRPIJ,4)
      I060 = I050 + IRPDPD(IRPIJ,2) * IRPDPD(IRPIJ,4)

      CALL GETLST(CORE(I040),1,IRPDPD(IRPIJ,4),2,IRPIJ,15)
      CALL GETLST(CORE(I050),1,IRPDPD(IRPIJ,4),1,IRPIJ,45)
C
      IF(ICLLVL.EQ.11.OR.ICLLVL.EQ.22.OR.ICLLVL.EQ.50.OR.
     1   ICLLVL.EQ.51)THEN
      CALL FTAU(CORE(I050),CORE(I010),CORE(I010),
     1          IRPDPD(IRPIJ,2),IRPDPD(IRPIJ,4),POP(1,2),POP(1,2),
     1          VRT(1,2),VRT(1,2),IRPIJ,ISPIN,FACT)
      ENDIF
C
C     CALL DOTPRD(CORE(I040),CORE(I050),IRPDPD(IRPIJ,2)*
C    1                                  IRPDPD(IRPIJ,4),ECCBB)
      ECCBB = ECCBB + SDOT(IRPDPD(IRPIJ,2)*IRPDPD(IRPIJ,4),
     1                     CORE(I040),1,CORE(I050),1)
   20 CONTINUE
C
      IF(IRPDPD(IRPIJ,14).EQ.0) GOTO 30
C
      ISPIN  = 3
C
      I050 = I040 + IRPDPD(IRPIJ,13) * IRPDPD(IRPIJ,14)
      I060 = I050 + IRPDPD(IRPIJ,13) * IRPDPD(IRPIJ,14)

      CALL GETLST(CORE(I040),1,IRPDPD(IRPIJ,14),2,IRPIJ,16)
      CALL GETLST(CORE(I050),1,IRPDPD(IRPIJ,14),1,IRPIJ,46)
C
      IF(ICLLVL.EQ.11.OR.ICLLVL.EQ.22.OR.ICLLVL.EQ.50.OR.
     1   ICLLVL.EQ.51)THEN
      CALL FTAU(CORE(I050),CORE(I000),CORE(I010),
     1          IRPDPD(IRPIJ,13),IRPDPD(IRPIJ,14),POP(1,1),POP(1,2),
     1          VRT(1,1),VRT(1,2),IRPIJ,ISPIN,FACT)
      ENDIF
C
C     CALL DOTPRD(CORE(I040),CORE(I050),IRPDPD(IRPIJ,13)*
C    1                                  IRPDPD(IRPIJ,14),ECCAB)
      ECCAB = ECCAB + SDOT(IRPDPD(IRPIJ,13)*IRPDPD(IRPIJ,14),
     1                     CORE(I040),1,CORE(I050),1)
   30 CONTINUE
   40 CONTINUE
C
C     EVALUATE F*T1 IF THIS IS A NON-HF REFERENCE.
C
C     IF(IREFNC.EQ.2.OR.IQRHFP.NE.0.OR.IQRHFM.NE.0)THEN
      IF(NONHF)THEN
      ECCAA = ECCAA + SDOT(NTAA,CORE(I000),1,CORE(I020),1)
      IF(IUHF.GT.0)THEN
      ECCBB = ECCBB + SDOT(NTBB,CORE(I010),1,CORE(I030),1)
      ENDIF
      ENDIF
C
      IF(IUHF.EQ.0)THEN
      ECCTOT = 2.0D+00 * ECCAA + ECCAB
      ELSE
      ECCTOT =           ECCAA + ECCAB + ECCBB
      ENDIF
      WRITE(6,1010) ECCAA,ECCBB,ECCAB,ECCTOT
 1010 FORMAT(' @CCENRG-I, Correlation energies. ECCAA  ',F20.12,/,
     1       '                                  ECCBB  ',F20.12,/,
     1       '                                  ECCAB  ',F20.12,/,
     1       '                                  Total  ',F20.12)
C
C     READ REFERENCE ENERGY SO WE MAY COMPUTE TOTAL CC ENERGY
C
      CALL GETREC(20,'JOBARC','SCFENEG ',IINTFP,EREF)
      ECCTOT = ECCTOT + EREF
      WRITE(6,1020) EREF,ECCTOT
 1020 FORMAT( ' @CCENRG-I, Reference   energy ',F20.12,/,
     1        '            CCSD/QCISD energy  ',F20.12)
      RETURN
      END
