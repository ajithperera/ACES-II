      SUBROUTINE G5TAU(G,GCA,T1,DISSYG,NUMSYG,VRTG,POPT,
     &               VRTT,IRREP,ISPIN)
C
C THIS ROUTINE FORMS THE FOLLOWING CONTRIBUTION TO
C GAMMA 5 REQUIRED IN CCSD
C
C  G(AB,CI) = G(AB,CI) - HALF GCA(CA)*T1(IB)
C                      + HALF GCA(CB)*T1(IA)
C
C FOR ISPIN =1 (AAAA CASE) AND ISPIN =2 (BBBB CASE)
C THE EQUATION GIVEN APLIES DIRECTLY. FOR ISPIN=3 
C (ABAB CASE) IT REDUCES TO
C
C G(Ab,Ci) = G(Ab,Ci) - HALF GCA(CA)*T1(ib)
C
C AND FOR ISPIN=4 (BABA) CASE IT REDUCES TO
C
C G(Ab,Ic) = G(Ab,Ic) - HALF GCA(cb)*T1(IA)
C
CEND 
C
C CODED SEPTEMBER/90 JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DISSYG,DIRPRD,POPT,VRTT,VRTG
      DIMENSION G(DISSYG,NUMSYG),T1(1),GCA(1)
      DIMENSION POPT(8),VRTT(8),VRTG(8)
      DIMENSION IOFFGR(8),IOFFGL(8),IOFFT1(8),
     &          IOFFGCA(8)
C
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &                DIRPRD(8,8)
C
      DATA HALF /0.5D0/
C
      NNM1O2(I)=((I-1)*I)/2
C
      IF(ISPIN.LT.3) THEN
C
C AAAA OR BBBB SPIN CASE
C
C DETERMINE FIRST OFFSETS FOR T2 AND T1
C
       IOFFGR(1)=0
       IOFFGL(1)=0
       IOFFT1(1)=0
       IOFFGCA(1)=0
       DO 1 IRREPJ=1,NIRREP-1
        IOFFT1(IRREPJ+1)=IOFFT1(IRREPJ)+POPT(IRREPJ)*VRTT(IRREPJ)
        IOFFGCA(IRREPJ+1)=IOFFGCA(IRREPJ)+VRTG(IRREPJ)*VRTG(IRREPJ)
1      CONTINUE
       IF(IRREP.EQ.1) THEN
        DO 2 IRREPJ=1,NIRREP-1
         IOFFGR(IRREPJ+1)=IOFFGR(IRREPJ)+VRTG(IRREPJ)*POPT(IRREPJ)
         IOFFGL(IRREPJ+1)=IOFFGL(IRREPJ)+NNM1O2(VRTG(IRREPJ))
2       CONTINUE
       ELSE
        DO 3 IRREPJ=1,NIRREP-1
         IRREPI=DIRPRD(IRREP,IRREPJ)
         IF(IRREPI.LT.IRREPJ) THEN
          IOFFGR(IRREPJ+1)=IOFFGR(IRREPJ)+VRTG(IRREPI)*POPT(IRREPJ)
          IOFFGL(IRREPJ+1)=IOFFGL(IRREPJ)+VRTG(IRREPI)*VRTT(IRREPJ)
         ELSE
          IOFFGR(IRREPJ+1)=IOFFGR(IRREPJ)+VRTG(IRREPI)*POPT(IRREPJ)
          IOFFGL(IRREPJ+1)=IOFFGL(IRREPJ)
         ENDIF
3       CONTINUE
       ENDIF
C
       DO 100 IRREPJ=1,NIRREP
C
        IRREPI=DIRPRD(IRREP,IRREPJ)
C
C  FIRST CASE IRREPI=IRREPJ
C
        IF(IRREPI.EQ.IRREPJ) THEN
         NUMI=POPT(IRREPJ)
         NUMB=VRTG(IRREPI)
         INDCI=IOFFGR(IRREPJ)
         INDT=IOFFT1(IRREPJ)
         INDG=IOFFGCA(IRREPI)
         INDL=IOFFGL(IRREPJ)
         
C
         IF(NNM1O2(NUMB).EQ.0) GO TO 100
C
         DO 10 II=1,NUMI
         INDT1=INDT+(II-1)*NUMB
         DO 10 IC=1,NUMB
          INDG1=INDG+(IC-1)*NUMB 
C
          INDCI=INDCI+1
C 
          INDAB=INDL
C
          DO 20 IB=2,NUMB
          INDIB=INDT1+IB
          INDCB=INDG1+IB
          DO 20 IA=1,IB-1
           INDIA=INDT1+IA
           INDCA=INDG1+IA
           INDAB=INDAB+1
C
           G(INDAB,INDCI)=G(INDAB,INDCI)-HALF*GCA(INDCA)*T1(INDIB)
     &                                  +HALF*GCA(INDCB)*T1(INDIA)
20        CONTINUE
10       CONTINUE
C
C  SECOND CASE IRREPI LT IRREPJ (TREAT ALSO IRREPI GT IRREPJ HERE)
C
        ELSE IF(IRREPI.LT.IRREPJ) THEN
C
         NUMI=POPT(IRREPJ)
         NUMC=VRTG(IRREPI)
         NUMB=VRTT(IRREPJ)
         INDT=IOFFT1(IRREPJ)
         INDG=IOFFGCA(IRREPI)
         INDCI=IOFFGR(IRREPJ)
         INDL=IOFFGL(IRREPJ)
C
         DO 30 II=1,NUMI
         INDT1=INDT+(II-1)*NUMB
         DO 30 IC=1,NUMC
          INDG1=INDG+(IC-1)*NUMC
C
          INDCI=INDCI+1         
C
          INDAB=INDL
C
          DO 50 IB=1,NUMB
           INDIB=INDT1+IB
           DO 60 IA=1,NUMC
            INDCA=INDG1+IA
            INDAB=INDAB+1
            G(INDAB,INDCI)=G(INDAB,INDCI)-HALF*GCA(INDCA)*T1(INDIB)
60         CONTINUE
50        CONTINUE
30       CONTINUE
C
C  NOW THE SECOND PART IRREPI.GT.IRREPJ)
C
         NUMI=POPT(IRREPI)
         NUMC=VRTG(IRREPJ)
         NUMA=VRTT(IRREPI)
C
         INDT=IOFFT1(IRREPI)
         INDG=IOFFGCA(IRREPJ)
         INDCI=IOFFGR(IRREPI)
         INDL=IOFFGL(IRREPJ)
C    
         DO 31 II=1,NUMI
         INDT1=INDT+(II-1)*NUMA
         DO 31 IC=1,NUMC
          INDG1=INDG+(IC-1)*NUMC
C
          INDCI=INDCI+1
C
          INDAB=INDL
C
          DO 51 IB=1,NUMC
           INDCB=INDG1+IB
           DO 61 IA=1,NUMA
            INDIA=INDT1+IA
            INDAB=INDAB+1
            G(INDAB,INDCI)=G(INDAB,INDCI)+HALF*GCA(INDCB)*T1(INDIA) 
61         CONTINUE
51        CONTINUE
31       CONTINUE
C
        ENDIF
100    CONTINUE
      ELSE IF(ISPIN.EQ.3) THEN
C
C  ABAB SPIN CASE
C
C  GET FIRST OFFSETS OF GCA, T1, AND G
C
       IOFFGR(1)=0
       IOFFGL(1)=0
       IOFFT1(1)=0
       IOFFGCA(1)=0
       DO 101 IRREPJ=1,NIRREP-1
        IRREPI=DIRPRD(IRREPJ,IRREP)
        IOFFGR(IRREPJ+1)=IOFFGR(IRREPJ)+POPT(IRREPJ)*VRTG(IRREPI)
        IOFFGL(IRREPJ+1)=IOFFGL(IRREPJ)+VRTT(IRREPJ)*VRTG(IRREPI)
        IOFFT1(IRREPJ+1)=IOFFT1(IRREPJ)+POPT(IRREPJ)*VRTT(IRREPJ)
        IOFFGCA(IRREPJ+1)=IOFFGCA(IRREPJ)+VRTG(IRREPJ)*VRTG(IRREPJ)
101    CONTINUE
C
       DO 200 IRREPJ=1,NIRREP
C
        IRREPI=DIRPRD(IRREP,IRREPJ)
        NUMB=VRTT(IRREPJ)
        NUMI=POPT(IRREPJ)
        NUMA=VRTG(IRREPI)
        INDL=IOFFGL(IRREPJ)
        INDCI=IOFFGR(IRREPJ)
        INDT=IOFFT1(IRREPJ)
        INDG=IOFFGCA(IRREPI)
C
        DO 210 II=1,NUMI
        INDT1=INDT+(II-1)*NUMB
        DO 210 IC=1,NUMA
         INDCI=INDCI+1
         INDG1=INDG+(IC-1)*NUMA
         INDAB=INDL
         DO 220 IB=1,NUMB
         INDIB=INDT1+IB
         DO 220 IA=1,NUMA
          INDAB=INDAB+1
          INDCA=INDG1+IA
          G(INDAB,INDCI)=G(INDAB,INDCI)-HALF*GCA(INDCA)*T1(INDIB)
220      CONTINUE

210     CONTINUE
200    CONTINUE
C
      ELSE IF(ISPIN.EQ.4) THEN
C
C  BABA SPIN CASE
C
C  GET FIRST OFFSETS OF GIK, T1, AND G
C
       IOFFGR(1)=0
       IOFFGL(1)=0
       IOFFT1(1)=0
       IOFFGCA(1)=0
       DO 201 IRREPJ=1,NIRREP-1
        IRREPI=DIRPRD(IRREPJ,IRREP)
        IOFFGR(IRREPJ+1)=IOFFGR(IRREPJ)+POPT(IRREPI)*VRTG(IRREPJ)
        IOFFGL(IRREPJ+1)=IOFFGL(IRREPJ)+VRTT(IRREPI)*VRTG(IRREPJ)
        IOFFT1(IRREPJ+1)=IOFFT1(IRREPJ)+POPT(IRREPJ)*VRTT(IRREPJ)
        IOFFGCA(IRREPJ+1)=IOFFGCA(IRREPJ)+VRTG(IRREPJ)*VRTG(IRREPJ)
201    CONTINUE
C
       DO 300 IRREPJ=1,NIRREP
C
        IRREPI=DIRPRD(IRREP,IRREPJ)
        NUMB=VRTT(IRREPI)
        NUMI=POPT(IRREPI)
        NUMA=VRTG(IRREPJ)
        INDL=IOFFGL(IRREPJ)
        INDCI=IOFFGR(IRREPJ)
        INDT=IOFFT1(IRREPI)
        INDG=IOFFGCA(IRREPJ)
C
        DO 310 IC=1,NUMA
        INDG1=INDG+(IC-1)*NUMA
        DO 310 II=1,NUMI 
         INDT1=INDT+(II-1)*NUMB
         INDCI=INDCI+1
         INDAB=INDL
         DO 320 IA=1,NUMA
         INDCA=INDG1+IA
         DO 320 IB=1,NUMB
          INDIB=INDT1+IB
          INDAB=INDAB+1
          G(INDAB,INDCI)=G(INDAB,INDCI)-HALF*GCA(INDCA)*T1(INDIB)
320      CONTINUE

310     CONTINUE
300    CONTINUE
      ENDIF
      RETURN
      END
