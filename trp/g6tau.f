      SUBROUTINE G6TAU(G,GIK,T1,DISSYG,NUMSYG,POPG,POPT,
     &               VRTT,IRREP,ISPIN)
C
C THIS ROUTINE FORMS THE FOLLOWING CONTRIBUTION TO
C GAMMA 6 REQUIRED IN CCSD
C
C  G(IJ,KA) = G(IJ,KA) - HALF GIK(IK)*T1(JA)
C                      + HALF GIK(JK)*T1(IA)
C
C FOR ISPIN =1 (AAAA CASE) AND ISPIN =2 (BBBB CASE)
C THE EQUATION GIVEN APLIES DIRECTLY. FOR ISPIN=3 
C (ABAB CASE) IT REDUCES TO
C
C G(Ij,Ka) = G(Ij,Ka) - HALF GIK(IK)*T1(ja)
C
C AND FOR ISPIN=4 (BABA) CASE IT REDUCES TO
C
C G(Ij,Ak) = G(Ij,Ak) - HALF GIK(jk)*T1(IA)
C
CEND 
C
C CODED SEPTEMBER/90 JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DISSYG,DIRPRD,POPT,VRTT,POPG
      DIMENSION G(DISSYG,NUMSYG),T1(1),GIK(1)
      DIMENSION POPT(8),VRTT(8),POPG(8)
      DIMENSION IOFFGR(8),IOFFGL(8),IOFFT1(8),
     &          IOFFGIK(8)
C
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &                DIRPRD(8,8)
C
      DATA HALF /0.5D0/
C
      NNM1O2(I)=((I-1)*I)/2
C
      IF(ISPIN.LT.3) THEN
C
C AAAA OR BBBB SPIN CASE
C
C DETERMINE FIRST OFFSETS FOR T2 AND T1
C
       IOFFGR(1)=0
       IOFFGL(1)=0
       IOFFT1(1)=0
       IOFFGIK(1)=0
       DO 1 IRREPJ=1,NIRREP-1
        IOFFT1(IRREPJ+1)=IOFFT1(IRREPJ)+POPT(IRREPJ)*VRTT(IRREPJ)
        IOFFGIK(IRREPJ+1)=IOFFGIK(IRREPJ)+POPG(IRREPJ)*POPG(IRREPJ)
1      CONTINUE
       IF(IRREP.EQ.1) THEN
        DO 2 IRREPJ=1,NIRREP-1
         IOFFGR(IRREPJ+1)=IOFFGR(IRREPJ)+POPG(IRREPJ)*VRTT(IRREPJ)
         IOFFGL(IRREPJ+1)=IOFFGL(IRREPJ)+NNM1O2(POPG(IRREPJ))
2       CONTINUE
       ELSE
        DO 3 IRREPJ=1,NIRREP-1
         IRREPI=DIRPRD(IRREP,IRREPJ)
         IF(IRREPI.LT.IRREPJ) THEN
          IOFFGR(IRREPJ+1)=IOFFGR(IRREPJ)+POPG(IRREPI)*VRTT(IRREPJ)
          IOFFGL(IRREPJ+1)=IOFFGL(IRREPJ)+POPG(IRREPI)*POPT(IRREPJ)
         ELSE
          IOFFGR(IRREPJ+1)=IOFFGR(IRREPJ)+POPG(IRREPI)*VRTT(IRREPJ)
          IOFFGL(IRREPJ+1)=IOFFGL(IRREPJ)
         ENDIF
3       CONTINUE
       ENDIF
C
       DO 100 IRREPJ=1,NIRREP
C
        IRREPI=DIRPRD(IRREP,IRREPJ)
C
C  FIRST CASE IRREPI=IRREPJ
C
        IF(IRREPI.EQ.IRREPJ) THEN
         NUMA=VRTT(IRREPJ)
         NUMJ=POPG(IRREPI)
         INDKA=IOFFGR(IRREPJ)
         INDT=IOFFT1(IRREPJ)
         INDG=IOFFGIK(IRREPI)
         INDL=IOFFGL(IRREPJ)
         
C
         IF(NNM1O2(NUMJ).EQ.0) GO TO 100
C
         DO 10 IA=1,NUMA
         DO 10 IK=1,NUMJ
          INDG1=INDG+(IK-1)*NUMJ 
C
          INDKA=INDKA+1
C 
          INDIJ=INDL
C
          DO 20 IJ=2,NUMJ
          INDJA=INDT+(IJ-1)*NUMA+IA
          INDJK=INDG1+IJ
          DO 20 II=1,IJ-1
           INDIA=INDT+(II-1)*NUMA+IA
           INDIK=INDG1+II
           INDIJ=INDIJ+1
C
           G(INDIJ,INDKA)=G(INDIJ,INDKA)-HALF*GIK(INDIK)*T1(INDJA)
     &                                  +HALF*GIK(INDJK)*T1(INDIA)
20        CONTINUE
10       CONTINUE
C
C  SECOND CASE IRREPI LT IRREPJ (TREAT ALSO IRREPI GT IRREPJ HERE)
C
        ELSE IF(IRREPI.LT.IRREPJ) THEN
C
         NUMA=VRTT(IRREPJ)
         NUMK=POPG(IRREPI)
         NUMJ=POPT(IRREPJ)
         INDT=IOFFT1(IRREPJ)
         INDG=IOFFGIK(IRREPI)
         INDKA=IOFFGR(IRREPJ)
         INDL=IOFFGL(IRREPJ)
         
         DO 30 IA=1,NUMA
         DO 30 IK=1,NUMK
          INDG1=INDG+(IK-1)*NUMK
C
          INDKA=INDKA+1         
C
          INDIJ=INDL
C
          DO 50 IJ=1,NUMJ
           INDJA=INDT+(IJ-1)*NUMA+IA
           DO 60 II=1,NUMK
            INDIK=INDG1+II
            INDIJ=INDIJ+1
            G(INDIJ,INDKA)=G(INDIJ,INDKA)-HALF*GIK(INDIK)*T1(INDJA)
60         CONTINUE
50        CONTINUE
30       CONTINUE
C
C  NOW THE SECOND PART IRREPI.GT.IRREPJ)
C
         NUMA=VRTT(IRREPI)
         NUMK=POPG(IRREPJ)
         NUMI=POPT(IRREPI)
         INDT=IOFFT1(IRREPI)
         INDG=IOFFGIK(IRREPJ)
         INDKA=IOFFGR(IRREPI)
         INDL=IOFFGL(IRREPJ)
C    
         DO 31 IA=1,NUMA
         DO 31 IK=1,NUMK
          INDG1=INDG+(IK-1)*NUMK
C
          INDKA=INDKA+1
C
          INDIJ=INDL
C
          DO 51 IJ=1,NUMK
           INDJK=INDG1+IJ
           DO 61 II=1,NUMI
            INDIA=INDT+(II-1)*NUMA+IA
            INDIJ=INDIJ+1
            G(INDIJ,INDKA)=G(INDIJ,INDKA)+HALF*GIK(INDJK)*T1(INDIA) 
61         CONTINUE
51        CONTINUE
31       CONTINUE
C
        ENDIF
100    CONTINUE
      ELSE IF(ISPIN.EQ.3) THEN
C
C  ABAB SPIN CASE
C
C  GET FIRST OFFSETS OF GIK, T1, AND G
C
       IOFFGR(1)=0
       IOFFGL(1)=0
       IOFFT1(1)=0
       IOFFGIK(1)=0
       DO 101 IRREPJ=1,NIRREP-1
        IRREPI=DIRPRD(IRREPJ,IRREP)
        IOFFGR(IRREPJ+1)=IOFFGR(IRREPJ)+VRTT(IRREPJ)*POPG(IRREPI)
        IOFFGL(IRREPJ+1)=IOFFGL(IRREPJ)+POPT(IRREPJ)*POPG(IRREPI)
        IOFFT1(IRREPJ+1)=IOFFT1(IRREPJ)+POPT(IRREPJ)*VRTT(IRREPJ)
        IOFFGIK(IRREPJ+1)=IOFFGIK(IRREPJ)+POPG(IRREPJ)*POPG(IRREPJ)
101    CONTINUE
C
       DO 200 IRREPJ=1,NIRREP
C
        IRREPI=DIRPRD(IRREP,IRREPJ)
        NUMJ=POPT(IRREPJ)
        NUMA=VRTT(IRREPJ)
        NUMI=POPG(IRREPI)
        INDL=IOFFGL(IRREPJ)
        INDKA=IOFFGR(IRREPJ)
        INDT=IOFFT1(IRREPJ)
        INDG=IOFFGIK(IRREPI)
C
        DO 210 IA=1,NUMA
        DO 210 IK=1,NUMI
         INDKA=INDKA+1
         INDG1=INDG+(IK-1)*NUMI
         INDIJ=INDL
         DO 220 IJ=1,NUMJ
         INDJA=INDT+(IJ-1)*NUMA+IA
         DO 220 II=1,NUMI
          INDIJ=INDIJ+1
          INDIK=INDG1+II
          G(INDIJ,INDKA)=G(INDIJ,INDKA)-HALF*GIK(INDIK)*T1(INDJA)
220      CONTINUE

210     CONTINUE
200    CONTINUE
C
      ELSE IF(ISPIN.EQ.4) THEN
C
C  BABA SPIN CASE
C
C  GET FIRST OFFSETS OF GIK, T1, AND G
C
       IOFFGR(1)=0
       IOFFGL(1)=0
       IOFFT1(1)=0
       IOFFGIK(1)=0
       DO 201 IRREPJ=1,NIRREP-1
        IRREPI=DIRPRD(IRREPJ,IRREP)
        IOFFGR(IRREPJ+1)=IOFFGR(IRREPJ)+VRTT(IRREPI)*POPG(IRREPJ)
        IOFFGL(IRREPJ+1)=IOFFGL(IRREPJ)+POPT(IRREPI)*POPG(IRREPJ)
        IOFFT1(IRREPJ+1)=IOFFT1(IRREPJ)+POPT(IRREPJ)*VRTT(IRREPJ)
        IOFFGIK(IRREPJ+1)=IOFFGIK(IRREPJ)+POPG(IRREPJ)*POPG(IRREPJ)
201    CONTINUE
C
       DO 300 IRREPJ=1,NIRREP
C
        IRREPI=DIRPRD(IRREP,IRREPJ)
        NUMJ=POPT(IRREPI)
        NUMA=VRTT(IRREPI)
        NUMI=POPG(IRREPJ)
        INDL=IOFFGL(IRREPJ)
        INDKA=IOFFGR(IRREPJ)
        INDT=IOFFT1(IRREPI)
        INDG=IOFFGIK(IRREPJ)
C
        DO 310 IK=1,NUMI
        INDG1=INDG+(IK-1)*NUMI
        DO 310 IA=1,NUMA
         INDKA=INDKA+1
         INDIJ=INDL
         DO 320 II=1,NUMI
         INDIK=INDG1+II
         DO 320 IJ=1,NUMJ
          INDJA=INDT+(IJ-1)*NUMA+IA
          INDIJ=INDIJ+1
          G(INDIJ,INDKA)=G(INDIJ,INDKA)-HALF*GIK(INDIK)*T1(INDJA)
320      CONTINUE

310     CONTINUE
300    CONTINUE
      ENDIF
      RETURN
      END
