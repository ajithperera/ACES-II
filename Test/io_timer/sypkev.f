      


      SUBROUTINE SYPKEV(EVEC,SCR,NBASIS,TOTLEN,ISTART,ISPIN)
C
C THIS ROUTINE ACCEPTS THE FULL SQUARE EIGENVECTOR LIST AND RETURNS
C  THE SAME LIST IN THE FOLLOWING SYMMETRY-ORDERED FORM.
C
C    AO-OCCUPIED MO (ORDERED BY IRREP)
C    AO-VIRTUAL  MO (ORDERED BY IRREP)
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,POP,VRT,AOPOP,TOTLEN
      DIMENSION EVEC(1),SCR(1)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),DIRPRD(8,8)
      COMMON /INFO /  NOCCO(2),NVRTO(2)
      COMMON /INFO2/NBASIS2,NBAS(8),NMO(8),POP(8,2),VRT(8,2)
      COMMON /AOPOPS/ AOPOP(8)
      COMMON /AOOFST/ INDOCC(8,2)
C
C FIRST FORM THE AO-OCCUPIED BLOCK OF THE EIGENVECTOR MATRIX
C
      IOFF1O=1
      IOFF1V=1+NBASIS*NOCCO(ISPIN)
      IOFF2=1
      DO 10 IRREP=1,NIRREP
       INDOCC(IRREP,ISPIN)=IOFF2+ISTART
       LENGTH=NBAS(IRREP)
       DO 20 IOCC=1,POP(IRREP,ISPIN)
        CALL SCOPY(LENGTH,EVEC(IOFF1O),1,SCR(IOFF2),1)
        IOFF2=IOFF2+LENGTH
        IOFF1O=IOFF1O+NBASIS
20     CONTINUE
       IOFF1O=IOFF1O+LENGTH
       DO 30 IVRT=1,VRT(IRREP,ISPIN)
        CALL SCOPY(LENGTH,EVEC(IOFF1V),1,SCR(IOFF2),1)
        IOFF2=IOFF2+LENGTH
        IOFF1V=IOFF1V+NBASIS
30     CONTINUE
       IOFF1V=IOFF1V+LENGTH
10    CONTINUE
C
C NOW OVERWRITE INPUT ARRAY WITH EIGENVECTOR ARRAY AND RETURN
C
      TOTLEN=IOFF2-1
      CALL SCOPY(TOTLEN,SCR,1,EVEC,1)
      RETURN
      END
