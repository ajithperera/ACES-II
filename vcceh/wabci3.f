


      SUBROUTINE WABCI3(ICORE,MAXCOR,IUHF)
C
C THIS ROUTINE CALCULATES THE FOLLOWING CONTRACTION
C FOR ABAB SPIN CASE.  
C
C  Z(Ab,Id) = w(Ab,Cd) * T1(C,I)
C
C  THE REMAINING CONTRACTIONS ARE DONE BEFORE THIS ROUTINE IS
C   CALLED AND THE TARGET LIST CONTAINS THEM ON ENTRY. 
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH,ALPHA,BETA
      LOGICAL INCORE
      DIMENSION ICORE(MAXCOR),IOFFT1(8,2)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      DATA ONE   /1.0/
      DATA ONEM  /-1.0/
      DATA ZILCH /0.0/
C
      CALL GETT1(ICORE,MAXCOR,MXCOR,IUHF,IOFFT1)
      IBOT=1
      BETA=ONE
      LSTTAR=129
      DO 20 IRREPDO=1,NIRREP
       TARDSZ=IRPDPD(IRREPDO,ISYTYP(1,29))
       TARDIS=IRPDPD(IRREPDO,ISYTYP(2,29))
       TARSIZ=TARDSZ*TARDIS
       LISTW =233
       WDSZ=IRPDPD(IRREPDO,ISYTYP(1,LISTW))
       WDIS=WDSZ
       ALPHA=ONE
       I000=1
       I010=I000+IINTFP*TARSIZ
C
C CONTRACTION: 
C                                
C  Z(Ab,Id) = w(Ab,Cd) * T1(C,i)
C
       I020=I010+IINTFP*WDSZ*WDIS
C
C TRANSPOSE KET INDICES TO GIVE W(Ab,dC)
C
       I030=I020+IINTFP*WDSZ
       I040=I030+IINTFP*WDSZ
       I050=I040+IINTFP*WDSZ
       IF (I050 .GT. MXCOR) THEN
         WRITE(6,*) ' OUT-OF-CORE NOT IMPLEMENTED IN WABIC3'
         CALL ERREX
       ENDIF
C
       CALL IZERO(ICORE,IINTFP*TARSIZ)
C
       CALL GETLST(ICORE(I010),1,WDIS,2,IRREPDO,LISTW)
       CALL SYMTR1(IRREPDO,VRT(1,1),VRT(1,2),WDSZ,ICORE(I010),
     &             ICORE(I020),ICORE(I030),ICORE(I040))
C
C EVALUATE V CONTRIBUTION WITH THE MATRIX MULTIPLICATION
C                               
C      Z(Ab,dI) = w(Ab,dC) * T1(C,I)            +

C
       IOFFZ=I000
       IOFFW=I010
       DO 330 IRREPC=1,NIRREP
        IRREPD=DIRPRD(IRREPC,IRREPDO)
        IRREPI=IRREPC
        NROWZ=WDSZ*VRT(IRREPD,2)
        NCOLZ=POP(IRREPI,1)
        NROWW=NROWZ
        NCOLW=VRT(IRREPC,1)
        NROWT=VRT(IRREPC,1)
        NCOLT=NCOLZ
        IOFFT=IOFFT1(IRREPC,1)
        IF(MIN(NROWZ,NCOLZ,NCOLW).GT.0)THEN
         CALL XGEMM('N','N',NROWZ,NCOLZ,NCOLW,ALPHA,ICORE(IOFFW),
     &              NROWW,ICORE(IOFFT),NROWT,BETA,ICORE(IOFFZ),
     &              NROWZ)
        ENDIF
        IOFFW=IOFFW+IINTFP*NROWW*NCOLW
        IOFFZ=IOFFZ+IINTFP*NROWZ*NCOLZ
330    CONTINUE
C
C NOW REARRANGE THIS SO THAT IT IS ORDERED IN THE SAME WAY AS THE NEXT 
C  TWO TERMS (WHICH IS ALSO THE WAY THAT THE TARGET LIST IS ORDERED).
C
C                  Z(Ab,dI) -> Z(Ab,Id)
C
       I020=I010+IINTFP*TARDSZ
       I030=I020+IINTFP*TARDSZ
       I040=I030+IINTFP*TARDSZ
       IF (I040 .GT. MXCOR) THEN
         WRITE(6,*) ' OUT-OF-CORE NOT IMPLEMENTED IN WABIC @2'
         CALL ERREX
       ENDIF
       CALL SYMTR1(IRREPDO,VRT(1,2),POP(1,1),TARDSZ,ICORE(I000),
     &             ICORE(I010),ICORE(I020),ICORE(I030))
C
C FINALLY, INCREMENT THE TARGET LIST WITH Z(Ab,Id) AND LEAVE.
C
       I020=I010+IINTFP*TARDSZ*TARDIS
       IF (I020 .GT. MXCOR) THEN
         WRITE(6,*) ' OUT-OF-CORE NOT IMPLEMENTED IN WABIC3 @2'
         CALL ERREX
       ENDIF
       CALL GETLST(ICORE(I010),1,TARDIS,2,IRREPDO,LSTTAR)
C       CALL IZERO(ICORE(I010),IINTFP*TARSIZ)
       CALL SAXPY(TARSIZ,ONE,ICORE(I010),1,ICORE(I000),1)
       CALL PUTLST(ICORE(I000),1,TARDIS,2,IRREPDO,LSTTAR)
20    CONTINUE
      RETURN
      END
