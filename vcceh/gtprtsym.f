      SUBROUTINE GTPRTSYM(ISCR,SCR,MAXCOR,NBAS,ISYMPRT,LABEL)
C
C DETERMINE SYMMETRY BLOCK CORRESPONDING TO PERTURBATION BY
C INSPECTING THE MATRIX ELEMENTS OF THE PERTURBATION FOR NONZERO
C ELEMENTS
C
CEND
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER POP,VRT,DIRPRD
      CHARACTER*8 LABEL
      DIMENSION ISCR(NBAS),SCR(MAXCOR),IAOPOP(8)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/SYMINF/NSTART,NIRREP,IRREPY(255,2),DIRPRD(8,8)
C
      PARAMETER (TOL=1.0D-5)
C
C DETERMINE SYMMETRIES OF ALL AO BASIS FUNCTIONS
C
      CALL GETREC(20,'JOBARC','NUMBASIR',NIRREP,IAOPOP)
C
      IOFF=0
      DO 10 IRREP=1,NIRREP
       NUMIRR=IAOPOP(IRREP)
       DO 20 I=1,NUMIRR
        IOFF=IOFF+1
        ISCR(IOFF)=IRREP
20     CONTINUE      
10    CONTINUE
C
      NBASTRI=(NBAS*(NBAS+1))/2
      CALL GETREC(20,'JOBARC',LABEL,NBASTRI*IINTFP,SCR)
C
      ITHRU=0
      DO 30 INDEX1=1,NBAS
       DO 40 INDEX2=1,INDEX1
        ITHRU=ITHRU+1
        IF(ABS(SCR(ITHRU)).GT.TOL)THEN
         ISYM1=ISCR(INDEX1)
         ISYM2=ISCR(INDEX2)
         ISYMPRT=DIRPRD(ISYM1,ISYM2)
        ENDIF
40     CONTINUE
30    CONTINUE
C
      WRITE(6,500)ISYMPRT
C
500   FORMAT(T3,'@GTPRTSYM-I, The perturbation transforms as symmetry',
     &          ' species ',I1,'.') 
C
      RETURN
      END 
