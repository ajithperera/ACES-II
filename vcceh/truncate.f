      SUBROUTINE TRUNCATE(NPERT,NSPACE, KMAX,
     &   NDIM, ZOUT, Z, HZ, Y1, TMP, SCR, MAXCOR)
C
      IMPLICIT DOUBLE PRECISION (A-H, O-Z)
C
      DIMENSION TMP(NDIM,NPERT), Z(NDIM, KMAX), 
     &   HZ(NDIM, KMAX), ZOUT(NDIM, NPERT), Y1(NDIM,NPERT),
     &   SCR(MAXCOR)
C
C  ZOUT CONTAINS APPROXIMATE SOLUTIONS UP TO THIS POINT
C
C  FIRST CONSTRUCT AN ORTHOGONAL SET OF VECTORS FROM ZOUT
C
      NVEC = NPERT
      I=0
   50 I = I + 1
      CALL GSCHMIDT(ZOUT(1,I), ZOUT, NDIM, I-1, TMP, RESID)
      IF (RESID .LT. 1.0D-8) THEN
C
C LINEAR DEPENDENCY DETECTED: ELIMINATE THIS VECTOR
C
        DO J = I, NVEC -1
          CALL SCOPY(NDIM,ZOUT(1,J+1),1,ZOUT(1,J),1)
        ENDDO
        NVEC = NVEC - 1
        I = I - 1
      ENDIF
      IF (I .LT. NVEC) GO TO 50
C
C  CONSTRUCT HZ CORRESPONDING TO ZOUT
C
      CALL ZERO(TMP, NDIM*NVEC)
      DO IPERT = 1, NVEC
        DO I = 1, NSPACE
          FACTI = SDOT(NDIM,Z(1,I),1,ZOUT(1,IPERT),1)
          CALL SAXPY(NDIM,FACTI,HZ(1,I),1,TMP(1,IPERT),1)
        ENDDO
      ENDDO
C
      CALL SCOPY(NDIM*NVEC,ZOUT,1,Z,1)
      CALL SCOPY(NDIM*NVEC,TMP,1,HZ,1)
C
      NSPACE = NVEC
C
C  RECONSTRUCT ZOUT
C
       IASMALL = 1
       IYSMALL = IASMALL + NSPACE * NSPACE
       IZSMALL =  IYSMALL + NSPACE*NPERT
       ISCR =  IZSMALL + NSPACE*NPERT
C
C   SOLVE LINEAR SYSTEM OF EQUATIONS
C
       CALL SOLVESMALL(NSPACE, SCR(IASMALL), SCR(IYSMALL),
     &    SCR(IZSMALL),
     &     ZOUT, Y1, Z, HZ, NPERT, NDIM, SCR(ISCR), MAXCOR-ISCR+1)
C
       RETURN
       END
