      SUBROUTINE WABCI1(ICORE,MAXCOR,IUHF)
C
C THIS ROUTINE CALCULATES THE FOLLOWING CONTRACTION
C FOR AAAA AND BBBB SPIN CASES.  
C
C  Z(ab,ci) = w(ab,cd) * T1(d,i)
C
C  THE REMAINING CONTRACTIONS ARE DONE BEFORE THIS ROUTINE IS
C   CALLED AND THE TARGET LIST CONTAINS THEM ON ENTRY. 
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH,ALPHA,BETA
      LOGICAL INCORE
      DIMENSION ICORE(MAXCOR),MNFULL(8),ABFULL(8),IOFFT1(8,2)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      DATA ONE   /1.0/
      DATA ONEM  /-1.0/
      DATA ZILCH /0.0/
      CALL GETT1(ICORE,MAXCOR,MXCOR,IUHF,IOFFT1)
      IBOT=1
      BETA=ONE
      DO 10 ISPIN=1,1 +IUHF
       LSTTAR=126+ISPIN
       DO 5 IRREP=1,NIRREP
        ABFULL(IRREP)=IRPDPD(IRREP,18+ISPIN)
        MNFULL(IRREP)=IRPDPD(IRREP,20+ISPIN)
5      CONTINUE
       DO 20 IRREPDO=1,NIRREP
        TARDSZ=IRPDPD(IRREPDO,ISYTYP(1,26+ISPIN))
        TARDIS=IRPDPD(IRREPDO,ISYTYP(2,26+ISPIN))
        TARSIZ=TARDSZ*TARDIS
        I000=1
        I010=I000+IINTFP*TARSIZ
C
C  Z(AB,CI) = W(AB,CD) * T1(D,I)   [ISPIN = 1]
C
C  Z(ab,ci) = w(ab,cd) * T1(d,i)   [ISPIN = 2]
C
        ALPHA=ONE
        LISTW =230+ISPIN
        DISFUL=ABFULL(IRREPDO)
        WDSZ=IRPDPD(IRREPDO,ISYTYP(1,LISTW))
        WDIS=IRPDPD(IRREPDO,ISYTYP(2,LISTW))
        I020=I010+IINTFP*DISFUL*WDSZ
        I030=I020+IINTFP*WDIS
        IF (I030 .GT. MXCOR) THEN
          WRITE(6,*) ' OUT OF CORE WABCI1 NOT IMPLEMENTED'
          CALL ERREX
        ENDIF
C
        CALL IZERO(ICORE,IINTFP*TARSIZ)
C
        CALL GETLST(ICORE(I010),1,WDIS,2,IRREPDO,LISTW)
C
C NOW EXPAND W(A<B,C<D) TO W(A<B,CD)
C
        CALL SYMEXP(IRREPDO,VRT(1,ISPIN),WDSZ,ICORE(I010))
C
C EVALUATE V CONTRIBUTION WITH THE MATRIX MULTIPLICATION
C                                          
C             Z(A<BC,I) =  W(A<BC,D)*T(D,I)   [ISPIN=1]
C                                         
C             Z(a<bc,i) =  W(a<bc,d)*T(d,i)   [ISPIN=2]
C
C DO PREAMBLE AND DO THE CONTRACTION
C
        IOFFZ=I000
        IOFFW=I010
        DO 330 IRREPD=1,NIRREP
         IRREPC=DIRPRD(IRREPD,IRREPDO)
         IRREPI=IRREPD
         NROWZ=WDSZ*VRT(IRREPC,ISPIN)
         NCOLZ=POP(IRREPI,ISPIN)
         NROWW=NROWZ
         NCOLW=VRT(IRREPD,ISPIN)
         NCOLT=POP(IRREPI,ISPIN)
         NROWT=NCOLW
         IOFFT=IOFFT1(IRREPD,ISPIN)
         IF(MIN(NROWZ,NCOLZ,NCOLW).GT.0)THEN
          CALL XGEMM('N','N',NROWZ,NCOLZ,NCOLW,ALPHA,ICORE(IOFFW),
     &               NROWW,ICORE(IOFFT),NROWT,BETA,ICORE(IOFFZ),
     &               NROWZ)
         ENDIF
         IOFFW=IOFFW+IINTFP*NROWW*NCOLW
         IOFFZ=IOFFZ+IINTFP*NROWZ*NCOLZ
330     CONTINUE
C
C INCREMENT THE LIST AND EXIT.
C
        I020=I010+IINTFP*TARDSZ*TARDIS
        IF (I020 .GT. MXCOR) THEN
          WRITE(6,*) ' OUT OF CORE WABCI1 NOT IMPLEMENTED @2'
          CALL ERREX
        ENDIF
        CALL GETLST(ICORE(I010),1,TARDIS,2,IRREPDO,LSTTAR)
        CALL SAXPY(TARSIZ,ONE,ICORE(I010),1,ICORE(I000),1)
        CALL PUTLST(ICORE(I000),1,TARDIS,2,IRREPDO,LSTTAR)
20     CONTINUE
10    CONTINUE
      RETURN
      END
