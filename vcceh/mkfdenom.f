
     
      SUBROUTINE MKFDENOM(ACORE,MAXCOR,IUHF,IRREPX)
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD,POP,VRT
      LOGICAL FIELD,GEOM,ROHF,QRHF,SEMI
C
      DIMENSION ACORE(MAXCOR/2)
      DIMENSION IOFFOCC(8,2),IOFFVRT(8,2),IOFFVO(8,2)
C
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),NJUNK(18)
      COMMON/INFO/NOCCO(2),NVRTO(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/DTRAN/FIELD,GEOM,ROHF,QRHF,SEMI
      COMMON/DIAGW/IOFFIJ(8,8),IOFFAB(8,8),IOFFAI(8,8)
C
      DATA ONE/1.D0/
C
      INDXF(I,J,N)=I+(J-1)*N
C
C READ IN ORBITAL ENERGIES
C
      IEVALA=1
      IDIJAB=IEVALA+NOCCO(1)+NVRTO(1)
      IDABAB=IDIJAB+NOCCO(1)*NOCCO(2)
      IDAIAB=IDABAB+NVRTO(1)*NVRTO(2)
      IDAIAB2=IDAIAB+NVRTO(1)*NVRTO(2)
      IEND  =IDAIAB2+NOCCO(1)*NVRTO(2)
      NONV=NOCCO(1)*NVRTO(1)
      NONO=NOCCO(1)*NOCCO(2)
      NVNV=NVRTO(1)*NVRTO(2)
      CALL GETREC(20,'JOBARC','FDIAGA  ',IINTFP*(NOCCO(1)+NVRTO(1)),
     &            ACORE(IEVALA)) 
      CALL GETREC(20,'JOBARC','DIAGWIJ3',IINTFP*NONO,
     &            ACORE(IDIJAB)) 
      CALL GETREC(20,'JOBARC','DIAGWAB3',IINTFP*NVNV,
     &            ACORE(IDABAB)) 
      CALL GETREC(20,'JOBARC','DIAGWRN1',IINTFP*NONV,
     &            ACORE(IDAIAB)) 
      CALL GETREC(20,'JOBARC','DIAGWRN2',IINTFP*NONV,
     &            ACORE(IDAIAB2)) 
      IF(IUHF.EQ.0) THEN
       IEVALB=IEVALA
      ELSE
       IEVALB=IEND
       IEND=IEVALB+NVRTO(2)+NOCCO(2)
       CALL GETREC(20,'JOBARC','FDIAGB  ',NOCCO(2)+NVRTO(2),
     &             ACORE(IEVALB)) 
      ENDIF
      MAXLEN=0
      DO 2 IRREPR=1,NIRREP
       IRREPL=DIRPRD(IRREPR,IRREPX)
       MAXLEN=MAX(MAXLEN,IRPDPD(IRREPR,14)*IRPDPD(IRREPL,13))
2     CONTINUE
      IF((MAXLEN+IEND).GE.MAXCOR) CALL INSMEM('DDENOM',MAXLEN+
     &         IEND,MAXCOR)
C
C DETERMINE OFFSETS
C
      IOFFOCC(1,1)=-1
      IOFFOCC(1,2)=-1
      IOFFVRT(1,1)=NOCCO(1)-1
      IOFFVRT(1,2)=NOCCO(2)-1
      DO 1 IRREP=1,NIRREP-1
       IOFFOCC(IRREP+1,1)=IOFFOCC(IRREP,1)+POP(IRREP,1)
       IOFFOCC(IRREP+1,2)=IOFFOCC(IRREP,2)+POP(IRREP,2)
       IOFFVRT(IRREP+1,1)=IOFFVRT(IRREP,1)+VRT(IRREP,1)
       IOFFVRT(IRREP+1,2)=IOFFVRT(IRREP,2)+VRT(IRREP,2)
1     CONTINUE
C
C LOOP OVER IRREPS
C
      DO 1000 IRREPR=1,NIRREP
C
       IRREPL=DIRPRD(IRREPX,IRREPR)
C
C LOOP OVER IRREP RIGHT HAND SIDE
C
       IND=0
C
       ILOCIJ=IDIJAB
       DO 900 IRREPRR=1,NIRREP
C
        IRREPRL=DIRPRD(IRREPR,IRREPRR)
        NOCCR=POP(IRREPRR,2)
        NOCCL=POP(IRREPRL,1)
        IOFFJ=IOFFOCC(IRREPRR,2)
        IOFFI=IOFFOCC(IRREPRL,1)
C
        DO 100 IJ=1,NOCCR
        EVALJ=ACORE(IEVALB+IOFFJ+IJ)
        DO 100 II=1,NOCCL
        EVALIJ=EVALJ+ACORE(IEVALA+IOFFI+II)
C
C LOOP OVER IRREP LEFT HAND SIDE
C
        ILOCAB=IDABAB
        DO 800 IRREPLR=1,NIRREP
C
         IRREPLL=DIRPRD(IRREPLR,IRREPL)
C
         NVRTR=VRT(IRREPLR,2)
         NVRTL=VRT(IRREPLL,1)
         IOFFB=IOFFVRT(IRREPLR,2)
         IOFFA=IOFFVRT(IRREPLL,1)
C
         istart=ind
         DO 200 IB=1,NVRTR
         EVALIJB=EVALIJ-ACORE(IEVALB+IOFFB+IB)
C
          DO 200 IA=1,NVRTL
          IND=IND+1
          isyma=irrepll
          isymb=irreplr
          isymi=irreprl
          isymj=irreprr
          isymab=dirprd(isyma,isymb)
          isymij=dirprd(isymi,isymj)
          isymai=dirprd(isyma,isymi)
          isymaj=dirprd(isyma,isymj)
          isymbi=dirprd(isymb,isymi)
          isymbj=dirprd(isymb,isymj)
          ilocab=idabab-1+ioffab(isymb,isymab)+indxf(ia,ib,nvrtl)
          ilocij=idijab-1+ioffij(isymj,isymij)+indxf(ii,ij,noccl)
          ilocai=idaiab2-1+ioffai(isymi,isymai)+indxf(ia,ii,nvrtl)
          ilocaj=idaiab-1+ioffai(isymj,isymaj)+indxf(ia,ij,nvrtl)
          ilocbi=idaiab-1+ioffai(isymi,isymbi)+indxf(ib,ii,nvrtr)
          ilocbj=idaiab2-1+ioffai(isymj,isymbj)+indxf(ib,ij,nvrtr)

          ACORE(IEND+IND)=(EVALIJB-ACORE(IEVALA+IOFFA+IA)
     &                    -ACORE(ILOCAB)
     &                    -ACORE(ILOCIJ)
     &       +ACORE(ILOCAI)+ACORE(ILOCBJ)+ACORE(ILOCAJ)+ACORE(ILOCBI))
          ILOCAB=ILOCAB+1
200      CONTINUE
800     CONTINUE
        ILOCIJ=ILOCIJ+1
100     CONTINUE
900    CONTINUE
       CALL PUTLST(ACORE(IEND+1),1,IRPDPD(IRREPR,14),1,IRREPR,450) 
1000  CONTINUE
C
      IF(IUHF.EQ.0) goto 33
C
C FOR UHF AND ROHF, DO ALSO AAAA AND BBBB CASES
C
      DO 2000 ISPIN=1,2
C
      IF(ISPIN.EQ.1) THEN
       IEVAL=IEVALA
      ELSE IF (ISPIN.EQ.2) THEN
       IEVAL=IEVALB
      ENDIF
C
C LOOP OVER IRREPS
C
       DO 3000 IRREPR=1,NIRREP
C
        IRREPL=DIRPRD(IRREPX,IRREPR)
C
        IND=0
C
C LOOP OVER IRREP RIGHT HAND SIDE
C
        DO 2900 IRREPRR=1,NIRREP
C
         IRREPRL=DIRPRD(IRREPR,IRREPRR)
C
         IF(IRREPRR.LT.IRREPRL) GO TO 2900
C
         NOCCR=POP(IRREPRR,ISPIN)
         NOCCL=POP(IRREPRL,ISPIN)
C
         IOFFJ=IOFFOCC(IRREPRR,ISPIN)
         IOFFI=IOFFOCC(IRREPRL,ISPIN)
C
         DO 2100 IJ=1,NOCCR
C
          IEND1=NOCCL
          IF(IRREPRR.EQ.IRREPRL) IEND1=IJ-1
C
          EVALJ=ACORE(IEVAL+IOFFJ+IJ)
C
         DO 2100 II=1,IEND1
C
          EVALIJ=EVALJ+ACORE(IEVAL+IOFFI+II)
C
C LOOP OVER IRREP LEFT HAND SIDE
C
          DO 2800 IRREPLR=1,NIRREP
C
           IRREPLL=DIRPRD(IRREPLR,IRREPL)
           IF(IRREPLR.LT.IRREPLL) GO TO 2800
C
           NVRTL=VRT(IRREPLL,ISPIN)
           NVRTR=VRT(IRREPLR,ISPIN)
C
           IOFFA=IOFFVRT(IRREPLL,ISPIN)
           IOFFB=IOFFVRT(IRREPLR,ISPIN)
C
           DO 2200 IB=1,NVRTR
C
           IEND2=NVRTL
           IF(IRREPLL.EQ.IRREPLR) IEND2=IB-1
C
           EVALIJB=EVALIJ-ACORE(IEVAL+IOFFB+IB)
C
           DO 2200 IA=1,IEND2
C
            IND=IND+1
            ACORE(IEND+IND)=ONE/(EVALIJB-ACORE(IEVAL+IOFFA+IA))
C
2200       CONTINUE
2800      CONTINUE
2100     CONTINUE
2900    CONTINUE
        CALL PUTLST(ACORE(IEND+1),1,IRPDPD(IRREPR,2+ISPIN),1,
     &              IRREPR,47)
3000   CONTINUE
2000  CONTINUE
C
C FOR ROHF AND CC METHODS CREATE ALSO DENOMINATOR LISTS FOR
C SINGLES
C
33      IF(.true.) THEN
C
       DO 4000 ISPIN=1,1+IUHF
C
        IF(ISPIN.EQ.1) THEN
         IEVAL=IEVALA
        ELSE IF(ISPIN.EQ.2) THEN
         IEVAL=IEVALB
        ENDIF
C
C LOOP OVER IRREPS
C
        IND=0
C
        DO 5000 IRREPR=1,NIRREP
C
         IRREPL=DIRPRD(IRREPX,IRREPR)
C
         NOCCR=POP(IRREPR,ISPIN)
         NVRTL=VRT(IRREPL,ISPIN)
C
         IOFFI=IOFFOCC(IRREPR,ISPIN)
C
         IOFFA=IOFFVRT(IRREPL,ISPIN)
C
         DO 5100 I=1,NOCCR
C
          EVALI=ACORE(IEVAL+IOFFI+I)
C
          DO 5200 IA=1,NVRTL
C
           EVALIA=EVALI-ACORE(IEVAL+IOFFA+IA)
C
           IND=IND+1
           ACORE(IEND+IND)=EVALIA
C
5200      CONTINUE
5100     CONTINUE
5000    CONTINUE
C
        CALL PUTLST(ACORE(IEND+1),1,1,1,9,447+ISPIN)
C
4000   CONTINUE
C
      ENDIF
C
      RETURN
      END
