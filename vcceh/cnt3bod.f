

      SUBROUTINE CNT3BOD(IRREPX,ICORE,MAXCOR,IUHF,LISTT0,LISTZ0)
C
C SOLVES FOR THE THREE-BODY CONTRIBUTIONS TO THE C2 EQUATION.  JUST
C LIKE DFT2INT2 EXCEPT THAT THE ONE-PARTICLE TERMS (Q) ARE NOT
C SYMMETRIC, WHILE THE T AMPLITUDES ARE TOTALLY SYMMETRIC.
C
C   Z(ab,ij) = T(ae,ij) * Q(be) - T(ab,im) * Q(mj)
C
C
C THE CONTRACTIONS ARE CARRIED OUT USING RING-ORDERED LISTS :
C
C   Z(ai,bj) = - T(ai,bm) * Q(mj) + T(ai,ej) * Q(be)
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH,snrm2
      LOGICAL RHF
C
      DIMENSION ICORE(MAXCOR),IOFFAB(8,2),IOFFIJ(8,2),IOFFAI(8,2)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /SYMLOC/ ISYMOFF(8,8,25)
C
      DATA ONE  /1.0/
      DATA ONEM /-1.0/
      DATA ZILCH/0.0/
C
C FIRST DO THE ALPHA-BETA SPIN CASE
C
C
C LOOP OVER IRREPS
C
      DO 100 IRREPZR=1,NIRREP
       IRREPZL=DIRPRD(IRREPZR,IRREPX)
       IRREPTR=IRREPZL
       IRREPTL=IRREPZL
       LISTT=LISTT0-1+4
       LISTZ=LISTZ0+2
       DISSYZ=IRPDPD(IRREPZL,ISYTYP(1,LISTZ))
       NUMDSZ=IRPDPD(IRREPZR,ISYTYP(2,LISTZ))
       DISSYT=IRPDPD(IRREPTL,ISYTYP(1,LISTT))
       NUMDST=IRPDPD(IRREPTR,ISYTYP(2,LISTT))
       MAXT=MAX(DISSYT,NUMDST,DISSYZ,NUMDSZ)
       I000=1
       I010=I000+IINTFP*NUMDSZ*DISSYZ
       I020=I010+IINTFP*NUMDST*DISSYT
       I030=I020+IINTFP*IRPDPD(IRREPX,22)
       I040=I030+IINTFP*IRPDPD(IRREPX,20)
       I050=I040+IINTFP*MAXT
       I060=I050+IINTFP*MAXT
       I070=I060+IINTFP*MAXT
       CALL GETLST(ICORE(I010),1,NUMDST,1,IRREPTR,LISTT)
       CALL GETLST(ICORE(I020),1,1,1,1+IUHF,491)
       CALL GETLST(ICORE(I030),1,1,1,1+IUHF,492)
C
C CARRY OUT  -T(AI,bm) * Q(mj) CONTRACTION
C
       IOFFQ0=I020
       IOFFT0=I010
       IOFFZ0=I000
       DO 110 IRREPM=1,NIRREP
        IRREPJ=DIRPRD(IRREPM,IRREPX)
        IRREPB=DIRPRD(IRREPM,IRREPTR)
        NUMM=POP(IRREPM,2)
        NUMJ=POP(IRREPJ,2)
        NUMB=VRT(IRREPB,2)
        NROW=DISSYT*NUMB
        NCOL=NUMJ
        NSUM=NUMM
        IOFFQ=IOFFQ0+IINTFP*(ISYMOFF(IRREPJ,IRREPX,22)-1)
        IOFFT=IOFFT0+IINTFP*DISSYT*(ISYMOFF(IRREPM,IRREPTR,10)-1)
        IOFFZ=IOFFZ0+IINTFP*DISSYZ*(ISYMOFF(IRREPJ,IRREPZR,10)-1)
        CALL XGEMM('N','N',NROW,NCOL,NSUM,ONEM,ICORE(IOFFT),NROW,
     &             ICORE(IOFFQ),NSUM,ZILCH,ICORE(IOFFZ),NROW)
110    CONTINUE
C
C NOW TRANSPOSE KET INDICES
C
       CALL SYMTR1(IRREPZR,VRT(1,2),POP(1,2),DISSYT,ICORE(I000),
     &             ICORE(I040),ICORE(I050),ICORE(I060))
       CALL SYMTR1(IRREPTR,VRT(1,2),POP(1,2),DISSYT,ICORE(I010),
     &             ICORE(I040),ICORE(I050),ICORE(I060))
C
C NOW CARRY OUT T(AI,je) * F(eb) CONTRACTION
C
       IOFFQ0=I030
       IOFFT0=I010
       IOFFZ0=I000
       DO 120 IRREPE=1,NIRREP
        IRREPB=DIRPRD(IRREPE,IRREPX)
        IRREPJ=DIRPRD(IRREPE,IRREPTR)
        NUMJ=POP(IRREPJ,2)
        NUME=VRT(IRREPE,2)
        NUMB=VRT(IRREPB,2)
        NROW=DISSYT*NUMJ
        NCOL=NUMB
        NSUM=NUME
        IOFFQ=IOFFQ0+IINTFP*(ISYMOFF(IRREPB,IRREPX,20)-1)
        IOFFT=IOFFT0+IINTFP*DISSYT*(ISYMOFF(IRREPE,IRREPTR,17)-1)
        IOFFZ=IOFFZ0+IINTFP*DISSYZ*(ISYMOFF(IRREPB,IRREPZR,17)-1)
        CALL XGEMM('N','N',NROW,NCOL,NSUM,ONE,ICORE(IOFFT),NROW,
     &             ICORE(IOFFQ),NSUM,ONE,ICORE(IOFFZ),NROW)
120    CONTINUE
C
C FOR RHF, TRANSPOSE KET INDICES OF TARGET AND AUGMENT RING LISTS
C
       IF(IUHF.EQ.0)THEN
        CALL SYMTR1(IRREPZR,POP(1,2),VRT(1,2),DISSYT,ICORE(I000),
     &              ICORE(I040),ICORE(I050),ICORE(I060))
        CALL GETLST(ICORE(I010),1,NUMDSZ,1,IRREPZR,LISTZ)
c        call mismatch(icore(i000),icore(i010),numdsz*dissyz,1.d-8)
        CALL SAXPY (NUMDSZ*DISSYZ,ONE,ICORE(I010),1,ICORE(I000),1)
        CALL PUTLST(ICORE(I000),1,NUMDSZ,1,IRREPZR,LISTZ)
       ELSE
        write(6,*)' uhf not coded '
       ENDIF
C
100   CONTINUE
C
      RETURN
      END
