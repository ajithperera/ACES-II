      SUBROUTINE MKAPBUHF(AMAT,SCRATCH,IRREP)
C
C   THIS ROUTINE MAKES THE FULL SPIN ORBITAL A MATRIX
C   TO TEST UHF->UHF STABILITY FOR A PARTICULAR DPD IRREP.
C
C   NOTE, THAT ONLY ONE BLOCK OF THE A MATRIX IS COMPUTED 
C
C   THE FORMULAS ARE GIVEN BY
C
C   A(AI,BJ) = <AB//IJ> + <AJ//IB>      AA-BLOCK
C
C   A(ai,bj) = <ab//ij> + <aj//ib>      BB-BLOCK
C
C   A(AI,bj) = 2  <Ab//Ij>              AB-BLOCK
C
C   A(ai,BJ) = 2  <aB//iJ>              BA-BLOCK (TRANPOSE OF AB-BLOCK)
C  
C AMAT HOLDS ALL FOUR PARTS OF THE A MATRIX 
C SCRATCH IS A SCRATCH ARRAY REQUIRED FOR CONTRUCTING A
C IRREP IS THE CORRESPONDING IRREP OF THE FORMED A MATRIX
C
CEND
C
C CODED JULY/90  JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DISSYW,DIRPRD,POP,VRT
      DIMENSION AMAT(1),SCRATCH(1)
      COMMON/INFO/NOCCO(2),NVRTO(2)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPY(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NJUNK(6)
      COMMON /SHIFT/ ISHIFT,NDRGEO
C
      DATA ONE,ONEM,TWO /1.0D+0,-1.0D+0,2.0D+0/
C
C     FORM FIRST THE AA AND BB-BLOCK
C
C  LOOP OVER SPINS, READ INTEGRALS AND PLOP THIS INTO THE
C  CORRECT PLACE IN THE A MATRIX
C
      NUMAA=IRPDPD(IRREP,ISYTYP(1,19+ishift))
      NUMBB=IRPDPD(IRREP,ISYTYP(1,20+ishift))

      MATDIM=NUMAA+NUMBB
      IOFFAAAA=1
      IOFFBBBB=IOFFAAAA+MATDIM*NUMAA+NUMAA
      IOFFBBAA=IOFFAAAA+NUMAA
      IOFFAABB=IOFFAAAA+MATDIM*NUMAA
      IOFF=IOFFAAAA
      DO 1000 ISPIN=1,2
C
C FIRST READ IN THE INTEGRALS <AB//IJ> (ORDERING AI,BJ)
C THEY ARE STORED ON LIST 19 AND THEN ADD IN THE <AI//BJ> INTS
C
#ifdef _RCC
       LISTW=18+ISPIN + ISHIFT 
#else
       LISTW=4+ISPIN + ISHIFT 
#endif
       NUMSYW=IRPDPD(IRREP,ISYTYP(2,LISTW))
       DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
       LISTW2=22+ISPIN + ISHIFT 
C
       DO 1001 IDIS=1,NUMSYW
        CALL GETLST(AMAT(IOFF),IDIS,1,1,IRREP,LISTW)
#ifdef _DEBUG_LVLM
        Write(6,"(a)") "The list 4.5"
        Write(6, "(6(1x,F10.8))") (AMAT(IOFF-1+I),I=1, DISSYW)
#endif
        CALL GETLST(SCRATCH,IDIS,1,1,IRREP,LISTW2)
#ifdef _DEBUG_LVLM
        Write(6,"(a)") "The list 23,24"
        Write(6, "(6(1x,F10.8))") (SCRATCH(I),I=1, DISSYW)
#endif

        CALL SAXPY (DISSYW,ONEM,SCRATCH,1,AMAT(IOFF),1)
CSSS        CALL SAXPY (DISSYW,ONE,SCRATCH,1,AMAT(IOFF),1)
        IOFF=IOFF+MATDIM
1001   CONTINUE
       IOFF=IOFFBBBB
C
1000  CONTINUE
C
C   NOW FORM THE AB BLOCK (THE BA BLOCK IS THE TRANSPOSED AS LONG
C   AS WE NOT SCALE WITH THE ORBITAL ENERGY DENOMINATOR
C
      LISTW=17 + ISHIFT 
      NUMSYW=IRPDPD(IRREP,ISYTYP(2,LISTW))
      DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
C
      IOFF2=IOFFAABB
      IOFF=IOFFBBAA
      DO 1002 IDIS=1,NUMSYW
       CALL GETLST(AMAT(IOFF),IDIS,1,1,IRREP,LISTW)
#ifdef _DEBUG_LVLM
        Write(6,"(a)") "The list 17"
        Write(6, "(6(1x,F10.8))") (AMAT(IOFF+I-1),I=1, DISSYW)
#endif
       CALL SSCAL (DISSYW,TWO,AMAT(IOFF),1)
       CALL SCOPY (DISSYW,AMAT(IOFF),1,AMAT(IOFF2),MATDIM)
       IOFF=IOFF+MATDIM
       IOFF2=IOFF2+1
1002  CONTINUE
C
C  ALL DONE, RETURN
C
      RETURN
      END
