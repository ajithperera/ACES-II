      SUBROUTINE MKD314(D3,EVAL,ISPIN,NO,NV,IRPIJK,POPT3)
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION D3(1)
      DOUBLE PRECISION EVAL(NO+NV)
      DIMENSION POPT3(8)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     1                DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /SYM/    POP(8,2),VRT(8,2),NTAA,NTBB,NF1AA,NF2AA,
     1                NF1BB,NF2BB
      COMMON /INFO/   NOCCO(2),NVRTO(2)
C
#include "maxbasfn.par"
      COMMON /ACTORB/ ABSVRT(MAXBASFN,8,2),ABSOCC(MAXBASFN,8,2)
C
      INDEX(I) = I*(I-1)/2
C
C     SUBROUTINE THAT IS SUPPOSED TO FORM A D3(A<B<C) DENOMINATOR
C     FOR GIVEN IRPABC = IRPIJK IN SYMMETRY.
C
      ISTART = 0
C
      IF(NIRREP.LT.3) GOTO 1010
C
C     ALL IRREPS OF A,B,C DIFFERENT
C
      DO  1000 IRPC=3,NIRREP
      IF(VRT(IRPC,ISPIN).EQ.0) GOTO 1000
      DO   990 IRPB=2,IRPC-1
      IF(VRT(IRPB,ISPIN).EQ.0) GOTO  990
      DO   980 IRPA=1,IRPB-1
      IF(VRT(IRPA,ISPIN).EQ.0) GOTO  980
C
      IRPAB =  DIRPRD(IRPA,IRPB)
      IRPAC =  DIRPRD(IRPA,IRPC)
      IRPBC =  DIRPRD(IRPB,IRPC)
      IRPABC = DIRPRD(IRPA,IRPBC)
C
      IF(IRPABC.NE.IRPIJK) GOTO 980
C
      DO  430 C=1,VRT(IRPC,ISPIN)
      DO  420 B=1,VRT(IRPB,ISPIN)
      DO  410 A=1,VRT(IRPA,ISPIN)
C
      ABC = ISTART + (C-1)*VRT(IRPB,ISPIN)*VRT(IRPA,ISPIN)
     1             + (B-1)*VRT(IRPA,ISPIN) + A
C
      D3(ABC) = EVAL(NO + ABSVRT(A,IRPA,ISPIN))
     1        + EVAL(NO + ABSVRT(B,IRPB,ISPIN))
     1        + EVAL(NO + ABSVRT(C,IRPC,ISPIN))
C
  410 CONTINUE
  420 CONTINUE
  430 CONTINUE
C
      ISTART = ISTART + VRT(IRPC,ISPIN)*VRT(IRPB,ISPIN)*VRT(IRPA,ISPIN)
C
  980 CONTINUE
  990 CONTINUE
 1000 CONTINUE
C
 1010 CONTINUE
C
      IF(NIRREP.LT.2) GOTO 3010
C
C     IRREPS OF B,C ARE THE SAME, IRREP OF A IS DIFFERENT
C
      DO  2000 IRPC=2,NIRREP
      IRPB = IRPC
      IF(VRT(IRPC,ISPIN).LT.2) GOTO 2000
      DO  1980 IRPA=1,IRPC-1
      IF(VRT(IRPA,ISPIN).EQ.0) GOTO 1980
C
      IRPAB =  DIRPRD(IRPA,IRPB)
      IRPAC =  DIRPRD(IRPA,IRPC)
      IRPBC =  DIRPRD(IRPB,IRPC)
      IRPABC = DIRPRD(IRPA,IRPBC)
C
      IF(IRPABC.NE.IRPIJK) GOTO 1980
C
      DO  1430 C=2,VRT(IRPC,ISPIN)
      DO  1420 B=1,C-1
      DO  1410 A=1,VRT(IRPA,ISPIN)
C
      ABC = ISTART + (INDEX(C-1) + B - 1) * VRT(IRPA,ISPIN) + A
C
      D3(ABC) = EVAL(NO + ABSVRT(A,IRPA,ISPIN))
     1        + EVAL(NO + ABSVRT(B,IRPB,ISPIN))
     1        + EVAL(NO + ABSVRT(C,IRPC,ISPIN))
C
 1410 CONTINUE
 1420 CONTINUE
 1430 CONTINUE
C
      ISTART = ISTART + ((VRT(IRPC,ISPIN)-1)*VRT(IRPC,ISPIN)/2)
     1                  *VRT(IRPA,ISPIN)
C
 1980 CONTINUE
 2000 CONTINUE
C
C     IRREPS OF A,B ARE THE SAME, IRREP OF C IS DIFFERENT
C
      DO  3000 IRPC=2,NIRREP
      IF(VRT(IRPC,ISPIN).EQ.0) GOTO 3000
      DO  2990 IRPB=1,IRPC-1
      IRPA = IRPB
      IF(VRT(IRPB,ISPIN).LT.2) GOTO 2990
C
      IRPAB =  DIRPRD(IRPA,IRPB)
      IRPAC =  DIRPRD(IRPA,IRPC)
      IRPBC =  DIRPRD(IRPB,IRPC)
      IRPABC = DIRPRD(IRPA,IRPBC)
C
      IF(IRPABC.NE.IRPIJK) GOTO 2990
C
      DO  2430 C=1,VRT(IRPC,ISPIN)
      DO  2420 B=2,VRT(IRPB,ISPIN)
      DO  2410 A=1,B-1
C
      ABC = ISTART + (C-1)*(VRT(IRPB,ISPIN)-1)*VRT(IRPB,ISPIN)/2
     1               + INDEX(B-1) + A
C
      D3(ABC) = EVAL(NO + ABSVRT(A,IRPA,ISPIN))
     1        + EVAL(NO + ABSVRT(B,IRPB,ISPIN))
     1        + EVAL(NO + ABSVRT(C,IRPC,ISPIN))
C
 2410 CONTINUE
 2420 CONTINUE
 2430 CONTINUE
C
      ISTART = ISTART + ((VRT(IRPB,ISPIN)-1)*VRT(IRPB,ISPIN)/2)
     1                  *VRT(IRPC,ISPIN)
C
 2990 CONTINUE
 3000 CONTINUE
C
 3010 CONTINUE
C
C     ALL IRREPS OF ARE THE SAME. THIS IS THE ONLY PIECE OF CODE
C     EXECUTED IN C1 JOBS.
C
      DO  4000 IRPC=1,NIRREP
      IRPB = IRPC
      IRPA = IRPC
      IF(VRT(IRPC,ISPIN).LT.3) GOTO 4000
C
      IRPAB =  DIRPRD(IRPA,IRPB)
      IRPAC =  DIRPRD(IRPA,IRPC)
      IRPBC =  DIRPRD(IRPB,IRPC)
      IRPABC = DIRPRD(IRPA,IRPBC)
C
      IF(IRPABC.NE.IRPIJK) GOTO 4000
C
      DO  3430 C=3,VRT(IRPC,ISPIN)
      DO  3420 B=2,C-1
      DO  3410 A=1,B-1
C
      ABC = ISTART + (C-3)*(C-2)*(C-1)/6 + INDEX(B-1) + A
C
      D3(ABC) = EVAL(NO + ABSVRT(A,IRPA,ISPIN))
     1        + EVAL(NO + ABSVRT(B,IRPB,ISPIN))
     1        + EVAL(NO + ABSVRT(C,IRPC,ISPIN))
C
 3410 CONTINUE
 3420 CONTINUE
 3430 CONTINUE
C
      ISTART = ISTART + (VRT(IRPC,ISPIN)-2)*(VRT(IRPC,ISPIN)-1)
     1                  *VRT(IRPC,ISPIN)/6
C
 4000 CONTINUE
C
      IF(ABC.EQ.POPT3(IRPIJK))THEN
C     WRITE(6,5000)
 5000 FORMAT(' @MKD314-I, AT LEAST ABC IS WHAT IT SHOULD BE. ')
      ELSE
      WRITE(6,5010) ABC,POPT3(IRPIJK)
 5010 FORMAT(' @MKD314-I, ERROR. ABC IS ',I20,' SHOULD BE ',I20)
      ENDIF
C
      RETURN
      END
