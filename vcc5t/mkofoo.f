      SUBROUTINE MKOFOO
      IMPLICIT INTEGER (A-Z)
C
      DIMENSION LENOO(8,8)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     1                DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /SYM/    POP(8,2),VRT(8,2),NTAA,NTBB,NF1AA,NF2AA,
     1                NF1BB,NF2BB
      COMMON /INFO/   NOCCO(2),NVRTO(2)
C
      COMMON /T3OFF/  IOFFVV(8,8,10),IOFFOO(8,8,10),IOFFVO(8,8,4)
C
      INDEX(I) = I*(I-1)/2
C
C     ROUTINE TO COMPUTE VARIOUS OO OFFSETS.
C
C     #    SPIN   ORDER
C
C     1     AA     I<J
C     2     BB     I<J
C     3     AA     I,J
C     4     BB     I,J
C     5     AB     I,J
C     6     BA     I,J
C
      DO   40 ISPIN=1,2
      DO   10 IRPBC=1,NIRREP
      DO   10 IRPC =1,NIRREP
       LENOO(IRPC,IRPBC)       = 0
      IOFFOO(IRPC,IRPBC,ISPIN) = 0
   10 CONTINUE
C
      DO   30 IRPBC=1,NIRREP
      DO   20 IRPC =1,NIRREP
      IRPB = DIRPRD(IRPBC,IRPC)
C
      IF(IRPB.GT.IRPC)THEN
      LENOO(IRPC,IRPBC) = 0
      ENDIF
C
      IF(IRPB.EQ.IRPC)THEN
      LENOO(IRPC,IRPBC) = 
     1             (POP(IRPC,ISPIN)-1)*POP(IRPC,ISPIN)/2
      ENDIF
      IF(IRPB.LT.IRPC)THEN
      LENOO(IRPC,IRPBC) = 
     1              POP(IRPC,ISPIN)   *POP(IRPB,ISPIN)
      ENDIF
C
      IF(IRPC.EQ.1)THEN
      IOFFOO(IRPC,IRPBC,ISPIN) = 0
      ELSE
      IOFFOO(IRPC,IRPBC,ISPIN) = IOFFOO(IRPC-1,IRPBC,ISPIN) + 
     1                            LENOO(IRPC-1,IRPBC)
      ENDIF
   20 CONTINUE
   30 CONTINUE
   40 CONTINUE
C
      DO  140 ISPIN=3,4
      DO  110 IRPBC=1,NIRREP
      DO  110 IRPC =1,NIRREP
       LENOO(IRPC,IRPBC)       = 0
      IOFFOO(IRPC,IRPBC,ISPIN) = 0
  110 CONTINUE
C
      DO  130 IRPBC=1,NIRREP
      DO  120 IRPC =1,NIRREP
      IRPB = DIRPRD(IRPBC,IRPC)
C
      LENOO(IRPC,IRPBC) = POP(IRPB,ISPIN-2) *
     1                    POP(IRPC,ISPIN-2)
C
      IF(IRPC.EQ.1)THEN
      IOFFOO(IRPC,IRPBC,ISPIN) = 0
      ELSE
      IOFFOO(IRPC,IRPBC,ISPIN) = IOFFOO(IRPC-1,IRPBC,ISPIN) + 
     1                            LENOO(IRPC-1,IRPBC)
      ENDIF
  120 CONTINUE
  130 CONTINUE
  140 CONTINUE
C
      DO  210 IRPAB=1,NIRREP
      DO  210 IRPB =1,NIRREP
      IRPA = DIRPRD(IRPAB,IRPB)
C
      LENOO(IRPB,IRPAB) =  POP(IRPA,1) * POP(IRPB,2)
C
      IF(IRPB.EQ.1)THEN
      IOFFOO(IRPB,IRPAB,5) = 0
      ELSE
      IOFFOO(IRPB,IRPAB,5) = IOFFOO(IRPB-1,IRPAB,5) + 
     1                        LENOO(IRPB-1,IRPAB)
      ENDIF
  210 CONTINUE
C
      DO  310 IRPAB=1,NIRREP
      DO  310 IRPB =1,NIRREP
      IRPA = DIRPRD(IRPAB,IRPB)
C
      LENOO(IRPB,IRPAB) =  POP(IRPA,2) * POP(IRPB,1)
C
      IF(IRPB.EQ.1)THEN
      IOFFOO(IRPB,IRPAB,6) = 0
      ELSE
      IOFFOO(IRPB,IRPAB,6) = IOFFOO(IRPB-1,IRPAB,6) + 
     1                        LENOO(IRPB-1,IRPAB)
      ENDIF
  310 CONTINUE
C
      RETURN
      END
