C     
C     THIS ROUTINE CHANGES THE CHARACTERISTICS OF A LIST, AND
C     SHOULD BE USED ONLY BY EXPERIENCED CRAPS PROGRAMMERS!
C     
C     INPUT: 
C     LIST - THE LIST NUMBER WHICH IS TO BE CHANGED.
C     SYTYPL - THE NEW SYMMETRY TYPE FOR THE BRA INDICES.
C     SYTYPR - THE NEW SYMMETRY TYPE FOR THE KET INDICES.
C     CHANGE - LOGICAL VARIABLE WHICH CONTROLS WHETHER OR
C              NOT THE CORRESPONDING ISYTYP ENTRIES ARE
C              CHANGED TO CORRESPOND TO THE NEW LIST TYPE.
C              SET THIS TO .TRUE. UNLESS YOU REALLY KNOW
C              WHAT YOU ARE DOING.
C     IrrepX - Overall irrep of list.
C     
      SUBROUTINE NEWTYP2(IrrepX, LIST,SYTYPL,SYTYPR,CHANGE)
      Implicit Integer (A-Z)
      LOGICAL CHANGE
      COMMON /IOPOS/ ICRSIZ,ICHCSZ,IOFF(2),LENREC
      COMMON /LISTS/ MOIO(10,500),MOIOWD(10,500),MOIOSZ(10,500),
     &   MOIODS(10,500),MOIOFL(10,500)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &   DIRPRD(8,8)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /AUXCACHE/ QUIKGET(10,500)
#ifdef _ASSERT
      if ((SYTYPL.lt.1).or.(22.lt.SYTYPL).or.
     &    (SYTYPR.lt.1).or.(22.lt.SYTYPR)    ) then
         print *, '@NEWTYP2: Assertion failed.'
         print *, '   SYTYPL = ',SYTYPL
         print *, '   SYTYPR = ',SYTYPR
         print *, '   LIST   = ',LIST
         call errex
      end if
#endif /* _ASSERT */
      TOTNEW=0
      TOTOLD=0
      IWORD2=QUIKGET(1,LIST)
      IWORD=MOIOWD(1,LIST)
      IREC =MOIO(1,LIST)
      ILIST=LIST
      DO 10 IrrepR=1,NIRREP
         IrrepL = DirPrd(IrrepR, IrrepX)
         NUMDIS=IRPDPD(IrrepR,SYTYPR)
         DISSIZ=IRPDPD(IrrepL,SYTYPL)
         TOTNEW=TOTNEW+NUMDIS*DISSIZ
         TOTOLD=TOTOLD+IRPDPD(IrrepL,ISYTYP(1,ILIST))*
     &      IRPDPD(IrrepR,ISYTYP(2,ILIST))
         MOIODS(IrrepR,LIST)=NUMDIS
         MOIOSZ(IrrepR,LIST)=DISSIZ
         MOIOWD(IrrepR,LIST)=IWORD
         MOIO  (IrrepR,LIST)=IREC
         IF(QUIKGET(IrrepR,LIST).NE.0)QUIKGET(IrrepR,LIST)=IWORD2
         IWORD=IWORD+NUMDIS*DISSIZ*IINTFP
         IWORD2=IWORD2+NUMDIS*DISSIZ*IINTFP
         IF(IWORD.GT.LENREC)THEN
            INC=(IWORD-1)/LENREC
            IWORD=IWORD-INC*LENREC
            IREC=IREC+INC
         ENDIF
 10   CONTINUE
      IF(CHANGE) THEN
         ISYTYP(1,ILIST)=SYTYPL
         ISYTYP(2,ILIST)=SYTYPR
      ENDIF
      RETURN
      END
