      SUBROUTINE OSL2AIII(ICORE,MAXCOR,IUHF,W)
C
C   THIS ROUTINE CALCULATES:
C
C  FOR ISPIN=1
C     L2INC(NA,IJ) = - W SUM(B) T2(Bm,Mn) * L2(ab,ij)
C     L2INC(Na,Ij) = + W SUM(B) T2(Bm,Mn) * L2(Ab,Ji)
C
C  FOR ISPIN=2:
C     L2INC(ma,ij) = - W SUM(b) T2(Nb,Mn) * L2(AB,IJ)
C     L2INC(Am,Ij) = + W SUM(b) T2(Nb,Mn) * L2(Ba,Ji)
C
CEND
C
CPROGRAMED BY PS SEPT/93
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,DISSYL,DISSYT,POP,VRT,DISTAR
      CHARACTER*4 STRING,STRTAR
C
      DIMENSION ICORE(MAXCOR)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &                DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
C
      INTEGER POPA,POPI,VRTA,VRTI
      COMMON /FSSYM/ POPA(8,2),POPI(8,2),VRTA(8,2),VRTI(8,2),
     &               NTAN(2),NTNA(2),NTAA(2),
     &               NTIN(2),NTNI(2),NTII(2),
     &               NTAI(2),NTIA(2),
     &               NF1AN(2),NF1AA(2),NF1IN(2),NF1II(2),NF1AI(2),
     &               NF2AN(2),NF2AA(2),NF2IN(2),NF2II(2),NF2AI(2)
C
      INTEGER FSDPDAN,FSDPDNA,FSDPDAA,FSDPDIN,FSDPDNI,FSDPDII,FSDPDAI,
     $   FSDPDIA
      COMMON /FSSYMPOP/ FSDPDAN(8,22),FSDPDNA(8,22),FSDPDAA(8,22),
     &                  FSDPDIN(8,22),FSDPDNI(8,22),FSDPDII(8,22),
     &                  FSDPDAI(8,22),FSDPDIA(8,22)
C
      COMMON /ACTIRR/ IRREPO(2),IRREPV(2)
C
      COMMON /FLAGS/ IFLAGS(100)
C
      DATA ONE,ONEM,ZILCH,HALF/1.D0,-1.D0,0.D0,0.5D0/
C
      DO 1000 ISPIN=1,IUHF+1
         DO 100 IRREP=1,NIRREP
            IRREPT=DIRPRD(IRREPO(1),IRREPO(2))
            IRREPI=DIRPRD(IRREP,IRREPV(ISPIN))
            IRREPJ=IRREPV(ISPIN)
C
C     AA AND BB CONTRIB
C
            LISTAR=60+ISPIN
            DISTAR=FSDPDAI(IRREP,ISYTYP(1,LISTAR))
            NUMTAR=FSDPDII(IRREP,ISYTYP(2,LISTAR))
            IF(DISTAR*NUMTAR.EQ.0) GOTO 200
C
            LISTL=146-ISPIN
            NUMSYL=FSDPDII(IRREP,ISYTYP(2,LISTL))
            DISSYL=FSDPDII(IRREP,ISYTYP(1,LISTL))
C
            LISTT=46
            NUMSYT=FSDPDAA(IRREPT,ISYTYP(2,LISTT))
            IF(ISPIN.EQ.1) THEN
               DISSYT=FSDPDIA(IRREPT,ISYTYP(1,LISTT))
               STRING='IAAA'
            ELSE
               DISSYT=FSDPDAI(IRREPT,ISYTYP(1,LISTT))
               STRING='AIAA'
            ENDIF
C
C     I000: L2(CONT) I010: L2  I020: T2   I030: TMP
C
            I000=1
            I010=I000+NUMTAR*DISTAR*IINTFP
            I020=I010+NUMSYL*DISSYL*IINTFP
            I030=I020+NUMSYT*DISSYT*IINTFP
            I040=I030+MAX(NUMSYT*DISSYT,NUMSYL*DISSYL)*IINTFP
C
            CALL FSGET(ICORE(I030),1,NUMSYL,1,IRREP,LISTL,'IIII')
            CALL TRANSP(ICORE(I030),ICORE(I010),NUMSYL,DISSYL)
            CALL FSGET(ICORE(I030),1,NUMSYT,1,IRREPT,LISTT,STRING)
            CALL TRANSP(ICORE(I030),ICORE(I020),NUMSYT,DISSYT)
C
            IOFF=I010
            DO 10 IRREP2=1,IRREPJ-1
               IRREP1=DIRPRD(IRREP,IRREP2)
               IOFF=IOFF+NUMSYL*IINTFP*
     $              VRTI(IRREP1,3-ISPIN)*VRTI(IRREP2,3-ISPIN)
 10         CONTINUE
            NSIZI=VRTI(IRREPI,3-ISPIN)
            NSIZJ=VRTI(IRREPJ,3-ISPIN)
C
            CALL XGEMM('N','N',NUMSYL*NSIZI,1,NSIZJ,ONEM,ICORE(IOFF),
     $           NUMSYL*NSIZI,ICORE(I020),NSIZJ,ZILCH,ICORE(I000),
     $           NUMSYL*NSIZI)
C
C     SCALE BY W
C
C     I000: L2(CONT)    I010: L2(TARGET)
C
            CALL TRANSP(ICORE(I000),ICORE(I010),DISTAR,NUMTAR)
            CALL FSGET(ICORE(I000),1,NUMTAR,1,IRREP,LISTAR,'AIII')
            CALL SAXPY(NUMTAR*DISTAR,W,ICORE(I010),1,ICORE(I000),1)
            CALL FSPUT(ICORE(I000),1,NUMTAR,1,IRREP,LISTAR,'AIII')
C
C     AB CONTRIB
C
 200        CONTINUE
            LISTAR=63
            NUMTAR=FSDPDII(IRREP,ISYTYP(2,LISTAR))
            IF(ISPIN.EQ.1) THEN
               DISTAR=FSDPDAI(IRREP,ISYTYP(1,LISTAR))
               STRTAR='AIII'
            ELSE
               DISTAR=FSDPDIA(IRREP,ISYTYP(1,LISTAR))
               STRTAR='IAII'
            ENDIF
            IF(DISTAR*NUMTAR.EQ.0) GOTO 100
C
            LISTL=146
            NUMSYL=FSDPDII(IRREP,ISYTYP(2,LISTL))
            DISSYL=FSDPDII(IRREP,ISYTYP(1,LISTL))
C
            LISTT=46
            NUMSYT=FSDPDAA(IRREPT,ISYTYP(2,LISTT))
            IF(ISPIN.EQ.1) THEN
               DISSYT=FSDPDIA(IRREPT,ISYTYP(1,LISTT))
               STRING='IAAA'
            ELSE
               DISSYT=FSDPDAI(IRREPT,ISYTYP(1,LISTT))
               STRING='AIAA'
            ENDIF
C
C     I000: L2(CONT) I010: L2  I020: TMP
C
            I000=1
            I010=I000+NUMTAR*DISTAR*IINTFP
            I020=I010+NUMSYL*DISSYL*IINTFP
            I030=I020+NUMSYL*DISSYL*IINTFP
C
            CALL FSGET(ICORE(I010),1,NUMSYL,1,IRREP,LISTL,'IIII')
            CALL TRANSP(ICORE(I010),ICORE(I020),NUMSYL,DISSYL)
            CALL SYMTRA2(IRREP,POPI(1,1),POPI(1,2),NUMSYL,DISSYL,
     $           ICORE(I020),ICORE(I010))
            IF(ISPIN.EQ.2) THEN
               I030=I020+NUMSYL*IINTFP
               I040=I030+NUMSYL*IINTFP
               I050=I040+NUMSYL*IINTFP
               CALL SYMTR1(IRREP,VRTI(1,1),VRTI(1,2),NUMSYL,
     $              ICORE(I010),ICORE(I020),ICORE(I030),ICORE(I040))
            ENDIF
C
C     I000: L2(CONT) I010: L2  I020: T2   I030: TMP
C
            I030=I020+NUMSYT*DISSYT*IINTFP
            I040=I030+NUMSYT*DISSYT*IINTFP
C
            CALL FSGET(ICORE(I030),1,NUMSYT,1,IRREPT,LISTT,STRING)
            CALL TRANSP(ICORE(I030),ICORE(I020),NUMSYT,DISSYT)
C
            IOFF=I010
            DO 20 IRREP2=1,IRREPJ-1
               IRREP1=DIRPRD(IRREP,IRREP2)
               IOFF=IOFF+NUMSYL*IINTFP*
     $              VRTI(IRREP1,ISPIN)*VRTI(IRREP2,3-ISPIN)
 20         CONTINUE
            NSIZI=VRTI(IRREPI,ISPIN)
            NSIZJ=VRTI(IRREPJ,3-ISPIN)
C
            CALL XGEMM('N','N',NUMSYL*NSIZI,1,NSIZJ,ONE,ICORE(IOFF),
     $           NUMSYL*NSIZI,ICORE(I020),NSIZJ,ZILCH,ICORE(I000),
     $           NUMSYL*NSIZI)
C
C     SCALE BY W
C
C     I000: L2(TARGET)    I010: L2(CONT)
C
            CALL TRANSP(ICORE(I000),ICORE(I010),DISTAR,NUMTAR)
            CALL FSGET(ICORE(I000),1,NUMTAR,1,IRREP,LISTAR,STRTAR)
            CALL SAXPY(NUMTAR*DISTAR,W,ICORE(I010),1,ICORE(I000),1)
            CALL FSPUT(ICORE(I000),1,NUMTAR,1,IRREP,LISTAR,STRTAR)
C
 100     CONTINUE
 1000 CONTINUE
      RETURN
      END
