      SUBROUTINE WVT323(T3,W,CORE,
     1                  IADT3,IADW,
     1                  IRPI,IRPJ,IRPK,IRPIJ,IRPIK,IRPJK,IRPIJK,
     1                  I,J,K,SCR1,SCR2,SCR3,IUHF,
     1                  T2IJAA,T2JKAB,T2IKAB,
     1                  LNVVIJ,LNVVJK,LNVVIK,ISPIN1,ISPIN2,
     &                  LZOFF,LWOFF,SIGN)
C    1                  LNVVIJ,LNVVJK,LNVVIK,ISPIN1,ISPIN2,IMODE)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DOUBLE PRECISION SIGN
      INTEGER LZOFF,LWOFF
      INTEGER VRT,POP,DIRPRD,DISSIZ,AI,AJ,AK
      DIMENSION T3(1),W(1),CORE(1),SCR1(1),SCR2(1),SCR3(1)
      DIMENSION T2IJAA(LNVVIJ,1),T2JKAB(LNVVJK,1),T2IKAB(LNVVIK,1)
      DIMENSION IADT3(8),IADW(8)
      DIMENSION IADT2(8),LENT2(8)
      DIMENSION IADT2E(8),LENT2E(8),IADG(8),LENG(8)
      DIMENSION LISTG(4),LISTT(3)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     1                DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /SYM/    POP(8,2),VRT(8,2),NTAA,NTBB,NF1AA,NF2AA,
     1                NF1BB,NF2BB
c      COMMON /FILES/  LUOUT,MOINTS
C
c      COMMON /GAMLIS/ LISGO1,LISGO2,LISGO3,LISGO4,LISGV1,LISGV2,
c     1                LISGV3,LISGV4
c      COMMON /LISWI/  LWIC11,LWIC12,LWIC13,LWIC14,
c     1                LWIC15,LWIC16,LWIC17,LWIC18,
c     1                LWIC21,LWIC22,LWIC23,LWIC24,
c     1                LWIC25,LWIC26,LWIC27,LWIC28,
c     1                LWIC31,LWIC32,LWIC33,
c     1                LWIC34,LWIC35,LWIC36,
c     1                LWIC37,LWIC38,LWIC39,LWIC40,LWIC41,LWIC42
c      COMMON /T2ILIS/ LIST2I1,LIST2I2,LIST2I3
C
      COMMON /T3OFF/  IOFFVV(8,8,10),IOFFOO(8,8,10),IOFFVO(8,8,4)
C
      INDEX(I) = I*(I-1)/2
C
c      IF(IMODE.EQ.1)THEN
c      SIGN  = -1.0D+00
c      LISTG(1) =  LWIC15
c      LISTG(2) =  LWIC16
c      LISTG(3) =  LWIC17
c      LISTG(4) =  LWIC18
c      LISTT(1) =  14
c      LISTT(2) =  15
c      LISTT(3) =  16
c      ENDIF
c      IF(IMODE.EQ.2)THEN
c      SIGN  =  1.0D+00
c      LISTG(1) =  LISGV1
c      LISTG(2) =  LISGV2
c      LISTG(3) =  LISGV3
c      LISTG(4) =  LISGV4
c      LISTT(1) =  44
c      LISTT(2) =  45
c      LISTT(3) =  46
c      ENDIF
c      IF(IMODE.NE.1.AND.IMODE.NE.2)THEN
c      WRITE(LUOUT,1010) IMODE
c 1010 FORMAT(' @WVT323-F, Invalid value of IMODE. Given ',I10)
c      STOP
c      ENDIF
      LISTG(1) = LZOFF + 1
      LISTG(2) = LZOFF + 2
      LISTG(3) = LZOFF + 3
      LISTG(4) = LZOFF + 4
      LISTT(1) = LWOFF + 1
      LISTT(2) = LWOFF + 2
      LISTT(3) = LWOFF + 3
C
C                        DBC  DA
C     G(AI,BC) =   SUM  T    T
C                  J<K   IJK  JK
C                   D
C       BA AB            AAB  AB
C                        AAB  AB
C
C                        DBC  DA
C     G(AJ,BC) = - SUM  T    T
C                  I<K   IJK  IK
C                   D
C       BA AB            AAB  AB
C                        AAB  AB
C
C     In RHF we are trying to compute
C
C     W(bC,aI) =   SUM  T(DCb,IJk) * <Jk||Da>
C                  j,K
C                   d
C     W IS W(B,C,D) WITH IRREP LABEL D. W IS GENERATED FROM A "PARTIAL"
C     SYMTRW1 : T3(D<B,C) ---> W(B,C,D).
C
C
C     NOTE : THERE IS A PHASE DILEMMA HERE. THUS, WHILE
C
C                     <AB//CI>  = - <AB/IC>
C                      AB  BA        AB AB
C
C            IT IS <AB/IC> WHICH IS STORED. HENCE, TO BE CONSISTENT,
C            WE INTRODUCE A MINUS SIGN WHICH DOES NOT APPEAR IN THE
C            GAMMA EQUATIONS.
C
C
C
      JK =                        (K-1)*POP(IRPJ,ISPIN1) + J
C
C     SET T2 ADDRESSES.
C
      DO   210 IRPA=1,NIRREP
      IRPD = DIRPRD(IRPA,IRPJK)
      LENT2(IRPA) = VRT(IRPA,ISPIN2) * VRT(IRPD,ISPIN1)
      IF(IRPA.EQ.1)THEN
      IADT2(IRPA) = 1
      ELSE
      IADT2(IRPA) = IADT2(IRPA-1) + LENT2(IRPA-1)
      ENDIF
  210 CONTINUE
C
C     SET GAMMA ADDRESSES.
C
      LENGTH = 0
      DO  220 IRPA=1,NIRREP
      IRPBC = DIRPRD(IRPA,IRPI)
      LENG(IRPA) = VRT(IRPA,ISPIN2) * IRPDPD(IRPBC,13)
      LENGTH = LENGTH + LENG(IRPA)
      IF(IRPA.EQ.1)THEN
      IADG(IRPA) = 1
      ELSE
      IADG(IRPA) = IADG(IRPA-1) + LENG(IRPA-1)
      ENDIF
  220 CONTINUE
C
C     CONTRACT TO GET CONTRIBUTION TO GAMMA.
C
      IF(LENGTH.GT.0) CALL ZERO(CORE,LENGTH)
c     Z = 0.0D+00
      DO  230 IRPD=1,NIRREP
      IRPA  = DIRPRD(IRPD,IRPJK)
      IF(VRT(IRPD,ISPIN1).EQ.0.OR.VRT(IRPA,ISPIN2).EQ.0) GOTO 230
      IF(LENG(IRPA).EQ.0) GOTO 230
      IRPBC = DIRPRD(IRPA,IRPI)
      CALL XGEMM('N','N',IRPDPD(IRPBC,13),VRT(IRPA,ISPIN2),
     1                                    VRT(IRPD,ISPIN1),
     1           -SIGN,
     1            W(IADW(IRPD)),IRPDPD(IRPBC,13),
     1            T2JKAB(IADT2(IRPA),JK),VRT(IRPD,ISPIN1),
     1            0.0D+00,CORE(IADG(IRPA)),IRPDPD(IRPBC,13))
c     CALL CHKSUM2(CORE(IADG(IRPA)),
c    1             IRPDPD(IRPBC,13)*VRT(IRPA,ISPIN2),Y)
c     CALL CHKSUM2(W(IADW(IRPD)),IRPDPD(IRPBC,13)*VRT(IRPD,ISPIN1),Z)
  230 CONTINUE
c     write(6,1020) i,y
c     write(6,1000) i,z
c     CALL SUMBLK(CORE,LENGTH)
c1000 FORMAT(' @WVT323-I, For ',I3,' Z is ',F15.9)
c1020 FORMAT(' @WVT323-I, For ',I3,' Y is ',F15.9)
c     write(6,*) iadg,leng,length
C
C     WRITE GAMMA TO LIST.
C
      IF(IUHF.EQ.0)THEN
      DO  240 IRPA=1,NIRREP
      IRPAI = DIRPRD(IRPA,IRPI)
      IF(VRT(IRPA,ISPIN2).EQ.0) GOTO 240
      IF(LENG(IRPA).EQ.0) GOTO 240
C
      CALL SYMTR3(IRPAI,VRT(1,2),VRT(1,1),IRPDPD(IRPAI,13),
     1            VRT(IRPA,ISPIN2),CORE(IADG(IRPA)),
     1              SCR1,SCR2,SCR3)
C
      AI = IOFFVO(IRPI,IRPAI,4) + (I-1)*VRT(IRPA,ISPIN2) + 1
      CALL GETLST(CORE(IADG(NIRREP) + LENG(NIRREP)),
     1            AI,VRT(IRPA,ISPIN2),2,IRPAI,LISTG(4))
C
      CALL VADD(CORE(IADG(NIRREP) + LENG(NIRREP)),
     1          CORE(IADG(NIRREP) + LENG(NIRREP)),
     1          CORE(IADG(IRPA)),LENG(IRPA), 1.0D+00)
C
      CALL PUTLST(CORE(IADG(NIRREP) + LENG(NIRREP)),
     1            AI,VRT(IRPA,ISPIN2),2,IRPAI,LISTG(4))
  240 CONTINUE
      ELSE
      DO  250 IRPA=1,NIRREP
      IRPAI = DIRPRD(IRPA,IRPI)
      IF(VRT(IRPA,ISPIN2).EQ.0) GOTO 250
      IF(LENG(IRPA).EQ.0) GOTO 250
      AI = IOFFVO(IRPI,IRPAI,2+ISPIN1) + (I-1)*VRT(IRPA,ISPIN2) + 1
      CALL GETLST(CORE(IADG(NIRREP) + LENG(NIRREP)),
     1            AI,VRT(IRPA,ISPIN2),2,IRPAI,LISTG(2+ISPIN1))
C
      CALL VADD(CORE(IADG(NIRREP) + LENG(NIRREP)),
     1          CORE(IADG(NIRREP) + LENG(NIRREP)),
     1          CORE(IADG(IRPA)),LENG(IRPA), 1.0D+00)
C
      CALL PUTLST(CORE(IADG(NIRREP) + LENG(NIRREP)),
     1            AI,VRT(IRPA,ISPIN2),2,IRPAI,LISTG(2+ISPIN1))
  250 CONTINUE
      ENDIF
C
      IK =                        (K-1)*POP(IRPI,ISPIN1) + I
C
C     SET T2 ADDRESSES.
C
      DO   310 IRPA=1,NIRREP
      IRPD = DIRPRD(IRPA,IRPIK)
      LENT2(IRPA) = VRT(IRPA,ISPIN2) * VRT(IRPD,ISPIN1)
      IF(IRPA.EQ.1)THEN
      IADT2(IRPA) = 1
      ELSE
      IADT2(IRPA) = IADT2(IRPA-1) + LENT2(IRPA-1)
      ENDIF
  310 CONTINUE
C
C     SET GAMMA ADDRESSES.
C
      LENGTH = 0
      DO  320 IRPA=1,NIRREP
      IRPBC = DIRPRD(IRPA,IRPJ)
      LENG(IRPA) = VRT(IRPA,ISPIN2) * IRPDPD(IRPBC,13)
      LENGTH = LENGTH + LENG(IRPA)
      IF(IRPA.EQ.1)THEN
      IADG(IRPA) = 1
      ELSE
      IADG(IRPA) = IADG(IRPA-1) + LENG(IRPA-1)
      ENDIF
  320 CONTINUE
C
C     CONTRACT TO GET CONTRIBUTION TO GAMMA.
C
      IF(LENGTH.GT.0) CALL ZERO(CORE,LENGTH)
      DO  330 IRPD=1,NIRREP
      IRPA  = DIRPRD(IRPD,IRPIK)
      IF(VRT(IRPD,ISPIN1).EQ.0.OR.VRT(IRPA,ISPIN2).EQ.0) GOTO 330
      IF(LENG(IRPA).EQ.0) GOTO 330
      IRPBC = DIRPRD(IRPA,IRPJ)
      CALL XGEMM('N','N',IRPDPD(IRPBC,13),VRT(IRPA,ISPIN2),
     1                                    VRT(IRPD,ISPIN1),
     1            SIGN,
     1            W(IADW(IRPD)),IRPDPD(IRPBC,13),
     1            T2IKAB(IADT2(IRPA),IK),VRT(IRPD,ISPIN1),
     1            0.0D+00,CORE(IADG(IRPA)),IRPDPD(IRPBC,13))
c     CALL CHKSUM2(CORE(IADG(IRPA)),LENG(IRPA),Z)
c     CALL CHKSUM2(CORE(IADG(IRPA)),
c    1             IRPDPD(IRPBC,13)*VRT(IRPA,ISPIN2),Y)
c     CALL CHKSUM2(W(IADW(IRPD)),IRPDPD(IRPBC,13)*VRT(IRPD,ISPIN1),Z)
  330 CONTINUE
c     write(6,1020) j,y
c     write(6,1000) j,z
c     CALL SUMBLK(CORE,LENGTH)
c     write(6,*) iadg,leng,length
C
C     WRITE GAMMA TO LIST.
C
      IF(IUHF.EQ.0)THEN
      DO  340 IRPA=1,NIRREP
      IRPAJ = DIRPRD(IRPA,IRPJ)
      IF(VRT(IRPA,ISPIN2).EQ.0) GOTO 340
      IF(LENG(IRPA).EQ.0) GOTO 340
C
      CALL SYMTR3(IRPAJ,VRT(1,2),VRT(1,1),IRPDPD(IRPAJ,13),
     1            VRT(IRPA,ISPIN2),CORE(IADG(IRPA)),
     1              SCR1,SCR2,SCR3)
C
      AJ = IOFFVO(IRPJ,IRPAJ,4) + (J-1)*VRT(IRPA,ISPIN2) + 1
      CALL GETLST(CORE(IADG(NIRREP) + LENG(NIRREP)),
     1            AJ,VRT(IRPA,ISPIN2),2,IRPAJ,LISTG(4))
C
      CALL VADD(CORE(IADG(NIRREP) + LENG(NIRREP)),
     1          CORE(IADG(NIRREP) + LENG(NIRREP)),
     1          CORE(IADG(IRPA)),LENG(IRPA), 1.0D+00)
C
      CALL PUTLST(CORE(IADG(NIRREP) + LENG(NIRREP)),
     1            AJ,VRT(IRPA,ISPIN2),2,IRPAJ,LISTG(4))
  340 CONTINUE
      ELSE
      DO  350 IRPA=1,NIRREP
      IRPAJ = DIRPRD(IRPA,IRPJ)
      IF(VRT(IRPA,ISPIN2).EQ.0) GOTO 350
      IF(LENG(IRPA).EQ.0) GOTO 350
      AJ = IOFFVO(IRPJ,IRPAJ,2+ISPIN1) + (J-1)*VRT(IRPA,ISPIN2) + 1
      CALL GETLST(CORE(IADG(NIRREP) + LENG(NIRREP)),
     1            AJ,VRT(IRPA,ISPIN2),2,IRPAJ,LISTG(2+ISPIN1))
C
      CALL VADD(CORE(IADG(NIRREP) + LENG(NIRREP)),
     1          CORE(IADG(NIRREP) + LENG(NIRREP)),
     1          CORE(IADG(IRPA)),LENG(IRPA), 1.0D+00)
C
      CALL PUTLST(CORE(IADG(NIRREP) + LENG(NIRREP)),
     1            AJ,VRT(IRPA,ISPIN2),2,IRPAJ,LISTG(2+ISPIN1))
  350 CONTINUE
      ENDIF
C
C                        DBC  DA
C     G(AK,BC) =   SUM  T    T
C                  I<J   IJK  IJ
C                   D
C       AB AB            AAB  AA
C                        AAB  AA
C
C     SET ADDRESSES FOR EXPANDED T2(D,A).
C
      DO  410 IRPA=1,NIRREP
      IRPD = DIRPRD(IRPA,IRPIJ)
      LENT2E(IRPA) = VRT(IRPA,ISPIN1)*VRT(IRPD,ISPIN1)
      IF(IRPA.EQ.1)THEN
      IADT2E(IRPA) = 1
      ELSE
      IADT2E(IRPA) = IADT2E(IRPA-1) + LENT2E(IRPA-1)
      ENDIF
  410 CONTINUE
C
      IF(IRPIJ.EQ.1)THEN
      IJ =                             INDEX(J-1) + I
      ELSE
      IJ =                             (J-1)*POP(IRPI,ISPIN1) + I
      ENDIF
C
C     SET GAMMA ADDRESSES.
C
      DO  420 IRPA=1,NIRREP
      IRPBC = DIRPRD(IRPA,IRPK)
      LENG(IRPA) = VRT(IRPA,ISPIN1) * IRPDPD(IRPBC,13)
      IF(IRPA.EQ.1)THEN
      IADG(IRPA) = 1
      ELSE
      IADG(IRPA) = IADG(IRPA-1) + LENG(IRPA-1)
      ENDIF
  420 CONTINUE
C
C     CONTRACT TO GET CONTRIBUTION TO GAMMA.
C
      DO  430 IRPD=1,NIRREP
      IRPA  = DIRPRD(IRPD,IRPIJ)
      IF(VRT(IRPD,ISPIN1).EQ.0.OR.VRT(IRPA,ISPIN1).EQ.0) GOTO 430
      IF(LENG(IRPA).EQ.0) GOTO 430
      IRPBC = DIRPRD(IRPA,IRPK)
      IRPAK = DIRPRD(IRPA,IRPK)
      AK = IOFFVO(IRPK,IRPAK,5-ISPIN1) + (K-1)*VRT(IRPA,ISPIN1) + 1
      CALL GETLST(CORE(IADG(IRPA)),
     1            AK,VRT(IRPA,ISPIN1),2,IRPAK,LISTG(5-ISPIN1))
      CALL XGEMM('N','N',IRPDPD(IRPBC,13),VRT(IRPA,ISPIN1),
     1           VRT(IRPD,ISPIN1),
     1            SIGN,W(IADW(IRPD)),IRPDPD(IRPBC,13),
     1           T2IJAA(IADT2E(IRPA),IJ),VRT(IRPD,ISPIN1),
     1            1.0D+00,
     1            CORE(IADG(IRPA)),IRPDPD(IRPBC,13))
      CALL PUTLST(CORE(IADG(IRPA)),
     1            AK,VRT(IRPA,ISPIN1),2,IRPAK,LISTG(5-ISPIN1))
  430 CONTINUE
C
      IF(IUHF.EQ.0) RETURN
C
C                        BCD  AD
C     G(AI,BC) = - SUM  T    T
C                  J<K   IJK  JK
C                   D
C       AA AA            AAB  AB
C
C                        BCD  AD
C     G(AJ,BC) =   SUM  T    T
C                  I<K   IJK  IK
C                   D
C       AA AA            AAB  AB
C
C
      JK =                        (K-1)*POP(IRPJ,ISPIN1) + J
C
      DO   10 IRPD=1,NIRREP
      IRPA = DIRPRD(IRPD,IRPJK)
      LENT2(IRPD) = VRT(IRPA,ISPIN1) * VRT(IRPD,ISPIN2)
      IF(IRPD.EQ.1)THEN
      IADT2(IRPD) = 1
      ELSE
      IADT2(IRPD) = IADT2(IRPD-1) + LENT2(IRPD-1)
      ENDIF
   10 CONTINUE
C
C     SET ADDRESSES FOR GAMMA.
C
      DO   20 IRPA=1,NIRREP
      IRPBC = DIRPRD(IRPA,IRPI)
      LENG(IRPA) = VRT(IRPA,ISPIN1) * IRPDPD(IRPBC,ISPIN1)
      IF(IRPA.EQ.1)THEN
      IADG(IRPA) = 1
      ELSE
      IADG(IRPA) = IADG(IRPA-1) + LENG(IRPA-1)
      ENDIF
   20 CONTINUE
C
      DO   30 IRPD=1,NIRREP
      IRPA  = DIRPRD(IRPD,IRPJK)
      IRPBC = DIRPRD(IRPA,IRPI)
      IF(VRT(IRPA,ISPIN1).EQ.0.OR.VRT(IRPD,ISPIN2).EQ.0) GOTO 30
      IRPAI = DIRPRD(IRPA,IRPI)
      AI = IOFFVO(IRPI,IRPAI,ISPIN1) + (I-1)*VRT(IRPA,ISPIN1) + 1
      CALL GETLST(CORE(IADG(IRPA)),
     1            AI,VRT(IRPA,ISPIN1),2,IRPAI,LISTG(ISPIN1))
      CALL XGEMM('N','T',IRPDPD(IRPBC,ISPIN1),VRT(IRPA,ISPIN1),
     1           VRT(IRPD,ISPIN2),
     1           -SIGN,T3(IADT3(IRPD)),IRPDPD(IRPBC,ISPIN1),
     1            T2JKAB(IADT2(IRPD),JK),VRT(IRPA,ISPIN1),
     1            1.0D+00,CORE(IADG(IRPA)),IRPDPD(IRPBC,ISPIN1))
      CALL PUTLST(CORE(IADG(IRPA)),
     1            AI,VRT(IRPA,ISPIN1),2,IRPAI,LISTG(ISPIN1))
   30 CONTINUE
C
      IK =                        (K-1)*POP(IRPI,ISPIN1) + I
C
      DO  110 IRPD=1,NIRREP
      IRPA = DIRPRD(IRPD,IRPIK)
      LENT2(IRPD) = VRT(IRPA,ISPIN1) * VRT(IRPD,ISPIN2)
      IF(IRPD.EQ.1)THEN
      IADT2(IRPD) = 1
      ELSE
      IADT2(IRPD) = IADT2(IRPD-1) + LENT2(IRPD-1)
      ENDIF
  110 CONTINUE
C
C     SET ADDRESSES FOR GAMMA.
C
      DO  120 IRPA=1,NIRREP
      IRPBC = DIRPRD(IRPA,IRPJ)
      LENG(IRPA) = VRT(IRPA,ISPIN1) * IRPDPD(IRPBC,ISPIN1)
      IF(IRPA.EQ.1)THEN
      IADG(IRPA) = 1
      ELSE
      IADG(IRPA) = IADG(IRPA-1) + LENG(IRPA-1)
      ENDIF
  120 CONTINUE
C
      DO  130 IRPD=1,NIRREP
      IRPA  = DIRPRD(IRPD,IRPIK)
      IRPBC = DIRPRD(IRPA,IRPJ)
      IF(VRT(IRPA,ISPIN1).EQ.0.OR.VRT(IRPD,ISPIN2).EQ.0) GOTO 130
      IRPAJ = DIRPRD(IRPA,IRPJ)
      AJ = IOFFVO(IRPJ,IRPAJ,ISPIN1) + (J-1)*VRT(IRPA,ISPIN1) + 1
      CALL GETLST(CORE(IADG(IRPA)),
     1            AJ,VRT(IRPA,ISPIN1),2,IRPAJ,LISTG(ISPIN1))
      CALL XGEMM('N','T',IRPDPD(IRPBC,ISPIN1),VRT(IRPA,ISPIN1),
     1           VRT(IRPD,ISPIN2),
     1            SIGN,T3(IADT3(IRPD)),IRPDPD(IRPBC,ISPIN1),
     1            T2IKAB(IADT2(IRPD),IK),VRT(IRPA,ISPIN1),
     1            1.0D+00,CORE(IADG(IRPA)),IRPDPD(IRPBC,ISPIN1))
      CALL PUTLST(CORE(IADG(IRPA)),
     1            AJ,VRT(IRPA,ISPIN1),2,IRPAJ,LISTG(ISPIN1))
  130 CONTINUE
      RETURN
      END
