 
      SUBROUTINE INITIN(ICORE,MAXCOR,IUHF)
C
C INITIALIZES THE T2 INCREMENT LIST WITH THE <AB//IJ> INTEGRALS.
C
C IN CASE OF TRIPLE EXCITATIONS (CCSD+T, CCSD(T), AND QCISD)
C ADD TWICE THE CONTRIBUTION DUE TO TRIPLES.
C
CEND
C
C EXTENDED FOR TRIPLES JUNE/91 JG
C
      IMPLICIT INTEGER (A-Z)
      LOGICAL TRIPNI1,TRIPNI2,TRIPIT,T3STOR
      DOUBLE PRECISION TWO
C
      DIMENSION ICORE(MAXCOR)
C
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/TRIPLES/TRIPNI1,TRIPNI2,TRIPIT,T3STOR
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &              DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),IJUNK(18)
C
      DATA TWO /2.D0/
      IBOT=1
      IF(IUHF.EQ.0)IBOT=3
C
      DO 15 ISPIN=IBOT,3
C
       IF(TRIPNI1) THEN
C
        DO 5 IRREP=1,NIRREP
         NUMSYM=IRPDPD(IRREP,ISYTYP(2,13+ISPIN))
         DSZSYM=IRPDPD(IRREP,ISYTYP(1,13+ISPIN))
         NSIZE=NUMSYM*DSZSYM
         I001=1
         I002=I001+IINTFP*NSIZE
         CALL GETLST(ICORE(I001),1,NUMSYM,1,IRREP,13+ISPIN)
C
C The list number 101, 102 switched to 1 and 2 in init2.F in vcc 
C 04/2008, Ajith Perera
C
         CALL GETLST(ICORE(I002),1,NUMSYM,1,IRREP,ISPIN)
         CALL SAXPY(NSIZE,TWO,ICORE(I002),1,ICORE(I001),1)
         CALL PUTLST(ICORE(I001),1,NUMSYM,1,IRREP,60+ISPIN)
5       CONTINUE
C 
       ELSE
C
        NSIZE=0
        DO 10 IRREP=1,NIRREP
         NUMSYM=IRPDPD(IRREP,ISYTYP(2,13+ISPIN))
         DSZSYM=IRPDPD(IRREP,ISYTYP(1,13+ISPIN))
         NSIZE=NSIZE+NUMSYM*DSZSYM
10      CONTINUE
C
        CALL GETALL(ICORE,NSIZE,1,13+ISPIN)
        CALL PUTALL(ICORE,NSIZE,1,60+ISPIN)
       ENDIF
C
15    CONTINUE
C
      RETURN
      END
