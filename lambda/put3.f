      SUBROUTINE PUT3(X,LISTX,IRREPX,GETIND,IRREPIND,POPR,POPS,
     &                TYPEPQ,TYPEPQF,TYPERS,GETTYP,ANTI)
C
C THIS ROUTINE ACCEPTS A LIST X(pqr;s) [ALL pqr FOR A SPECIFIC
C VALUE OF s] AND WRITES THE ELEMENTS TO THEIR APPROPRIATE LOCATIONS
C ON DISK.  INPUT IS THE SPECIFIC VALUE OF s AS WELL AS ITS SYMMETRY. 
C
C THE ORDERING OF SYMMETRIES IN THE INPUT VECTOR IS AS FOLLOWS
C
C       X(p q r)
C
C         GX 1 1
C         GX 2 1
C         . . .
C         GX 1 2
C         GX 2 2
C         . . .
C
C GX IS DEFINED BY THE RESTRICTION 
C     GAMMA(P)*GAMMA(Q)*GAMMA(R)*GAMMA(S)=GAMMA(X)
C
CEND
      IMPLICIT INTEGER (A-Z)
      LOGICAL ANTI
      CHARACTER*3 GETTYP
      DIMENSION X(*),POPR(*),POPS(*)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFEA(2),NFMI(2)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYMLOC/ISYMOFF(8,8,25)
      COMMON/SYMINF/NSTART,NIRREP,IRREPY(255,2),DIRPRD(8,8)
C
      IOFFB=1
C
      IF(GETTYP.EQ.'123')THEN
C
C WRITE OUT X(pqr) FOR s=GETIND, IRREP(s)=IRREPIND
C
       IRREPS=IRREPIND
       S=GETIND
       DO 10 IRREPR=1,NIRREP
        IRREPRS=DIRPRD(IRREPR,IRREPS)
        IRREPPQ=DIRPRD(IRREPX,IRREPRS)
        DISSYX=IRPDPD(IRREPPQ,ISYTYP(1,LISTX))
        IF(ANTI)THEN
         DISSYXF=IRPDPD(IRREPPQ,TYPEPQF)
        ELSE
         DISSYXF=DISSYX
        ENDIF
        NUMDIS=IRPDPD(IRREPRS,ISYTYP(2,LISTX))
        NUMR=POPR(IRREPR)
C
C CALCULATE DISTRIBUTION OFFSET TO THE FIRST VALUE OF r FOR THIS s
C
        IDIS0=ISYMOFF(IRREPS,IRREPRS,TYPERS)+(S-1)*NUMR
        IF(ANTI)THEN
         IOFF00=IOFFB
         IDISTAR=IDIS0
         DO 110 R=1,NUMR
          CALL ASSYM2(IRREPPQ,POPP,1,X(IOFF00))
          CALL PUTLST(X(IOFFB),IDISTAR,1,1,IRREPRS,LISTX)
          IOFFB=IOFFB+IINTFP*DISSYXF
          IDISTAR=IDISTAR+1
110      CONTINUE
        ELSE
         CALL PUTLST(X(IOFFB),IDIS0,NUMR,1,IRREPRS,LISTX)
         IOFFB=IOFFB+IINTFP*DISSYXF*NUMR
        ENDIF
10     CONTINUE
C
      ELSEIF(GETTYP.EQ.'124')THEN
C
C WRITE OUT X(pqs) FOR r=GETIND, IRREP(r)=IRREPIND
C
       IRREPR=IRREPIND
       R=GETIND
       DO 20 IRREPS=1,NIRREP
        IRREPRS=DIRPRD(IRREPR,IRREPS)
        IRREPPQ=DIRPRD(IRREPX,IRREPRS)
        DISSYX=IRPDPD(IRREPPQ,ISYTYP(1,LISTX))
        IF(ANTI)THEN
         DISSYXF=IRPDPD(IRREPPQ,TYPEPQF)
        ELSE
         DISSYXF=DISSYX
        ENDIF
        NUMDIS=IRPDPD(IRREPRS,ISYTYP(2,LISTX))
        NUMR=POPR(IRREPR)
        NUMS=POPS(IRREPS)
        DO 11 S=1,NUMS
C
C CALCULATE DISTRIBUTION OFFSET TO THIS rs DISTRIBUTION
C
         IDIS0=ISYMOFF(IRREPS,IRREPRS,TYPERS)+(S-1)*NUMR+R-1
         IF(ANTI)THEN
          CALL ASSYM2(IRREPPQ,POPP,1,X(IOFFB))
         ENDIF
         CALL PUTLST(X(IOFFB),IDIS0,1,1,IRREPRS,LISTX)
         IOFFB=IOFFB+IINTFP*DISSYXF
11      CONTINUE
20     CONTINUE
C
      ENDIF
C
      RETURN
      END
