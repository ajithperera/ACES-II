      SUBROUTINE ROM3L(ICORE,MAXCOR,IUHF)
C
C CALCULATES THE SECOND-ORDER LAMBDA AMPLITUDES IN THIRD- AND
C FOURTH-ORDER ROHF-MBPT
C
CEND
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD,POP,VRT,DISSYL
      LOGICAL BREDUNDANT
C
      DIMENSION ICORE(MAXCOR)
C
      COMMON /FLAGS2/IFLAGS2(500)
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/INFO/NOCCO(2),NVRTO(2)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
C
      DATA ONE/1.0D0/
C
C FORM L1[2] AMPLITUDES
C
      DO 100 ISPIN=1,2
       I000=1
       I010=I000+IINTFP*NT(ISPIN)
       MXCOR=MAXCOR-IINTFP*NT(ISPIN)
       CALL IZERO(ICORE(I000),IINTFP*NT(ISPIN))

       BREDUNDANT=IFLAGS2(155).EQ.0 

C
C T2[2]*F IN L1[2] CONTRIBUTION
C
       CALL T2FINL1(ICORE(I000),ICORE(I010),MXCOR,IUHF,ISPIN,BREDUNDANT)
C
C W*T1[1] IN L1[2] CONTRIBUTION
C
      CALL W2T1INL1(ICORE(I000),ICORE(I010),MXCOR,IUHF,ISPIN,BREDUNDANT)

C 
C DENOMINATOR WEIGHT L1[2]
C
       CALL NEWL1(ICORE(I000),ICORE(I010),MXCOR,ISPIN)
C
C ADD T1[2] TO L1[2] 
C
       CALL GETLST(ICORE(I010),1,1,1,ISPIN,190)
       CALL SAXPY(NT(ISPIN),ONE,ICORE(I010),1,ICORE(I000),1)
       CALL PUTLST(ICORE(I000),1,1,1,ISPIN,190)
C
100   CONTINUE
C
C FORM L2[2] AMPLITUDES
C
      DO 200 ISPIN=1,3
C
C MEMORY FOR T1[1] AND F(E,M) 
C
       IF(ISPIN.LT.3) THEN
        ISPIN1=ISPIN
        ISPIN2=ISPIN
       ELSE
        ISPIN1=1
        ISPIN2=2
       ENDIF
       LSTDEN=47+ISPIN
       LSTLAM=143+ISPIN
C
       IT1=1
       IT2=IT1+IINTFP*NT(ISPIN1)
       IF1=IT2+IINTFP*NT(ISPIN2)
       IF2=IF1+IINTFP*NT(ISPIN1)
       I000=IF2+IINTFP*NT(ISPIN2)
       CALL GETLST(ICORE(IT1),1,1,1,ISPIN1,90)
       CALL GETLST(ICORE(IT2),1,1,1,ISPIN2,90)
       CALL GETLST(ICORE(IF1),1,1,1,2+ISPIN1,93)
       CALL GETLST(ICORE(IF2),1,1,1,2+ISPIN2,93)
C
       DO 300 IRREP=1,NIRREP
C
        NUMSYL=IRPDPD(IRREP,ISYTYP(2,43+ISPIN)) 
        DISSYL=IRPDPD(IRREP,ISYTYP(1,43+ISPIN))
        NSIZE=NUMSYL*DISSYL
        I010=I000+IINTFP*NUMSYL*DISSYL
        IEND=I010+IINTFP*NUMSYL*DISSYL
        IF(IEND.GT.MAXCOR) CALL INSMEM('ROM3L2',IEND,MAXCOR)
        CALL IZERO(ICORE(I000),IINTFP*NUMSYL*DISSYL)
C
C THE ONLY ADDITIONAL TERM IS A DISCONNECTED DIRECT PRODUCT OF F AND T1[1]
C
        CALL F1T1INL2(ICORE(I000),ICORE(IT1),ICORE(IT2),ICORE(IF1),
     &                ICORE(IF2),DISSYL,NUMSYL,POP(1,ISPIN1),      
     &                POP(1,ISPIN2),VRT(1,ISPIN1),VRT(1,ISPIN2),
     &                IRREP,ISPIN)
C
C DENOMINATOR WEIGHT L2[2]
C
        CALL GETLST(ICORE(I010),1,NUMSYL,1,IRREP,LSTDEN)
        CALL VECPRD(ICORE(I000),ICORE(I010),ICORE(I000),NSIZE)
C
C ADD T2[2] TO L2[2]
C
        CALL GETLST(ICORE(I010),1,NUMSYL,1,IRREP,LSTLAM)
        CALL SAXPY(NSIZE,ONE,ICORE(I010),1,ICORE(I000),1)
        CALL PUTLST(ICORE(I000),1,NUMSYl,1,IRREP,LSTLAM)
C       
300    CONTINUE
C 
200   CONTINUE

      RETURN
      END
