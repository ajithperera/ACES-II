      SUBROUTINE OSL1IA(ICORE,MAXCOR,IUHF,W)
C
C  THIS ROUTINE COMPUTES:
C
C     L1INC(A,M)= W [ SUM(X) TERMXA ]  (ISPIN=1)
C
C     L1INC(a,n)= W [ SUM(X) TERMXB ]  (ISPIN=2)
C
C  WITH
C
C     TERM1A  = - L2(Na,Mn)                    TERM1B = - L2(Am,Mn)
C     TERM2A  = + SUM(I) T1(N,I) * L2(Na,Mi) + SUM(i) T1(m,i) * L2(Na,In)
C     TERM2B  = + SUM(i) T1(m,i) * L2(Am,In) + SUM(A) T1(N,I) * L2(Am,Mi)
C     TERM3A  = - SUM(b) T1(b,n) * L2(Ba,Mn)
C     TERM3B  = - SUM(B) T1(B,M) * L2(Ab,Mn)
C     TERM4A  = + SUM(I) T2(Nm,In) * L1(a,i)
C     TERM4B  = + SUM(i) T2(Nm,Mi) * L1(A,I)
C     TERM5A  = - SUM(Ij) TAU(Nm,Ij) * L2(Na,Ji)
C     TERM5B  = - SUM(iJ) TAU(Nm,Ji) * L2(Am,Ij)
C     TERM6A  = + SUM(IB)  T2(Bm,In) * L2(ab,in)
C     TERM6B  = + SUM(ib)  T2(Nb,Mi) * L2(AB,IM)
C     TERM7A  = + SUM(ib) TAU(bm,ni) * L2(Ba,In)
C               + SUM(Ib) TAU(Nb,In) * L2(Ba,Mi)
C     TERM7B  = + SUM(IB) TAU(BN,MI) * L2(Ab,Mi)
C               + SUM(iB) TAU(Bm,Mi) * L2(Ab,In)
C     TERM8A  = - SUM(bIj) T1(b,n) * TAU(Nm,Ji) * L2(Ba,Ij)
C     TERM8B  = - SUM(BiJ) T1(B,M) * TAU(Nm,Ij) * L2(Ab,Ji)
C     TERM9A  = - SUM(bIj) T2(Nm,In) * (T1(b,j)-T1(B,J)) * L2(ab,ij)
C               - SUM(BIJ) T2(Nm,In) * (T1(B,J)-T1(b,j)) * L2(Ba,Ji)
C     TERM9B  = - SUM(BiJ) T2(Nm,Mi) * (T1(B,J)-T1(b,j)) * L2(AB,IJ)
C               - SUM(bij) T2(Nm,Mi) * (T1(b,j)-T1(B,J)) * L2(Ab,Ij)
C     TERM10A = + SUM(IJB) T1(N,I) * T2(Bm,Jn) * L2(ab,ij)
C               + SUM(Ijb) T1(N,I) * T2(bm,jn) * L2(Ba,Ji)
C               - SUM(iJb) T1(m,i) * T2(Nb,Jn) * L2(Ba,Ij)
C     TERM10B = + SUM(ijb) T1(m,i) * T2(Nb,Mj) * L2(AB,IJ)
C               + SUM(iJB) T1(m,i) * T2(BN,JM) * L2(Ab,Ij)
C               - SUM(IjB) T1(N,I) * T2(Bm,Mj) * L2(Ab,Ji)
C
CEND
C
C CODED PS SEP/93
C
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,DISSYL,DISSYT,POP,VRT,DISSYT2,DISSYL2
      CHARACTER*4 STRING
C
      DIMENSION ICORE(MAXCOR)
      DIMENSION I0(2),NSTAR(2)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &                DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
C
      INTEGER POPA,POPI,VRTA,VRTI
      COMMON /FSSYM/ POPA(8,2),POPI(8,2),VRTA(8,2),VRTI(8,2),
     &               NTAN(2),NTNA(2),NTAA(2),
     &               NTIN(2),NTNI(2),NTII(2),
     &               NTAI(2),NTIA(2),
     &               NF1AN(2),NF1AA(2),NF1IN(2),NF1II(2),NF1AI(2),
     &               NF2AN(2),NF2AA(2),NF2IN(2),NF2II(2),NF2AI(2)
C
      INTEGER FSDPDAN,FSDPDNA,FSDPDAA,FSDPDIN,FSDPDNI,FSDPDII,FSDPDAI,
     $   FSDPDIA
      COMMON /FSSYMPOP/ FSDPDAN(8,22),FSDPDNA(8,22),FSDPDAA(8,22),
     &                  FSDPDIN(8,22),FSDPDNI(8,22),FSDPDII(8,22),
     &                  FSDPDAI(8,22),FSDPDIA(8,22)
C
      COMMON /ACTIRR/ IRREPO(2),IRREPV(2)
C
      COMMON /FLAGS/ IFLAGS(100)
C
      DATA ONE,ONEM,ZILCH,HALF/1.D0,-1.D0,0.D0,0.5D0/
C
      FACT=ONE
      NSTAR(1)=NTIA(1)
      NSTAR(2)=NTIA(2)
      IF((NSTAR(1).EQ.0).AND.(NSTAR(2).EQ.0)) RETURN
C
C     READ IN T1 AMPLITUDES REQUIRED.
C     DIRTY TRICKS ARE PLAYED BECAUSE (A,I) IN THE FIRST TERM IS 
C     UNRESTRICTED BUT IN THE SECOND ONE ONLY INACTIVE IS ALLOWED
C
      LISTT1=90
      NSIZTA=FSDPDII(1,9)
      NSIZTB=FSDPDII(1,10)
      IF(NSIZTA.NE.NSIZTB) THEN
         WRITE(6,*)'@OSL1IA-E FATAL ERROR: NSIZTA.NE.NSIZTB',
     $      NSIZTA,NSIZTB
         CALL ERREX
      ENDIF
      NSIZTNA=NT(1)
      NSIZTNB=NT(2)
C
C     I0(1):  T1A-T1B  I0(2): T1B-T1A  I010:  T1A  I020:  T1B
C
      I0(1)=1
      I0(2)=I0(1)+MAX(NSIZTA,NSIZTNA)*IINTFP
      I010=I0(2)+MAX(NSIZTB,NSIZTNB)*IINTFP
      I020=I010+NSIZTA*IINTFP
      I030=I020+NSIZTB*IINTFP
      CALL FSGETT1(ICORE(I010),1,LISTT1,'II',9)
      CALL FSGETT1(ICORE(I020),2,LISTT1,'II',10)
      CALL SCOPY(NSIZTA,ICORE(I010),1,ICORE(I0(1)),1)
      CALL SCOPY(NSIZTA,ICORE(I020),1,ICORE(I0(2)),1)
      CALL SAXPY(NSIZTA,ONEM,ICORE(I020),1,ICORE(I0(1)),1)
      CALL SAXPY(NSIZTA,ONEM,ICORE(I010),1,ICORE(I0(2)),1)
      CALL FSPUTT1(ICORE(I0(1)),1,LISTT1,'II',9)
      CALL FSPUTT1(ICORE(I0(2)),2,LISTT1,'II',10)
      CALL GETLST(ICORE(I0(1)),1,1,1,1,LISTT1)
      CALL GETLST(ICORE(I0(2)),1,1,1,2,LISTT1)
      CALL FSPUTT1(ICORE(I010),1,LISTT1,'II',9)
      CALL FSPUTT1(ICORE(I020),2,LISTT1,'II',10)
C
      I000=I010
      DO 1000 ISPIN=1,IUHF+1
C
         NSIZTAR=NSTAR(ISPIN)
         IF(NSIZTAR.EQ.0) GOTO 1000
C
C     I0(1):  T1A-T1B  I0(2): T1B-T1A  I000: L1(CONT)
C
         I010=I000+NSIZTAR*IINTFP
C
C     FIRST TERM
C
         LISTL=146
         IRREP=DIRPRD(IRREPO(1),IRREPO(2))
         NUMSYL=FSDPDAA(IRREP,ISYTYP(2,LISTL))
         IF(ISPIN.EQ.1) THEN
            DISSYL=FSDPDAI(IRREP,ISYTYP(1,LISTL))
            STRING='AIAA'
         ELSE
            DISSYL=FSDPDIA(IRREP,ISYTYP(1,LISTL))
            STRING='IAAA'
         ENDIF
         IF(NSIZTAR.NE.DISSYL) THEN
            WRITE(6,*)'@OSL1IA-E FATAL ERROR: NSIZTAR.NE.DISSYL',
     $           NSIZTAR,DISSYL
            CALL ERREX
         ENDIF
         CALL FSGET(ICORE(I000),1,NUMSYL,1,IRREP,LISTL,STRING)
         CALL SSCAL(NSIZTAR,ONEM,ICORE(I000),1)
C
C     SECOND AND TENTH TERM
C     (WITH L2'(Na,Mi)=L2(Na,Mi)+SUM(JB) T2(Bm,Jn)*L2(ab,ij)
C                               +SUM(jb) T2(bm,jn)*L2(Ba,Ji)
C           L2'(Na,In)=L2(Na,In)-SUM(Jb) T2(Nb,Jn)*L2(Ba,Ij)      ISPIN=1
C           L2'(Am,In)=L2(Am,In)+SUM(jb) T2(Nb,Mj)*L2(AB,IJ)
C                               +SUM(jB) T2(BN,JM)*L2(Ab,Ij)
C           L2'(Am,Mi)=L2(Am,Mi)-SUM(jB) T2(Bm,Mj)*L2(Ba,Ji)      ISPIN=2
C
C     AA AND BB SPINCASE
C
         NSIZT=NTAI(ISPIN)
         LISTL=146
         IRREP=DIRPRD(IRREPO(ISPIN),IRREPV(ISPIN))
C
         IF(ISPIN.EQ.1) THEN
            DISSYL=FSDPDAI(IRREP,ISYTYP(1,LISTL))
            NUMSYL=FSDPDAI(IRREP,ISYTYP(2,LISTL))
            STRING='AIAI'
         ELSE
            DISSYL=FSDPDIA(IRREP,ISYTYP(1,LISTL))
            NUMSYL=FSDPDIA(IRREP,ISYTYP(2,LISTL))
            STRING='IAIA'
         ENDIF
C
         IF(NSIZT.NE.NUMSYL) THEN
            WRITE(6,*)'@OSL1IA-E FATAL ERROR: NSIZT.NE.NUMSYL',
     $           NSIZT,NUMSYL
            CALL ERREX
         ENDIF
C
         IF(NSIZTAR.NE.DISSYL) THEN
            WRITE(6,*)'@OSL1IA-E FATAL ERROR: NSIZTAR.NE.DISSYL',
     $           NSIZTAR,DISSYL
            CALL ERREX
         ENDIF
C
C     I000: L1(CONT) I010: T1  I020: L1
C
         I020=I010+NSIZT*IINTFP
         I030=I020+NUMSYL*DISSYL*IINTFP
C
         CALL FSGETT1(ICORE(I010),ISPIN,90,'AI',8+ISPIN)
         CALL FSGET(ICORE(I020),1,NUMSYL,1,IRREP,LISTL,STRING)
C
C           NOW THE CORRECTION FOR TENTH TERM
C
C
C           AA AND BB CASES
C
            LISTT2=35+ISPIN
            LISTL2=136-ISPIN
            NUMSYL2=FSDPDII(IRREP,ISYTYP(2,LISTL2))
            DISSYL2=FSDPDII(IRREP,ISYTYP(1,LISTL2))
            NUMSYT2=FSDPDII(IRREP,ISYTYP(2,LISTT2))
            DISSYT2=FSDPDAA(IRREP,ISYTYP(1,LISTT2))
C
C     I000: L1(CONT) I010: L1  I020: L2  I030: T2(2)   I040:  L2(2)
C
            I040=I030+NUMSYT2*DISSYT2*IINTFP
            I050=I040+NUMSYL2*DISSYL2*IINTFP
C
            CALL FSGET(ICORE(I040),1,NUMSYL2,1,IRREP,LISTL2,'IIII')
            CALL FSGET(ICORE(I030),1,NUMSYT2,1,IRREP,LISTT2,'AAII')
C
            CALL XGEMM('N','N',1,NUMSYL2,DISSYL2,ONEM,ICORE(I030),
     $        1,ICORE(I040),DISSYL2,ONE,ICORE(I020),1)
C
C           AB CASE
C
            LISTT2=36-ISPIN
            LISTL2=138-ISPIN
            NUMSYL2=FSDPDII(IRREP,ISYTYP(2,LISTL2))
            DISSYL2=FSDPDII(IRREP,ISYTYP(1,LISTL2))
            NUMSYT2=FSDPDII(IRREP,ISYTYP(2,LISTT2))
            DISSYT2=FSDPDAA(IRREP,ISYTYP(1,LISTT2))
C
C     I000: L1(CONT) I010: L1  I020: L2  I030: T2(2)   I040:  L2(2)
C
            I040=I030+NUMSYT2*DISSYT2*IINTFP
            I050=I040+NUMSYL2*DISSYL2*IINTFP
C
            CALL FSGET(ICORE(I040),1,NUMSYL2,1,IRREP,LISTL2,'IIII')
            CALL FSGET(ICORE(I030),1,NUMSYT2,1,IRREP,LISTT2,'AAII')
C
            CALL XGEMM('N','N',1,NUMSYL2,DISSYL2,ONEM,ICORE(I030),
     $        1,ICORE(I040),DISSYL2,ONE,ICORE(I020),1)
C
C           END OF TENTH CORRECTION
C
C
C     I000: L1(CONT) I010: L1  I020: L2
C
         CALL XGEMM('N','N',DISSYL,1,NUMSYL,ONE,ICORE(I020),DISSYL,
     $        ICORE(I010),NSIZT,ONE,ICORE(I000),NSIZTAR)
C
C     AB SPINCASE
C
         NSIZT=NTAI(3-ISPIN)
         LISTL=146
         IRREP=DIRPRD(IRREPO(3-ISPIN),IRREPV(3-ISPIN))
C
         IF(ISPIN.EQ.1) THEN
            DISSYL=FSDPDAI(IRREP,ISYTYP(1,LISTL))
            NUMSYL=FSDPDIA(IRREP,ISYTYP(2,LISTL))
            STRING='AIIA'
         ELSE
            DISSYL=FSDPDIA(IRREP,ISYTYP(1,LISTL))
            NUMSYL=FSDPDAI(IRREP,ISYTYP(2,LISTL))
            STRING='IAAI'
         ENDIF
C
         IF(NSIZT.NE.NUMSYL) THEN
            WRITE(6,*)'@OSL1IA-E FATAL ERROR: NSIZT.NE.NUMSYL',
     $           NSIZT,NUMSYL
            CALL ERREX
         ENDIF
C
         IF(NSIZTAR.NE.DISSYL) THEN
            WRITE(6,*)'@OSL1IA-E FATAL ERROR: NSIZTAR.NE.DISSYL',
     $           NSIZTAR,DISSYL
            CALL ERREX
         ENDIF
C
C     I000: L1(CONT) I010: T1  I020: L1
C
         I020=I010+NSIZT*IINTFP
         I030=I020+NUMSYL*DISSYL*IINTFP
C
         CALL FSGETT1(ICORE(I010),3-ISPIN,90,'AI',11-ISPIN)
         CALL FSGET(ICORE(I020),1,NUMSYL,1,IRREP,LISTL,STRING)
C
C           NOW THE CORRECTION FOR TENTH TERM
C
            LISTT2=40-ISPIN
            LISTL2=140-ISPIN
            NUMSYL2=FSDPDII(1,ISYTYP(2,LISTL2))
            DISSYL2=FSDPDII(1,ISYTYP(1,LISTL2))
            NUMSYT2=FSDPDII(1,ISYTYP(2,LISTT2))
            DISSYT2=FSDPDAA(1,ISYTYP(1,LISTT2))
C
C     I000: L1(CONT) I010: L1  I020: L2  I030: T2(2)   I040:  L2(2)
C
            I040=I030+NUMSYT2*DISSYT2*IINTFP
            I050=I040+NUMSYL2*DISSYL2*IINTFP
C
            CALL FSGET(ICORE(I040),1,NUMSYL2,1,1,LISTL2,'IIII')
            CALL FSGET(ICORE(I030),1,NUMSYT2,1,1,LISTT2,'AAII')
C
            IOFF=I040
            IRREPI=IRREPO(ISPIN)
            DO 200 IRREP1=1,IRREPI-1
               IOFF=IOFF+POPI(IRREP1,ISPIN)*VRTI(IRREP1,3-ISPIN)*
     $              DISSYL2*IINTFP
 200        CONTINUE
            CALL XGEMM('N','N',1,NUMSYL*DISSYL,DISSYL2,ONEM,ICORE(I030),
     $        1,ICORE(IOFF),DISSYL2,ONE,ICORE(I020),1)
C
C           END OF TENTH CORRECTION
C
C
C     I000: L1(CONT) I010: L1  I020: L2
C
         CALL XGEMM('N','N',DISSYL,1,NUMSYL,ONE,ICORE(I020),DISSYL,
     $        ICORE(I010),NSIZT,ONE,ICORE(I000),NSIZTAR)
C
C     THIRD AND EIGHTH TERM
C     (WITH L2'(Ba,Mn)=L2(Ba,Mn) - SUM(Ij) TAU(Nm,Ji)*L2(Ba,Ij)
C      AND  L2'(Ab,Mn)=L2(Ab,Mn) - SUM(iJ) TAU(Nm,Ij)*L2(Ab,Ji)  )
C
         NSIZT=NTIA(3-ISPIN)
         LISTL=146
         IRREP=DIRPRD(IRREPO(3-ISPIN),IRREPO(ISPIN))
C
         DISSYL=FSDPDII(IRREP,ISYTYP(1,LISTL))
         NUMSYL=FSDPDAA(IRREP,ISYTYP(2,LISTL))
         STRING='IIAA'
C
C     I000: L1(CONT) I010: T1  I020: L2
C
         I020=I010+NSIZT*IINTFP
         I030=I020+NUMSYL*DISSYL*IINTFP
C
         CALL FSGETT1(ICORE(I010),3-ISPIN,90,'IA',11-ISPIN)
         CALL FSGET(ICORE(I020),1,NUMSYL,1,IRREP,LISTL,STRING)
C
C          NOW CORRECT FOR THE EIGHTH TERM
C
c&test DID NOT GIVE CONTRIB IN FIRST TEST
           LISTT2=46
           LISTL2=146
           DISSYT2=FSDPDAA(IRREP,ISYTYP(1,LISTT2))
           NUMSYT2=FSDPDII(IRREP,ISYTYP(2,LISTT2))
           DISSYL2=FSDPDII(IRREP,ISYTYP(1,LISTL2))
           NUMSYL2=FSDPDII(IRREP,ISYTYP(2,LISTL2))
           NSIZTA=NTAI(1)
           NSIZTB=NTAI(2)
C
C  I000: L1(CONT) I010: T1  I020: L2 I030: TAU  I040: T1A I050: T1B I060: TMP
C
           I040=I030+NUMSYT2*DISSYT2*IINTFP
           I050=I040+NSIZTA*IINTFP
           I060=I050+NSIZTB*IINTFP
           I070=I060+NUMSYT2*DISSYT2*IINTFP
           IF(I070.GT.MAXCOR) CALL INSMEM('OSL1IA',I070,MAXCOR)
C
           CALL FSGETT1(ICORE(I040),1,90,'AI',9)
           CALL FSGETT1(ICORE(I050),2,90,'AI',10)
           CALL FSGET(ICORE(I060),1,NUMSYT2,1,IRREP,LISTT2,'AAII')
           CALL FTAU(ICORE(I060),ICORE(I040),ICORE(I050),DISSYT2,NUMSYT2
     $        ,POPI(1,1),POPI(1,2),VRTA(1,1),VRTA(1,2),IRREP,3,ONE)
           CALL SYMTRA(IRREP,POPI(1,1),POPI(1,2),DISSYT2,
     $        ICORE(I060),ICORE(I030))
C
C  I000: L1(CONT) I010: T1  I020: L2 I030: TAU  I040: L2(2)
C
           I050=I040+NUMSYL2*DISSYL2*IINTFP
           IF(I050.GT.MAXCOR) CALL INSMEM('OSL1IA',I050,MAXCOR)
C
           CALL FSGET(ICORE(I040),1,NUMSYL2,1,IRREP,LISTL2,'IIII')
C
           CALL XGEMM('N','T',DISSYL2,1,NUMSYL2,ONE,ICORE(I040),
     $        DISSYL2,ICORE(I030),DISSYT2,ONE,ICORE(I020),DISSYL)
C
C          END OF EIGHTH TERM CORRECTION
C
C
C     I000: L1(CONT) I010: T1  I020: L2  I030,I040,I050: SCR
C
         IF(ISPIN.EQ.1) THEN
             I040=I030+3*NUMSYL*IINTFP
             I050=I040+3*NUMSYL*IINTFP
             I060=I050+3*NUMSYL*IINTFP
             CALL SYMTR3(IRREP,VRTI(1,1),VRTI(1,2),DISSYL,NUMSYL,
     $           ICORE(I020),ICORE(I030),ICORE(I040),ICORE(I050))
         ENDIF
         IRREPJ=IRREPO(3-ISPIN)
         IOFF=I020
         DO 300 IRREPL=1,IRREPJ-1
            IRREPI=DIRPRD(IRREP,IRREPL)
            IOFF=IOFF+VRTI(IRREPL,ISPIN)*VRTI(IRREPI,3-ISPIN)*IINTFP
 300     CONTINUE
C
         CALL XGEMM('N','N',NSIZTAR,1,NSIZT,ONEM,ICORE(IOFF),NSIZTAR,
     $        ICORE(I010),NSIZT,ONE,ICORE(I000),NSIZTAR)
C
C     FOURTH AND NINTH TERMS
C     (WITH: L1'(a,i)=L1(a,i)+SUM(bj) (T1(b,j)-T1(B,J))*L2(ba,ij)
C                            -SUM(Bj) (T1(B,J)-T1(b,j))*L2(Ba,Ji)
C      AND:  L1'(A,I)=L1(A,I)+SUM(JB) (T1(B,J)-T1(b,j))*L2(BA,IJ)
C                            -SUM(jb) (T1(b,j)-T1(B,J))*L2(Ab,Ij)
C      NOTE:  B,J,b,j ARE OF TYPE 'N'  )
C
         NSIZL=NTII(3-ISPIN)
         LISTT=46
         IRREP=DIRPRD(IRREPV(ISPIN),IRREPV(3-ISPIN))
C
         DISSYT=FSDPDAA(IRREP,ISYTYP(1,LISTT))
         IF(ISPIN.EQ.1) THEN
            NUMSYT=FSDPDIA(IRREP,ISYTYP(2,LISTT))
            STRING='AAIA'
         ELSE
            NUMSYT=FSDPDAI(IRREP,ISYTYP(2,LISTT))
            STRING='AAAI'
         ENDIF
C
C     I000: L1(CONT) I010: L1
C
         I020=I010+NSIZL*IINTFP
C
         CALL FSGETT1(ICORE(I010),3-ISPIN,190,'II',11-ISPIN)
C
C           NOW CORRECTION FOR NINTH TERM
C                 (ONLY TOTALLY SYMMETRIC IRREP REQUIRED)
C
C  AA AND BB SPINCASES
C
            LISTL2=136-ISPIN
            NUMSYL2=FSDPDII(1,ISYTYP(2,LISTL2))
            DISSYL2=IRPDPD(1,ISYTYP(1,LISTL2))
C
C     I0(1):  T1A-T1B  I0(2): T1B-T1A  I000: L1(CONT) I010: L1  I020: L2   
C
            I030=I020+NUMSYL2*DISSYL2*IINTFP
C
            CALL FSGET(ICORE(I020),1,NUMSYL2,1,1,LISTL2,'NNII')
C
            CALL XGEMM('N','N',1,NUMSYL2,DISSYL2,ONE,ICORE(I0(3-ISPIN))
     $        ,1,ICORE(I020),DISSYL2,ONE,ICORE(I010),1)
C
C  AB SPINCASE
C
            LISTL2=138-ISPIN
            NUMSYL2=FSDPDII(1,ISYTYP(2,LISTL2))
            DISSYL2=IRPDPD(1,ISYTYP(1,LISTL2))
C
C     I0(1):  T1A-T1B  I0(2): T1B-T1A  I000: L1(CONT) I010: L1  I020: L2
C
            I030=I020+NUMSYL2*DISSYL2*IINTFP
C
            CALL FSGET(ICORE(I020),1,NUMSYL2,1,1,LISTL2,'NNII')
C
            CALL XGEMM('N','N',1,NUMSYL2,DISSYL2,ONEM,ICORE(I0(ISPIN)),
     $        1,ICORE(I020),DISSYL2,ONE,ICORE(I010),1)
C
C           END OF NINTH TERM CONTRIBUTION
C
C
C     I000: L1(CONT) I010: L1  I020: T2
C
         I030=I020+NUMSYT*DISSYT*IINTFP
C
         CALL FSGET(ICORE(I020),1,NUMSYT,1,IRREP,LISTT,STRING)
C
         IRREPC=IRREPO(ISPIN)
         IOFF=I010
         DO 400 IRREPL=1,IRREPC-1
            IOFF=IOFF+POPI(IRREPL,3-ISPIN)*VRTI(IRREPL,3-ISPIN)*IINTFP
 400     CONTINUE
C
         CALL XGEMM('N','T',NSIZTAR,1,NUMSYT,ONE,ICORE(IOFF),NSIZTAR,
     $        ICORE(I020),1,ONE,ICORE(I000),NSIZTAR)
C
C     FIFTH TERM
C
c&test NO CONTRIB IN FIRST TEST
         LISTT=46
         LISTL=146
C
         IRREP=DIRPRD(IRREPV(ISPIN),IRREPV(3-ISPIN))
C
         NUMSYL=FSDPDII(IRREP,ISYTYP(2,LISTL))
         DISSYT=FSDPDAA(IRREP,ISYTYP(1,LISTT))
         NUMSYT=FSDPDII(IRREP,ISYTYP(2,LISTT))
         IF(ISPIN.EQ.1) THEN
            DISSYL=FSDPDAI(IRREP,ISYTYP(1,LISTL))
            STRING='AIII'
         ELSE
            DISSYL=FSDPDIA(IRREP,ISYTYP(1,LISTL))
            STRING='IAII'
         ENDIF
         NSIZTA=NTAI(1)
         NSIZTB=NTAI(2)
C
C     I000: L2(CONT)  I010: L2   I020:  TAU  I030: T1A   I040: T1B I050: TMP
C
         I020=I010+IINTFP*NUMSYL*DISSYL
         I030=I020+IINTFP*NUMSYT*DISSYT
         I040=I030+IINTFP*NSIZTA
         I050=I040+IINTFP*NSIZTB
         I060=I050+IINTFP*NUMSYT*DISSYT
         IF(I060.GT.MAXCOR) CALL INSMEM('OSL1IA',I060,MAXCOR)
C
         CALL FSGETT1(ICORE(I030),1,90,'AI',9)
         CALL FSGETT1(ICORE(I040),2,90,'AI',10)
         CALL FSGET(ICORE(I050),1,NUMSYT,1,IRREP,LISTT,'AAII')
         CALL FTAU(ICORE(I050),ICORE(I030),ICORE(I040),DISSYT,NUMSYT,
     $        POPI(1,1),POPI(1,2),VRTA(1,1),VRTA(1,2),IRREP,3,ONE)
         CALL SYMTRA(IRREP,POPI(1,1),POPI(1,2),DISSYT,
     $        ICORE(I050),ICORE(I020))
         CALL FSGET(ICORE(I010),1,NUMSYL,1,IRREP,LISTL,STRING)
C
         CALL XGEMM('N','T',DISSYL,1,NUMSYL,ONEM,ICORE(I010),DISSYL,
     $        ICORE(I020),DISSYT,ONE,ICORE(I000),NSIZTAR)
C
C     SIXTH TERM
C
         LISTT=38-ISPIN
         LISTL=136-ISPIN
C
         IRREP=DIRPRD(IRREPO(3-ISPIN),IRREPV(3-ISPIN))
C
         DISSYL=FSDPDIA(IRREP,ISYTYP(1,LISTL))
         NUMSYT=FSDPDAA(IRREP,ISYTYP(2,LISTT))
         DISSYT=FSDPDII(IRREP,ISYTYP(1,LISTT))
         NUMSYL=FSDPDII(IRREP,ISYTYP(2,LISTL))
C
C     I000: L2(CONT)  I010: L2   I020:  T2
C
         I020=I010+IINTFP*NUMSYL*DISSYL
         I030=I020+IINTFP*NUMSYT*DISSYT
         IF(I030.GT.MAXCOR) CALL INSMEM('OSL1IA',I030,MAXCOR)
C
         CALL FSGET(ICORE(I020),1,NUMSYT,1,IRREP,LISTT,'IIAA')
         CALL FSGET(ICORE(I010),1,NUMSYL,1,IRREP,LISTL,'IAII')
C
         CALL XGEMM('N','N',DISSYL,1,NUMSYL,ONE,ICORE(I010),DISSYL,
     $        ICORE(I020),DISSYT,ONE,ICORE(I000),NSIZTAR)
C
C     SEVENTH TERM
C
C     AA AND BB SPINCASE
C
         LISTT=36-ISPIN
         LISTL=135+ISPIN
C
         IRREP=DIRPRD(IRREPO(3-ISPIN),IRREPV(3-ISPIN))
C
         DISSYL=FSDPDIA(IRREP,ISYTYP(1,LISTL))
         DISSYT=FSDPDII(IRREP,ISYTYP(1,LISTT))
         NUMSYT=FSDPDAA(IRREP,ISYTYP(2,LISTT))
         NUMSYL=FSDPDII(IRREP,ISYTYP(2,LISTL))
         NSIZTA=NTIA(3-ISPIN)
         NSIZTB=NTAI(3-ISPIN)
C
C     I000: L2(CONT)  I010: L2   I020:  T2  I030: T1A   I040: T1B
C
         I020=I010+IINTFP*NUMSYL*DISSYL
         I030=I020+IINTFP*NUMSYT*DISSYT
         I040=I030+IINTFP*NSIZTA
         I050=I040+IINTFP*NSIZTB
         IF(I050.GT.MAXCOR) CALL INSMEM('OSL1IA',I050,MAXCOR)
C
         CALL FSGET(ICORE(I020),1,NUMSYT,1,IRREP,LISTT,'IIAA')
         CALL FSGETT1(ICORE(I030),3-ISPIN,90,'IA',11-ISPIN)
         CALL FSGETT1(ICORE(I040),3-ISPIN,90,'AI',11-ISPIN)
         CALL OSTAU35(ICORE(I020),DISSYT,IRREP,
     $        ICORE(I030),NSIZTA,IRREPO(3-ISPIN),VRTI(1,3-ISPIN),
     $        ICORE(I040),NSIZTB,IRREPV(3-ISPIN),POPI(1,3-ISPIN),
     $        ONE,ONE)
         CALL FSGET(ICORE(I010),1,NUMSYL,1,IRREP,LISTL,'IAII')
C
         CALL XGEMM('N','N',DISSYL,1,NUMSYL,ONE,ICORE(I010),DISSYL,
     $        ICORE(I020),DISSYT,ONE,ICORE(I000),NSIZTAR)
C
C     AB SPINCASE
C
         LISTT=37+ISPIN
         LISTL=137+ISPIN
C
         IRREP=DIRPRD(IRREPV(ISPIN),IRREPO(3-ISPIN))
C
         DISSYL=FSDPDIA(IRREP,ISYTYP(1,LISTL))
         DISSYT=FSDPDII(IRREP,ISYTYP(1,LISTT))
         NUMSYT=FSDPDAA(IRREP,ISYTYP(2,LISTT))
         NUMSYL=FSDPDII(IRREP,ISYTYP(2,LISTL))
         NSIZTA=NTIA(3-ISPIN)
         NSIZTB=NTAI(ISPIN)
C
C     I000: L2(CONT)  I010: L2   I020:  T2  I030: T1A   I040: T1B
C
         I020=I010+IINTFP*NUMSYL*DISSYL
         I030=I020+IINTFP*NUMSYT*DISSYT
         I040=I030+IINTFP*NSIZTA
         I050=I040+IINTFP*NSIZTB
         IF(I050.GT.MAXCOR) CALL INSMEM('OSL1IA',I050,MAXCOR)
C
         CALL FSGET(ICORE(I020),1,NUMSYT,1,IRREP,LISTT,'IIAA')
         CALL FSGETT1(ICORE(I030),3-ISPIN,90,'IA',11-ISPIN)
         CALL FSGETT1(ICORE(I040),ISPIN,90,'AI',8+ISPIN)
         CALL OSTAU35(ICORE(I020),DISSYT,IRREP,
     $        ICORE(I030),NSIZTA,IRREPO(3-ISPIN),VRTI(1,3-ISPIN),
     $        ICORE(I040),NSIZTB,IRREPV(ISPIN),POPI(1,ISPIN),
     $        ONE,ONE)
         CALL FSGET(ICORE(I010),1,NUMSYL,1,IRREP,LISTL,'IAII')
C
         CALL XGEMM('N','N',DISSYL,1,NUMSYL,ONE,ICORE(I010),DISSYL,
     $        ICORE(I020),DISSYT,ONE,ICORE(I000),NSIZTAR)
C
C     SCALE BY W
C
C     I000: L1(CONT)    I010: L1(TARGET)
C
         CALL FSGETT1(ICORE(I010),ISPIN+2,90,'IA',8+ISPIN)
         CALL SAXPY(NSIZTAR,W,ICORE(I000),1,ICORE(I010),1)
         CALL FSPUTT1(ICORE(I010),ISPIN+2,90,'IA',8+ISPIN)
C
 1000 CONTINUE
      RETURN
      END
