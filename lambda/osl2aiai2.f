      SUBROUTINE OSL2AIAI2(ICORE,MAXCOR,IUHF,W)
C
C  THIS ROUTINE COMPUTES:
C
C     L2INC(Am,In) = W [ - L2(am,in)
C                        + SUM(B) T1(B,M) * L2(ab,ni)
C                        - SUM(J) T1(N,J) * L2(ma,ij)
C                        + SUM(JB) TAU(BN,MJ) * L2(ab,ij)
C                        - SUM(jb) T2(Nb,Mj) * L2(Ba,Ji) ]
C
C     L2INC(Na,Mi) = W [ - L2(AN,IM)
C                        + SUM(b) T1(b,n) * L2(AB,MI)
C                        - SUM(j) T1(m,j) * L2(AN,JI)
C                        + SUM(jb) TAU(bm,nj) * L2(AB,IJ)
C                        - SUM(JB) T2(Bm,Jn) * L2(Ab,Ij) ]
CEND
C
CCODED BY PS SEPT/93
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,DISSYL,DISSYT,POP,VRT,DISTAR
      CHARACTER*4 STRING,STRTAR
C
      DIMENSION ICORE(MAXCOR)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &                DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
C
      INTEGER POPA,POPI,VRTA,VRTI
      COMMON /FSSYM/ POPA(8,2),POPI(8,2),VRTA(8,2),VRTI(8,2),
     &               NTAN(2),NTNA(2),NTAA(2),
     &               NTIN(2),NTNI(2),NTII(2),
     &               NTAI(2),NTIA(2),
     &               NF1AN(2),NF1AA(2),NF1IN(2),NF1II(2),NF1AI(2),
     &               NF2AN(2),NF2AA(2),NF2IN(2),NF2II(2),NF2AI(2)
C
      INTEGER FSDPDAN,FSDPDNA,FSDPDAA,FSDPDIN,FSDPDNI,FSDPDII,FSDPDAI,
     $   FSDPDIA
      COMMON /FSSYMPOP/ FSDPDAN(8,22),FSDPDNA(8,22),FSDPDAA(8,22),
     &                  FSDPDIN(8,22),FSDPDNI(8,22),FSDPDII(8,22),
     &                  FSDPDAI(8,22),FSDPDIA(8,22)
C
      COMMON /ACTIRR/ IRREPO(2),IRREPV(2)
C
      COMMON /FLAGS/ IFLAGS(100)
C
      DATA ONE,ONEM,ZILCH,HALF/1.D0,-1.D0,0.D0,0.5D0/
C
      DO 1000 ISPIN=1,IUHF+1
         LISTAR=63
         DO 100 IRREP=1,NIRREP
            IF(ISPIN.EQ.1) THEN
               DISTAR=FSDPDIA(IRREP,ISYTYP(1,LISTAR))
               NUMTAR=FSDPDIA(IRREP,ISYTYP(2,LISTAR))
               STRTAR='IAIA'
            ELSE
               DISTAR=FSDPDAI(IRREP,ISYTYP(1,LISTAR))
               NUMTAR=FSDPDAI(IRREP,ISYTYP(2,LISTAR))
               STRTAR='AIAI'
            ENDIF
            IF(DISTAR*NUMTAR.EQ.0) GOTO 100
C
C     I000: L2(CONT)
C
            I000=1
            I010=I000+DISTAR*NUMTAR*IINTFP
C
C     FIRST TERM
C
            LISTL=146-ISPIN
            NUMSYL=FSDPDIA(IRREP,ISYTYP(2,LISTL))
            DISSYL=FSDPDIA(IRREP,ISYTYP(1,LISTL))
            STRING='IAIA'
C
            IF(DISTAR.NE.DISSYL.OR.NUMTAR.NE.NUMSYL) THEN
                WRITE(6,*)'@OSL2AIAI2-E FATAL ERROR: DISTAR.NE.DISSYL',
     $               DISTAR,DISSYL,' OR NUMTAR.NE.NUMSYL',NUMTAR,NUMSYL
                CALL ERREX
            ENDIF
C
            CALL FSGET(ICORE(I000),1,NUMSYL,1,IRREP,LISTL,STRING)
            CALL SSCAL(NUMSYL*DISSYL,ONEM,ICORE(I000),1)
C
C     SECOND TERM
C
            NSIZT=NTIA(ISPIN)
            LISTL=136-ISPIN
            IRREPL=DIRPRD(IRREPO(3-ISPIN),IRREPO(ISPIN))
C
            NUMSYL=FSDPDII(IRREPL,ISYTYP(2,LISTL))
            DISSYL=FSDPDIA(IRREPL,ISYTYP(1,LISTL))
C
            IF(NSIZT.NE.DISSYL) THEN
               WRITE(6,*)'@OSL2AIAI2-E FATAL ERROR: NSIZT.NE.DISSYL',
     $              NSIZT,DISSYL
               CALL ERREX
            ENDIF
C
C     I000: L2(CONT) I010: T1  I020: L2
C
            I020=I010+NSIZT*IINTFP
            I030=I020+NUMSYL*DISSYL*IINTFP
C
            CALL FSGETT1(ICORE(I010),ISPIN,90,'IA',8+ISPIN)
            CALL FSGET(ICORE(I020),1,NUMSYL,1,IRREPL,LISTL,'IAII')
C
            IRREPI=DIRPRD(IRREP,IRREPO(3-ISPIN))
            IOFF=I020
            DO 101 IRREP2=1,IRREPI-1
              IRREPC=DIRPRD(IRREP2,IRREPL)
              IOFF=IOFF+VRTI(IRREPC,3-ISPIN)*POPI(IRREP2,3-ISPIN)*
     $             DISSYL*IINTFP
101         CONTINUE
            IRREPC=DIRPRD(IRREPI,IRREPL)
            NSIZL=VRTI(IRREPC,3-ISPIN)*POPI(IRREPI,3-ISPIN)
C
            IF(DISTAR*NUMTAR.NE.NSIZL) THEN
               WRITE(6,*)'@OSL21AIAI2-E FATAL ERROR: DISTAR*NUMTAR.',
     $              'NE.NSIZL',DISTAR*NUMTAR,NSIZL
               CALL ERREX
            ENDIF
C
            CALL XGEMM('N','N',1,NSIZL,DISSYL,ONE,ICORE(I010),1,
     $           ICORE(IOFF),DISSYL,ONE,ICORE(I000),1)
C
C     THIRD TERM
C
            NSIZT=NTAI(ISPIN)
            LISTL=136-ISPIN
            IRREPL=DIRPRD(IRREPV(ISPIN),IRREPV(3-ISPIN))
C
            NUMSYL=FSDPDII(IRREPL,ISYTYP(2,LISTL))
            DISSYL=FSDPDAI(IRREPL,ISYTYP(1,LISTL))
C
            IF(NSIZT.NE.DISSYL) THEN
               WRITE(6,*)'@OSL2AIAI2-E FATAL ERROR: NSIZT.NE.DISSYL',
     $              NSIZT,DISSYL
               CALL ERREX
            ENDIF
C
C     I000: L2(CONT) I010: T1  I020: L2
C
            I020=I010+NSIZT*IINTFP
            I030=I020+NUMSYL*DISSYL*IINTFP
C
            CALL FSGETT1(ICORE(I010),ISPIN,90,'AI',8+ISPIN)
            CALL FSGET(ICORE(I020),1,NUMSYL,1,IRREPL,LISTL,'AIII')
C
            IRREPI=DIRPRD(IRREP,IRREPO(3-ISPIN))
            IOFF=I020
            DO 102 IRREP2=1,IRREPI-1
              IRREPC=DIRPRD(IRREP2,IRREPL)
              IOFF=IOFF+VRTI(IRREPC,3-ISPIN)*POPI(IRREP2,3-ISPIN)*
     $             DISSYL*IINTFP
102         CONTINUE
            IRREPC=DIRPRD(IRREPI,IRREPL)
            NSIZL=VRTI(IRREPC,3-ISPIN)*POPI(IRREPI,3-ISPIN)
C
            IF(DISTAR*NUMTAR.NE.NSIZL) THEN
               WRITE(6,*)'@OSL21AIAI2-E FATAL ERROR: DISTAR*NUMTAR.',
     $              'NE.NSIZL',DISTAR*NUMTAR,NSIZL
               CALL ERREX
            ENDIF
C
            CALL XGEMM('N','N',1,NSIZL,DISSYL,ONEM,ICORE(I010),1,
     $           ICORE(IOFF),DISSYL,ONE,ICORE(I000),1)
C
C     FOURTH TERM
C
            LISTT=33+ISPIN
            LISTL=136-ISPIN
C
            IRREPL=DIRPRD(IRREPV(ISPIN),IRREPO(ISPIN))
            DISSYL=FSDPDII(IRREPL,ISYTYP(1,LISTL))
            NUMSYL=FSDPDII(IRREPL,ISYTYP(2,LISTL))
            DISSYT=FSDPDAA(IRREPL,ISYTYP(1,LISTT))
            NUMSYT=FSDPDII(IRREPL,ISYTYP(2,LISTT))
            NSIZTA=NTIA(ISPIN)
            NSIZTB=NTAI(ISPIN)
C
C     I000: L2(CONT)  I010: L2   I020:  T2   I030: T1A    I040:  T1B
C
            I020=I010+IINTFP*NUMSYL*DISSYL
            I030=I020+IINTFP*NUMSYT*DISSYT
            I040=I030+IINTFP*NSIZTA
            I050=I040+IINTFP*NSIZTB
            IF(I050.GT.MAXCOR) CALL INSMEM('OSL2AIAI2',I050,MAXCOR)
C
            CALL FSGET(ICORE(I020),1,NUMSYT,1,IRREPL,LISTT,'AAII')
            CALL FSGETT1(ICORE(I030),ISPIN,90,'IA',8+ISPIN)
            CALL FSGETT1(ICORE(I040),ISPIN,90,'AI',8+ISPIN)
            CALL OSTAU35(ICORE(I020),NUMSYT,IRREPL,
     $           ICORE(I030),NSIZTA,IRREPO(ISPIN),VRTI(1,ISPIN),
     $           ICORE(I040),NSIZTB,IRREPV(ISPIN),POPI(1,ISPIN),
     $           ONE,ONE)
            CALL FSGET(ICORE(I010),1,NUMSYL,1,IRREPL,LISTL,'IIII')
C
            IRREPI=DIRPRD(IRREP,IRREPO(3-ISPIN))
            IOFF=I010
            DO 103 IRREP2=1,IRREPI-1
              IRREPC=DIRPRD(IRREP2,IRREPL)
              IOFF=IOFF+VRTI(IRREPC,3-ISPIN)*POPI(IRREP2,3-ISPIN)*
     $             DISSYL*IINTFP
103         CONTINUE
            IRREPC=DIRPRD(IRREPI,IRREPL)
            NSIZL=VRTI(IRREPC,3-ISPIN)*POPI(IRREPI,3-ISPIN)
C
            IF(DISTAR*NUMTAR.NE.NSIZL) THEN
               WRITE(6,*)'@OSL21AIAI2-E FATAL ERROR: DISTAR*NUMTAR.',
     $              'NE.NSIZL',DISTAR*NUMTAR,NSIZL
               CALL ERREX
            ENDIF
C
            CALL XGEMM('N','N',1,NSIZL,DISSYL,ONEM,ICORE(I020),DISSYT,
     $           ICORE(IOFF),DISSYL,ONE,ICORE(I000),1)
C
C     FIFTH TERM
C
            LISTT=38-ISPIN
            LISTL=138-ISPIN
C
            IRREPL=DIRPRD(IRREPV(ISPIN),IRREPO(ISPIN))
            DISSYL=FSDPDII(IRREPL,ISYTYP(1,LISTL))
            NUMSYL=FSDPDII(IRREPL,ISYTYP(2,LISTL))
            DISSYT=FSDPDAA(IRREPL,ISYTYP(1,LISTT))
            NUMSYT=FSDPDII(IRREPL,ISYTYP(2,LISTT))
C
C     I000: L2(CONT)  I010: L2   I020:  T2
C
            I020=I010+IINTFP*NUMSYL*DISSYL
            I030=I020+IINTFP*NUMSYT*DISSYT
            IF(I030.GT.MAXCOR) CALL INSMEM('OSL2AIAI2',I030,MAXCOR)
C
            CALL FSGET(ICORE(I020),1,NUMSYT,1,IRREPL,LISTT,'AAII')
            CALL FSGET(ICORE(I010),1,NUMSYL,1,IRREPL,LISTL,'IIII')
C
            IRREPI=DIRPRD(IRREP,IRREPO(3-ISPIN))
            IOFF=I010
            DO 104 IRREP2=1,IRREPI-1
              IRREPC=DIRPRD(IRREP2,IRREPL)
              IOFF=IOFF+VRTI(IRREPC,3-ISPIN)*POPI(IRREP2,3-ISPIN)*
     $             DISSYL*IINTFP
 104        CONTINUE
            IRREPC=DIRPRD(IRREPI,IRREPL)
            NSIZL=VRTI(IRREPC,3-ISPIN)*POPI(IRREPI,3-ISPIN)
C
            IF(DISTAR*NUMTAR.NE.NSIZL) THEN
               WRITE(6,*)'@OSL21AIAI2-E FATAL ERROR: DISTAR*NUMTAR.',
     $              'NE.NSIZL',DISTAR*NUMTAR,NSIZL
               CALL ERREX
            ENDIF
C
            CALL XGEMM('N','N',1,NSIZL,DISSYL,ONEM,ICORE(I020),DISSYT,
     $           ICORE(IOFF),DISSYL,ONE,ICORE(I000),1)
C
C     SCALE BY W
C
C     I000: L2(CONT)    I010: L2(TARGET)
C
            CALL FSGET(ICORE(I010),1,NUMTAR,1,IRREP,LISTAR,STRTAR)
            CALL SAXPY(NUMTAR*DISTAR,W,ICORE(I000),1,ICORE(I010),1)
            CALL FSPUT(ICORE(I010),1,NUMTAR,1,IRREP,LISTAR,STRTAR)
100      CONTINUE
1000  CONTINUE
      RETURN
      END
