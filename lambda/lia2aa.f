      SUBROUTINE LIA2AA(W,DVV,XIA,ISPIN,POP,VRT,
     &                  DISSYW,NUMSYW,NVRTSQ,LISTW,IRREP)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DISSYW,DIRPRD,POP,VRT
      DIMENSION W(NVRTSQ,1),DVV(1),XIA(1),POP(8),VRT(8),IPD(8)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
C
C   OFFSETS FOR DENSITY MATRIX
C
      IPD(1)=0
      DO 1 IRREPJ=1,NIRREP-1
       IPD(IRREPJ+1)=IPD(IRREPJ)+VRT(IRREPJ)*VRT(IRREPJ)
1     CONTINUE
C
C      PICK UP THE INTEGRALS 
C
      CALL GETLST(W,1,NUMSYW,1,IRREP,LISTW)
      CALL SYMEXP2(IRREP,VRT,NVRTSQ,DISSYW,NUMSYW,W,W)
C
C  LOOP OVER IRREPS OF VIRTUAL ORBITAL A AND OCCUPIED ORBITAL I
C
C  OFFSETS
C
      IOFF=0
      IOFFWA=0
      IOFFWB=0
C
      DO 100 IRREP1=1,NIRREP
C
C  POPULATION CORRESPONDING TO A AND I
C
       NVRT1=VRT(IRREP1)
       NOCC1=POP(IRREP1)
C
C  DETERMINE IRREP OF M AND N
C
       IRREP2=DIRPRD(IRREP,IRREP1)
C
C  POPULATION WHICH CORRESPONDS TO M AND N
C
       NVRT2=VRT(IRREP2)
C
C  OFFSET IN D
C
       IOFFD1=IPD(IRREP2)
C
       DO 10 IA=1,NVRT1
       IOFFW2=IOFFWA+(IA-1)*NVRT2
       DO 10 I=1,NOCC1
       IOFFW1=IOFFWB+(I-1)*NVRT2
       IND=IA+(I-1)*NVRT1+IOFF
       DO 10 N=1,NVRT2
       IOFFW3=IOFFW1+N
       IOFFD2=IOFFD1+(N-1)*NVRT2
       XIA(IND)=XIA(IND)
     &         -SDOT(NVRT2,DVV(IOFFD2+1),1,W(IOFFW2+1,IOFFW3),1)
10     CONTINUE
       IOFFWA=IOFFWA+NVRT2*NVRT1
       IOFFWB=IOFFWB+NVRT2*NOCC1
       IOFF=IOFF+NVRT1*NOCC1
100    CONTINUE
C
      RETURN
      END
