      SUBROUTINE LRNGDRVR(ICORE,MAXCOR,LISTW,LISTT,CALMOD,MAXSIZ,
     &                  INCREM,LSTSCR)
      IMPLICIT INTEGER (A-Z)
      LOGICAL SPINAD
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD,UCC
      CHARACTER*3 CALMOD
      DIMENSION ICORE(MAXCOR)
      COMMON/FLAGS/IFLAGS(100)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/METH/MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD,UCC
C
      SPINAD=.FALSE.
      IF(IFLAGS(11).EQ.0) SPINAD=.TRUE.
C
C LOOP OVER IRREPS.
C
       DO 100 IRREP=1,NIRREP
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
        DISSYT=IRPDPD(IRREP,ISYTYP(1,LISTT))
        NUMSYW=IRPDPD(IRREP,ISYTYP(2,LISTW))
        NUMSYT=IRPDPD(IRREP,ISYTYP(2,LISTT))
        DISSYQ=DISSYT
        NUMSYQ=DISSYW
        MAXSIZ=MAX(NUMSYQ*DISSYQ,MAXSIZ)
        I000=1
        I010=I000+IINTFP*NUMSYQ*DISSYQ
        I020=I010+IINTFP*NUMSYT*DISSYT
        I030=I020+IINTFP*DISSYW*NUMSYW
C
C CAN WE DO IT IN CORE?
C
        IF(I030.LT.MAXCOR)THEN
C
C FORM THE SYMMETRY PACKED Q VECTOR.
C
         CALL LRNGPRD(ICORE(I020),ICORE(I010),ICORE(I000),
     &               DISSYW,NUMSYW,DISSYT,NUMSYT,DISSYQ,NUMSYQ,
     &               LISTW,LISTT,IRREP,SPINAD)
        ELSE
         CALL INSMEM('LRNGDRV',I090,MAXCOR)
        ENDIF
        IF(CALMOD.EQ.'WxT'.AND.(.NOT.SPINAD))THEN
         CALL TRANSP(ICORE(I000),ICORE(I010),NUMSYQ,DISSYQ)
         CALL DCOPY(NUMSYQ*DISSYQ,ICORE(I010),1,ICORE(I000),1)
         ITMP=NUMSYQ
         NUMSYQ=DISSYQ
         DISSYQ=ITMP
        ENDIF
C
C AUGMENT Q(ABAB) AND DUMP IT TO DISK.
C
        IF(INCREM.EQ.1)THEN
         CALL GETLST(ICORE(I020),1,NUMSYQ,1,IRREP,LSTSCR)
         CALL VADD(ICORE(I020),ICORE(I020),ICORE(I000),
     &                   DISSYQ*NUMSYQ,1.D0)
         IF(SPINAD) THEN
          IF((QCISD.OR.CCSD).AND.IRREP.EQ.1) THEN
           IUHF=0
           CALL DISCO2(ICORE(I020),ICORE(I000),MAXCOR,IUHF,'AABB')
          ENDIF
          CALL MPMT(ICORE(I020),NUMSYQ)
         ENDIF
         CALL PUTLST(ICORE(I020),1,NUMSYQ,1,IRREP,LSTSCR)

        CALL DCOPY(DISSYQ*NUMSYQ,ICORE(I020),1,ICORE(I000),1)

        ELSE

         IF(SPINAD) THEN
          IF((QCISD.OR.CCSD).AND.IRREP.EQ.1) THEN
           IUHF=0
           CALL DISCO2(ICORE(I000),ICORE(I010),MAXCOR,IUHF,'AABB')
          ENDIF
          CALL MPMT(ICORE(I000),NUMSYQ)
         ENDIF

         CALL PUTLST(ICORE(I000),1,NUMSYQ,1,IRREP,LSTSCR)
        ENDIF

100    CONTINUE
       RETURN
       END 
