      SUBROUTINE R1L1Y1B(L1,R1,Y1,ICORE,MAXCOR,IUHF)
C
C Y1(a,i) = L(a,n)*R(me)*W(nm,ie)
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH
      DIMENSION ICORE(MAXCOR),R1(*),L1(*),Y1(*),I0X(2)
      COMMON/STATSYM/IRREPX
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMLOC/ISYMOFF(8,8,25)
C
      DATA ONE,ONEM,ZILCH/1.0D0,-1.0D0,0.0D0/
C
C
C FORM INTERMEDIATE X(ni) = R(me) * W(nm,ie)
C
      I0X(1)=1
      IF(IUHF.NE.0)THEN
       I0X(2)=I0X(1)+IINTFP*IRPDPD(IRREPX,21)
      ELSE
       I0X(2)=I0X(1)
      ENDIF
      I000=I0X(2)+IINTFP*IRPDPD(IRREPX,22)
      MXCOR = MAXCOR - I000 + 1
      LENIJ=IRPDPD(IRREPX,21)+IUHF*IRPDPD(IRREPX,22)
      CALL ZERO(ICORE(I0X(1)),LENIJ)
      IOFF=1
      DO 1 ISPIN=1,1+IUHF
       CALL SYMTRA (IRREPX,VRT(1,ISPIN),POP(1,ISPIN),1,R1(IOFF),
     &              ICORE(I000))
       CALL SCOPY  (IRPDPD(IRREPX,8+ISPIN),ICORE(I000),1,R1(IOFF),1)
       IOFF=IOFF+IINTFP*IRPDPD(IRREPX,8+ISPIN)
1     CONTINUE
      CALL DVOINOO(IRREPX,ICORE(I0X(1)),R1,ICORE(I000),MXCOR,IUHF,7)
      IOFF=1
      DO 2 ISPIN=1,1+IUHF
       CALL SYMTRA (IRREPX,POP(1,ISPIN),VRT(1,ISPIN),1,R1(IOFF),
     &              ICORE(I000))
       CALL SCOPY  (IRPDPD(IRREPX,8+ISPIN),ICORE(I000),1,R1(IOFF),1)
       IOFF=IOFF+IINTFP*IRPDPD(IRREPX,8+ISPIN)
2     CONTINUE
C
      IOFFL=1
      IOFFY=1
      DO 10 ISPIN=1,1+IUHF
       IOFFX = I0X(ISPIN)
C
C FORM PRODUCT Y1(ai) = -L(an)*X(ni)
C
       DO 11 IRREP=1,NIRREP
        NUMA=VRT(IRREP,ISPIN)
        NUMI=POP(IRREP,ISPIN)
        CALL XGEMM('N','N',NUMA,NUMI,NUMI,ONEM,L1(IOFFL),NUMA,
     &             ICORE(IOFFX),NUMI,ONE,Y1(IOFFY),NUMA)
        IOFFL=IOFFL+IINTFP*NUMA*NUMI
        IOFFX=IOFFX+IINTFP*NUMI*NUMI
        IOFFY=IOFFY+IINTFP*NUMA*NUMI
11     CONTINUE
C
10    CONTINUE
      RETURN
      END
