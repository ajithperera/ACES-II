
      
      SUBROUTINE MTRAC1(F,Z,DISSYZ,POP,IRREP)
C
C  THIS ROUTINE CALCULATES THE CONTRACTION
C
C  F(M,I) = F(M,I) + SUM N Z(MN,IN)
C
C  THE CONSIDERED SPIN CASE IS HERE AAAA OR BBBB ONLY, Z
C  IS STORED AS M<N: I, N , BUT THE SUM RUNS OVER ALL
C  M AND N AND ONE MUST USE THE ANTISYMMETRY OF Z
C 
C   Z(MN,IN) = - Z(NM,IN)
C
C  THIS TERM IS REQUIRED IN UHF-CCSD ONLY
C
CEND
C
C  CODED JULY/90   JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD,DISSYZ,POP
      DIMENSION F(1),Z(DISSYZ,1),POP(8),IPF(8),IPZ(8)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &                DIRPRD(8,8)
C
      IPF(1)=0
      IPZ(1)=0
      DO 1 IRREPJ=1,NIRREP-1
       IRREPI=DIRPRD(IRREP,IRREPJ) 
       IPF(IRREPJ+1)=IPF(IRREPJ)+POP(IRREPJ)*POP(IRREPJ)
       IF(IRREPI.LT.IRREPJ) THEN
        IPZ(IRREPJ+1)=IPZ(IRREPJ)+POP(IRREPJ)*POP(IRREPI)
       ELSE IF(IRREPI.EQ.IRREPJ) THEN
        IPZ(IRREPJ+1)=IPZ(IRREPJ)+POP(IRREPJ)*(POP(IRREPJ)-1)/2
       ELSE
        IPZ(IRREPJ+1)=IPZ(IRREPJ)
       ENDIF
1     CONTINUE
C
      IOFFZ1=0
      DO 100 IRREPJ=1,NIRREP
       NOCCJ=POP(IRREPJ)
       IRREPI=DIRPRD(IRREPJ,IRREP)
       NOCCI=POP(IRREPI)
       IF(NOCCI.EQ.0.OR.NOCCJ.EQ.0) GO TO 100
       INDF=IPF(IRREPI)
       DO 20 I=1,NOCCI
       DO 20 M=1,NOCCI
       INDF=INDF+1
       IF(IRREPI.LT.IRREPJ) THEN
       IOFFZ2=IPZ(IRREPJ)
       DO 2 N=1,NOCCJ
       INDN1=(N-1)*NOCCI+I+IOFFZ1
       INDN2=(N-1)*NOCCI+M+IOFFZ2
       F(INDF)=F(INDF)+Z(INDN2,INDN1)
2      CONTINUE
       ELSE IF(IRREPI.GT.IRREPJ) THEN
       IOFFZ2=IPZ(IRREPI)
       DO 3 N=1,NOCCJ
       INDN1=(N-1)*NOCCI+I+IOFFZ1
       INDN2=(M-1)*NOCCJ+N+IOFFZ2
       F(INDF)=F(INDF)-Z(INDN2,INDN1)
3      CONTINUE
       ELSE IF(IRREPI.EQ.IRREPJ) THEN
       IOFFZ2=IPZ(IRREPJ)
       DO 4 N=1,M-1
       INDN1=(N-1)*NOCCI+I+IOFFZ1
       INDN2=N+((M-1)*(M-2))/2+IOFFZ2 
       F(INDF)=F(INDF)-Z(INDN2,INDN1)
4      CONTINUE
       DO 5 N=M+1,NOCCJ
       INDN1=(N-1)*NOCCI+I+IOFFZ1
       INDN2=M+((N-1)*(N-2))/2+IOFFZ2
       F(INDF)=F(INDF)+Z(INDN2,INDN1)
5      CONTINUE
       ENDIF
20     CONTINUE
      IOFFZ1=IOFFZ1+NOCCI*NOCCJ
100   CONTINUE
      RETURN
      END
