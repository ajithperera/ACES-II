      SUBROUTINE TPDIJAB0_R(ICORE,MAXCOR,IUHF,L0,LISTR1,LISTR1OFF,
     &                      LISTR2)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL MBPT2,CC
      INTEGER DIRPRD,DISSYG,POP,VRT
      DOUBLE PRECISION L0
      DIMENSION ICORE(MAXCOR)
      DIMENSION I0T(2),I0R(2)
C
C CALCULATION OF THE `ZEROTH' IJAB CONTRIBUTION TO
C THE EOM-CCSD TWO-PARTICLE DENSITY MATRIX
C
C  1/4 P(IJ) P(AB) RTAU(IJ,AB) L0
C
C Where RTAU(IJ,AB) = R(IJ,AB) + R(I,A) T(J,B) + T(I,A) R(J,B)
C
C                              - R(I,B) T(J,A) - R(J,A) T(I,A)
C NOTE THAT 4*G(IJ,AB) IS CALCULATED
C
CEND
C
C CODED JG OCTOBER/93
C
      COMMON/REFTYPE/MBPT2,CC
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      DATA ONE,TWO /1.0D0,2.D0/
C
C READ IN T1 AMPLITUDES
C
      I0T(1)=1
      I0T(2)=I0T(1)+IRPDPD(1,9)*IINTFP*IUHF
      I0R(1)=I0T(2)+IRPDPD(1,10)*IINTFP
      I0R(2)=I0R(1)+IRPDPD(IRREPX,9)*IINTFP*IUHF
      ISTART=I0R(2)+IRPDPD(IRREPX,10)*IINTFP
C
      CALL GETLST(ICORE(I0T(1)),1,1,1,1,90)
      CALL GETLST(ICORE(I0R(1)),1,1,1,LISTR1OFF+1,LISTR1)
C
      IF(IUHF.EQ.1) THEN
       CALL GETLST(ICORE(I0T(2)),1,1,1,2,90)
       CALL GETLST(ICORE(I0R(2)),1,1,1,LISTR1OFF+2,LISTR1)
      ENDIF
C
      DO 1000 ISPIN=3-2*IUHF,3
C
       DO 200 IRREP=1,NIRREP
C
        LISTG =113+ISPIN
        LISTT = LISTR2 - 1 + ISPIN
        NUMSYG=IRPDPD(IRREP,ISYTYP(2,LISTG))
        DISSYG=IRPDPD(IRREP,ISYTYP(1,LISTG))
C
        I000=ISTART
        IEND=I000+IINTFP*NUMSYG*DISSYG
        IF(IEND.GE.MAXCOR) CALL INSMEM('TPDABIJ1',IEND,MAXCOR)
C
        CALL GETLST(ICORE(I000),1,NUMSYG,1,IRREP,LISTT)
C
C FORM RTAU AMPLITUDES
C
        IF(ISPIN.LT.3) THEN
         CALL DTAU(IRREP,IRREP,1,1,ICORE(I000),ICORE(I0T(ISPIN)),
     &             ICORE(I0T(ISPIN)),ICORE(I0R(ISPIN)),
     &             ICORE(I0R(ISPIN)),ISPIN,ONE)
        ELSE
         CALL DTAU(IRREP,IRREP,1,1,ICORE(I000),ICORE(I0T(1)),
     &             ICORE(I0T(2)),ICORE(I0R(1)),ICORE(I0R(2)),3,ONE)
        ENDIF
        CALL SSCAL(NUMSYG*DISSYG,L0,ICORE(I000),1)
        CALL GETLST(ICORE(IEND),1,NUMSYG,1,IRREP,LISTG)
        CALL SAXPY(NUMSYG*DISSYG,-ONE,ICORE(IEND),1,ICORE(I000),1)
        CALL PUTLST(ICORE(I000),1,NUMSYG,1,IRREP,LISTG)
C
200    CONTINUE
C
1000  CONTINUE
C
      TWO=0.D0
      IF(IUHF.EQ.0)
     &   CALL CHECKGAM1(ICORE,16,116,TWO,IUHF,2,VRT)
      IF(IUHF.EQ.1) THEN
       CALL CHECKGAM(ICORE,16,116,TWO)
       CALL CHECKGAM(ICORE,14,114,TWO)
       CALL CHECKGAM(ICORE,15,115,TWO)
      ENDIF
C
      RETURN
      END
