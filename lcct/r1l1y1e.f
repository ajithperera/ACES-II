      SUBROUTINE R1L1Y1E(F1,L1,R1,Y1,ICORE,MAXCOR,IUHF)
C
C Y1(a,i) = R(me)*F(ie)*L(a,m)
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH
      DIMENSION ICORE(MAXCOR),R1(*),F1(*),I0X(2),I0F(2),I0R(2),L1(*),
     &          Y1(*)
      COMMON/STATSYM/IRREPX
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMLOC/ISYMOFF(8,8,25)
C
      DATA ONE,ONEM,ZILCH/1.0D0,-1.0D0,0.0D0/
C
C CALCULATE X(mi) = R(me)*F(ie) INTERMEDIATES
C
      I0X(1)=1
      I0F(1)=1
      I0R(1)=1
      IF(IUHF.NE.0)THEN
       I0X(2)=I0X(1)+IINTFP*IRPDPD(IRREPX,21)
       I0F(2)=I0F(1)+IINTFP*NT(1)
       I0R(2)=I0R(1)+IINTFP*IRPDPD(IRREPX,9)
      ELSE
       I0X(2)=I0X(1)
       I0F(2)=I0F(1)
       I0R(2)=I0R(1)
      ENDIF
      I000=I0X(2)+IINTFP*IRPDPD(IRREPX,22)
      DO 5 ISPIN=1,1+IUHF
       DO 6 IRREPI=1,NIRREP
        IRREPM=DIRPRD(IRREPI,IRREPX)
        IRREPE=IRREPI
        NUMM=POP(IRREPM,ISPIN)
        NUMI=POP(IRREPI,ISPIN)
        NUME=VRT(IRREPE,ISPIN)
        NROW=NUMM
        NCOL=NUMI
        NSUM=NUME
        IOFFR=I0R(ISPIN)+IINTFP*(ISYMOFF(IRREPM,IRREPX,8+ISPIN)-1)
        IOFFF=I0F(ISPIN)+IINTFP*(ISYMOFF(IRREPI,1,8+ISPIN)-1)
        IOFFX=I0X(ISPIN)+IINTFP*(ISYMOFF(IRREPI,IRREPX,20+ISPIN)-1)
        CALL XGEMM('T','N',NROW,NCOL,NSUM,ONE,R1(IOFFR),NSUM,
     &             F1(IOFFF),NSUM,ZILCH,ICORE(IOFFX),NROW)
6      CONTINUE
5     CONTINUE
C
      IOFFL=1
      IOFFY=1
      DO 10 ISPIN=1,1+IUHF
       IOFFX = I0X(ISPIN)
C
C FORM PRODUCT Y1(ai) = -L(am)*X(mi)
C
       DO 11 IRREP=1,NIRREP
        NUMA=VRT(IRREP,ISPIN)
        NUMI=POP(IRREP,ISPIN)
        CALL XGEMM('N','N',NUMA,NUMI,NUMI,ONEM,L1(IOFFL),NUMA,
     &             ICORE(IOFFX),NUMI,ONE,Y1(IOFFY),NUMA)
        IOFFL=IOFFL+IINTFP*NUMA*NUMI
        IOFFX=IOFFX+IINTFP*NUMI*NUMI
        IOFFY=IOFFY+IINTFP*NUMA*NUMI
11     CONTINUE
C
10    CONTINUE
      return
      end
