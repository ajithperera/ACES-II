      SUBROUTINE CALCXI(ICORE,MAXCOR,IUHF,LISTL1,LISTL1OFF,LISTL2,
     &   LISTL2RS,LISTR1,LISTR1OFF,LISTR2,LISTR2RS,LISTZ1,LISTZ1OFF,
     &   LISTZ2,LISTTMP,LISTTMP2)
C
C DRIVER FOR EVALUATION OF THE  XI AMPLITUDES INVOLVED IN EOM-CC GRADIENT
C THEORY  XI = <0|L Hbar|Q><Q|R|P> [SEE J.F. STANTON, JCP XX, XXXX (1993)]
C
C
C PROGRAMMED BY J.F. STANTON, AUSTIN, 1993
C
CEND
      IMPLICIT INTEGER(A-Z)
      DOUBLE PRECISION ONE,ZILCH,XINORM,ZETANORM,SNRM2,SQRT
      LOGICAL MBPT2,CC
      DIMENSION ICORE(MAXCOR)
      COMMON/STATSYM/IRREPX
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      LOGICAL SS, SD, DS, DD
      COMMON/DRVHBAR/SS, SD, DS, DD
C
      DATA ONE,ZILCH/1.0D0,0.0D0/
C
C ZERO OUT XI AMPLITUDE LISTS AND CALCULATE TOTAL LENGTH OF VECTOR
C
      LENTOT=0
      DO 10 ISPIN=3,3-2*IUHF,-1
       LIST=60+ISPIN
       CALL ZEROLIST(ICORE,MAXCOR,LIST)
       LENTOT=LENTOT+ISYMSZ(ISYTYP(1,LIST),ISYTYP(2,LIST))
10    CONTINUE
      DO 11 ISPIN=1,1+IUHF
       LENTOT=LENTOT+NT(ISPIN)
       CALL ZERO(ICORE,NT(ISPIN))
       CALL PUTLST(ICORE,1,1,1,ISPIN+2,90)
11    CONTINUE
C
      CALL UPDMOI(1,LENTOT,1,474,0,0)
      CALL UPDMOI(1,LENTOT,1, 88,0,0)
C
C EVALUATE XI AMPLITUDES
C
      CALL R1L0Y1(ICORE,MAXCOR,IUHF,LISTR1,LISTR1OFF)
      CALL R1L1Y1(ICORE,MAXCOR,IUHF,LISTL1,LISTL1OFF,LISTR1,LISTR1OFF)
      CALL R1L2Y1(ICORE,MAXCOR,IUHF,LISTL2,LISTR1,LISTR1OFF)
      CALL R1L1Y2(ICORE,MAXCOR,IUHF,LISTL1,LISTL1OFF,LISTR1,LISTR1OFF)
      CALL R1L2Y2(ICORE,MAXCOR,IUHF,LISTL2,LISTL2RS,LISTR1,LISTR1OFF,
     &   LISTR2,LISTR2RS)
      CALL R2L1Y1(ICORE,MAXCOR,IUHF,LISTL1,LISTL1OFF,LISTR2,LISTR2RS)
      CALL R2L2Y1(ICORE,MAXCOR,IUHF,LISTL2,LISTL2RS,LISTR2,LISTR2RS,
     &   LISTTMP,LISTTMP2)
      CALL R2L2Y2(ICORE,MAXCOR,IUHF,LISTL2,LISTL2RS,LISTR2,LISTR2RS)
      XINORM = ZILCH
      ZETANORM = ZILCH
      IOFF=1

      DO 12 ISPIN=1,1+IUHF
         CALL GETLST(ICORE(IOFF),1,1,1,2+ISPIN,90)
         XINORM = XINORM + SNRM2(NT(ISPIN),ICORE(IOFF),1)**2
         IOFF=IOFF+NT(ISPIN)*IINTFP
 12   CONTINUE
      IOFF2=IOFF
      DO 13 ISPIN=3,3-2*IUHF,-1
         LIST=60+ISPIN
         LENGTH=ISYMSZ(ISYTYP(1,LIST),ISYTYP(2,LIST))
         CALL GETALL(ICORE(IOFF),LENGTH,1,LIST)
         IOFF=IOFF+LENGTH*IINTFP
 13   CONTINUE
C     Add in the contraction <0|L3 W|D>
      xinorm=zilch
      DO 14 ISPIN=3,3-2*IUHF,-1
         LIST=200+ISPIN
         LENGTH=ISYMSZ(ISYTYP(1,LIST),ISYTYP(2,LIST))
         CALL GETALL(ICORE(IOFF),LENGTH,1,LIST)
         CALL SAXPY(LENGTH,ONE,ICORE(IOFF),1,ICORE(IOFF2),1)
         XINORM = XINORM + SNRM2(LENGTH,ICORE(IOFF2),1)**2
         IOFF2=IOFF2+LENGTH*IINTFP
 14   CONTINUE

      CALL PUTLST(ICORE,1,1,1,1,474)
      WRITE(6,3000)SQRT(XINORM)
C
C SOLVE ZETA EQUATIONS
C
      call hbardiag(1,icore,maxcor/iintfp,iuhf,2)
      CALL DOZETA(ICORE,MAXCOR,IUHF,LENTOT,LISTZ1,LISTZ1OFF,LISTZ2)

      CALL GETLST  (ICORE,1,1,1,1,474)
      ZETANORM=SNRM2(LENTOT,ICORE,1)**2
      WRITE(6,3001)sqrt(zetanorm)
C
C
      RETURN
 3000 FORMAT(T3,'@CALCXI-I, Norm of xi amplitudes: ',F15.10,'.')
 3001 FORMAT(T3,'@CALCXI-I, Norm of zeta amplitudes: ',F15.10,'.')
      END
