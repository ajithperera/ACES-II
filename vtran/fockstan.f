      SUBROUTINE FOCKSTAN(F,BUF,NBAS,NIRREP,IUHF)
C
C THIS ROUTINE CREATES THE VARIOUS FOCK MATRIX LISTS AND
C  WRITES THEM TO MOINTS.
C
      IMPLICIT INTEGER (A-Z)
C
      CHARACTER*1 SP(2)
      DOUBLE PRECISION F(NBAS,NBAS),BUF(3*NBAS*NBAS),ONEM
      DIMENSION POP(8),VRT(8)
C
      COMMON /INFO/ NOCCO(2),NVRTO(2)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      DATA ONEM /-1.0/
      DATA SP /'A','B'/
C
      DO 1000 ISPIN=1,1+MIN(1,IUHF)
       CALL GETREC(20,'JOBARC','SYMPOPO'//SP(ISPIN),NIRREP,POP)
       CALL GETREC(20,'JOBARC','SYMPOPV'//SP(ISPIN),NIRREP,VRT)
C
       IOFFBF=NBAS*NBAS+1
       CALL GETREC(20,'JOBARC','FOCK'//SP(ISPIN),NBAS*NBAS*IINTFP,F)
       CALL AO2MO3(F,F,BUF,BUF(IOFFBF),NBAS,NBAS,ISPIN)
C
C   GET RID OF DIAGONAL ELEMENTS AFTER WRITING THEM TO JOBARC
C
       IF(ISPIN.EQ.1)THEN
        CALL SCOPY(NBAS,F,NBAS+1,BUF,1)
        CALL PUTREC(20,'JOBARC','ROHFEVLA',NBAS*IINTFP,BUF)
        CALL SAXPY(NBAS,ONEM,F,NBAS+1,F,NBAS+1)
       ELSEIF(ISPIN.EQ.2)THEN
        CALL SCOPY(NBAS,F,NBAS+1,BUF,1)
        CALL PUTREC(20,'JOBARC','ROHFEVLB',NBAS*IINTFP,BUF)
        CALL SAXPY(NBAS,ONEM,F,NBAS+1,F,NBAS+1)
       ENDIF
C
C FORM SYMMETRY PACKED OCCUPIED-OCCUPIED PART.
C
       ITHRU=0
       IOFF=0
       DO 10 IRREP=1,NIRREP
        DO 20 J=1,POP(IRREP)
         DO 21 I=1,POP(IRREP)
          ITHRU=ITHRU+1
          BUF(ITHRU)=F(I+IOFF,J+IOFF)
21       CONTINUE
20      CONTINUE
        IOFF=IOFF+POP(IRREP)
10     CONTINUE
       CALL PUTREC(20,'JOBARC','STANFIJ'//SP(ISPIN),ITHRU*IINTFP,BUF)
C
C FORM SYMMETRY PACKED VIRTUAL-VIRTUAL PART.
C
       ITHRU=0
       IOFF=NOCCO(ISPIN)
       DO 110 IRREP=1,NIRREP
        DO 120 J=1,VRT(IRREP)
         DO 121 I=1,VRT(IRREP)
          ITHRU=ITHRU+1
          BUF(ITHRU)=F(I+IOFF,J+IOFF)
121      CONTINUE
120     CONTINUE
        IOFF=IOFF+VRT(IRREP)
110    CONTINUE
       CALL PUTREC(20,'JOBARC','STANFAB'//SP(ISPIN),ITHRU*IINTFP,BUF)
C
C FORM SYMMETRY PACKED VIRTUAL-OCCUPIED PART, but only for the beta
C Fock matrix.
C
       ITHRU=0
       IOFFA=NOCCO(ISPIN)
       IOFFI=0
       DO 1210 IRREP=1,NIRREP
         DO 1220 I=1,POP(IRREP)
          DO 1221 A=1,VRT(IRREP)
           ITHRU=ITHRU+1
           BUF(ITHRU)=F(A+IOFFA,I+IOFFI)
1221      CONTINUE
1220     CONTINUE
         IOFFI=IOFFI+POP(IRREP)
         IOFFA=IOFFA+VRT(IRREP)
1210   CONTINUE
       CALL PUTREC(20,'JOBARC','STANFAJ'//SP(ISPIN),ITHRU*IINTFP,BUF)
C
 1000 CONTINUE
C
      RETURN
      END 
