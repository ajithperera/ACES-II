 
 
 


      SUBROUTINE SYMVEC(LHTYPE,RHTYPE,LHSPIN,RHSPIN,STOMOD,
     &                  NOCCO,NVRTO,DIRPRD,IRREP,
     &                  NIRREP,NORB,NUMIRR,WATTS,ISIZE,LABEL)
C
C CONSTRUCTS THE SYMMETRY VECTORS.
C
      IMPLICIT INTEGER (A-Z)
      CHARACTER*5 LABEL
      CHARACTER*3 LHTYPE,RHTYPE
      CHARACTER*1 ADD
      DIMENSION DIRPRD(8,8),IRREP(NORB,2),
     &          NUMIRR(NIRREP),WATTS(ISIZE*2),NOCCO(2),NVRTO(2)
      COMMON /FILES/ LUOUT,MOINTS
      IF(LHTYPE.EQ.'OCC')THEN
       NORBL=NOCCO(LHSPIN)
       NOFFL=0
      ELSEIF(LHTYPE.EQ.'VRT')THEN
       NORBL=NVRTO(LHSPIN)
       NOFFL=NOCCO(LHSPIN)
      ENDIF
      IF(RHTYPE.EQ.'OCC')THEN
       NORBR=NOCCO(RHSPIN)
       NOFFR=0
      ELSEIF(RHTYPE.EQ.'VRT')THEN
       NORBR=NVRTO(RHSPIN)
       NOFFR=NOCCO(RHSPIN)
      ENDIF
C
C CASE 1.  I<J OR A<B LISTS (STOMOD=0) OR I<=J OR A<=B LISTS (STOMOD=1).
C
      IF(STOMOD.LE.1)THEN
       IIN=0
       NTOT=0
       DO 5 NIR=1,NIRREP
        NUMDIS=0
        DO 10 I=1+NOFFR,NOFFR+NORBR
         DO 15 J=1+NOFFR,I-1+STOMOD
          NUMDIS=NUMDIS+1
          IRXXXI=IRREP(I,RHSPIN)
          IRXXXJ=IRREP(J,RHSPIN)
          IR=DIRPRD(IRXXXI,IRXXXJ) 
          IF(IR.EQ.NIR)THEN
           IIN=IIN+1
           WATTS(IIN)=NUMDIS
          ENDIF
15       CONTINUE
10      CONTINUE
        NUMIRR(NIR)=IIN-NTOT
        NTOT=IIN
5      CONTINUE
       IF(IIN.NE.ISIZE)THEN
        WRITE(LUOUT,100)
100     FORMAT(T3,'@SYMLST-F, Error in generating symmetry vectors.')
        CALL ERREX
       ENDIF
C
C CASE 2. I,J OR A,B LISTS.
C
      ELSEIF(STOMOD.EQ.2)THEN
       IIN=0
       NTOT=0
       DO 105 NIR=1,NIRREP
        NUMDIS=0
        DO 110 I=1+NOFFR,NOFFR+NORBR
        DO 115 J=1+NOFFL,NOFFL+NORBL
         NUMDIS=NUMDIS+1
         IRXXXI=IRREP(I,RHSPIN)
         IRXXXJ=IRREP(J,LHSPIN)
         IR=DIRPRD(IRXXXI,IRXXXJ)
         IF(IR.EQ.NIR)THEN
          IIN=IIN+1
          WATTS(IIN)=NUMDIS
         ENDIF
115      CONTINUE
110     CONTINUE
        NUMIRR(NIR)=IIN-NTOT
        NTOT=IIN
105    CONTINUE
      ENDIF
      IF(STOMOD.EQ.0)ADD='0'
      IF(STOMOD.EQ.1)ADD='1'
      IF(STOMOD.EQ.2)ADD='2'
      CALL PUTREC(20,'JOBARC',LABEL//ADD//'  ',ISIZE,WATTS)
      CALL PUTREC(20,'JOBARC',LABEL//ADD//'X ',NIRREP,NUMIRR)
C
C GENERATE INVERSE VECTOR
C
      DO 200 I=1,ISIZE
       ITMP=WATTS(I)
       WATTS(ISIZE+ITMP)=I
200   CONTINUE
      CALL PUTREC(20,'JOBARC',LABEL//ADD//'I ',ISIZE,WATTS(ISIZE+1))
      RETURN
      END
