      SUBROUTINE INTRN3F(EVEC,AOINT,BUF1,RHF,NBASIS,
     &                   NBAS,NMO,NSTART,NEND,ISIZE,
     &                   IOFFN,NSIZE,ISPIN,IREORD)
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL RHF
      CHARACTER*80 FNAME
      INTEGER DIRPRD,DISZAO,DISZMO
C
      DIMENSION EVEC(1),AOINT(1),BUF1(1),BUF2(1),IREORD(1)
      DIMENSION NBAS(8),NMO(8),ISIZE(8),IOFFN(8,2)
      DIMENSION NSTART(8),NEND(8)
C
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/AOOFST/INDOCC(8,2)   
      COMMON/SYMINF/NDUMMY,NIRREP,IRREPA(255,2),DIRPRD(8,8)
C
      DATA AZERO,ONE/0.D0,1.D0/
C
      IF(.NOT.RHF.AND.ISPIN.EQ.1) THEN
       CALL GFNAME('HALFT   ',FNAME,ILENGTH)
      ENDIF
C
C LOOP OVER ALL IRREPS
C
      IOFF2=1
      IOFF3=1
C
      DO 1000 IRREP1=1,NIRREP-1
C
       NMO1=NMO(IRREP1)
       NAO1=NBAS(IRREP1)
       NSTART1=NSTART(IRREP1)
       NEND1=NEND(IRREP1)
C
       DO 2000 IRREP2=IRREP1+1,NIRREP
C
        IOFF3I=IOFF3
C
        NAO2=NBAS(IRREP2)
        NMO2=NMO(IRREP2)
C
        DISZAO=NAO1*NAO2
        NUMDIS=NMO2*(NEND1-NSTART1+1)

#ifdef _DEBUG_LVLM
       Write(6,*)
       write(*,"(t3,a,a,3(1x,i2))"), "@INTRN3: The 1-index",
     &      " transformed Ints for irrep at entry 2 intrn3",
     &       Irrep1, irrep2,ispin
       Ioff = 0
       Do KL = 1, DISZAO
          Write(6,*), "Printing the distribution No. ", KL
          Write(*,'(4(1X,F12.7)))'), (Aoint(Ioff + I), I=1, NAO2*NMO2)
          Ioff = Ioff + NAO2*NMO2
       End Do
#endif
C
C LOOP OVER ALL OCCUPIED ORBITALS
C
        IOFFC2=INDOCC(IRREP2,ISPIN)
C
        DO 100 IMO=NSTART1,NEND1
C
C  TRANSFORM SECOND INDEX  (Mu I | SIGMA RHO) --> (P I | SIGMA RHO )
C
         CALL XGEMM('T','N',NMO2,DISZAO,NAO2,ONE,EVEC(IOFFC2),
     &              NAO2,AOINT(IOFF2),NAO2,AZERO,BUF1,NMO2)
C
         CALL TRANSP(BUF1,AOINT(IOFF3),DISZAO,NMO2)
C
         IOFF2=IOFF2+NAO2*DISZAO
         IOFF3=IOFF3+NMO2*DISZAO
C
100     CONTINUE
C
C  NOW WE HAVE THE (PI,SIGMA RHO) INTEGRALS ORDERED AS   X X P I 
C
C  FOR UHF, WRITE INTEGRALS TO DISK SINCE WE WILL USE THEM AGAIN
C
#ifdef _DEBUG_LVLM
       Write(6,*)
       write(*,"(t3,a,a,2(1x,i2))"), "@INTRN3: The 2-index",
     &      " transformed Ints for irrep after stage 2 intrn3",
     &       Irrep, ispin
       Ioff = ioff3i
       Do KL = 1, NUMDIS
          Write(6,*), "Printing the distribution No. ", KL
          Write(*,'(4(1X,F12.7)))'), (Aoint(Ioff + I-1), I=1, diszao)
          Ioff = Ioff + Diszao
       End Do
#endif
        IF(.NOT.RHF.AND.ISPIN.EQ.1) THEN
C
         OPEN(UNIT=79,FORM='UNFORMATTED',STATUS='UNKNOWN',
     &        ACCESS='SEQUENTIAL',FILE=FNAME(1:ILENGTH))
         REWIND(UNIT=79)
C
         IOFFW=IOFF3I
         DO 101 I=1,NUMDIS
          WRITE(79) (AOINT(IND),IND=IOFFW,IOFFW+DISZAO-1)
          IOFFW=IOFFW+DISZAO
101      CONTINUE
C 
         REWIND(UNIT=79)
C
        ENDIF
C
C NOW TRANSFORM NEW LHS TO RS, SPIN CASE ISPIN,ISPIN
C
        IOFF4=IOFF3I
        IOFF5=IOFF3I
C
        DO 200 IDIS=1,NUMDIS
C
C
         IOFFC1=INDOCC(IRREP1,ISPIN)
         IOFFC2=INDOCC(IRREP2,ISPIN)
C
         CALL GHTRAN(AOINT(IOFF4),AOINT(IOFF5),EVEC,IOFFC1,
     &               IOFFC2,1,1,BUF1,0,1,NAO1,NAO2,NMO1,NMO2)
C
          IOFF4=IOFF4+NAO1*NAO2
          IOFF5=IOFF5+NMO1*NMO2
C
200      CONTINUE
C
C DUMP INTEGRALS TO HF2
C
        ILNBUF=600
        IOFF=1+ILNBUF
        CALL DHF23F(AOINT(IOFF3I),BUF1,BUF1(IOFF),ILNBUF,IRREP1,
     &             IRREP2,NMO1,NMO2,NSTART1,NEND1,IOFFN(1,ISPIN),
     &             IOFFN(1,ISPIN),IREORD,RHF,ISPIN)
C
        IF(.NOT.RHF.AND.ISPIN.EQ.1) THEN
C
         IOFFW=IOFF3I
         DO 1101 I=1,NUMDIS
          READ(79) (AOINT(IND),IND=IOFFW,IOFFW+DISZAO-1)
          IOFFW=IOFFW+DISZAO
1101     CONTINUE
C
         CLOSE(UNIT=79,STATUS='DELETE')
C
C NOW TRANSFORM NEW LHS TO RS, SPIN CASE ISPIN,ISPIN
C
         IOFF4=IOFF3I
         IOFF5=IOFF3I
C
         DO 1200 IDIS=1,NUMDIS
C
          IOFFC1=INDOCC(IRREP1,2)
          IOFFC2=INDOCC(IRREP2,2)
C
          CALL GHTRAN(AOINT(IOFF4),AOINT(IOFF5),EVEC,IOFFC1,
     &                IOFFC2,1,1,BUF1,
     &                0,1,NAO1,NAO2,NMO1,NMO2)
C
          IOFF4=IOFF4+NAO1*NAO2
          IOFF5=IOFF5+NMO1*NMO2
C
1200     CONTINUE
C
C DUMP INTEGRALS TO HF2
C
         ILNBUF=600
         IOFF=1+ILNBUF
         CALL DHF23F(AOINT(IOFF3I),BUF1,BUF1(IOFF),ILNBUF,IRREP1,
     &               IRREP2,NMO1,NMO2,NSTART1,NEND1,IOFFN(1,1),
     &               IOFFN(1,2),IREORD,RHF,3)
C
        ENDIF
2000   CONTINUE
1000  CONTINUE
C
      RETURN
      END
