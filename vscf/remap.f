


      SUBROUTINE REMAP(CMPPGP,IORDGP,NATOMS,NORBCMP,COORD,
     &                 SCR,IMEMCMP,IPOPCMP,IMAP)
C
C THIS ROUTINE FORMS A POINTER VECTOR RELATING THE ATOMIC ORDERING
C  USED IMPLICITY BY VMOL TO THAT SPECIFIED IN THE Z-MATRIX.  THIS
C  IS RATHER EASILY CALCULATED FROM INFORMATION RESIDENT IN THE JOBARC
C  FILE.  UPON RETURN, THE INTEGER VECTOR IMAP CONTAINS THE 
C  Z-MATRIX POSITION OF THE ATOMS, AS INDEXED BY THE VMOL ORDERING.
C  FOR EXAMPLE, IF IMAP(3)=11, THIS MEANS THAT THE 3RD ATOM USED BY
C  VMOL IS ACTUALLY THE ELEVENTH ATOM IN THE Z-MATRIX.
C
CEND
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL QSAME
      CHARACTER*4 CMPPGP
      DIMENSION COORD(3,NATOMS),IMEMCMP(NATOMS),IPOPCMP(NORBCMP)
      DIMENSION IMAP(NATOMS),SCR(3*NATOMS)
      CALL IZERO(IMAP,NATOMS)
C
C LOOP OVER ORBITS IN COMPUTATIONAL POINT GROUP
C
      IOFF=1
      IATOM=1
      DO 10 IORBIT=1,NORBCMP
C
C DETERMINE THE REFERENCE ATOM FOR THE ORBIT (THE ONE USED BY VMOL
C   TO GENERATE THE REDUNDANT CENTERS)
C 
       INUMATM=IPOPCMP(IORBIT)
       IREFATM=IMEMCMP(IOFF)
       IMAP(IATOM)=IREFATM
       IATOM=IATOM+1
C
C NOW USE THE ALGORITHM OF VMOL TO GENERATE THE REDUNDANT CENTERS
C   IN THE COMPUTATIONAL POINT GROUP.  THE VALUE OF NGEN IS THE
C   NUMBER OF DISTINCT REDUNDANT CENTERS RETURNED
C
       CALL VMLGEN(CMPPGP,IORDGP,COORD(1,IREFATM),SCR,NGEN)
C
C NOW LOOP OVER DISTINCT REDUNDANT CENTERS AND FIND THEIR 
C  POSITION IN THE Z-MATRIX ORDER
C
       DO 11 ICENT=1,NGEN
        ILOC=-1
        IOFF1=IOFF+1
        DO 20 I=1,INUMATM
         IPOS=IMEMCMP(IOFF1)
         IF(QSAME(SCR(1+(ICENT-1)*3),COORD(1,IPOS)))ILOC=IPOS
         IOFF1=IOFF1+1
20      CONTINUE
        IMAP(IATOM)=ILOC
        IATOM=IATOM+1
11     CONTINUE
       IOFF=IOFF+INUMATM
10    CONTINUE
      RETURN
      END
