      SUBROUTINE T2FULAO(QORIG,QTRAN,ISCR,NAO,NMO,NAO1,
     &                   LABEL1,LABEL2,LABEL3,LABEL4,IRE,
     &                   ICHOICE)
C
C THIS ROUTINE TRANSFORMS A QUANTITY FROM THE SO BASIS
C TO THE FULL (NON-SYMMETRY-ADAPTED AND CARTESIAN) AO BASIS.
C
CEND
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      CHARACTER*8 LABEL1,LABEL2,LABEL3,LABEL4
      DIMENSION QORIG(NAO,NMO),QTRAN(NAO1,NMO,2),ISCR(1)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      DATA ONE /1.0/
      DATA ZILCH /0.0/
C
C PICK UP QUANTITY TO BE TRANSFORMED FROM JOBARC
C
      CALL GETREC(20,'JOBARC',LABEL1,IINTFP*NAO*NMO,QORIG)
C
C PICK UP TRANSFORMATION MATRIX 
C
      IF(ICHOICE.EQ.0)THEN
       CALL GETREC(20,'JOBARC','CMP2ZMAT',IINTFP*NAO1*NAO,ISCR)
       CALL XGEMM('N','N',NAO1,NMO,NAO,ONE,ISCR,NAO1,
     &            QORIG,NAO,ZILCH,QTRAN,NAO1)
      ELSE
       CALL GETREC(20,'JOBARC','ZMAT2CMP',IINTFP*NAO1*NAO,ISCR)
       CALL XGEMM('N','N',NAO,NAO1,NMO,ONE,QORIG,NAO,
     &            ISCR,NMO,ZILCH,QTRAN,NAO)
      ENDIF
C
C PERFORM TRANSFORMATION   TRANS*QORIG
C
      IF(IRE.NE.0)THEN
C
C NOW PICK UP NMO LENGTH QUANTITY UPON WHICH THE VECTORS ARE
C TO BE SORTED
C
       CALL GETREC(20,'JOBARC',LABEL3,IINTFP*NMO,QORIG)
       DO 10 I=1,NMO
        ISCR(I)=I
10     CONTINUE
       CALL PIKSR2(NMO,QORIG,ISCR)
       CALL PUTREC(20,'JOBARC',LABEL4,IINTFP*NMO,QORIG)
C
C NOW REORDER
C
       DO 11 ITO=1,NMO
        IFROM=ISCR(ITO)
        CALL SCOPY(NAO1,QTRAN(1,IFROM,1),1,QTRAN(1,ITO,2),1)
11     CONTINUE 
       CALL SCOPY(NAO1*NMO,QTRAN(1,1,2),1,QTRAN,1)
C
C WRITE TRANSFORMED MATRIX TO LABEL2
C
      ENDIF
      If (ICHOICE .EQ. 0) THEN
         CALL PUTREC(20,'JOBARC',LABEL2,IINTFP*NAO1*NMO,QTRAN)
      ELSE
         CALL PUTREC(20,'JOBARC',LABEL2,IINTFP*NAO*NAO1,QTRAN)
      ENDIF
C
      RETURN
      END
