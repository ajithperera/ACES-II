
      SUBROUTINE VMLGEN(PGRP,IORDGP,QREF,QGEN,IGEN)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL QSAME
      CHARACTER*4 PGRP
      DIMENSION QREF(3),QGEN(3,IORDGP-1)
      DO 10 I=1,IORDGP-1
       CALL SCOPY(3,QREF,1,QGEN(1,I),1)
10    CONTINUE
      IF(PGRP(1:3).EQ.'C2v')THEN
       QGEN(1,1)=-QGEN(1,1)
       QGEN(2,2)=-QGEN(2,2)
       QGEN(1,3)=-QGEN(1,3)
       QGEN(2,3)=-QGEN(2,3)
      ELSEIF(PGRP(1:3).EQ.'C2h')THEN
       QGEN(3,1)=-QGEN(3,1)
       QGEN(1,2)=-QGEN(1,2)
       QGEN(2,2)=-QGEN(2,2)
       QGEN(1,3)=-QGEN(1,3)
       QGEN(2,3)=-QGEN(2,3)
       QGEN(3,3)=-QGEN(3,3)
      ELSEIF(PGRP(1:3).EQ.'D2 ')THEN 
       QGEN(1,1)=-QGEN(1,1)
       QGEN(2,1)=-QGEN(2,1)
       QGEN(1,2)=-QGEN(1,2)
       QGEN(3,2)=-QGEN(3,2)
       QGEN(2,3)=-QGEN(2,3)
       QGEN(3,3)=-QGEN(3,3)
      ELSEIF(PGRP(1:3).EQ.'C2 ')THEN
       QGEN(1,1)=-QGEN(1,1)
       QGEN(2,1)=-QGEN(2,1)
      ELSEIF(PGRP(1:3).EQ.'C s')THEN
       QGEN(3,1)=-QGEN(3,1)
      ELSEIF(PGRP(1:3).EQ.'C i')THEN
       QGEN(1,1)=-QGEN(1,1)
       QGEN(2,1)=-QGEN(2,1)
       QGEN(3,1)=-QGEN(3,1)
      ELSEIF(PGRP(1:3).EQ.'D2h')THEN
C
C THIS COULD WELL BE WRONG!!!! CHECK!!!
C
       QGEN(1,1)=-QGEN(1,1)
       QGEN(2,2)=-QGEN(2,2)
       QGEN(1,3)=-QGEN(1,3)
       QGEN(2,3)=-QGEN(2,3)
       QGEN(3,4)=-QGEN(3,4)
       QGEN(1,5)=-QGEN(1,5)
       QGEN(3,5)=-QGEN(3,5)
       QGEN(2,6)=-QGEN(2,6)
       QGEN(3,6)=-QGEN(3,6)
       QGEN(1,7)=-QGEN(1,7)
       QGEN(2,7)=-QGEN(2,7)
       QGEN(3,7)=-QGEN(3,7)
      ENDIF
C
C NOW NOT ALL OF THESE CENTERS WILL BE UNIQUE.  DETERMINE WHICH ONES
C  ARE UNIQUE AND STUFF THESE POSITIONS AT THE BOTTOM OF QGEN
C
      IGEN=0 
      DO 20 I=1,IORDGP-1
C
C SEE IF THE GENERATED CENTER IS EQUAL TO THE REFERENCE CENTER
C
       IF(QSAME(QREF,QGEN(1,I)))GOTO 20
       DO 22 J=1,I-1
C
C SEE IF THE GENERATED CENTER IS EQUAL TO A PREVIOUSLY GENERATED
C   REDUNDANT CENTER
C
        IF(QSAME(QGEN(1,J),QGEN(1,I)))GOTO 20
22     CONTINUE
C
C THIS IS A UNIQUE CENTER.  COPY IT TO THE FIRST AVAILABLE POSITION IN QGEN
C
       IGEN=IGEN+1
       CALL SCOPY(3,QGEN(1,I),1,QGEN(1,IGEN),1)
20    CONTINUE
      RETURN
      END
