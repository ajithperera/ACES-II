      SUBROUTINE PKSORT21(IBKNUM,ICHAIN,BUCK,IBUCK,NINBCK,BUF,IBUF,
     &                    IBKNMOF,LDIM1,IRECL,NBKINT,ILNBUF,IBKDIS,
     &                    NBUCK,IUHF)
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      PARAMETER(LUINT=10)
      PARAMETER(LUSRT1=21)
      PARAMETER(LUSRT2=22)
      PARAMETER(LUSRT3=23)
C
      INTEGER AND,OR
      CHARACTER*80 FNAME
C
      DIMENSION BUCK(NBKINT,3*NBUCK),IBUCK(NBKINT,3*NBUCK)
      DIMENSION NINBCK(3*NBUCK),ICHAIN(3*NBUCK)
      DIMENSION BUF(ILNBUF),IBUF(ILNBUF)
      DIMENSION IBKNMOF(LDIM1),IBKNUM(IBKDIS)
C
      DIMENSION IWHEREJ(1200),IWHEREK1(1200),IWHEREK2(1200)
      DIMENSION BUFJ(1200),BUFK1(1200),BUFK2(1200)
      DIMENSION IBUFJ(1200),IBUFK1(1200),IBUFK2(1200)
      DIMENSION ILOOKUP(600)
C
      DIMENSION IPKOFF(73)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /FILES/ LUOUT,MOINTS
      COMMON /FLAGS/ IFLAGS(100)
#include "symm2.com"
      COMMON /PKOFF/ IPKOFF
      COMMON /TIMEINFO/ TIMEIN, TIMENOW, TIMETOT, TIMENEW
C
      DATA TWO /2.0/
C
      IUPKI(INT)=AND(INT,IALONE)
      IUPKJ(INT)=AND(ISHFT(INT,-IBITWD),IALONE)
      IUPKK(INT)=AND(ISHFT(INT,-2*IBITWD),IALONE)
      IUPKL(INT)=AND(ISHFT(INT,-3*IBITWD),IALONE)
      IPACK(I,J,K,L)=OR(OR(OR(I,ISHFT(J,IBITWD)),ISHFT(K,2*IBITWD)),
     &                  ISHFT(L,3*IBITWD))
      INDX(I,J)=J+(I*(I-1))/2
      INDX2(I,J,N)=I+(J-1)*N
C
      CALL TIMER(1)
C
      IF(IUHF.EQ.0) THEN
        SCALE=0.25
      ELSE
        SCALE=0.5
      ENDIF
C
      NREC1=1
      NREC2=1
      NREC3=1
      CALL GFNAME('AOJ     ',FNAME,ILENGTH)
      OPEN(UNIT=LUSRT1,FILE=FNAME(1:ILENGTH),FORM='UNFORMATTED',
     &     ACCESS='DIRECT',RECL=IRECL)
      CALL TIMER(1)
      CALL ZERO(BUCK,NBKINT*3*NBUCK)
      CALL IZERO(IBUCK,NBKINT*3*NBUCK)
      CALL IZERO(NINBCK,3*NBUCK)
      CALL IZERO(ICHAIN,3*NBUCK)
      NAOBUF=0
      NAOINT=0
C
C  Read in the AO integrals one buffer at a time.  Make sure that we
C  have the indices in canonical order, i.e. i>=j, k>=l, and ij>=kl.
C
      CALL GFNAME('IIII    ',FNAME,ILENGTH)
      OPEN(LUINT,FILE=FNAME(1:ILENGTH),STATUS='OLD',FORM='UNFORMATTED',
     &     ACCESS='SEQUENTIAL')
      CALL LOCATE(LUINT,'TWOELSUP')
    1 READ(LUINT)BUF,IBUF,NUT
      NAOBUF=NAOBUF+1
      CALL ZERO(BUFJ,1200)
      CALL IZERO(IBUFJ,1200)
      CALL IZERO(IWHEREJ,1200)
      DO 10 INT=1,NUT
        IX=IUPKI(IBUF(INT))
        JX=IUPKJ(IBUF(INT))
        KX=IUPKK(IBUF(INT))
        LX=IUPKL(IBUF(INT))
        I=MAX(IX,JX)
        J=MIN(IX,JX)
        K=MAX(KX,LX)
        L=MIN(KX,LX)
        IND1=INDX(I,J)
        IND2=INDX(K,L)
        IF(IND1.LT.IND2) THEN
          IX=I
          JX=J
          I=K
          J=L
          K=IX
          L=JX
        ENDIF
        X=BUF(INT)
C
C  Now deal with the integrals based on what symmetry type it is, i.e.,
C  (aa|aa), (aa|bb), or (ab|ab).
C
        IND1=INDX(INEWVC(I),INEWVC(J))
        IND2=INDX(INEWVC(K),INEWVC(L))
        IREPOFF=INDX(IDXVEC(I),IDXVEC(I))
C
        IBK=IBKNMOF(IREPOFF)+IND2
        IBKET=IBKNUM(IBK)
C
        IWHEREJ(INT)=IBKET
        IBUFJ(INT)=IPKOFF(IREPOFF)+INDX2(IND1,IND2,IRPDS1(IREPOFF))
        BUFJ(INT)=BUF(INT)
C
   10 CONTINUE
C
C  Now we put the integrals into the appropriate buckets.
C
      CALL IZERO(ILOOKUP,600)
      DO 400 ITYPE=1,NBUCK
        CALL WHENEQ(ILNBUF,IWHEREJ,1,ITYPE,ILOOKUP,NVAL)
        IOFF=1
        INBUCK=NINBCK(ITYPE)
  401   IDUMP=MIN(NVAL,NBKINT-INBUCK)
        CALL GATHER(IDUMP,BUCK(INBUCK+1,ITYPE),BUFJ,ILOOKUP(IOFF))
        CALL IGATHER(IDUMP,IBUCK(INBUCK+1,ITYPE),IBUFJ,ILOOKUP(IOFF))
        INBUCK=INBUCK+IDUMP
        IF(INBUCK.EQ.NBKINT) THEN
          CALL PLUNK(LUSRT1,BUCK(1,ITYPE),IBUCK(1,ITYPE),ICHAIN(ITYPE),
     &               INBUCK,NBKINT,NREC1)
          NVAL=NVAL-IDUMP
          IOFF=IOFF+IDUMP
          IF(NVAL.NE.0) GOTO 401
        ENDIF
        NINBCK(ITYPE)=INBUCK
  400 CONTINUE
C
      IF(NUT.EQ.ILNBUF) GOTO 1
C
      NAOINT=(NAOBUF-1)*ILNBUF+NUT
      CLOSE(LUINT,STATUS='KEEP')
C
      IF(NIRREP.EQ.1) GOTO 200
C
      CALL GFNAME('IIJJ    ',FNAME,ILENGTH)
      OPEN(LUINT,FILE=FNAME(1:ILENGTH),STATUS='OLD',FORM='UNFORMATTED',
     &     ACCESS='SEQUENTIAL')
      CALL LOCATE(LUINT,'TWOELSUP')
      NAOBUF=0
    2 READ(LUINT)BUF,IBUF,NUT
      NAOBUF=NAOBUF+1
      CALL ZERO(BUFJ,1200)
      CALL IZERO(IBUFJ,1200)
      CALL IZERO(IWHEREJ,1200)
      DO 20 INT=1,NUT
        IX=IUPKI(IBUF(INT))
        JX=IUPKJ(IBUF(INT))
        KX=IUPKK(IBUF(INT))
        LX=IUPKL(IBUF(INT))
        I=MAX(IX,JX)
        J=MIN(IX,JX)
        K=MAX(KX,LX)
        L=MIN(KX,LX)
        IND1=INDX(I,J)
        IND2=INDX(K,L)
        IF(IND1.LT.IND2) THEN
          IX=I
          JX=J
          I=K
          J=L
          K=IX
          L=JX
        ENDIF
        X=BUF(INT)
C
        IND1=INDX(INEWVC(I),INEWVC(J))
        IND2=INDX(INEWVC(K),INEWVC(L))
        IREPOFF=INDX(IDXVEC(I),IDXVEC(K))
        IJUNK=INDX(IDXVEC(I)-1,IDXVEC(K))
        IBK=IBKNMOF(IREPOFF)+IND2
        IBKET=IBKNUM(IBK)
C
        IWHEREJ(INT)=IBKET
        IBUFJ(INT)=IPKOFF(IREPOFF)+INDX2(IND1,IND2,IRPDS2(2*IJUNK-1))
        BUFJ(INT)=BUF(INT)
C
   20 CONTINUE
C
      CALL IZERO(ILOOKUP,600)
      DO 500 ITYPE=1,NBUCK
        CALL WHENEQ(ILNBUF,IWHEREJ,1,ITYPE,ILOOKUP,NVAL)
        IOFF=1
        INBUCK=NINBCK(ITYPE)
  501   IDUMP=MIN(NVAL,NBKINT-INBUCK)
        CALL GATHER(IDUMP,BUCK(INBUCK+1,ITYPE),BUFJ,ILOOKUP(IOFF))
        CALL IGATHER(IDUMP,IBUCK(INBUCK+1,ITYPE),IBUFJ,ILOOKUP(IOFF))
        INBUCK=INBUCK+IDUMP
        IF(INBUCK.EQ.NBKINT) THEN
          CALL PLUNK(LUSRT1,BUCK(1,ITYPE),IBUCK(1,ITYPE),ICHAIN(ITYPE),
     &               INBUCK,NBKINT,NREC1)
          NVAL=NVAL-IDUMP
          IOFF=IOFF+IDUMP
          IF(NVAL.NE.0) GOTO 501
        ENDIF
        NINBCK(ITYPE)=INBUCK
  500 CONTINUE
C
      IF(NUT.EQ.ILNBUF) GOTO 2
C
      NAOINT=NAOINT+(NAOBUF-1)*ILNBUF+NUT
      CLOSE(LUINT,STATUS='KEEP')
C
  200 WRITE(LUOUT,5000)NAOINT
 5000 FORMAT(T3,'@PKSORT21-I, ',I8,' AO integrals sorted as PK ',
     &          'list elements.')
C
C  Flush remaining buffers
C
      DO 50 I=1,NBUCK
        IBK1=I
        CALL PLUNK(LUSRT1,BUCK(1,IBK1),IBUCK(1,IBK1),ICHAIN(IBK1),
     &             NINBCK(IBK1),NBKINT,NREC1)
   50 CONTINUE
C
      CALL TIMER(1)
C
      NREC1=NREC1-1
      WRITE(LUOUT,5010)NREC1,TIMENEW
 5010 FORMAT(T3,'@PKSORT21-I, Wrote out ',I8,' records of J ',
     &          'integrals.',/,
     &       T14,'Integral sort required ',F10.3,' seconds.',/)
C
      RETURN
      END
