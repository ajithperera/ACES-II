      SUBROUTINE COMCHR(NATOMS,LENGTH,LENMAX,IDEGEN,IOPNUM,
     &                  IPTR,NBFATM,ILCATM,VREF,VGEN,SCR,IANG,
     &                  NBAS,SYOP,CHAR)
C
C THIS ROUTINE COMPUTES THE CHARACTER OF A GROUP REPRESENTATION 
C  AND IS USED IN DETERMINING THE IRREDUCIBLE REPRESENTATION OF
C  MOLECULAR ORBITALS.  
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION VREF(NBAS,5),VGEN(LENMAX),IPTR(1),LENGTH(5)
      DIMENSION NBFATM(NATOMS,5),ILCATM(NATOMS,5),SCR(1)
      DIMENSION SYOP(1),IANG(5),DBUF(9),DBUF2(9)
C
      DATA ZILCH /0.0/
C
      IPOS(I) =1+(I-1)*NATOMS
      IPOS2(I)=1+(I-1)*9
C
      CHAR=0.0
C
C LOOP OVER THE LEVEL OF DEGENERACY OF THE REPRESENTATION
C
      DO 10 ICOUNT=1,IDEGEN
C
C NORMALIZE VECTOR
C
       Z=0.0
       DO 21 I=1,LENGTH(ICOUNT)
        Z=Z+VREF(I,ICOUNT)*VREF(I,ICOUNT)
21     CONTINUE
       IF(Z.EQ.ZILCH) THEN
         CHAR=-999.
         RETURN
       ELSE
         X=1.0/SQRT(Z)
       ENDIF
       DO 22 I=1,LENGTH(ICOUNT)
        VREF(I,ICOUNT)=VREF(I,ICOUNT)*X
22     CONTINUE 
C
C MAP THE REFERENCE VECTOR INTO THE PERMUTED ATOMIC ORDERING
C
       DO 20 I=1,NATOMS
        LENATM=NBFATM(I,ICOUNT)
        IF(LENATM.EQ.-1)GOTO 20
        ISTART=ILCATM(I,ICOUNT)
        ICENGEN=IPTR(I+IPOS(IOPNUM)-1)
        ISTART2=ILCATM(ICENGEN,ICOUNT)
        CALL SCOPY(LENATM,VREF(ISTART,ICOUNT),1,VGEN(ISTART2),1)
20     CONTINUE
C
C IF THIS IS A P FUNCTION REP. THEN ROTATE THE X,Y AND Z COMPONENTS BY
C   THE CARTESIAN REPRESENTATION OF THE SYMMETRY OPERATION
C
       IF(IANG(ICOUNT).EQ.1)THEN
        ITOTATM=LENGTH(ICOUNT)/3
        CALL MXM(SYOP(IPOS2(IOPNUM)),3,VGEN,3,SCR,ITOTATM)
        CALL SCOPY(ITOTATM*3,SCR,1,VGEN,1)
       ENDIF
C
C IF THIS IS A D FUNCTION REP. THEN ROTATE THIS AS WELL.
C
       IF(IANG(ICOUNT).EQ.2)THEN
        ITOTATM=LENGTH(ICOUNT)/6
        IOFF1=1
        CALL TRANSP(SYOP(IPOS2(IOPNUM)),DBUF2,3,3)
        DO 30 NATOM=1,ITOTATM
         CALL EXPND4(VGEN(IOFF1),DBUF,3)
         CALL MXM(SYOP(IPOS2(IOPNUM)),3,DBUF,3,SCR,3)
         CALL MXM(SCR,3,DBUF2,3,DBUF,3)
         CALL SQUEZ4(DBUF,VGEN(IOFF1),3)
         IOFF1=IOFF1+6
30      CONTINUE 
       ENDIF
C
C COMPUTE CHARACTER
C
       CHAR=CHAR+SDOT(LENGTH(ICOUNT),VREF(1,ICOUNT),1,VGEN,1)
10    CONTINUE
      RETURN
      END
