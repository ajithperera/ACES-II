
      SUBROUTINE MULPOP(OVRLP,DENS,SCR,ELECNUM,NRCENT,NRORB,
     &                  NRANGM,NAMN,CHARGT,TOTELEC,IPOPORB,
     &                  ANGLBL,NBAS,IUHF)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)

      PARAMETER(LUINT=10)

      LOGICAL bINTOpen
      CHARACTER*8 CDENS(2)
cYAU      CHARACTER*4 ANGLBL(NBAS),ATMLBL
      CHARACTER*4 ANGLBL(NBAS),ATMLBL(NBAS)
#ifdef _DOIT_THE_GWALTNEY_WAY
      CHARACTER*4 NAMN(NBAS)
#else
      DIMENSION NAMN(NBAS)
#endif /* _DOIT_THE_GWALTNEY_WAY */
      DIMENSION OVRLP(NBAS*NBAS),DENS(NBAS*NBAS),SCR(NBAS,NBAS)
      DIMENSION ELECNUM((IUHF+1)*NBAS),CHARGT(NBAS)
      DIMENSION NRCENT(NBAS),NRORB(NBAS),NRANGM(NBAS)
      DIMENSION TOTELEC(NBAS),IPOPORB(NBAS)

      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /FILES/ LUOUT,MOINTS

      DATA CDENS /'SCFDENSA','SCFDENSB'/

#ifdef _TRACE_VSCF
      write(*,*) '@VSCF: Entered ', __FILE__
#endif /* _TRACE_VSCF */

      CALL GETREC(20,'JOBARC','AOOVRLAP',NBAS*NBAS*IINTFP,OVRLP)
      DO ISPIN=1,(IUHF+1)
         CALL GETREC(20,'JOBARC',CDENS(ISPIN),NBAS*NBAS*IINTFP,DENS)
         CALL MXM(DENS,NBAS,OVRLP,NBAS,SCR,NBAS)
         DO I=1,NBAS
            ELECNUM((ISPIN-1)*NBAS+I)=SCR(I,I)
         END DO
      END DO

      INQUIRE(FILE='INT',OPENED=bINTOpen)
      IF (.NOT.bINTOpen) THEN
         OPEN(LUINT,FILE='INT',ACCESS='SEQUENTIAL',FORM='UNFORMATTED')
         bINTOpen=.TRUE.
      END IF

      CALL LOCATE(LUINT,'MULPOPDA')
      READ(LUINT) NCENT,NBF
      READ(LUINT) NCENT,(NAMN(I),CHARGT(I),I=1,NCENT)
      DO I=1,NBF
         READ(LUINT) NRCENT(I),NRORB(I),NRANGM(I)
      END DO

      CALL ZERO(TOTELEC,NBAS)
      DO ISPIN=1,(IUHF+1)
         DO J=1,NBAS
            TOTELEC(NRORB(J))=TOTELEC(NRORB(J))
     &                       +ELECNUM((ISPIN-1)*NBAS+J)
         END DO
      END DO

      CALL LOCATE(LUINT,'SYMINPUT')
      READ(LUINT) IJUNK,(ATMLBL,I=1,IJUNK),(ANGLBL(J),J=1,IJUNK),REPULS
      WRITE(LUOUT,'(T3,A,/)') 'Mulliken Population Analysis'
      WRITE(LUOUT,'(T6,A,10X,A,13X,A)') 'Basis Func','Alpha','Beta'
      WRITE(LUOUT,5002)
 5002 FORMAT(T5,45('-'))
      DO 140 I=1,NBAS
        WRITE(LUOUT,5010)I,NAMN(NRORB(I)),ANGLBL(I),
     &                   ELECNUM(I),ELECNUM(NBAS+I)
 5010   FORMAT(T5,I3,';',1X,A4,1X,A4,5X,F8.5,10X,F8.5)
  140 CONTINUE
C
      WRITE(LUOUT,5011)
 5011 FORMAT(/)
      WRITE(LUOUT,5020)
 5020 FORMAT(T3,'Mulliken Charge Analysis',/)
      WRITE(LUOUT,5015)
 5015 FORMAT(T4,'Unique Center',7X,'# Atoms',6X,'Charge per atom',
     &       4X,'Charge per center')
      WRITE(LUOUT,5016)
 5016 FORMAT(T4,69('-'))
      CALL GETREC(20,'JOBARC','COMPPOPV',NCENT,IPOPORB)
      DO 150 I=1,NCENT
        ZJUNK=CHARGT(I)-TOTELEC(I)
        WRITE(LUOUT,5022)NAMN(I),IPOPORB(I),ZJUNK/DFLOAT(IPOPORB(I)),
     &                   ZJUNK
 5022   FORMAT(T8,A4,15X,I1,12X,F8.5,13X,F8.5)
  150 CONTINUE
      WRITE(LUOUT,5011)

      IF (bINTOpen) CLOSE(LUINT,STATUS='KEEP')

#ifdef _TRACE_VSCF
      write(*,*) '@VSCF: Leaving ', __FILE__
#endif /* _TRACE_VSCF */

      RETURN
      END

