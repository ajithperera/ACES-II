
      SUBROUTINE PROJ(NATOMS,IOPNUM,
     &                  IPTR,NBFATM,ILCATM,VREF,VGEN,SCR,
     &                  NBAS,SYOP,NUMANG)
C
C THIS ROUTINE COMPUTES THE CHARACTER OF A GROUP REPRESENTATION 
C  AND IS USED IN DETERMINING THE IRREDUCIBLE REPRESENTATION OF
C  MOLECULAR ORBITALS.  
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION VREF(NBAS),VGEN(NBAS),IPTR(1)
      DIMENSION NBFATM(NATOMS),ILCATM(NATOMS),SCR(1)
      DIMENSION SYOP(1),DBUF(81),DBUF2(81),NUMANG(NATOMS,5)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      DATA ZILCH /0.0/
C
      IPOS(I) =1+(I-1)*NATOMS
      IPOS2(I)=1+(I-1)*9
C
C LOOP OVER INDIVIDUAL ATOMS
C
      IOFF0=0
      DO 10 I=1,NATOMS
C
C ROTATE ALL P AND D REPRESENTATIONS BY THE 3x3 SYMMETRY 
C OPERATION.
C
       NUMS=NUMANG(I,1)
       NUMP=NUMANG(I,2)
       NUMD=NUMANG(I,3)
       NUMF=NUMANG(I,4)
       NUMG=NUMANG(I,5)
       IOFFP=NUMS+ILCATM(I)
       IOFFD=IOFFP+3*NUMP
       IOFFF=IOFFD+6*NUMD
       IOFFG=IOFFF+10*NUMF
       IF(NUMP.NE.0)THEN
        CALL MXM(SYOP(IPOS2(IOPNUM)),3,VREF(IOFFP),3,SCR,NUMP)
        CALL SCOPY(NUMP*3,SCR,1,VREF(IOFFP),1)
       ENDIF
       IF(NUMD.NE.0)THEN
        DO 20 IDCOUNT=1,NUMD
         CALL EXPND4(VREF(IOFFD),DBUF,3,2)
         CALL MXM(SYOP(IPOS2(IOPNUM)),3,DBUF,3,SCR,3)
         CALL TRANSP(SCR,DBUF,3,3)
         CALL MXM(SYOP(IPOS2(IOPNUM)),3,DBUF,3,SCR,3)
         CALL TRANSP(SCR,DBUF,3,3)
         CALL SQUEZ4(DBUF,VREF(IOFFD),3,2)
         IOFFD=IOFFD+6
20      CONTINUE
       ENDIF
       IF(NUMF.NE.0)THEN
        DO 30 IFCOUNT=1,NUMF
         CALL EXPND4(VREF(IOFFF),DBUF,3,3)
         CALL MXM(SYOP(IPOS2(IOPNUM)),3,DBUF,3,SCR,9)
         CALL TRANSP(SCR,DBUF,3,9)
         CALL MXM(SYOP(IPOS2(IOPNUM)),3,DBUF,3,SCR,9)
         CALL TRANSP(SCR,DBUF,3,9)
         CALL MXM(SYOP(IPOS2(IOPNUM)),3,DBUF,3,SCR,9)
         CALL TRANSP(SCR,DBUF,3,9)
         CALL SQUEZ4(DBUF,VREF(IOFFF),3,3)
         IOFFF=IOFFF+10
30      CONTINUE
       ENDIF
       IF(NUMG.NE.0)THEN
        DO 40 IGCOUNT=1,NUMG
         CALL EXPND4(VREF(IOFFG),DBUF,3,4)
         CALL MXM(SYOP(IPOS2(IOPNUM)),3,DBUF,3,SCR,27)
         CALL TRANSP(SCR,DBUF,3,27)
         CALL MXM(SYOP(IPOS2(IOPNUM)),3,DBUF,3,SCR,27)
         CALL TRANSP(SCR,DBUF,3,27)
         CALL MXM(SYOP(IPOS2(IOPNUM)),3,DBUF,3,SCR,27)
         CALL TRANSP(SCR,DBUF,3,27)
         CALL MXM(SYOP(IPOS2(IOPNUM)),3,DBUF,3,SCR,27)
         CALL TRANSP(SCR,DBUF,3,27)
         CALL SQUEZ4(DBUF,VREF(IOFFG),3,4)
         IOFFG=IOFFG+15
40      CONTINUE
       ENDIF
C
C NOW MAP THE REFERENCE VECTOR INTO THE PERMUTED ATOMIC ORDERING
C
       LENATM=NBFATM(I)
       IF(LENATM.EQ.-1)GOTO 10
       ISTART=ILCATM(I)
       ICENGEN=IPTR(I+IPOS(IOPNUM)-1)
       ISTART2=ILCATM(ICENGEN)
       CALL SCOPY(LENATM,VREF(ISTART),1,VGEN(ISTART2),1)
10    CONTINUE
      RETURN
      END
