      SUBROUTINE Y14B(CORE,MAXCOR,IUHF)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DISSIZA,DISSIZB,DISSIZY
      INTEGER POP,VRT,DIRPRD
      DIMENSION CORE(1)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYM/    POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /INFO/   NOCA,NOCB,NVRTA,NVRTB
      COMMON /T3OFF/  IOFFVV(8,8,10),IOFFOO(8,8,10),IOFFVO(8,8,4)
C
      WRITE(6,1000)
 1000 FORMAT(' @Y14B-I, Y3 * Y5 contributions. ')
C
C     Y(B,E,L,M) = SUM  W(M,D,A,L) * W(A,B,D,E)
C                  A,D
C
C     BELM = MDAL * ABDE
C     belm = mdal * abde
C
      DO  100 ISPIN=1,IUHF+1
C
      LENA = 0
      LENB = 0
      DO   10 IRREP=1,NIRREP
      LENB = LENB + IRPDPD(IRREP,18+ISPIN) * IRPDPD(IRREP,20+ISPIN)
      LENA = LENA + IRPDPD(IRREP,18+ISPIN) * IRPDPD(IRREP,18+ISPIN)
   10 CONTINUE
C
      I000 = 1
      I010 = I000 + LENB
      I020 = I010 + LENB
      I030 = I020 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I030
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
 1020 FORMAT(' @Y14B-I, Insufficient memory. Need ',I15,' . Got ',I15)
      STOP 'Y14B'
      ENDIF
C
      CALL GETALL(CORE(I000),LENB,1,53+ISPIN)
C     W(A,M,D,L)/W(a,m,d,l) ---> W(A,L,D,M)/W(a,l,d,m)
      CALL SSTGEN(CORE(I000),CORE(I010),LENB,
     1            VRT(1,ISPIN),POP(1,ISPIN),VRT(1,ISPIN),POP(1,ISPIN),
     1            CORE(I020),1,'1432')
C     W(A,L,D,M)/W(a,l,d,m) ---> W(A,D,L,M)/W(a,d,l,m)
      CALL SSTGEN(CORE(I010),CORE(I000),LENB,
     1            VRT(1,ISPIN),POP(1,ISPIN),VRT(1,ISPIN),POP(1,ISPIN),
     1            CORE(I020),1,'1324')
C
      I020 = I010 + LENA
      I030 = I020 + LENA
      I040 = I030 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I040
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14B'
      ENDIF
C
      IOFFA = I020
      DO   20 IRREP=1,NIRREP
      DISSIZA = IRPDPD(IRREP,ISPIN)
      NDISA   = IRPDPD(IRREP,ISPIN)
      IF(DISSIZA.EQ.0.OR.NDISA.EQ.0) GOTO 20
C     Get W(A<B,D<E)/W(a<b,d<e)
      CALL GETLIST(CORE(IOFFA),1,NDISA,2,IRREP,ISPIN)
      DISSIZA = IRPDPD(IRREP,18+ISPIN)
C     W(A<B,D<E) ---> W(A,B,D<E)
      CALL SYMEXP2(IRREP,VRT(1,ISPIN),DISSIZA,IRPDPD(IRREP,ISPIN),
     1             NDISA,CORE(IOFFA),CORE(IOFFA))
      NDISA   = IRPDPD(IRREP,18+ISPIN)
C     W(A,B,D<E) ---> W(A,B,D,E)
      CALL  SYMEXP(IRREP,VRT(1,ISPIN),DISSIZA,CORE(IOFFA))
      IOFFA   = IOFFA + DISSIZA * NDISA
   20 CONTINUE
C
C     W(A,B,D,E) ---> W(A,D,B,E)
      CALL SSTGEN(CORE(I020),CORE(I010),LENA,
     1            VRT(1,ISPIN),VRT(1,ISPIN),VRT(1,ISPIN),VRT(1,ISPIN),
     1            CORE(I030),1,'1324')
C
C     W(A,D,L,M) at I000, W(A,D,B,E) at I010. Form Y(B,E,L,M) at I020.
C
      IOFFB = I000
      IOFFA = I010
      DO   30 IRPAD=1,NIRREP
C
      IF(IRPDPD(IRPAD,18+ISPIN).EQ.0.OR.
     1   IRPDPD(IRPAD,20+ISPIN).EQ.0) GOTO 30
C
      DISSIZA = IRPDPD(IRPAD,18+ISPIN)
      NDISA   = IRPDPD(IRPAD,18+ISPIN)
      DISSIZB = IRPDPD(IRPAD,18+ISPIN)
      NDISB   = IRPDPD(IRPAD,20+ISPIN)
C
      I030 = I020 + NDISA * NDISB
      I040 = I030 + NDISA * NDISB
      I050 = I040 + MAX(NDISA,NDISB)
      I060 = I050 + MAX(NDISA,NDISB)
      I070 = I060 + MAX(NDISA,NDISB)
C      
      NEED = IINTFP * I070
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14B'
      ENDIF
C
      CALL XGEMM('T','N',NDISA,NDISB,DISSIZB,2.0D+00,
     1           CORE(IOFFA),DISSIZA,
     1           CORE(IOFFB),DISSIZB,0.0D+00,
     1           CORE(I020) ,NDISA)
C
      CALL GETLIST(CORE(I030),1,NDISB,2,IRPAD,24+ISPIN)
      NSIZ = NDISA * NDISB
      CALL    VADD(CORE(I030),CORE(I030),CORE(I020),NSIZ, 1.0D+00)
      CALL PUTLIST(CORE(I030),1,NDISB,2,IRPAD,24+ISPIN)
C
      IOFFA = IOFFA + DISSIZA * NDISA
      IOFFB = IOFFB + DISSIZB * NDISB
   30 CONTINUE
  100 CONTINUE
C
C     BELM = MdaL * aBdE
C     belm = mDAl * AbDe
C
      DO  200 ISPIN=1,IUHF+1
C
      LENA = 0
      LENB = 0
      DO  110 IRREP=1,NIRREP
      LENB = LENB + IRPDPD(IRREP,21-ISPIN) * IRPDPD(IRREP,20+ISPIN)
      LENA = LENA + IRPDPD(IRREP,21-ISPIN) * IRPDPD(IRREP,18+ISPIN)
  110 CONTINUE
C
      I000 = 1
      I010 = I000 + LENB
      I020 = I010 + LENB
      I030 = I020 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I030
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14B'
      ENDIF
C
      CALL GETALL(CORE(I000),LENB,1,60-ISPIN-1+IUHF)
C     W(a,M,d,L)/W(A,m,D,l) ---> W(a,L,d,M)/W(A,l,D,m)
      CALL SSTGEN(CORE(I000),CORE(I010),LENB,
     1        VRT(1,3-ISPIN),POP(1,ISPIN),VRT(1,3-ISPIN),POP(1,ISPIN),
     1            CORE(I020),1,'1432')
C     W(a,L,d,M)/W(A,l,D,m) ---> W(a,d,L,M)/W(A,D,l,m)
      CALL SSTGEN(CORE(I010),CORE(I000),LENB,
     1        VRT(1,3-ISPIN),POP(1,ISPIN),VRT(1,3-ISPIN),POP(1,ISPIN),
     1            CORE(I020),1,'1324')
C
      I020 = I010 + LENA
      I030 = I020 + LENA
      I040 = I030 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I040
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14B'
      ENDIF
C
      IOFFA = I020
      DO  120 IRREP=1,NIRREP
      DISSIZA = IRPDPD(IRREP,13)
      NDISA   = IRPDPD(IRREP,13)
      IF(DISSIZA.EQ.0.OR.NDISA.EQ.0) GOTO 120
C     Get W(B,a,E,d)/W(A,b,D,e)
      CALL GETLIST(CORE(IOFFA),1,NDISA,2,IRREP,3)
      IOFFA   = IOFFA + DISSIZA * NDISA
  120 CONTINUE
C
C     ISPIN = 1 : W(B,a,E,d) ---> W(B,E,a,d)
C     ISPIN = 2 : W(A,b,D,e) ---> W(A,D,b,e)
      CALL SSTGEN(CORE(I020),CORE(I010),LENA,
     1            VRT(1,1),VRT(1,2),VRT(1,1),VRT(1,2),
     1            CORE(I030),1,'1324')
C
C     ISPIN = 1 : W(a,d,L,M) at I000, W(B,E,a,d) at I010. Form Y(B,E,L,M)
C                 at I020.
C     ISPIN = 2 : W(A,D,l,m) at I000, W(A,D,b,e) at I010. Form Y(b,e,l,m)
C                 at I020.
C
      IOFFB = I000
      IOFFA = I010
      DO  130 IRPAD=1,NIRREP
C
      IF(IRPDPD(IRPAD,21-ISPIN).EQ.0.OR.
     1   IRPDPD(IRPAD,18+ISPIN).EQ.0.OR.
     1   IRPDPD(IRPAD,20+ISPIN).EQ.0) GOTO 130
C
      DISSIZA = IRPDPD(IRPAD,19)
      NDISA   = IRPDPD(IRPAD,20)
      DISSIZB = IRPDPD(IRPAD,21-ISPIN)
      NDISB   = IRPDPD(IRPAD,20+ISPIN)
C
      I030 = I020 + MAX(DISSIZA,NDISA) * NDISB
      I040 = I030 + MAX(DISSIZA,NDISA) * NDISB
C      
      NEED = IINTFP * I040
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14B'
      ENDIF
C
      IF(ISPIN.EQ.1)THEN
      CALL XGEMM('N','N',DISSIZA,NDISB,NDISA,2.0D+00,
     1           CORE(IOFFA),DISSIZA,
     1           CORE(IOFFB),DISSIZB,0.0D+00,
     1           CORE(I020),DISSIZA)
      DISSIZY = DISSIZA
      ELSE
      CALL XGEMM('T','N',NDISA,NDISB,DISSIZB,2.0D+00,
     1           CORE(IOFFA),DISSIZA,
     1           CORE(IOFFB),DISSIZB,0.0D+00,
     1           CORE(I020) ,NDISA)
      DISSIZY = NDISA
      ENDIF
C
      CALL GETLIST(CORE(I030),1,NDISB,2,IRPAD,24+ISPIN)
      NSIZ = DISSIZY * NDISB
      CALL    VADD(CORE(I030),CORE(I030),CORE(I020),NSIZ, 1.0D+00)
      CALL PUTLIST(CORE(I030),1,NDISB,2,IRPAD,24+ISPIN)
C
      IOFFA = IOFFA + DISSIZA * NDISA
      IOFFB = IOFFB + DISSIZB * NDISB
  130 CONTINUE
  200 CONTINUE
C
C     BeLm = mDaL * aBDe
C     bElM = MdAl * AbdE
C
      DO  300 ISPIN=1,IUHF+1
C
      LENA = 0
      LENB = 0
      DO  210 IRREP=1,NIRREP
      LENB = LENB + IRPDPD(IRREP,13) * IRPDPD(IRREP,14)
      LENA = LENA + IRPDPD(IRREP,13) * IRPDPD(IRREP,13)
  210 CONTINUE
C
      I000 = 1
      I010 = I000 + LENB
      I020 = I010 + LENB
      I030 = I020 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I030
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14B'
      ENDIF
C
      CALL GETALL(CORE(I000),LENB,1,58-ISPIN-1+IUHF)
C     W(a,m,D,L)/W(A,M,d,l) ---> W(a,L,D,m)/W(A,l,d,M)
      CALL SSTGEN(CORE(I000),CORE(I010),LENB,
     1        VRT(1,3-ISPIN),POP(1,3-ISPIN),VRT(1,ISPIN),POP(1,ISPIN),
     1            CORE(I020),1,'1432')
C     W(a,L,D,m)/W(A,l,d,M) ---> W(a,D,L,m)/W(A,d,l,M)
      CALL SSTGEN(CORE(I010),CORE(I000),LENB,
     1        VRT(1,3-ISPIN),POP(1,ISPIN),VRT(1,ISPIN),POP(1,3-ISPIN),
     1            CORE(I020),1,'1324')
C
      I020 = I010 + LENA
      I030 = I020 + LENA
      I040 = I030 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I040
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14B'
      ENDIF
C
      IF(ISPIN.EQ.1)THEN
      IOFFA = I020
      ELSE
      IOFFA = I010
      ENDIF
      DO  220 IRREP=1,NIRREP
      DISSIZA = IRPDPD(IRREP,13)
      NDISA   = IRPDPD(IRREP,13)
      IF(DISSIZA.EQ.0.OR.NDISA.EQ.0) GOTO 220
C     Get W(B,a,D,e)/W(A,b,E,d)
      CALL GETLIST(CORE(IOFFA),1,NDISA,2,IRREP,3)
C
C     W(B,a,D,e) ---> W(a,B,D,e)
      IF(ISPIN.EQ.1)THEN
      I050 = I040 + DISSIZA
      I060 = I050 + DISSIZA
      I070 = I060 + DISSIZA
      NEED = IINTFP * I070
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14B'
      ENDIF
      CALL SYMTR3(IRREP,VRT(1,1),VRT(1,2),DISSIZA,NDISA,CORE(IOFFA),
     1            CORE(I040),CORE(I050),CORE(I060))
      ENDIF
      IOFFA   = IOFFA + DISSIZA * NDISA
  220 CONTINUE
C
C     ISPIN = 1 : W(a,B,D,e) ---> W(a,D,B,e)
C     ISPIN = 2 : W(A,b,E,d) ---> W(A,E,b,d) ---> W(A,d,b,E)
      IF(ISPIN.EQ.1)THEN
      CALL SSTGEN(CORE(I020),CORE(I010),LENA,
     1            VRT(1,2),VRT(1,1),VRT(1,1),VRT(1,2),
     1            CORE(I030),1,'1324')
      ELSE
      CALL SSTGEN(CORE(I010),CORE(I020),LENA,
     1            VRT(1,1),VRT(1,2),VRT(1,1),VRT(1,2),
     1            CORE(I030),1,'1324')
      CALL SSTGEN(CORE(I020),CORE(I010),LENA,
     1            VRT(1,1),VRT(1,1),VRT(1,2),VRT(1,2),
     1            CORE(I030),1,'1432')
      ENDIF
C
C     ISPIN = 1 : W(a,D,L,m) at I000, W(a,D,B,e) at I010. Form Y(B,e,L,m)
C                 at I020.
C     ISPIN = 2 : W(A,d,l,M) at I000, W(A,d,b,E) at I010. Form Y(b,E,l,M)
C                 at I020.
C
      IOFFB = I000
      IOFFA = I010
      DO  230 IRPAD=1,NIRREP
C
      IF(IRPDPD(IRPAD,13).EQ.0.OR.IRPDPD(IRPAD,14).EQ.0) GOTO 230
C
      DISSIZA = IRPDPD(IRPAD,13)
      NDISA   = IRPDPD(IRPAD,13)
      DISSIZB = IRPDPD(IRPAD,13)
      NDISB   = IRPDPD(IRPAD,14)
C
      I030 = I020 + NDISA * NDISB
      I040 = I030 + NDISA * NDISB
C      
      NEED = IINTFP * I040
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14B'
      ENDIF
C
      CALL XGEMM('T','N',NDISA,NDISB,DISSIZB,-2.0D+00,
     1           CORE(IOFFA),DISSIZA,
     1           CORE(IOFFB),DISSIZB,0.0D+00,
     1           CORE(I020) ,NDISA)
C
      DISSIZY = NDISA
      CALL GETLIST(CORE(I030),1,NDISB,2,IRPAD,26+ISPIN)
      NSIZ = DISSIZY * NDISB
      CALL    VADD(CORE(I030),CORE(I030),CORE(I020),NSIZ, 1.0D+00)
      CALL PUTLIST(CORE(I030),1,NDISB,2,IRPAD,26+ISPIN)
C
      IOFFA = IOFFA + DISSIZA * NDISA
      IOFFB = IOFFB + DISSIZB * NDISB
  230 CONTINUE
  300 CONTINUE
C
C     BElm = mDAl * ABDE
C     beLM = MdaL * abde
C
      DO  400 ISPIN=1,IUHF+1
C
      LENA = 0
      LENB = 0
      DO  310 IRREP=1,NIRREP
      LENB = LENB + IRPDPD(IRREP,18+ISPIN) * IRPDPD(IRREP,23-ISPIN)
      LENA = LENA + IRPDPD(IRREP,18+ISPIN) * IRPDPD(IRREP,18+ISPIN)
  310 CONTINUE
C
      I000 = 1
      I010 = I000 + LENB
      I020 = I010 + LENB
      I030 = I020 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I030
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14B'
      ENDIF
C
      CALL GETALL(CORE(I000),LENB,1,57+ISPIN)
C     W(A,m,D,l)/W(a,m,D,L) ---> W(A,l,D,m)/W(a,L,d,M)
      CALL SSTGEN(CORE(I000),CORE(I010),LENB,
     1        VRT(1,ISPIN),POP(1,3-ISPIN),VRT(1,ISPIN),POP(1,3-ISPIN),
     1            CORE(I020),1,'1432')
C     W(A,l,D,m)/W(a,L,d,M) ---> W(A,D,l,m)/W(a,d,L,M)
      CALL SSTGEN(CORE(I010),CORE(I000),LENB,
     1        VRT(1,ISPIN),POP(1,3-ISPIN),VRT(1,ISPIN),POP(1,3-ISPIN),
     1            CORE(I020),1,'1324')
C
      I020 = I010 + LENA
      I030 = I020 + LENA
      I040 = I030 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I040
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14B'
      ENDIF
C
      IOFFA = I020
      DO  320 IRREP=1,NIRREP
      DISSIZA = IRPDPD(IRREP,ISPIN)
      NDISA   = IRPDPD(IRREP,ISPIN)
      IF(DISSIZA.EQ.0.OR.NDISA.EQ.0) GOTO 320
C     Get W(A<B,D<E)/W(a<b,d<e)
      CALL GETLIST(CORE(IOFFA),1,NDISA,2,IRREP,ISPIN)
      DISSIZA = IRPDPD(IRREP,18+ISPIN)
C     W(A<B,D<E) ---> W(A,B,D<E)
      CALL SYMEXP2(IRREP,VRT(1,ISPIN),DISSIZA,IRPDPD(IRREP,ISPIN),
     1             NDISA,CORE(IOFFA),CORE(IOFFA))
      NDISA   = IRPDPD(IRREP,18+ISPIN)
C     W(A,B,D<E) ---> W(A,B,D,E)
      CALL  SYMEXP(IRREP,VRT(1,ISPIN),DISSIZA,CORE(IOFFA))
      IOFFA   = IOFFA + DISSIZA * NDISA
  320 CONTINUE
C
C     W(A,B,D,E)/W(a,b,d,e) ---> W(A,D,B,E)/W(a,b,d,e)
      CALL SSTGEN(CORE(I020),CORE(I010),LENA,
     1            VRT(1,ISPIN),VRT(1,ISPIN),VRT(1,ISPIN),VRT(1,ISPIN),
     1            CORE(I030),1,'1324')
C
C     ISPIN = 1 : W(A,D,l,m) at I000, W(A,D,B,E) at I010. Form Y(B,E,l,m)
C                 at I020.
C     ISPIN = 2 : W(a,d,L,M) at I000, W(a,d,b,e) at I010. Form Y(b,e,L,M)
C                 at I020.
C
      IOFFB = I000
      IOFFA = I010
      DO  330 IRPAD=1,NIRREP
C
      IF(IRPDPD(IRPAD,18+ISPIN).EQ.0.OR.
     1   IRPDPD(IRPAD,23-ISPIN).EQ.0) GOTO 330
C
      DISSIZA = IRPDPD(IRPAD,18+ISPIN)
      NDISA   = IRPDPD(IRPAD,18+ISPIN)
      DISSIZB = IRPDPD(IRPAD,18+ISPIN)
      NDISB   = IRPDPD(IRPAD,23-ISPIN)
C
      I030 = I020 + NDISA * NDISB
      I040 = I030 + NDISA * NDISB
C      
      NEED = IINTFP * I040
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14B'
      ENDIF
C
      CALL XGEMM('T','N',NDISA,NDISB,DISSIZB,2.0D+00,
     1           CORE(IOFFA),DISSIZA,
     1           CORE(IOFFB),DISSIZB,0.0D+00,
     1           CORE(I020) ,NDISA)
C
      DISSIZY = NDISA
      CALL GETLIST(CORE(I030),1,NDISB,2,IRPAD,28+ISPIN)
      NSIZ = DISSIZY * NDISB
      CALL    VADD(CORE(I030),CORE(I030),CORE(I020),NSIZ, 1.0D+00)
      CALL PUTLIST(CORE(I030),1,NDISB,2,IRPAD,28+ISPIN)
C
      IOFFA = IOFFA + DISSIZA * NDISA
      IOFFB = IOFFB + DISSIZB * NDISB
  330 CONTINUE
  400 CONTINUE
C
C     BElm = mdal * aBdE
C     beLM = MDAL * AbDe
C
      DO  500 ISPIN=1,IUHF+1
C
      LENA = 0
      LENB = 0
      DO  410 IRREP=1,NIRREP
      LENB = LENB + IRPDPD(IRREP,21-ISPIN) * IRPDPD(IRREP,23-ISPIN)
      LENA = LENA + IRPDPD(IRREP,21-ISPIN) * IRPDPD(IRREP,18+ISPIN)
  410 CONTINUE
C
      I000 = 1
      I010 = I000 + LENB
      I020 = I010 + LENB
      I030 = I020 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I030
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14B'
      ENDIF
C
      CALL GETALL(CORE(I000),LENB,1,56-ISPIN-1+IUHF)
C     W(a,m,d,l)/W(A,M,D,L) ---> W(a,l,d,m)/W(A,L,D,M)
      CALL SSTGEN(CORE(I000),CORE(I010),LENB,
     1    VRT(1,3-ISPIN),POP(1,3-ISPIN),VRT(1,3-ISPIN),POP(1,3-ISPIN),
     1            CORE(I020),1,'1432')
C     W(a,l,d,m)/W(A,L,D,M) ---> W(a,d,l,m)/W(A,D,L,M)
      CALL SSTGEN(CORE(I010),CORE(I000),LENB,
     1    VRT(1,3-ISPIN),POP(1,3-ISPIN),VRT(1,3-ISPIN),POP(1,3-ISPIN),
     1            CORE(I020),1,'1324')
C
      I020 = I010 + LENA
      I030 = I020 + LENA
      I040 = I030 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I040
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14B'
      ENDIF
C
      IOFFA = I020
      DO  420 IRREP=1,NIRREP
      DISSIZA = IRPDPD(IRREP,13)
      NDISA   = IRPDPD(IRREP,13)
      IF(DISSIZA.EQ.0.OR.NDISA.EQ.0) GOTO 420
C     Get W(B,a,E,d)/W(A,b,D,e)
      CALL GETLIST(CORE(IOFFA),1,NDISA,2,IRREP,3)
      IOFFA   = IOFFA + DISSIZA * NDISA
  420 CONTINUE
C
C     W(B,a,E,d)/W(A,b,D,e) ---> W(B,E,a,d)/W(A,D,b,e)
      CALL SSTGEN(CORE(I020),CORE(I010),LENA,
     1            VRT(1,1),VRT(1,2),VRT(1,1),VRT(1,2),
     1            CORE(I030),1,'1324')
C
C     ISPIN = 1 : W(a,d,l,m) at I000, W(B,E,a,d) at I010. Form Y(B,E,l,m)
C                 at I020.
C     ISPIN = 2 : W(A,D,L,M) at I000, W(A,D,b,e) at I010. Form Y(b,e,L,M)
C                 at I020.
C
      IOFFB = I000
      IOFFA = I010
      DO  430 IRPAD=1,NIRREP
C
      IF(IRPDPD(IRPAD,19      ).EQ.0.OR.
     1   IRPDPD(IRPAD,20      ).EQ.0.OR.
     1   IRPDPD(IRPAD,23-ISPIN).EQ.0) GOTO 430
C
      DISSIZA = IRPDPD(IRPAD,19)
      NDISA   = IRPDPD(IRPAD,20)
      DISSIZB = IRPDPD(IRPAD,21-ISPIN)
      NDISB   = IRPDPD(IRPAD,23-ISPIN)
C
      I030 = I020 + MAX(DISSIZA,NDISA) * NDISB
      I040 = I030 + MAX(DISSIZA,NDISA) * NDISB
C      
      NEED = IINTFP * I040
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14B'
      ENDIF
C
      IF(ISPIN.EQ.1)THEN
      CALL XGEMM('N','N',DISSIZA,NDISB,DISSIZB,2.0D+00,
     1           CORE(IOFFA),DISSIZA,
     1           CORE(IOFFB),DISSIZB,0.0D+00,
     1           CORE(I020) ,DISSIZA)
      DISSIZY = DISSIZA
      ELSE
      CALL XGEMM('T','N',NDISA,NDISB,DISSIZB,2.0D+00,
     1           CORE(IOFFA),DISSIZA,
     1           CORE(IOFFB),DISSIZB,0.0D+00,
     1           CORE(I020) ,NDISA)
      DISSIZY = NDISA
      ENDIF
C
      CALL GETLIST(CORE(I030),1,NDISB,2,IRPAD,28+ISPIN)
      NSIZ = DISSIZY * NDISB
      CALL    VADD(CORE(I030),CORE(I030),CORE(I020),NSIZ, 1.0D+00)
      CALL PUTLIST(CORE(I030),1,NDISB,2,IRPAD,28+ISPIN)
C
      IOFFA = IOFFA + DISSIZA * NDISA
      IOFFB = IOFFB + DISSIZB * NDISB
  430 CONTINUE
  500 CONTINUE
      RETURN
      END
