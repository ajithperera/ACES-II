      SUBROUTINE Y14A(CORE,MAXCOR,IUHF)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DISSIZA,DISSIZB
      INTEGER POP,VRT,DIRPRD
      DIMENSION CORE(1)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYM/    POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /INFO/   NOCA,NOCB,NVRTA,NVRTB
      COMMON /T3OFF/  IOFFVV(8,8,10),IOFFOO(8,8,10),IOFFVO(8,8,4)
C
      WRITE(6,1000)
 1000 FORMAT(' @Y14A-I, Y3 * Y4 contributions. ')
C
C     Y(B,E,L,M) = SUM  W(K,B,E,I) * W(K,L,I,M)
C                  I,K
C
C     BELM = KBEI * KLIM
C     belm = kbei * klim
C
      DO  100 ISPIN=1,IUHF+1
C
      LENA = 0
      LENB = 0
      DO   10 IRREP=1,NIRREP
      LENA = LENA + IRPDPD(IRREP,18+ISPIN) * IRPDPD(IRREP,20+ISPIN)
      LENB = LENB + IRPDPD(IRREP,20+ISPIN) * IRPDPD(IRREP,20+ISPIN)
   10 CONTINUE
C
      I000 = 1
      I010 = I000 + LENA
      I020 = I010 + LENA
      I030 = I020 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I030
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
 1020 FORMAT(' @Y14A-I, Insufficient memory. Need ',I15,' . Got ',I15)
      STOP 'Y14A'
      ENDIF
C
      CALL GETALL(CORE(I010),LENA,1,53+ISPIN)
C     W(E,K,B,I)/W(e,k,b,i) ---> W(E,B,K,I)/W(e,b,k,i)
      CALL SSTGEN(CORE(I010),CORE(I000),LENA,
     1            VRT(1,ISPIN),POP(1,ISPIN),VRT(1,ISPIN),POP(1,ISPIN),
     1            CORE(I020),1,'1324')
C
      I020 = I010 + LENB
      I030 = I020 + LENB
      I040 = I030 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I040
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14A'
      ENDIF
C
      IOFFB = I020
      DO   20 IRREP=1,NIRREP
CSSS      DISSIZB = IRPDPD(IRREP, 2+ISPIN)
CSSS      NDISB   = IRPDPD(IRREP, 2+ISPIN)
C
C Note the very subtle error here. This error can only
C manifest when you have irreps with zero occupied 
C and non-zero virtual orbitals. 04/07/2003, Ajith Perera
C 
      DISSIZB = IRPDPD(IRREP, 20+ISPIN)
      NDISB   = IRPDPD(IRREP, 20+ISPIN)
  
      IF(DISSIZB.EQ.0.OR.NDISB.EQ.0) GOTO 20
C     Get W(K<L,I<M)
      CALL GETLIST(CORE(IOFFB),1,NDISB,2,IRREP,6+ISPIN)
      DISSIZB = IRPDPD(IRREP,20+ISPIN)
C     W(K<L,I<M) ---> W(K,L,I<M)
      CALL SYMEXP2(IRREP,POP(1,ISPIN),DISSIZB,IRPDPD(IRREP,2+ISPIN),
     1             NDISB,CORE(IOFFB),CORE(IOFFB))
      NDISB   = IRPDPD(IRREP,20+ISPIN)
C     W(K,L,I<M) ---> W(K,L,I,M)
      CALL  SYMEXP(IRREP,POP(1,ISPIN),DISSIZB,CORE(IOFFB))
      IOFFB   = IOFFB + DISSIZB * NDISB
   20 CONTINUE
C
C     W(K,L,I,M) ---> W(K,I,L,M)
      CALL SSTGEN(CORE(I020),CORE(I010),LENB,
     1            POP(1,ISPIN),POP(1,ISPIN),POP(1,ISPIN),POP(1,ISPIN),
     1            CORE(I030),1,'1324')
C
C     W(E,B,K,I) at I000, W(K,I,L,M) at I010. Form Y(E,B,L,M) at I020.
C
      IOFFA = I000
      IOFFB = I010
      DO   30 IRPKI=1,NIRREP
C
      IF(IRPDPD(IRPKI,20+ISPIN).EQ.0) GOTO 30
C
      DISSIZA = IRPDPD(IRPKI,18+ISPIN)
      NDISA   = IRPDPD(IRPKI,20+ISPIN)
      DISSIZB = IRPDPD(IRPKI,20+ISPIN)
      NDISB   = IRPDPD(IRPKI,20+ISPIN)
C
      I030 = I020 + DISSIZA * NDISB
      I040 = I030 + DISSIZA * NDISB
      I050 = I040 + MAX(DISSIZA,NDISB)
      I060 = I050 + MAX(DISSIZA,NDISB)
      I070 = I060 + MAX(DISSIZA,NDISB)
C      
      NEED = IINTFP * I070
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14A'
      ENDIF
C
      CALL XGEMM('N','N',DISSIZA,NDISB,NDISA,2.0D+00,
     1           CORE(IOFFA),DISSIZA,
     1           CORE(IOFFB),DISSIZB,0.0D+00,
     1           CORE(I020) ,DISSIZA)
C     Y(E,B,L,M) ---> Y(B,E,L,M)
      CALL SYMTR3(IRPKI,VRT(1,ISPIN),VRT(1,ISPIN),DISSIZA,NDISB,
     1            CORE(I020),CORE(I040),CORE(I050),CORE(I060))
C
      CALL GETLIST(CORE(I030),1,NDISB,2,IRPKI,24+ISPIN)
      NSIZ = DISSIZA * NDISB
      CALL    VADD(CORE(I030),CORE(I030),CORE(I020),NSIZ, 1.0D+00)
      CALL PUTLIST(CORE(I030),1,NDISB,2,IRPKI,24+ISPIN)
C
      IOFFA = IOFFA + DISSIZA * NDISA
      IOFFB = IOFFB + DISSIZB * NDISB
   30 CONTINUE
  100 CONTINUE
C
C     BELM = kBEi * kLiM
C     belm = KbeI * KlIm
C
      DO  200 ISPIN=1,IUHF+1
C
      LENA = 0
      LENB = 0
      DO  110 IRREP=1,NIRREP
      LENA = LENA + IRPDPD(IRREP,18+ISPIN) * IRPDPD(IRREP,23-ISPIN)
      LENB = LENB + IRPDPD(IRREP,14)       * IRPDPD(IRREP,14)
  110 CONTINUE
C
      I000 = 1
      I010 = I000 + LENA
      I020 = I010 + LENA
      I030 = I020 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I030
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14A'
      ENDIF
C
      CALL GETALL(CORE(I010),LENA,1,57+ISPIN)
C     W(E,k,B,i)/W(e,K,b,I) ---> W(E,B,k,i)/W(e,b,K,I)
      CALL SSTGEN(CORE(I010),CORE(I000),LENA,
     1        VRT(1,ISPIN),POP(1,3-ISPIN),VRT(1,ISPIN),POP(1,3-ISPIN),
     1            CORE(I020),1,'1324')
C
      I020 = I010 + LENB
      I030 = I020 + LENB
      I040 = I030 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I040
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14A'
      ENDIF
C
      IOFFB = I020
      DO  120 IRREP=1,NIRREP
      DISSIZB = IRPDPD(IRREP,14)
      NDISB   = IRPDPD(IRREP,14)
      IF(DISSIZB.EQ.0.OR.NDISB.EQ.0) GOTO 120
C     Get W(L,k,M,i)/W(K,l,I,m)
      CALL GETLIST(CORE(IOFFB),1,NDISB,2,IRREP,9)
      IOFFB   = IOFFB + DISSIZB * NDISB
  120 CONTINUE
C
C     W(L,k,M,i) ---> W(L,M,k,i)
C     W(K,l,I,m) ---> W(K,I,l,m)
C
      CALL SSTGEN(CORE(I020),CORE(I010),LENB,
     1            POP(1,1),POP(1,2),POP(1,1),POP(1,2),
     1            CORE(I030),1,'1324')
C
C     ISPIN = 1 : W(E,B,k,i) at I000, W(L,M,k,i) at I010. Form Y(E,B,L,M)
C                 at I020.
C     ISPIN = 2 : W(e,b,K,I) at I000, W(K,I,l,m) at I010. Form Y(e,b,l,m)
C                 at I020.
C
      IOFFA = I000
      IOFFB = I010
      DO  130 IRPKI=1,NIRREP
C
      IF(IRPDPD(IRPKI,20+ISPIN).EQ.0) GOTO 130
C
      DISSIZA = IRPDPD(IRPKI,18+ISPIN)
      NDISA   = IRPDPD(IRPKI,23-ISPIN)
      DISSIZB = IRPDPD(IRPKI,21)
      NDISB   = IRPDPD(IRPKI,22)
C
      I030 = I020 + DISSIZA * MAX(DISSIZB,NDISB)
      I040 = I030 + DISSIZA * MAX(DISSIZB,NDISB)
      I050 = I040 + MAX(DISSIZA,DISSIZB,NDISB)
      I060 = I050 + MAX(DISSIZA,DISSIZB,NDISB)
      I070 = I060 + MAX(DISSIZA,DISSIZB,NDISB)
C      
      NEED = IINTFP * I070
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14A'
      ENDIF
C
      IF(ISPIN.EQ.1)THEN
      CALL XGEMM('N','T',DISSIZA,DISSIZB,NDISA,2.0D+00,
     1           CORE(IOFFA),DISSIZA,
     1           CORE(IOFFB),DISSIZB,0.0D+00,
     1           CORE(I020),DISSIZA)
      ELSE
      CALL XGEMM('N','N',DISSIZA,NDISB,NDISA,2.0D+00,
     1           CORE(IOFFA),DISSIZA,
     1           CORE(IOFFB),DISSIZB,0.0D+00,
     1           CORE(I020) ,DISSIZA)
      ENDIF
      IF(ISPIN.EQ.1)THEN
      NDISY = DISSIZB
      ELSE
      NDISY = NDISB
      ENDIF
C     Y(E,B,L,M)/Y(e,b,l,m) ---> Y(B,E,L,M)/Y(b,e,l,m)
      CALL SYMTR3(IRPKI,VRT(1,ISPIN),VRT(1,ISPIN),DISSIZA,NDISY,
     1            CORE(I020),CORE(I040),CORE(I050),CORE(I060))
C
      CALL GETLIST(CORE(I030),1,NDISY,2,IRPKI,24+ISPIN)
      NSIZ = DISSIZA * NDISY
      CALL    VADD(CORE(I030),CORE(I030),CORE(I020),NSIZ, 1.0D+00)
      CALL PUTLIST(CORE(I030),1,NDISY,2,IRPKI,24+ISPIN)
C
      IOFFA = IOFFA + DISSIZA * NDISA
      IOFFB = IOFFB + DISSIZB * NDISB
  130 CONTINUE
  200 CONTINUE
C
C     BeLm = kBeI * kLIm
C     bElM = KbEi * KliM
C
      DO  300 ISPIN=1,IUHF+1
C
      LENA = 0
      LENB = 0
      DO  210 IRREP=1,NIRREP
      LENA = LENA + IRPDPD(IRREP,13) * IRPDPD(IRREP,14)
      LENB = LENB + IRPDPD(IRREP,14) * IRPDPD(IRREP,14)
  210 CONTINUE
C
      I000 = 1
      I010 = I000 + LENA
      I020 = I010 + LENA
      I030 = I020 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I030
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14A'
      ENDIF
C
      CALL GETALL(CORE(I010),LENA,1,58-ISPIN-1+IUHF)
C     W(e,k,B,I)/W(E,K,b,i) ---> W(e,B,k,I)/W(E,b,K,i)
      CALL SSTGEN(CORE(I010),CORE(I000),LENA,
     1        VRT(1,3-ISPIN),POP(1,3-ISPIN),VRT(1,ISPIN),POP(1,ISPIN),
     1            CORE(I020),1,'1324')
C
      I020 = I010 + LENB
      I030 = I020 + LENB
      I040 = I030 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I040
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14A'
      ENDIF
C
      IOFFB = I010
      DO  220 IRREP=1,NIRREP
      DISSIZB = IRPDPD(IRREP,14)
      NDISB   = IRPDPD(IRREP,14)
      IF(DISSIZB.EQ.0.OR.NDISB.EQ.0) GOTO 220
C     Get W(L,k,I,m)/W(K,l,M,i)
      CALL GETLIST(CORE(IOFFB),1,NDISB,2,IRREP,9)
      IOFFB   = IOFFB + DISSIZB * NDISB
  220 CONTINUE
C
C     W(L,k,I,m) ---> W(L,I,k,m)
C     W(K,l,M,i) ---> W(K,M,l,i)
      CALL SSTGEN(CORE(I010),CORE(I020),LENB,
     1            POP(1,1),POP(1,2),POP(1,1),POP(1,2),
     1            CORE(I030),1,'1324')
C
C     W(L,I,k,m) ---> W(L,m,k,I)
C     W(K,M,l,i) ---> W(K,i,l,M)
      CALL SSTGEN(CORE(I020),CORE(I010),LENB,
     1            POP(1,1),POP(1,1),POP(1,2),POP(1,2),
     1            CORE(I030),1,'1432')
C
C     ISPIN = 1 : W(e,B,k,I) at I000, W(L,m,k,I) at I010. Form Y(e,B,L,m)
C                 at I020.
C     ISPIN = 2 : W(E,b,K,i) at I000, W(K,i,l,M) at I010. Form Y(E,b,l,M)
C                 at I020.
C
      IOFFA = I000
      IOFFB = I010
      DO  230 IRPKI=1,NIRREP
C
      IF(IRPDPD(IRPKI,14).EQ.0.OR.IRPDPD(IRPKI,13).EQ.0) GOTO 230
C
      DISSIZA = IRPDPD(IRPKI,13)
      NDISA   = IRPDPD(IRPKI,14)
      DISSIZB = IRPDPD(IRPKI,14)
      NDISB   = IRPDPD(IRPKI,14)
C
      I030 = I020 + DISSIZA * MAX(DISSIZB,NDISB)
      I040 = I030 + DISSIZA * MAX(DISSIZB,NDISB)
      I050 = I040 + MAX(DISSIZA,DISSIZB,NDISB)
      I060 = I050 + MAX(DISSIZA,DISSIZB,NDISB)
      I070 = I060 + MAX(DISSIZA,DISSIZB,NDISB)
C      
      NEED = IINTFP * I070
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14A'
      ENDIF
C
      IF(ISPIN.EQ.1)THEN
      CALL XGEMM('N','T',DISSIZA,DISSIZB,NDISA,-2.0D+00,
     1           CORE(IOFFA),DISSIZA,
     1           CORE(IOFFB),DISSIZB,0.0D+00,
     1           CORE(I020),DISSIZA)
      ELSE
      CALL XGEMM('N','N',DISSIZA,NDISB,NDISA,-2.0D+00,
     1           CORE(IOFFA),DISSIZA,
     1           CORE(IOFFB),DISSIZB,0.0D+00,
     1           CORE(I020) ,DISSIZA)
      ENDIF
C
      IF(ISPIN.EQ.1)THEN
      NDISY = DISSIZB
      ELSE
      NDISY = NDISB
      ENDIF
C     Y(e,B,L,m)/Y(E,b,l,M) ---> Y(B,e,L,m)/Y(b,E,l,M)
      CALL SYMTR3(IRPKI,VRT(1,3-ISPIN),VRT(1,ISPIN),DISSIZA,NDISY,
     1            CORE(I020),CORE(I040),CORE(I050),CORE(I060))
C
      CALL GETLIST(CORE(I030),1,NDISY,2,IRPKI,26+ISPIN)
      NSIZ = DISSIZA * NDISY
      CALL    VADD(CORE(I030),CORE(I030),CORE(I020),NSIZ, 1.0D+00)
      CALL PUTLIST(CORE(I030),1,NDISY,2,IRPKI,26+ISPIN)
C
      IOFFA = IOFFA + DISSIZA * NDISA
      IOFFB = IOFFB + DISSIZB * NDISB
  230 CONTINUE
  300 CONTINUE
C
C     BElm = kBEi * klim
C     beLM = KbeI * KLIM
C
      DO  400 ISPIN=1,IUHF+1
C
      LENA = 0
      LENB = 0
      DO  310 IRREP=1,NIRREP
      LENA = LENA + IRPDPD(IRREP,18+ISPIN) * IRPDPD(IRREP,23-ISPIN)
      LENB = LENB + IRPDPD(IRREP,23-ISPIN) * IRPDPD(IRREP,23-ISPIN)
  310 CONTINUE
C
      I000 = 1
      I010 = I000 + LENA
      I020 = I010 + LENA
      I030 = I020 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I030
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14A'
      ENDIF
C
      CALL GETALL(CORE(I010),LENA,1,57+ISPIN)
C     W(E,k,B,i)/W(e,K,b,I) ---> W(E,B,i,k)/W(e,b,K,I)
      CALL SSTGEN(CORE(I010),CORE(I000),LENA,
     1        VRT(1,ISPIN),POP(1,3-ISPIN),VRT(1,ISPIN),POP(1,3-ISPIN),
     1            CORE(I020),1,'1324')
C
      I020 = I010 + LENB
      I030 = I020 + LENB
      I040 = I030 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I040
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14A'
      ENDIF
C
      IOFFB = I020
      DO  320 IRREP=1,NIRREP
C
CSSS      DISSIZB = IRPDPD(IRREP, 5-ISPIN)
CSSS      NDISB   = IRPDPD(IRREP, 5-ISPIN)
C Note the comments above. 03/07/2003, Ajith Perera
C 
      DISSIZB = IRPDPD(IRREP, 23-ISPIN)
      NDISB   = IRPDPD(IRREP, 23-ISPIN)

      IF(DISSIZB.EQ.0.OR.NDISB.EQ.0) GOTO 320
C     Get W(K<L,I<M)
      CALL GETLIST(CORE(IOFFB),1,NDISB,2,IRREP,9-ISPIN-1+IUHF)
      DISSIZB = IRPDPD(IRREP,23-ISPIN)
C     W(K<L,I<M) ---> W(K,L,I<M)
      CALL SYMEXP2(IRREP,POP(1,3-ISPIN),DISSIZB,IRPDPD(IRREP,5-ISPIN),
     1             NDISB,CORE(IOFFB),CORE(IOFFB))
      NDISB   = IRPDPD(IRREP,23-ISPIN)
C     W(K,L,I<M) ---> W(K,L,I,M)
      CALL  SYMEXP(IRREP,POP(1,3-ISPIN),DISSIZB,CORE(IOFFB))
      IOFFB   = IOFFB + DISSIZB * NDISB
  320 CONTINUE
C
C     W(K,L,I,M) ---> W(K,I,L,M)
      CALL SSTGEN(CORE(I020),CORE(I010),LENB,
     1    POP(1,3-ISPIN),POP(1,3-ISPIN),POP(1,3-ISPIN),POP(1,3-ISPIN),
     1            CORE(I030),1,'1324')
C
C     ISPIN = 1 : W(E,B,k,i) at I000, W(k,i,l,m) at I010. Form Y(E,B,l,m)
C                 at I020.
C     ISPIN = 2 : W(e,b,K,I) at I000, W(K,I,L,M) at I010. Form Y(e,b,L,M)
C                 at I020.
C
      IOFFA = I000
      IOFFB = I010
      DO  330 IRPKI=1,NIRREP
C
      IF(IRPDPD(IRPKI,18+ISPIN).EQ.0.OR.
     1   IRPDPD(IRPKI,23-ISPIN).EQ.0)    GOTO 330
C
      DISSIZA = IRPDPD(IRPKI,18+ISPIN)
      NDISA   = IRPDPD(IRPKI,23-ISPIN)
      DISSIZB = IRPDPD(IRPKI,23-ISPIN)
      NDISB   = IRPDPD(IRPKI,23-ISPIN)
C
      I030 = I020 + DISSIZA * NDISB
      I040 = I030 + DISSIZA * NDISB
      I050 = I040 + MAX(DISSIZA,NDISB)
      I060 = I050 + MAX(DISSIZA,NDISB)
      I070 = I060 + MAX(DISSIZA,NDISB)
C      
      NEED = IINTFP * I070
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14A'
      ENDIF
C
      CALL XGEMM('N','N',DISSIZA,NDISB,NDISA,2.0D+00,
     1           CORE(IOFFA),DISSIZA,
     1           CORE(IOFFB),DISSIZB,0.0D+00,
     1           CORE(I020) ,DISSIZA)
C     Y(E,B,l,m)/Y(e,b,L,M) ---> Y(B,E,l,m)/Y(e,b,L,M)
      CALL SYMTR3(IRPKI,VRT(1,ISPIN),VRT(1,ISPIN),DISSIZA,NDISB,
     1            CORE(I020),CORE(I040),CORE(I050),CORE(I060))
C
      CALL GETLIST(CORE(I030),1,NDISB,2,IRPKI,28+ISPIN)
      NSIZ = DISSIZA * NDISB
      CALL    VADD(CORE(I030),CORE(I030),CORE(I020),NSIZ, 1.0D+00)
      CALL PUTLIST(CORE(I030),1,NDISB,2,IRPKI,28+ISPIN)
C
      IOFFA = IOFFA + DISSIZA * NDISA
      IOFFB = IOFFB + DISSIZB * NDISB
  330 CONTINUE
  400 CONTINUE
C
C     BElm = KBEI * KlIm
C     bELM = kbei * kLiM
C
      DO  500 ISPIN=1,IUHF+1
C
      LENA = 0
      LENB = 0
      DO  410 IRREP=1,NIRREP
      LENA = LENA + IRPDPD(IRREP,18+ISPIN) * IRPDPD(IRREP,20+ISPIN)
      LENB = LENB + IRPDPD(IRREP,14)       * IRPDPD(IRREP,14)
  410 CONTINUE
C
      I000 = 1
      I010 = I000 + LENA
      I020 = I010 + LENA
      I030 = I020 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I030
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14A'
      ENDIF
C
      CALL GETALL(CORE(I010),LENA,1,53+ISPIN)
C     W(E,K,B,I)/W(e,k,b,i) ---> W(E,B,K,I)/W(e,b,k,i)
      CALL SSTGEN(CORE(I010),CORE(I000),LENA,
     1            VRT(1,ISPIN),POP(1,ISPIN),VRT(1,ISPIN),POP(1,ISPIN),
     1            CORE(I020),1,'1324')
C
      I020 = I010 + LENB
      I030 = I020 + LENB
      I040 = I030 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I040
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14A'
      ENDIF
C
      IOFFB = I020
      DO  420 IRREP=1,NIRREP
      DISSIZB = IRPDPD(IRREP,14)
      NDISB   = IRPDPD(IRREP,14)
      IF(DISSIZB.EQ.0.OR.NDISB.EQ.0) GOTO 420
C     Get W(K,l,I,m)/W(L,k,M,i)
      CALL GETLIST(CORE(IOFFB),1,NDISB,2,IRREP,9)
      IOFFB   = IOFFB + DISSIZB * NDISB
  420 CONTINUE
C
C     W(K,l,I,m) ---> W(K,I,l,m)
C     W(L,k,M,i) ---> W(L,M,k,i)
      CALL SSTGEN(CORE(I020),CORE(I010),LENB,
     1            POP(1,1),POP(1,2),POP(1,1),POP(1,2),
     1            CORE(I030),1,'1324')
C
C     ISPIN = 1 : W(E,B,K,I) at I000, W(K,I,l,m) at I010. Form Y(E,B,L,M)
C                 at I020.
C     ISPIN = 2 : W(e,b,k,i) at I000, W(L,M,k,i) at I010. Form Y(e,b,l,m)
C                 at I020.
C
      IOFFA = I000
      IOFFB = I010
      DO  430 IRPKI=1,NIRREP
C
      IF(IRPDPD(IRPKI,18+ISPIN).EQ.0.OR.IRPDPD(IRPKI,20+ISPIN).EQ.0.OR.
     1   IRPDPD(IRPKI,23-ISPIN).EQ.0) GOTO 430
C
      DISSIZA = IRPDPD(IRPKI,18+ISPIN)
      NDISA   = IRPDPD(IRPKI,20+ISPIN)
      DISSIZB = IRPDPD(IRPKI,21)
      NDISB   = IRPDPD(IRPKI,22)
C
      I030 = I020 + DISSIZA * MAX(DISSIZB,NDISB)
      I040 = I030 + DISSIZA * MAX(DISSIZB,NDISB)
      I050 = I040 + MAX(DISSIZA,DISSIZB,NDISB)
      I060 = I050 + MAX(DISSIZA,DISSIZB,NDISB)
      I070 = I060 + MAX(DISSIZA,DISSIZB,NDISB)
C      
      NEED = IINTFP * I070
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14A'
      ENDIF
C
      IF(ISPIN.EQ.1)THEN
      CALL XGEMM('N','N',DISSIZA,NDISB,NDISA,2.0D+00,
     1           CORE(IOFFA),DISSIZA,
     1           CORE(IOFFB),DISSIZB,0.0D+00,
     1           CORE(I020) ,DISSIZA)
      ELSE
      CALL XGEMM('N','T',DISSIZA,DISSIZB,NDISA,2.0D+00,
     1           CORE(IOFFA),DISSIZA,
     1           CORE(IOFFB),DISSIZB,0.0D+00,
     1           CORE(I020),DISSIZA)
      ENDIF
C
      IF(ISPIN.EQ.1)THEN
      NDISY = NDISB
      ELSE
      NDISY = DISSIZB
      ENDIF
C     Y(E,B,l,m)/Y(e,b,L,M) ---> Y(B,E,l,m)/Y(b,e,L,M)
      CALL SYMTR3(IRPKI,VRT(1,ISPIN),VRT(1,ISPIN),DISSIZA,NDISY,
     1            CORE(I020),CORE(I040),CORE(I050),CORE(I060))
C
      CALL GETLIST(CORE(I030),1,NDISY,2,IRPKI,28+ISPIN)
      NSIZ = DISSIZA * NDISY
      CALL    VADD(CORE(I030),CORE(I030),CORE(I020),NSIZ, 1.0D+00)
      CALL PUTLIST(CORE(I030),1,NDISY,2,IRPKI,28+ISPIN)
C
      IOFFA = IOFFA + DISSIZA * NDISA
      IOFFB = IOFFB + DISSIZB * NDISB
  430 CONTINUE
  500 CONTINUE
      RETURN
      END
