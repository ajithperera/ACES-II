      SUBROUTINE Y14C(CORE,MAXCOR,IUHF)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DISSIZA,DISSIZB,DISSIZY
      INTEGER POP,VRT,DIRPRD
      DIMENSION CORE(1)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYM/    POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /INFO/   NOCA,NOCB,NVRTA,NVRTB
      COMMON /T3OFF/  IOFFVV(8,8,10),IOFFOO(8,8,10),IOFFVO(8,8,4)
C
      WRITE(6,1000)
 1000 FORMAT(' @Y14C-I, Y3 * Y3 contributions. ')
C
C     Y(B,E,L,M) = - SUM  W(M,B,C,J) * W(J,C,E,L)
C                    J,C
C
C     BELM = JCEL * MBCJ
C     belm = jcel * mbcj
C
      DO  100 ISPIN=1,IUHF+1
C
      LENA = 0
      LENB = 0
      LENC = 0
      DO   10 IRREP=1,NIRREP
      LENA = LENA + IRPDPD(IRREP,18+ISPIN) * IRPDPD(IRREP,20+ISPIN)
      LENB = LENB + IRPDPD(IRREP,18+ISPIN) * IRPDPD(IRREP,20+ISPIN)
      LENC = LENC + IRPDPD(IRREP,18+ISPIN) * IRPDPD(IRREP,20+ISPIN)
   10 CONTINUE
      LEN  = MAX(LENA,LENB,LENC)
C
      I000 = 1
      I010 = I000 + LEN
      I020 = I010 + LEN
      I030 = I020 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I030
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
 1020 FORMAT(' @Y14C-I, Insufficient memory. Need ',I15,' . Got ',I15)
      STOP 'Y14C'
      ENDIF
C
      CALL GETALL(CORE(I010),LENA,1,53+ISPIN)
C     W(E,J,C,L)/W(e,j,c,l) ---> W(E,L,C,J)/W(e,l,c,j)
      CALL SSTGEN(CORE(I010),CORE(I000),LENA,
     1            VRT(1,ISPIN),POP(1,ISPIN),VRT(1,ISPIN),POP(1,ISPIN),
     1            CORE(I020),1,'1432')
C
      I020 = I010 + LEN
      I030 = I020 + LEN
      I040 = I030 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I040
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14C'
      ENDIF
C
      CALL GETALL(CORE(I020),LENB,1,53+ISPIN)
C     W(C,M,B,J)/W(c,m,b,j) ---> W(C,J,B,M)/W(c,j,b,m)
      CALL SSTGEN(CORE(I020),CORE(I010),LENB,
     1            VRT(1,ISPIN),POP(1,ISPIN),VRT(1,ISPIN),POP(1,ISPIN),
     1            CORE(I030),1,'1432')
C
C     W(E,L,C,J) at I000, W(C,J,B,M) at I010. Form Y(E,L,B,M) at I020.
C
      IOFFA = I000
      IOFFB = I010
      IOFFY = I020
      DO   20 IRPCJ=1,NIRREP
C
      IF(IRPDPD(IRPCJ, 8+ISPIN).EQ.0) GOTO 20
      DISSIZA = IRPDPD(IRPCJ, 8+ISPIN)
      NDISA   = IRPDPD(IRPCJ, 8+ISPIN)
      DISSIZB = IRPDPD(IRPCJ, 8+ISPIN)
      NDISB   = IRPDPD(IRPCJ, 8+ISPIN)
C
      CALL XGEMM('N','N',DISSIZA,NDISB,NDISA,4.0D+00,
     1           CORE(IOFFA),DISSIZA,
     1           CORE(IOFFB),DISSIZB,0.0D+00,
     1           CORE(IOFFY),DISSIZA)
C
      IOFFA = IOFFA + DISSIZA * NDISA
      IOFFB = IOFFB + DISSIZB * NDISB
      IOFFY = IOFFY + DISSIZA * NDISB
   20 CONTINUE
C
C     Y(E,L,B,M) ---> Y(E,B,L,M)
C
      CALL SSTGEN(CORE(I020),CORE(I010),LENC,
     1            VRT(1,ISPIN),POP(1,ISPIN),VRT(1,ISPIN),POP(1,ISPIN),
     1            CORE(I030),1,'1324')
      
      IOFFY = I010
      DO   30 IRPLM=1,NIRREP
C
      IF(IRPDPD(IRPLM,18+ISPIN).EQ.0.OR.
     1   IRPDPD(IRPLM,20+ISPIN).EQ.0) GOTO 30
C
      DISSIZY = IRPDPD(IRPLM,18+ISPIN)
      NDISY   = IRPDPD(IRPLM,20+ISPIN)
C
      I030 = I020 +     DISSIZY*NDISY
      I040 = I030 + MAX(DISSIZY,NDISY)
      I050 = I040 + MAX(DISSIZY,NDISY)
      I060 = I050 + MAX(DISSIZY,NDISY)
C      
      NEED = IINTFP * I060
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14C'
      ENDIF
C
C     Y(E,B,L,M) ---> Y(B,E,L,M)
      CALL SYMTR3(IRPLM,VRT(1,ISPIN),VRT(1,ISPIN),DISSIZY,NDISY,
     1            CORE(IOFFY),CORE(I030),CORE(I040),CORE(I050))
C
      CALL GETLIST(CORE(I020),1,NDISY,2,IRPLM,24+ISPIN)
      NSIZ = DISSIZY * NDISY
      CALL    VADD(CORE(I020),CORE(I020),CORE(IOFFY),NSIZ,-1.0D+00)
      CALL PUTLIST(CORE(I020),1,NDISY,2,IRPLM,24+ISPIN)
C
      IOFFY = IOFFY + DISSIZY * NDISY
   30 CONTINUE
  100 CONTINUE
C
C     BeLm = JceL * mBcJ
C     bElM = jCEl * MbCj
C
      DO  200 ISPIN=1,IUHF+1
C
      LENA = 0
      LENB = 0
      LENC = 0
      DO  110 IRREP=1,NIRREP
      LENA = LENA + IRPDPD(IRREP,13-ISPIN) * IRPDPD(IRREP,13-ISPIN)
      LENB = LENB + IRPDPD(IRREP,13-ISPIN) * IRPDPD(IRREP,10+ISPIN)
      LENC = LENC + IRPDPD(IRREP,13-ISPIN) * IRPDPD(IRREP,10+ISPIN)
  110 CONTINUE
      LEN  = MAX(LENA,LENB,LENC)
C
      I000 = 1
      I010 = I000 + LEN
      I020 = I010 + LEN
      I030 = I020 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I030
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14C'
      ENDIF
C
      CALL GETALL(CORE(I010),LENA,1,60-ISPIN-1+IUHF)
C     W(e,J,c,L)/W(E,j,C,l) ---> W(e,L,c,J)/W(E,l,C,j)
      CALL SSTGEN(CORE(I010),CORE(I000),LENA,
     1        VRT(1,3-ISPIN),POP(1,ISPIN),VRT(1,3-ISPIN),POP(1,ISPIN),
     1            CORE(I020),1,'1432')
C
      I020 = I010 + LEN
      I030 = I020 + LEN
      I040 = I030 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I040
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14C'
      ENDIF
C
      CALL GETALL(CORE(I020),LENB,1,58-ISPIN-1+IUHF)
C     W(c,m,B,J)/W(C,M,b,j) ---> W(c,J,B,m)/W(C,j,b,M)
      CALL SSTGEN(CORE(I020),CORE(I010),LENB,
     1        VRT(1,3-ISPIN),POP(1,3-ISPIN),VRT(1,ISPIN),POP(1,ISPIN),
     1            CORE(I030),1,'1432')
C
C     ISPIN = 1 : W(e,L,c,J) at I000, W(c,J,B,m) at I010. Form Y(e,L,B,m)
C                 at I020.
C     ISPIN = 2 : W(E,l,C,j) at I000, W(C,j,b,M) at I010. Form Y(E,l,b,M)
C                 at I020.
C
      IOFFA = I000
      IOFFB = I010
      IOFFY = I020
      DO  120 IRPCJ=1,NIRREP
C
      IF(IRPDPD(IRPCJ,13-ISPIN).EQ.0.OR.
     1   IRPDPD(IRPCJ,10+ISPIN).EQ.0) GOTO 120
      DISSIZA = IRPDPD(IRPCJ,13-ISPIN)
      NDISA   = IRPDPD(IRPCJ,13-ISPIN)
      DISSIZB = IRPDPD(IRPCJ,13-ISPIN)
      NDISB   = IRPDPD(IRPCJ,10+ISPIN)
C
      CALL XGEMM('N','N',DISSIZA,NDISB,NDISA,4.0D+00,
     1           CORE(IOFFA),DISSIZA,
     1           CORE(IOFFB),DISSIZB,0.0D+00,
     1           CORE(IOFFY),DISSIZA)
C
      IOFFA = IOFFA + DISSIZA * NDISA
      IOFFB = IOFFB + DISSIZB * NDISB
      IOFFY = IOFFY + DISSIZA * NDISB
  120 CONTINUE
C
C     Y(e,L,B,m)/Y(E,l,b,M) ---> Y(e,B,L,m)/Y(E,b,l,M)
C
      CALL SSTGEN(CORE(I020),CORE(I010),LENC,
     1        VRT(1,3-ISPIN),POP(1,ISPIN),VRT(1,ISPIN),POP(1,3-ISPIN),
     1            CORE(I030),1,'1324')
      
      IOFFY = I010
      DO  130 IRPLM=1,NIRREP
C
      IF(IRPDPD(IRPLM,13).EQ.0.OR.
     1   IRPDPD(IRPLM,14).EQ.0) GOTO 130
C
      DISSIZY = IRPDPD(IRPLM,13)
      NDISY   = IRPDPD(IRPLM,14)
C
      I030 = I020 +     DISSIZY*NDISY
      I040 = I030 + MAX(DISSIZY,NDISY)
      I050 = I040 + MAX(DISSIZY,NDISY)
      I060 = I050 + MAX(DISSIZY,NDISY)
C      
      NEED = IINTFP * I060
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14C'
      ENDIF
C
C     Y(e,B,L,m)/Y(E,b,l,M) ---> Y(B,e,L,m)/Y(b,E,l,M)
      CALL SYMTR3(IRPLM,VRT(1,3-ISPIN),VRT(1,ISPIN),DISSIZY,NDISY,
     1            CORE(IOFFY),CORE(I030),CORE(I040),CORE(I050))
C
      CALL GETLIST(CORE(I020),1,NDISY,2,IRPLM,26+ISPIN)
      NSIZ = DISSIZY * NDISY
      CALL    VADD(CORE(I020),CORE(I020),CORE(IOFFY),NSIZ,-1.0D+00)
      CALL PUTLIST(CORE(I020),1,NDISY,2,IRPLM,26+ISPIN)
C
      IOFFY = IOFFY + DISSIZY * NDISY
  130 CONTINUE
  200 CONTINUE
C
C     BeLm = jCeL * mBCj
C     bElM = JcEl * MbcJ
C
      DO  300 ISPIN=1,IUHF+1
C
      LENA = 0
      LENB = 0
      LENC = 0
      DO  210 IRREP=1,NIRREP
      LENA = LENA + IRPDPD(IRREP,13-ISPIN) * IRPDPD(IRREP,10+ISPIN)
      LENB = LENB + IRPDPD(IRREP,10+ISPIN) * IRPDPD(IRREP,10+ISPIN)
      LENC = LENC + IRPDPD(IRREP,13-ISPIN) * IRPDPD(IRREP,10+ISPIN)
  210 CONTINUE
      LEN  = MAX(LENA,LENB,LENC)
C
      I000 = 1
      I010 = I000 + LEN
      I020 = I010 + LEN
      I030 = I020 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I030
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14C'
      ENDIF
C
      CALL GETALL(CORE(I010),LENA,1,58-ISPIN-1+IUHF)
C     W(e,j,C,L)/W(E,J,c,l) ---> W(e,L,C,J)/W(E,l,c,J)
      CALL SSTGEN(CORE(I010),CORE(I000),LENA,
     1        VRT(1,3-ISPIN),POP(1,3-ISPIN),VRT(1,ISPIN),POP(1,ISPIN),
     1            CORE(I020),1,'1432')
C
      I020 = I010 + LEN
      I030 = I020 + LEN
      I040 = I030 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I040
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14C'
      ENDIF
C
      CALL GETALL(CORE(I020),LENB,1,57+ISPIN)
C     W(C,m,B,j)/W(c,M,b,J) ---> W(C,j,B,m)/W(c,J,b,M)
      CALL SSTGEN(CORE(I020),CORE(I010),LENB,
     1        VRT(1,ISPIN),POP(1,3-ISPIN),VRT(1,ISPIN),POP(1,3-ISPIN),
     1            CORE(I030),1,'1432')
C
C     ISPIN = 1 : W(e,L,C,j) at I000, W(C,j,B,m) at I010. Form Y(e,L,B,m)
C                 at I020.
C     ISPIN = 2 : W(E,l,c,J) at I000, W(c,J,b,M) at I010. Form Y(E,l,b,M)
C                 at I020.
C
      IOFFA = I000
      IOFFB = I010
      IOFFY = I020
      DO  220 IRPCJ=1,NIRREP
C
      IF(IRPDPD(IRPCJ,13-ISPIN).EQ.0.OR.
     1   IRPDPD(IRPCJ,10+ISPIN).EQ.0) GOTO 220
      DISSIZA = IRPDPD(IRPCJ,13-ISPIN)
      NDISA   = IRPDPD(IRPCJ,10+ISPIN)
      DISSIZB = IRPDPD(IRPCJ,10+ISPIN)
      NDISB   = IRPDPD(IRPCJ,10+ISPIN)
C
      CALL XGEMM('N','N',DISSIZA,NDISB,NDISA,4.0D+00,
     1           CORE(IOFFA),DISSIZA,
     1           CORE(IOFFB),DISSIZB,0.0D+00,
     1           CORE(IOFFY),DISSIZA)
C
      IOFFA = IOFFA + DISSIZA * NDISA
      IOFFB = IOFFB + DISSIZB * NDISB
      IOFFY = IOFFY + DISSIZA * NDISB
  220 CONTINUE
C
C     Y(e,L,B,m)/Y(E,l,b,M) ---> Y(e,B,L,m)/Y(E,b,l,M)
C
      CALL SSTGEN(CORE(I020),CORE(I010),LENC,
     1        VRT(1,3-ISPIN),POP(1,ISPIN),VRT(1,ISPIN),POP(1,3-ISPIN),
     1            CORE(I030),1,'1324')
      
      IOFFY = I010
      DO  230 IRPLM=1,NIRREP
C
      IF(IRPDPD(IRPLM,13).EQ.0.OR.
     1   IRPDPD(IRPLM,14).EQ.0) GOTO 230
C
      DISSIZY = IRPDPD(IRPLM,13)
      NDISY   = IRPDPD(IRPLM,14)
C
      I030 = I020 +     DISSIZY*NDISY
      I040 = I030 + MAX(DISSIZY,NDISY)
      I050 = I040 + MAX(DISSIZY,NDISY)
      I060 = I050 + MAX(DISSIZY,NDISY)
C      
      NEED = IINTFP * I060
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14C'
      ENDIF
C
C     Y(e,B,L,m)/Y(E,b,l,M) ---> Y(B,e,L,m)/Y(b,E,l,M)
      CALL SYMTR3(IRPLM,VRT(1,3-ISPIN),VRT(1,ISPIN),DISSIZY,NDISY,
     1            CORE(IOFFY),CORE(I030),CORE(I040),CORE(I050))
C
      CALL GETLIST(CORE(I020),1,NDISY,2,IRPLM,26+ISPIN)
      NSIZ = DISSIZY * NDISY
      CALL    VADD(CORE(I020),CORE(I020),CORE(IOFFY),NSIZ,-1.0D+00)
      CALL PUTLIST(CORE(I020),1,NDISY,2,IRPLM,26+ISPIN)
C
      IOFFY = IOFFY + DISSIZY * NDISY
  230 CONTINUE
  300 CONTINUE
C
C     BElm = JcEl * mBcJ
C     beLM = jCeL * MbCj
C
      DO  400 ISPIN=1,IUHF+1
C
      LENA = 0
      LENB = 0
      LENC = 0
      DO  310 IRREP=1,NIRREP
      LENA = LENA + IRPDPD(IRREP,10+ISPIN) * IRPDPD(IRREP,13-ISPIN)
      LENB = LENB + IRPDPD(IRREP,13-ISPIN) * IRPDPD(IRREP,10+ISPIN)
      LENC = LENC + IRPDPD(IRREP,10+ISPIN) * IRPDPD(IRREP,10+ISPIN)
  310 CONTINUE
      LEN  = MAX(LENA,LENB,LENC)
C
      I000 = 1
      I010 = I000 + LEN
      I020 = I010 + LEN
      I030 = I020 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I030
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14C'
      ENDIF
C
      CALL GETALL(CORE(I010),LENA,1,55+ISPIN)
C     W(E,J,c,l)/W(e,j,C,L) ---> W(E,l,c,J)/W(e,L,C,j)
      CALL SSTGEN(CORE(I010),CORE(I000),LENA,
     1        VRT(1,ISPIN),POP(1,ISPIN),VRT(1,3-ISPIN),POP(1,3-ISPIN),
     1            CORE(I020),1,'1432')
C
      I020 = I010 + LEN
      I030 = I020 + LEN
      I040 = I030 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I040
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14C'
      ENDIF
C
      CALL GETALL(CORE(I020),LENB,1,58-ISPIN-1+IUHF)
C     W(c,m,B,J)/W(C,M,b,j) ---> W(c,J,B,m)/W(C,j,b,M)
      CALL SSTGEN(CORE(I020),CORE(I010),LENB,
     1        VRT(1,3-ISPIN),POP(1,3-ISPIN),VRT(1,ISPIN),POP(1,ISPIN),
     1            CORE(I030),1,'1432')
C
C     ISPIN = 1 : W(E,l,c,J) at I000, W(c,J,B,m) at I010. Form Y(E,l,B,m)
C                 at I020.
C     ISPIN = 2 : W(e,L,C,j) at I000, W(C,j,b,M) at I010. Form Y(e,L,b,M)
C                 at I020.
C
      IOFFA = I000
      IOFFB = I010
      IOFFY = I020
      DO  320 IRPCJ=1,NIRREP
C
      IF(IRPDPD(IRPCJ,13-ISPIN).EQ.0.OR.
     1   IRPDPD(IRPCJ,10+ISPIN).EQ.0) GOTO 320
      DISSIZA = IRPDPD(IRPCJ,10+ISPIN)
      NDISA   = IRPDPD(IRPCJ,13-ISPIN)
      DISSIZB = IRPDPD(IRPCJ,13-ISPIN)
      NDISB   = IRPDPD(IRPCJ,10+ISPIN)
C
      CALL XGEMM('N','N',DISSIZA,NDISB,NDISA,4.0D+00,
     1           CORE(IOFFA),DISSIZA,
     1           CORE(IOFFB),DISSIZB,0.0D+00,
     1           CORE(IOFFY),DISSIZA)
C
      IOFFA = IOFFA + DISSIZA * NDISA
      IOFFB = IOFFB + DISSIZB * NDISB
      IOFFY = IOFFY + DISSIZA * NDISB
  320 CONTINUE
C
C     Y(E,l,B,m)/Y(e,L,b,M) ---> Y(E,B,l,m)/Y(e,b,M,M)
C
      CALL SSTGEN(CORE(I020),CORE(I010),LENC,
     1        VRT(1,ISPIN),POP(1,3-ISPIN),VRT(1,ISPIN),POP(1,3-ISPIN),
     1            CORE(I030),1,'1324')
      
      IOFFY = I010
      DO  330 IRPLM=1,NIRREP
C
      IF(IRPDPD(IRPLM,18+ISPIN).EQ.0.OR.
     1   IRPDPD(IRPLM,23-ISPIN).EQ.0) GOTO 330
C
      DISSIZY = IRPDPD(IRPLM,18+ISPIN)
      NDISY   = IRPDPD(IRPLM,23-ISPIN)
C
      I030 = I020 +     DISSIZY*NDISY
      I040 = I030 + MAX(DISSIZY,NDISY)
      I050 = I040 + MAX(DISSIZY,NDISY)
      I060 = I050 + MAX(DISSIZY,NDISY)
C      
      NEED = IINTFP * I060
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14C'
      ENDIF
C
C     Y(E,B,l,m)/Y(e,b,L,M) ---> Y(B,E,l,m)/Y(b,e,L,M)
      CALL SYMTR3(IRPLM,VRT(1,ISPIN),VRT(1,ISPIN),DISSIZY,NDISY,
     1            CORE(IOFFY),CORE(I030),CORE(I040),CORE(I050))
C
      CALL GETLIST(CORE(I020),1,NDISY,2,IRPLM,28+ISPIN)
      NSIZ = DISSIZY * NDISY
      CALL    VADD(CORE(I020),CORE(I020),CORE(IOFFY),NSIZ,-1.0D+00)
      CALL PUTLIST(CORE(I020),1,NDISY,2,IRPLM,28+ISPIN)
C
      IOFFY = IOFFY + DISSIZY * NDISY
  330 CONTINUE
  400 CONTINUE
C
C     BElm = jCEl * mBCj
C     beLM = JceL * MbcJ
C
      DO  500 ISPIN=1,IUHF+1
C
      LENA = 0
      LENB = 0
      LENC = 0
      DO  410 IRREP=1,NIRREP
      LENA = LENA + IRPDPD(IRREP,10+ISPIN) * IRPDPD(IRREP,10+ISPIN)
      LENB = LENB + IRPDPD(IRREP,10+ISPIN) * IRPDPD(IRREP,10+ISPIN)
      LENC = LENC + IRPDPD(IRREP,10+ISPIN) * IRPDPD(IRREP,10+ISPIN)
  410 CONTINUE
      LEN  = MAX(LENA,LENB,LENC)
C
      I000 = 1
      I010 = I000 + LEN
      I020 = I010 + LEN
      I030 = I020 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I030
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14C'
      ENDIF
C
      CALL GETALL(CORE(I010),LENA,1,57+ISPIN)
C     W(E,j,C,l)/W(e,J,c,L) ---> W(E,l,C,j)/W(e,L,c,J)
      CALL SSTGEN(CORE(I010),CORE(I000),LENA,
     1        VRT(1,ISPIN),POP(1,3-ISPIN),VRT(1,ISPIN),POP(1,3-ISPIN),
     1            CORE(I020),1,'1432')
C
      I020 = I010 + LEN
      I030 = I020 + LEN
      I040 = I030 + 2 * (NOCA*NOCB + NVRTA*NVRTB)
C
      NEED = IINTFP * I040
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14C'
      ENDIF
C
      CALL GETALL(CORE(I020),LENB,1,57+ISPIN)
C     W(C,m,B,j)/W(c,M,b,J) ---> W(C,j,B,m)/W(c,J,b,M)
      CALL SSTGEN(CORE(I020),CORE(I010),LENB,
     1        VRT(1,ISPIN),POP(1,3-ISPIN),VRT(1,ISPIN),POP(1,3-ISPIN),
     1            CORE(I030),1,'1432')
C
C     ISPIN = 1 : W(E,l,C,j) at I000, W(C,j,B,m) at I010. Form Y(E,l,B,m)
C                 at I020.
C     ISPIN = 2 : W(e,L,c,J) at I000, W(c,J,b,M) at I010. Form Y(e,L,b,M)
C                 at I020.
C
      IOFFA = I000
      IOFFB = I010
      IOFFY = I020
      DO  420 IRPCJ=1,NIRREP
C
      IF(IRPDPD(IRPCJ,10+ISPIN).EQ.0) GOTO 420
      DISSIZA = IRPDPD(IRPCJ,10+ISPIN)
      NDISA   = IRPDPD(IRPCJ,10+ISPIN)
      DISSIZB = IRPDPD(IRPCJ,10+ISPIN)
      NDISB   = IRPDPD(IRPCJ,10+ISPIN)
C
      CALL XGEMM('N','N',DISSIZA,NDISB,NDISA,4.0D+00,
     1           CORE(IOFFA),DISSIZA,
     1           CORE(IOFFB),DISSIZB,0.0D+00,
     1           CORE(IOFFY),DISSIZA)
C
      IOFFA = IOFFA + DISSIZA * NDISA
      IOFFB = IOFFB + DISSIZB * NDISB
      IOFFY = IOFFY + DISSIZA * NDISB
  420 CONTINUE
C
C     Y(E,l,B,m)/Y(e,L,b,M) ---> Y(E,B,l,m)/Y(e,b,M,M)
C
      CALL SSTGEN(CORE(I020),CORE(I010),LENC,
     1        VRT(1,ISPIN),POP(1,3-ISPIN),VRT(1,ISPIN),POP(1,3-ISPIN),
     1            CORE(I030),1,'1324')
      
      IOFFY = I010
      DO  430 IRPLM=1,NIRREP
C
      IF(IRPDPD(IRPLM,18+ISPIN).EQ.0.OR.
     1   IRPDPD(IRPLM,23-ISPIN).EQ.0) GOTO 430
C
      DISSIZY = IRPDPD(IRPLM,18+ISPIN)
      NDISY   = IRPDPD(IRPLM,23-ISPIN)
C
      I030 = I020 +     DISSIZY*NDISY
      I040 = I030 + MAX(DISSIZY,NDISY)
      I050 = I040 + MAX(DISSIZY,NDISY)
      I060 = I050 + MAX(DISSIZY,NDISY)
C      
      NEED = IINTFP * I060
      IF(NEED.GT.MAXCOR)THEN
      WRITE(6,1020) NEED,MAXCOR
      STOP 'Y14C'
      ENDIF
C
C     Y(E,B,l,m)/Y(e,b,L,M) ---> Y(B,E,l,m)/Y(b,e,L,M)
      CALL SYMTR3(IRPLM,VRT(1,ISPIN),VRT(1,ISPIN),DISSIZY,NDISY,
     1            CORE(IOFFY),CORE(I030),CORE(I040),CORE(I050))
C
      CALL GETLIST(CORE(I020),1,NDISY,2,IRPLM,28+ISPIN)
      NSIZ = DISSIZY * NDISY
      CALL    VADD(CORE(I020),CORE(I020),CORE(IOFFY),NSIZ,-1.0D+00)
      CALL PUTLIST(CORE(I020),1,NDISY,2,IRPLM,28+ISPIN)
C
      IOFFY = IOFFY + DISSIZY * NDISY
  430 CONTINUE
  500 CONTINUE
      RETURN
      END
