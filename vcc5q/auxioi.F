      SUBROUTINE AUXIOI
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DISTSZ,SIZAAO,SIZAAV,SIZBBO,SIZBBV,POP,VRT,DIRPRD
      DIMENSION BUF(1024)
      DIMENSION SIZAAO(8),SIZAAV(8),SIZBBO(8),SIZBBV(8)
C
      COMMON /AUXIO/ DISTSZ(8,100),NDISTS(8,100),INIWRD(8,100),LNPHYR,
     1               LUAUX
C
      COMMON /SYM/    POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
 
#include "icdacc.com"

      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
C
C     Length (in double precision words) of physical records.
C
      LNPHYR = 1024
C
C     Unit number.
C
      LUAUX = 91
C
      CALL IZERO(DISTSZ,8 * 100)
      CALL IZERO(NDISTS,8 * 100)
      CALL IZERO(INIWRD,8 * 100)
C
C     Distribution sizes and numbers of distributions.
C
C     Y5  : ABCD AAAA, BBBB, ABAB (Lists  1- 3)
C     Y10 : ABCD AAAA, BBBB, ABAB (Lists  4- 6)
C     Y4  : IJKL AAAA, BBBB, ABAB (Lists  7- 9)
C     Y12 : IJKL AAAA, BBBB, ABAB (Lists 10-12)
C
C     Y8  : CEMK AAAA, BBBB, ABAB, BABA, ABBA, BAAB (Lists 13-18)
C     Y13 : CEMK AAAA, BBBB, ABAB, BABA, ABBA, BAAB (Lists 19-24)
C     Y14 : MBLE AAAA, BBBB, ABAB, BABA, ABBA, BAAB (Lists 25-30)
C
C     Note : for Y8 and Y13 AAAA and BBBB are stored as full lists.
C            Storage for these lists is (C,E,M,K).
C
C     Compute lengths of (alpha,alpha) and (beta,beta) distributions
C     of a given symmetry symmetry.
C
      DO  120 IRREP=1,NIRREP
C
      SIZAAO(IRREP) = 0
      SIZAAV(IRREP) = 0
      SIZBBO(IRREP) = 0
      SIZBBV(IRREP) = 0
C
      DO  110 JRREP=1,NIRREP
      KRREP = DIRPRD(IRREP,JRREP)
C
      SIZAAO(IRREP) = SIZAAO(IRREP) + POP(JRREP,1)*POP(KRREP,1)
      SIZAAV(IRREP) = SIZAAV(IRREP) + VRT(JRREP,1)*VRT(KRREP,1)
      SIZBBO(IRREP) = SIZBBO(IRREP) + POP(JRREP,2)*POP(KRREP,2)
      SIZBBV(IRREP) = SIZBBV(IRREP) + VRT(JRREP,2)*VRT(KRREP,2)
  110 CONTINUE
c      WRITE(6,*) POP(IRREP,1),POP(IRREP,2),VRT(IRREP,1),VRT(IRREP,2)
c      WRITE(6,*) SIZAAO(IRREP),SIZAAV(IRREP),SIZBBO(IRREP),SIZBBV(IRREP)
  120 CONTINUE
C
      DO   10 IRREP=1,NIRREP
      DISTSZ(IRREP, 1) = IRPDPD(IRREP, 1)
      DISTSZ(IRREP, 2) = IRPDPD(IRREP, 2)
      DISTSZ(IRREP, 3) = IRPDPD(IRREP,13)
      DISTSZ(IRREP, 4) = IRPDPD(IRREP, 1)
      DISTSZ(IRREP, 5) = IRPDPD(IRREP, 2)
      DISTSZ(IRREP, 6) = IRPDPD(IRREP,13)
      DISTSZ(IRREP, 7) = IRPDPD(IRREP, 3)
      DISTSZ(IRREP, 8) = IRPDPD(IRREP, 4)
      DISTSZ(IRREP, 9) = IRPDPD(IRREP,14)
      DISTSZ(IRREP,10) = IRPDPD(IRREP, 3)
      DISTSZ(IRREP,11) = IRPDPD(IRREP, 4)
      DISTSZ(IRREP,12) = IRPDPD(IRREP,14)
C
      DISTSZ(IRREP,13) = SIZAAV(IRREP)
      DISTSZ(IRREP,14) = SIZBBV(IRREP)
      DISTSZ(IRREP,15) = IRPDPD(IRREP,13)
      DISTSZ(IRREP,16) = IRPDPD(IRREP,13)
      DISTSZ(IRREP,17) = IRPDPD(IRREP,13)
      DISTSZ(IRREP,18) = IRPDPD(IRREP,13)
C
      DISTSZ(IRREP,19) = SIZAAV(IRREP)
      DISTSZ(IRREP,20) = SIZBBV(IRREP)
      DISTSZ(IRREP,21) = IRPDPD(IRREP,13)
      DISTSZ(IRREP,22) = IRPDPD(IRREP,13)
      DISTSZ(IRREP,23) = IRPDPD(IRREP,13)
      DISTSZ(IRREP,24) = IRPDPD(IRREP,13)
C
      DISTSZ(IRREP,25) = SIZAAV(IRREP)
      DISTSZ(IRREP,26) = SIZBBV(IRREP)
      DISTSZ(IRREP,27) = IRPDPD(IRREP,13)
      DISTSZ(IRREP,28) = IRPDPD(IRREP,13)
      DISTSZ(IRREP,29) = IRPDPD(IRREP,19)
      DISTSZ(IRREP,30) = IRPDPD(IRREP,20)
C
      NDISTS(IRREP, 1) = IRPDPD(IRREP, 1)
      NDISTS(IRREP, 2) = IRPDPD(IRREP, 2)
      NDISTS(IRREP, 3) = IRPDPD(IRREP,13)
      NDISTS(IRREP, 4) = IRPDPD(IRREP, 1)
      NDISTS(IRREP, 5) = IRPDPD(IRREP, 2)
      NDISTS(IRREP, 6) = IRPDPD(IRREP,13)
      NDISTS(IRREP, 7) = IRPDPD(IRREP, 3)
      NDISTS(IRREP, 8) = IRPDPD(IRREP, 4)
      NDISTS(IRREP, 9) = IRPDPD(IRREP,14)
      NDISTS(IRREP,10) = IRPDPD(IRREP, 3)
      NDISTS(IRREP,11) = IRPDPD(IRREP, 4)
      NDISTS(IRREP,12) = IRPDPD(IRREP,14)
C
      NDISTS(IRREP,13) = SIZAAO(IRREP)
      NDISTS(IRREP,14) = SIZBBO(IRREP)
      NDISTS(IRREP,15) = IRPDPD(IRREP,14)
      NDISTS(IRREP,16) = IRPDPD(IRREP,14)
      NDISTS(IRREP,17) = IRPDPD(IRREP,14)
      NDISTS(IRREP,18) = IRPDPD(IRREP,14)
C
      NDISTS(IRREP,19) = SIZAAO(IRREP)
      NDISTS(IRREP,20) = SIZBBO(IRREP)
      NDISTS(IRREP,21) = IRPDPD(IRREP,14)
      NDISTS(IRREP,22) = IRPDPD(IRREP,14)
      NDISTS(IRREP,23) = IRPDPD(IRREP,14)
      NDISTS(IRREP,24) = IRPDPD(IRREP,14)
C
      NDISTS(IRREP,25) = SIZAAO(IRREP)
      NDISTS(IRREP,26) = SIZBBO(IRREP)
      NDISTS(IRREP,27) = IRPDPD(IRREP,14)
      NDISTS(IRREP,28) = IRPDPD(IRREP,14)
      NDISTS(IRREP,29) = IRPDPD(IRREP,22)
      NDISTS(IRREP,30) = IRPDPD(IRREP,21)
C
   10 CONTINUE
C
C     Initial words of symmetry sublists.
C
      DO   30 ILIST =1,30
      DO   20 IRREP =1,NIRREP
C
      IF(ILIST.EQ.1.AND.IRREP.EQ.1)THEN
      INIWRD(IRREP,ILIST) = 1
      ENDIF
C
      IF(               IRREP.GT.1)THEN
      NSIZE = DISTSZ(IRREP-1,ILIST) * NDISTS(IRREP-1,ILIST)
      INIWRD(IRREP,ILIST) = INIWRD(IRREP-1,ILIST) + NSIZE
      ENDIF
C
      IF(ILIST.GT.1.AND.IRREP.EQ.1)THEN
      NSIZE = DISTSZ(NIRREP,ILIST-1) * NDISTS(NIRREP,ILIST-1)
      INIWRD(IRREP,ILIST) = INIWRD(NIRREP,ILIST-1) + NSIZE
      ENDIF
C
   20 CONTINUE
   30 CONTINUE
C
      NWDTOT = INIWRD(NIRREP,30) + DISTSZ(NIRREP,30)*NDISTS(NIRREP,30)
      WRITE(6,1010) NWDTOT
 1010 FORMAT(' @AUXIOI-I, Total number of words on MOIAUX ',I15)
C
C     Open the file(s).
C
      OPEN(UNIT=LUAUX,STATUS='NEW',FILE='AUXMOI',ACCESS='DIRECT',
     1     FORM='UNFORMATTED',RECL=1024 * idaccm * 2)
C
C     Initialize the file(s).
C
      NRECS = NWDTOT/LNPHYR + 1
      CALL ZERO(BUF,LNPHYR)
      DO   40 IREC=1,NRECS
      WRITE(LUAUX,REC=IREC) BUF
   40 CONTINUE
C
      RETURN
      END
