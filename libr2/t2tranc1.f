      SUBROUTINE T2TRANC1(ICASE,T2MO,T2MIX,EVEC1,EVEC2,
     &                    SCR,NAO,NMO,ISPIN,IUHF)
C
C THIS ROUTINE TRANSFORMS T2 VECTORS BETWEEN THE MO AND MIXED
C AO/MO REPRESENTATIONS:
C
C        T2(XX,IJ) <== T2(AB,IJ) [ICASE=1]
C
C        T2(AB,IJ) <== T2(XX,IJ) [ICASE=2]
C
C FOR C1 CASES.
C
CEND
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,POP,VRT
      CHARACTER*8 LABELX(2),LABELY(2)
      DIMENSION T2MO(*),T2MIX(*)
      DIMENSION EVEC1(NAO,NMO),EVEC2(NAO,NMO),SCR(NAO*NAO)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/INFO/NOCCO(2),NVRTO(2)
      DATA LABELX /'SCFEVECA','SCFEVECB'/
      DATA LABELY /'SCFEVECA','SCFEVECB'/
      DATA ZILCH,ONE /0.0D0,1.0D0/
C
      NNM1O2(I)=(I*(I-1))/2
C
      IF(ICASE.EQ.1)THEN
C
       IF(ISPIN.LE.2)THEN
        NVRT1=NVRTO(ISPIN)
        NVRT2=NVRTO(ISPIN)
        NOCC1=NOCCO(ISPIN)
        NOCC2=NOCCO(ISPIN)
        NUMIJ=NNM1O2(NOCCO(ISPIN))
        NUMAB=NVRT1*NVRT2
        CALL GETREC(20,'JOBARC',LABELX(ISPIN),IINTFP*NAO*NMO,EVEC1)
        CALL SCOPY (NAO*NMO,EVEC1,1,EVEC2,1)
       ELSE
        NVRT1=NVRTO(1)
        NVRT2=NVRTO(2)
        NOCC1=NOCCO(1)
        NOCC2=NOCCO(2)
        NUMIJ=NOCC1*NOCC2
        NUMAB=NVRT1*NVRT2
        CALL GETREC(20,'JOBARC',LABELX(1),IINTFP*NAO*NMO,EVEC1)
        CALL GETREC(20,'JOBARC',LABELX(1+IUHF),IINTFP*NAO*NMO,EVEC2)
       ENDIF 
C
       IOFFMO=1
       IOFFMIX=1
       IOFFEVC1=NOCC1+1
       IOFFEVC2=NOCC2+1
       DO 10 IJ=1,NUMIJ
        CALL XGEMM('N','N',NAO,NVRT1,NVRT2,ONE,EVEC1(1,IOFFEVC1),NAO,
     &             T2MO(IOFFMO),NVRT1,ZILCH,SCR,NAO)
        CALL XGEMM('N','T',NAO,NAO,NVRT2,ONE,SCR,NAO,EVEC2(1,IOFFEVC2),
     &             NAO,ZILCH,T2MIX(IOFFMIX),NAO)
        IOFFMO=IOFFMO+NVRT1*NVRT2
        IOFFMIX=IOFFMIX+NAO*NAO
10     CONTINUE
C
      ELSE
C
       IF(ISPIN.LE.2)THEN
        NVRT1=NVRTO(ISPIN)
        NVRT2=NVRTO(ISPIN)
        NOCC1=NOCCO(ISPIN)
        NOCC2=NOCCO(ISPIN)
        NUMIJ=NNM1O2(NOCCO(ISPIN))
        NUMAB=NVRT1*NVRT2
        CALL GETREC(20,'JOBARC',LABELY(ISPIN),IINTFP*NAO*NMO,EVEC1)
        CALL SCOPY (NAO*NMO,EVEC1,1,EVEC2,1)
       ELSE
        NVRT1=NVRTO(1)
        NVRT2=NVRTO(2)
        NOCC1=NOCCO(1)
        NOCC2=NOCCO(2)
        NUMIJ=NOCC1*NOCC2
        CALL GETREC(20,'JOBARC',LABELY(1),IINTFP*NAO*NMO,EVEC1)
        CALL GETREC(20,'JOBARC',LABELY(1+IUHF),IINTFP*NAO*NMO,EVEC2)
       ENDIF 
       IOFFMO=1
       IOFFMIX=1
       IOFFEVC1=NOCC1+1
       IOFFEVC2=NOCC2+1
       DO 20 IJ=1,NUMIJ
        CALL XGEMM('T','N',NVRT1,NAO,NAO,ONE,EVEC1(1,IOFFEVC1),NAO,
     &             T2MIX(IOFFMIX),NAO,ZILCH,SCR,NVRT1)
        CALL XGEMM('N','N',NVRT1,NVRT2,NAO,ONE,SCR,NVRT1,
     &             EVEC2(1,IOFFEVC2),NAO,ZILCH,T2MO(IOFFMO),NVRT1)
        IOFFMO=IOFFMO+NVRT1*NVRT2
        IOFFMIX=IOFFMIX+NAO*NAO
20     CONTINUE
C
      ENDIF
C
      RETURN
      END      
