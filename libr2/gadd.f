       SUBROUTINE GADD(ICORE,MAXCOR,IUHF,G,ISPIN,TYPE)
C
C THIS ROUTINE COMPUTES THE CONTRIBUTIONS
C
C  Q(I,M) =SUM  L(E,I) T(E,M)    [ISPIN=1, TYPE='MI']
C           E
C
C  Q(i,m) =SUM  L(e,i) T(e,m)    [ISPIN=2, TYPE='MI']
C           e
C
C    
C  Q(E,A) = - SUM  L(A,M) T(E,M)    [ISPIN=1, TYPE='EA']
C              M
C
C  Q(e,a) = - SUM  L(a,m) T(e,m)    [ISPIN=2, TYPE='EA']
C             m
C
C
C WHICH ARE USED TO AUGMENT THE G INTERMEDIATE "ON THE FLY" IN
C   ROUTINES G1INL2 AND G2INL2.
C
CEND
      IMPLICIT INTEGER (A-Z)
      CHARACTER*2 TYPE
      DIMENSION ICORE(MAXCOR)
      DOUBLE PRECISION ONE,ONEM,G(1)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      DATA ONE  /1.0/
      DATA ONEM /-1.0/
C
      LENL=NT(ISPIN)
      LENT=LENL
      I000=1
      I010=I000+IINTFP*LENL
      I020=I010+IINTFP*LENT
C
C PICK UP LAMBDA(1) AND T1 VECTORS.
C
      CALL GETLST(ICORE(I000),1,1,2,ISPIN,190)
      CALL GETLST(ICORE(I010),1,1,1,ISPIN,90)
      IOFFTAR=1
      IOFFT1 =I010
      IOFFL  =I000 
      IF(TYPE.EQ.'MI')THEN
C
C                                                                t
C LOOP OVER IRREPS AND FORM Q(I,M) FROM THE MATRIX PRODUCT L(E,I) T(E,M)
C
C
       DO 100 IRREP=1,NIRREP
        NVRT=VRT(IRREP,ISPIN)
        NOCC=POP(IRREP,ISPIN)
        CALL XGEMM('T','N',NOCC,NOCC,NVRT,ONE,ICORE(IOFFT1),NVRT,
     &             ICORE(IOFFL),NVRT,ONE,G(IOFFTAR),NOCC)
        IOFFTAR=IOFFTAR+NOCC*NOCC
        IOFFL  =IOFFL  +NOCC*NVRT*IINTFP
        IOFFT1 =IOFFT1 +NOCC*NVRT*IINTFP
100    CONTINUE
      ELSEIF(TYPE.EQ.'EA')THEN
C
C                                                                       t 
C LOOP OVER IRREPS AND FORM Q(E,A) FROM THE MATRIX PRODUCT L(E,M) T(A,M)
C
C
       DO 200 IRREP=1,NIRREP
        NVRT=VRT(IRREP,ISPIN)
        NOCC=POP(IRREP,ISPIN)
        CALL XGEMM('N','T',NVRT,NVRT,NOCC,ONEM,ICORE(IOFFT1),NVRT,
     &             ICORE(IOFFL),NVRT,ONE,G(IOFFTAR),NVRT)
        IOFFTAR=IOFFTAR+NVRT*NVRT
        IOFFL  =IOFFL  +NOCC*NVRT*IINTFP
        IOFFT1 =IOFFT1 +NOCC*NVRT*IINTFP
200    CONTINUE
      ELSE
       WRITE(6,800)TYPE
800    FORMAT(T3,'@GADD-F, Type ',A2,' unknown.')
       CALL ERREX
      ENDIF
      RETURN
      END
