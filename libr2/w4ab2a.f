      SUBROUTINE W4AB2A(ICORE,MAXCOR,IUHF,IOFFLIST)
C
C THIS ROUTINE CALCULATES ONE OF THE TERMS WHICH CONTRIBUTES
C  TO THE W(MnIf) INTERMEDIATE WHICH IS NEEDED FOR CCSD GRADIENTS
C  AND CCSDT MODELS.  
C
C    Z(MnIf) = - SUM T1(n,e) * W (MefI) - SUM T1(M,E) * W (EnfI)
C                 e                        E
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH,ALPHA,BETA
      LOGICAL RHF
      DIMENSION ICORE(MAXCOR),IOFFT1(8,2)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      DATA ONE   /1.0/
      DATA ONEM  /-1.0/
      DATA ZILCH /0.0/
      RHF=.TRUE.
      IF(IUHF.NE.0)RHF=.FALSE.
      CALL GETT1(ICORE,MAXCOR,MXCOR,IUHF,IOFFT1)
      LSTTAR=110+IOFFLIST
C
C ALPHA IS ONE SINCE WE STORE -W(MBEJ) ON DISK
C
      ALPHA=ONE
      BETA =ZILCH
C
C FIRST CONTRACTION: 
C                                
C    Z(MnFi) = - SUM T1(M,E) * W1(EnfI) 
C                 E                    
C
C HERE W1 IS SIMILAR TO THE RING INTERMEDIATES AND IS STORED ON
C  LIST 56 AS EI,fn. 
C
C
C READ THE TRANSPOSE OF THE FIRST SET OF INTERMEDIATES INTO CORE FOR
C  ALL IRREPS I(fn,EI)
C
  
      LISTW =118
      ISIZE=ISYMSZ(ISYTYP(1,LISTW),ISYTYP(2,LISTW))
      I000=1
      I010=I000+ISIZE*IINTFP
      I020=I010+ISIZE*IINTFP
      CALL GETALT(ICORE(I010),ISIZE,1,LISTW)
C
C RESORT TO I(fI,En)
C
      CALL SSTRNG(ICORE(I010),ICORE(I000),ISIZE,ISIZE,ICORE(I020),
     &            'BBAA')
C
C NOW LOOP OVER IRREPS AND FORM PRODUCT
C
      IOFFIR=I000
      DO 10 IRREPDO=1,NIRREP
       WDSZ=IRPDPD(IRREPDO,ISYTYP(1,21+IUHF))
       WDIS=IRPDPD(IRREPDO,ISYTYP(2,21+IUHF))
       TARDSZ=IRPDPD(IRREPDO,ISYTYP(1,LSTTAR))
       TARDIS=IRPDPD(IRREPDO,ISYTYP(2,LSTTAR))
       TARSIZ=TARDIS*TARDSZ
C
C TRANSPOSE BRA AND KET INDICES TO GIVE I(If,nE)
C
       I020=I010+IINTFP*MAX(WDSZ,WDIS)
       I030=I020+IINTFP*MAX(WDSZ,WDIS)
       I040=I030+IINTFP*MAX(WDSZ,WDIS)
       CALL SYMTR1(IRREPDO,VRT(1,1),POP(1,2),WDSZ,ICORE(IOFFIR),
     &             ICORE(I010),ICORE(I020),ICORE(I030))
       CALL SYMTR3(IRREPDO,VRT(1,2),POP(1,1),WDSZ,WDIS,
     &             ICORE(IOFFIR),ICORE(I010),ICORE(I020),ICORE(I030))
C
C NOW FORM THE PRODUCT MATRIX [Z(If,nM)] WITH MATRIX MULTIPLICATION
C                                
C            Z(If,nM) = SUM I(Ifn,E) * T(E,M)
C                        E
C
       I020=I010+IINTFP*TARDIS*TARDSZ
       IOFFZ=I010
       CALL IZERO(ICORE(IOFFZ),IINTFP*TARDIS*TARDSZ)
       DO 20 IRREPE=1,NIRREP
        IRREPN=DIRPRD(IRREPE,IRREPDO)
        IRREPM=IRREPE
        IOFFT=IOFFT1(IRREPE,1) 
        NROWI=WDSZ*POP(IRREPN,2)
        NCOLI=VRT(IRREPE,1)
        NROWZ=NROWI
        NCOLZ=POP(IRREPM,1)
        NROWT=VRT(IRREPE,1)
        NCOLT=POP(IRREPM,1)
        NSUM =NCOLI
        IF(MIN(NROWZ,NCOLZ,NSUM).GT.0)THEN
         CALL XGEMM('N','N',NROWZ,NCOLZ,NSUM,ALPHA,ICORE(IOFFIR),
     &              NROWI,ICORE(IOFFT),NROWT,BETA,ICORE(IOFFZ),
     &              NROWZ)
        ENDIF
        IOFFZ=IOFFZ+IINTFP*NROWZ*NCOLZ
        IOFFIR=IOFFIR+IINTFP*NROWI*NCOLI
20     CONTINUE
C
C NOW TRANSPOSE TARGET AND SWAP RESULTANT BRA INDICES
C
C      Z(If,nM) -> Z(nM,If) -> Z(Mn,If)
C
       I030=I020+IINTFP*TARSIZ
       CALL TRANSP(ICORE(I010),ICORE(I020),TARDSZ,TARDIS)
c YAU : old
c      CALL ICOPY(TARSIZ*IINTFP,ICORE(I020),1,ICORE(I010),1)
c YAU : new
       CALL DCOPY(TARSIZ,ICORE(I020),1,ICORE(I010),1)
c YAU : end
       I030=I020+IINTFP*TARDIS
       I040=I030+IINTFP*TARDIS
       I050=I040+IINTFP*TARDIS
       CALL SYMTR3(IRREPDO,POP(1,2),POP(1,1),TARDSZ,TARDIS,
     &             ICORE(I010),ICORE(I020),ICORE(I030),ICORE(I040))
C
C NOW AUGMENT TARGET LIST
C
       I030=I020+IINTFP*TARSIZ
       CALL GETLST(ICORE(I020),1,TARDIS,1,IRREPDO,LSTTAR)
       CALL SAXPY (TARSIZ,ONE,ICORE(I020),1,ICORE(I010),1)
       CALL PUTLST(ICORE(I010),1,TARDIS,1,IRREPDO,LSTTAR)
10    CONTINUE 
C
C SECOND PIECE
C
C    Z(MnIf) = SUM T1(n,e) * W (MefI) 
C               e                    
C
C READ IN FIRST SET OF INTERMEDIATES INTO CORE FOR
C  ALL IRREPS I(eI,fM)
C
C      
      LISTW=125+IUHF
      ISIZE=ISYMSZ(ISYTYP(1,LISTW),ISYTYP(2,LISTW))
      I000=1
      I010=I000+ISIZE*IINTFP
      I020=I010+ISIZE*IINTFP
      CALL GETALL(ICORE(I010),ISIZE,1,LISTW)
C
C RESORT TO I(eM,fI).
C
      CALL SSTRNG(ICORE(I010),ICORE(I000),ISIZE,ISIZE,ICORE(I020),
     &            'BABA')
C
C NOW LOOP OVER IRREPS AND FORM PRODUCT
C
      IOFFIR=I000
      DO 110 IRREPDO=1,NIRREP
       WDSZ=IRPDPD(IRREPDO,ISYTYP(1,25+IUHF))
       WDIS=WDSZ
       TARDSZ=IRPDPD(IRREPDO,ISYTYP(1,LSTTAR))
       TARDIS=IRPDPD(IRREPDO,ISYTYP(2,LSTTAR))
       TARSIZ=TARDIS*TARDSZ
C
C DO IN-PLACE TRANSPOSITION OF I MATRIX AND THEN SWAP BRA AND
C  KET INDICES, GIVING I(If,Me).
C
       CALL MTRAN2(ICORE(IOFFIR),WDIS)
       I020=I010+IINTFP*MAX(WDSZ,WDIS)
       I030=I020+IINTFP*MAX(WDSZ,WDIS)       
       I040=I030+IINTFP*MAX(WDSZ,WDIS)       
       CALL SYMTR1(IRREPDO,VRT(1,2),POP(1,1),WDSZ,ICORE(IOFFIR),
     &             ICORE(I010),ICORE(I020),ICORE(I030))
       CALL SYMTR3(IRREPDO,VRT(1,2),POP(1,1),WDSZ,WDIS,ICORE(IOFFIR),
     &             ICORE(I010),ICORE(I020),ICORE(I030))
C
C NOW FORM THE PRODUCT MATRIX [Z(If,Mn)] WITH MATRIX MULTIPLICATION
C
C            Z(If,Mn) = I(IfM,e) * T(e,n)
C
       I020=I010+IINTFP*TARDIS*TARDSZ
       IOFFZ=I010
       CALL IZERO(ICORE(IOFFZ),IINTFP*TARDIS*TARDSZ)
       DO 120 IRREPE=1,NIRREP
        IRREPM=DIRPRD(IRREPE,IRREPDO)
        IRREPN=IRREPE
        IOFFT=IOFFT1(IRREPE,2) 
        NROWI=WDSZ*POP(IRREPM,1)
        NCOLI=VRT(IRREPE,2)
        NROWZ=NROWI
        NCOLZ=POP(IRREPN,2)
        NROWT=VRT(IRREPE,2)
        NCOLT=POP(IRREPN,2)
        NSUM =NCOLI
        IF(MIN(NROWZ,NCOLZ,NSUM).GT.0)THEN
         CALL XGEMM('N','N',NROWZ,NCOLZ,NSUM,ALPHA,ICORE(IOFFIR),
     &              NROWI,ICORE(IOFFT),NROWT,BETA,ICORE(IOFFZ),
     &              NROWZ)
        ENDIF
        IOFFZ=IOFFZ+IINTFP*NROWZ*NCOLZ
        IOFFIR=IOFFIR+IINTFP*NROWI*NCOLI
120    CONTINUE
C
C NOW TRANSPOSE IT AND WRITE IT OUT.
C
C  Z(If,Mn) -> Z(Mn,If)
C
       I030=I020+IINTFP*TARDIS*TARDSZ
       CALL TRANSP(ICORE(I010),ICORE(I020),TARDSZ,TARDIS)
C
C NOW INCREMENT LISTS ON DISK
C
       CALL GETLST(ICORE(I010),1,TARDIS,1,IRREPDO,LSTTAR)
       CALL SAXPY (TARSIZ,ONE,ICORE(I020),1,ICORE(I010),1)
       CALL PUTLST(ICORE(I010),1,TARDIS,1,IRREPDO,LSTTAR)
110   CONTINUE 
      RETURN
      END
