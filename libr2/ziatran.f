      SUBROUTINE ZIATRAN(ZIA,Z2AO,EVEC1,EVEC2,SCR1,
     &                   NAO,NMO,ISPIN,IUHF,IRREPIJ)
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,POP,VRT
      CHARACTER*8 LABELX(2)
      DIMENSION Z2AO(*),ZIA(*)
      DIMENSION EVEC1(NAO,NMO),EVEC2(NAO,NMO),SCR1(*)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYMINF/NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON/INFO/NOCCO(2),NVRTO(2)
      COMMON/ZIA/ IOFFZIA(8,2)
      COMMON/AOSYM/IAOPOP(8),IOFFAO(8),IOFFV(8,2),IOFFO(8,2),
     &             IRPDPDAO(8),IRPDPDAOS(8),ISTART(8,8),ISTARTMO(8,3)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      DATA LABELX /'SCFEVECA','SCFEVECB'/
      DATA ZILCH,ONE,IONE /0.0D0,1.0D0,1/

C
C GET MO COEFFICIENTS
C
      IF(ISPIN.LE.2)THEN
        CALL GETREC(20,'JOBARC',LABELX(ISPIN),
     &              IINTFP*NAO*NMO,EVEC1)
        CALL SCOPY (NAO*NMO,EVEC1,1,EVEC2,1)
        ISPIN1=ISPIN
        ISPIN2=ISPIN
      ELSE IF (ISPIN.EQ.3) THEN
        CALL GETREC(20,'JOBARC',LABELX(1),
     &              IINTFP*NAO*NMO,EVEC1)
        CALL GETREC(20,'JOBARC',LABELX(1+IUHF),
     &              IINTFP*NAO*NMO,EVEC2)
        ISPIN1=1
        ISPIN2=2
      ELSE IF (ISPIN.EQ.4) THEN
        CALL GETREC(20,'JOBARC',LABELX(1+IUHF),
     &              IINTFP*NAO*NMO,EVEC1)
        CALL GETREC(20,'JOBARC',LABELX(1),
     &              IINTFP*NAO*NMO,EVEC2)
        ISPIN1=2
        ISPIN2=1
      ENDIF
C
      IOFFZ2=1
C
C LOOP OVER DISTRIBUTIONS
C
      DO 100 IRREPJ=1,NIRREP
        IRREPI=DIRPRD(IRREPIJ,IRREPJ)
        NOCCI=POP(IRREPI,ISPIN1)
        NOCCJ=POP(IRREPJ,ISPIN2)
        CALL ZERO(SCR1,IAOPOP(IRREPI)*NOCCI)
        DO 200 J=1,NOCCJ
          IOFFR1=IOFFO(IRREPJ,ISPIN2)+(J-1)
          IOFFL1=IOFFAO(IRREPJ)
          DO 300 I=1,NOCCI
C
C LOOP OVER SYMMETRIES OF M
C
            DO 400 IRREPB=1,NIRREP
              IRREPA=DIRPRD(IRREPB,IRREPIJ)
              NAOA=IAOPOP(IRREPA)
              NAOB=IAOPOP(IRREPB)
              IF (IRREPJ.EQ.IRREPB) THEN
C                                  
C SUM(nu) Z(i,m;mu,nu)*C(nu;m) --> Z(i,m;mu,m)
C SUM(m) Z(i,m;mu,m) --> Z(i;mu)
C
                IOFFIV=(I-1)*NAOA+1
                CALL XGEMM('N','N',NAOA,IONE,NAOB,ONE,Z2AO(IOFFZ2),
     &               NAOA,EVEC2(IOFFL1,IOFFR1),NAO,ONE,SCR1(IOFFIV),
     &               NAOA) 
              ENDIF
              IOFFZ2=IOFFZ2+NAOA*NAOB
 400        CONTINUE
 300      CONTINUE
 200    CONTINUE
C
C SUM(mu) C(mu;a)(transpose)* Z(mu;i) --> Z(a;i)
C
        IOFFR2=IOFFV(IRREPI,ISPIN1)
        IOFFL2=IOFFAO(IRREPI)
        NUMA=VRT(IRREPI,ISPIN1)
        NAOA=IAOPOP(IRREPI)
        IOFFZ=IOFFZIA(IRREPI,ISPIN1)
        CALL XGEMM('T','N',NUMA,NOCCI,NAOA,ONE,EVEC1(IOFFL2,IOFFR2),
     &              NAO,SCR1(IONE),NAOA,ONE,ZIA(IOFFZ),NUMA) 
 100  CONTINUE


      RETURN
      END
