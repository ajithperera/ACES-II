      SUBROUTINE L1T13(Q,DISSYQ,NUMSYQ,SCR,T1A,T1B,L1B,FACT,
     &                 ISPIN1,ISPIN2,LISTQ,FLAG)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD,DISSYQ,POP,VRT
      DOUBLE PRECISION L1B
      CHARACTER*1 FLAG
      DIMENSION Q(DISSYQ,NUMSYQ),SCR(1),T1A(1),T1B(1),L1B(1)
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
C
      DATA ONE /1.0D0/,AZERO/0.D0/
C
      CALL ZERO(SCR,NT(ISPIN2)+NF1(ISPIN2))
      IOFFS1=1+NT(ISPIN2)
      IOFFS2=1
      IOFFT=1
      DO 100 IRREP=1,NIRREP
       NOCC=POP(IRREP,ISPIN2)
       NVRT=VRT(IRREP,ISPIN2)
       IF(MIN(NVRT,NOCC).NE.0) THEN
       CALL XGEMM('T','N',NOCC,NOCC,NVRT,ONE,L1B(IOFFT),NVRT,
     &            T1B(IOFFT),NVRT,AZERO,SCR(IOFFS1),NOCC)
       CALL XGEMM('N','N',NVRT,NOCC,NOCC,ONE,T1B(IOFFT),NVRT,
     &            SCR(IOFFS1),NOCC,AZERO,SCR(IOFFS2),NVRT)
       ENDIF
       IOFFS1=IOFFS1+NOCC*NOCC
       IOFFS2=IOFFS2+NOCC*NVRT
       IOFFT=IOFFT+NOCC*NVRT
100   CONTINUE
C
C  FORM THE DIRECT PRODUCT OF THESE PRODUCT WITH T1
C
C  GET FIRST Q IN ORDER TO UPDATE
C
      CALL GETLST(Q,1,NUMSYQ,1,1,LISTQ)
C
C  CHECK WHICH ORDERING IS REQUIRED 
C
C IA,JB ORDERING :
C
      IF(FLAG.EQ.'N') THEN
       DO 200 IDIS=1,NUMSYQ
       DO 200 IDSZ=1,DISSYQ
        Q(IDSZ,IDIS)=Q(IDSZ,IDIS)+FACT*SCR(IDIS)*T1A(IDSZ)
200    CONTINUE
C
C JB,IA ORDERING
C
      ELSE
       DO 300 IDIS=1,NUMSYQ
       DO 300 IDSZ=1,DISSYQ
        Q(IDSZ,IDIS)=Q(IDSZ,IDIS)+FACT*T1A(IDIS)*SCR(IDSZ)
300    CONTINUE
      ENDIF
C
C SAVE UPDATED Q ON DISK
C
      CALL PUTLST(Q,1,NUMSYQ,1,1,LISTQ)
C
C  ALL ODNE
C
      RETURN
      END
