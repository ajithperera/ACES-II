


      SUBROUTINE LDWAAAA(W,WDSZ,ABESIZ,IRRDPD,IRREPF,ISPIN,
     &                   LISTW,NUMF,FIRSTF,LASTF)
C
C THIS ROUTINE READS THE W(A<B,E<F) INTEGRAL LISTS AND IS
C  EFFECTIVELY SOME KIND OF OUT-OF-CORE SYMEXP2 (A LIBRARY ROUTINE)
C  IT READS A SET OF W(A<B E) VECTORS FOR F=FIRSTF TO F=LASTF WITHIN
C  A GIVEN DPD IRREP FOR A SPECIFIC IRREP OF F.  
C  THERE IS SEPARATE CODE FOR IRREP=1 AND ALL
C  OTHER IRREPS SINCE THE LATER CASES ARE SIMPLE AND IRREP=1 
C  REQUIRES NON-SEQUENTIAL I/O.  THIS ROUTINE IS A DEPENDENT OF
C  W5OAA1, WHICH PERFORMS AN OUT-OF-CORE CONTRACTION TO FORM A
C  PIECE OF THE W(abci) INTERMEDIATE FOR SPIN CASES AAAA AND BBBB.
C
CEND
      IMPLICIT INTEGER (A-Z)
      LOGICAL SWITCH
      DOUBLE PRECISION W,ONE,ONEM
      DIMENSION W(ABESIZ,NUMF)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
C
      DATA ONE  / 1.0/
      DATA ONEM /-1.0/
C
      NNM1O2(I)=(I*(I-1))/2
      INDXT(I,J)=I+((J-1)*(J-2))/2
      INDXF(I,J,N)=I+(J-1)*N
C
C CODE FOR IRREP=1
C
      IF(IRRDPD.EQ.1)THEN
       IOFF=0
       DO 5 IRR=1,IRREPF-1
        IOFF=IOFF+NNM1O2(VRT(IRR,ISPIN))
5      CONTINUE
C
C IOFF BEGINS AS THE ADDRESS OF THE FIRST PHYSICAL RECORD CONTAINING 
C  THE IRREPF DISTRIBUTIONS MINUS ONE
C
       NUME=VRT(IRREPF,ISPIN)
       IDISF=1
       DO 10 F=FIRSTF,LASTF
        IOFFW=1
        DO 20 E=1,NUME
         IF(E.LT.F)THEN
          IPOS=INDXT(E,F)+IOFF
          CALL GETLST(W(IOFFW,IDISF),IPOS,1,1,IRRDPD,LISTW)
         ELSEIF(E.GT.F)THEN
          IPOS=INDXT(F,E)+IOFF
          CALL GETLST(W(IOFFW,IDISF),IPOS,1,1,IRRDPD,LISTW)
          CALL SSCAL(WDSZ,ONEM,W(IOFFW,IDISF),1)
         ELSEIF(E.EQ.F)THEN
          CALL ZERO(W(IOFFW,IDISF),WDSZ)
         ENDIF
         IOFFW=IOFFW+WDSZ
20      CONTINUE
        IDISF=IDISF+1
10     CONTINUE
C
C
      ELSE
C
C CODE FOR ALL OTHER DPD IRREPS.  
C
       IRREPE=DIRPRD(IRREPF,IRRDPD)
C
C CASE I. IRREPE<IRREPF
C
       IF(IRREPE.LT.IRREPF)THEN
        IOFF=0
        DO 105 IRREP1=1,IRREPF-1
         IRREP2=DIRPRD(IRREP1,IRRDPD)
         IF(IRREP2.GT.IRREP1)GOTO 105
         IOFF=IOFF+VRT(IRREP1,ISPIN)*VRT(IRREP2,ISPIN)
105     CONTINUE
        NUME=VRT(IRREPE,ISPIN)
        NREAD=LASTF-FIRSTF+1
        ISTART=IOFF+INDXF(1,FIRSTF,NUME)
        CALL GETLST(W,ISTART,NREAD*NUME,1,IRRDPD,LISTW)
       ELSE
C
C CASE II. IRREPE>IRREPF.  WE MUST READ IRREPE<IRREPF AND NEGATE
C
        IOFF=0 
        DO 205 IRREP1=1,IRREPE-1
         IRREP2=DIRPRD(IRREP1,IRRDPD)
         IF(IRREP2.GT.IRREP1)GOTO 205
         IOFF=IOFF+VRT(IRREP1,ISPIN)*VRT(IRREP2,ISPIN)
205     CONTINUE
        NUME=VRT(IRREPE,ISPIN)
        ILOCX=1
        DO 206 E=1,NUME
         ILOCY=1
         DO 207 F=FIRSTF,LASTF
          IPOS=IOFF+INDXF(F,E,VRT(IRREPF,ISPIN))
          CALL GETLST(W(ILOCX,ILOCY),IPOS,1,1,IRRDPD,LISTW)
          ILOCY=ILOCY+1
207      CONTINUE
         ILOCX=ILOCX+WDSZ
206     CONTINUE
        CALL VMINUS(W,ABESIZ*NUMF)
       ENDIF
      ENDIF
C
      RETURN
      END
