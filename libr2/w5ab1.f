      SUBROUTINE W5AB1(ICORE,MAXCOR,IUHF,TERM1,TERM2,TERM3,TAU,
     &                 IOFFLIST)
C
C THIS ROUTINE CALCULATES THREE OF THE FIVE CONTRACTIONS WHICH 
C  CONTRIBUTE TO THE W(aMeF) INTERMEDIATE WHICH IS USED IN CCSD
C  GRADIENTS AND CCSDT MODELS FOR AAAA AND BBBB SPIN CASES.  
C
C  FIRST CONTRACTION:
C           Z(aMeF) = SUM T1(M,G) * W (eF,aG)
C  SECOND CONTRACTION:
C           Z(aMeF) = -T2(Fe,Mn) * F(an)
C  THIRD CONTRACTION
C           Z(aM,eF) = TAU(nO,eF) <aM|nO>
C
C  THE REMAINING CONTRACTIONS ARE DONE BEFORE THIS ROUTINE IS
C   CALLED AND THE TARGET LIST CONTAINS THEM ON ENTRY. 
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH,ALPHA,BETA
      LOGICAL CCGRAD,TERM1,TERM2,TERM3,TAU
      DIMENSION ICORE(MAXCOR),IOFFT1(8,2)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /FLAGS/ IFLAGS(100)
      DATA ONE   /1.0/
      DATA ONEM  /-1.0/
      DATA ZILCH /0.0/
      DATA CCGRAD /.TRUE./
C
      CALL GETT1(ICORE,MAXCOR,MXCOR,IUHF,IOFFT1)
      IBOT=1
      BETA=ONE
      LSTTAR=129+IOFFLIST
      DO 20 IRREPDO=1,NIRREP
       TARDSZ=IRPDPD(IRREPDO,ISYTYP(1,29))
       TARDIS=IRPDPD(IRREPDO,ISYTYP(2,29))
       TARSIZ=TARDSZ*TARDIS
       LISTW =233
       WDSZ=IRPDPD(IRREPDO,ISYTYP(1,LISTW))
       WDIS=IRPDPD(IRREPDO,ISYTYP(2,LISTW))
       ALPHA=ONE
       CALL IZERO(ICORE,IINTFP*TARSIZ)
       I000=1
       I010=I000+IINTFP*TARSIZ
       IF(TERM1)THEN
C
        IF(IFLAGS(93).EQ.2)GOTO 999
C
C FIRST CONTRACTION: 
C                                
C           Z(aMeF) =  T1(M,G) * W (eF,aG)
C
       I020=I010+IINTFP*WDSZ*WDIS
       I030=I020+IINTFP*WDSZ
       I040=I030+IINTFP*WDSZ
       I050=I040+IINTFP*WDSZ
C
C CHECK TO SEE IF THERE IS SUFFICIENT CORE FOR THE IN-CORE ALGORITHM
C
       IF(I050.LE.MXCOR)THEN
C
C DO IN-CORE ALGORITHM
C
        CALL GETLST(ICORE(I010),1,WDIS,2,IRREPDO,LISTW)
C
C TRANSPOSE KET INDICES TO GIVE W(Fe,aG)
C
        CALL SYMTR1(IRREPDO,VRT(1,1),VRT(1,2),WDSZ,ICORE(I010),
     &              ICORE(I020),ICORE(I030),ICORE(I040))
C
C EVALUATE V CONTRIBUTION WITH THE MATRIX MULTIPLICATION
C                                           +
C             Z(Fe,aM) =  W(Fe,aG) * T(M,G)
C
        IOFFZ=I000
        IOFFW=I010
        DO 330 IRREPG=1,NIRREP
         IRREPA=DIRPRD(IRREPG,IRREPDO)
         IRREPM=IRREPG
         NROWZ=WDSZ*VRT(IRREPA,2)
         NCOLZ=POP(IRREPM,1)
         NROWW=NROWZ
         NCOLW=VRT(IRREPG,1)
         NROWT=VRT(IRREPG,1)
         NCOLT=POP(IRREPM,1)
         IOFFT=IOFFT1(IRREPG,1)
         IF(MIN(NROWZ,NCOLZ,NCOLW).GT.0)THEN
        CALL XGEMM('N','N',NROWZ,NCOLZ,NCOLW,ALPHA,ICORE(IOFFW),
     &              NROWW,ICORE(IOFFT),NROWT,BETA,ICORE(IOFFZ),
     &              NROWZ)
         ENDIF
         IOFFW=IOFFW+IINTFP*NROWW*NCOLW
         IOFFZ=IOFFZ+IINTFP*NROWZ*NCOLZ
330     CONTINUE
       ELSE
C
C CALL OUT OF CORE ALGORITHM
C
        USECOR=MXCOR-I010
        CALL W5OAB1(ICORE(I010),USECOR,IUHF,ICORE(IOFFT1(1,1)),
     &              ICORE(I000),IRREPDO,LISTW,1,WDSZ,TARDIS)
       ENDIF  
C
999    CONTINUE
C
C NOW REARRANGE THIS SO THAT IT IS ORDERED IN THE SAME WAY AS THE NEXT 
C  TWO TERMS (WHICH IS ALSO THE WAY THAT THE TARGET LIST IS ORDERED).
C
C                  Z(Fe,aM) -> Z(Fe,Ma)
C
       I020=I010+IINTFP*TARDSZ
       I030=I020+IINTFP*TARDSZ
       I040=I030+IINTFP*TARDSZ
       CALL SYMTR1(IRREPDO,VRT(1,2),POP(1,1),TARDSZ,ICORE(I000),
     &             ICORE(I010),ICORE(I020),ICORE(I030))
       ENDIF
C
C FINALLY, INCREMENT THE TARGET LIST WITH Z(Fe,Ma) AND LEAVE.
C
       I020=I010+IINTFP*TARDSZ*TARDIS
       IF(I020.LE.MAXCOR)THEN
        CALL GETLST(ICORE(I010),1,TARDIS,2,IRREPDO,LSTTAR)
        CALL SAXPY(TARSIZ,ONE,ICORE(I010),1,ICORE(I000),1)
       ELSE
        IOFF=I000
        I020=I010+IINTFP*TARDSZ
        DO 300 IDIS=1,TARDIS
         CALL GETLST(ICORE(I010),IDIS,1,1,IRREPDO,LSTTAR)
         CALL SAXPY (TARDSZ,ONE,ICORE(I010),1,ICORE(IOFF),1)
         IOFF=IOFF+TARDSZ*IINTFP
300     CONTINUE
       ENDIF
       CALL PUTLST(ICORE(I000),1,TARDIS,2,IRREPDO,LSTTAR)
20    CONTINUE
      RETURN
      END
