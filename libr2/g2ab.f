      SUBROUTINE G2AB(T,L,T1,G,ISAME,ISPIN,POP1,POP2,VRT1,
     &                VRT2,NOCCSQ,DISSYW,DISSYT,NUMSYW,NUMSYT,
     &                LISTL,LISTT, IRREP,TMP,IUHF,FACT)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAl ISAME 
      DOUBLE PRECISION L
      INTEGER DISSYT, DISSYW, DIRPRD,POP1,POP2,VRT1,VRT2
      DIMENSION L(DISSYW,NUMSYW),T(DISSYT,NUMSYT),G(NOCCSQ)
      DIMENSION TMP(1),T1(DISSYW*NUMSYW)
      DIMENSION POP1(8),POP2(8),VRT1(8),VRT2(8) 
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),DIRPRD(8,8)
C
      DATA ONE,ONEM,TWO /1.0D0,-1.D0,2.D0/,HALF /0.5D0/
C
C      PICK UP FIRST THE L AMPLITUDES
C
      CALL GETLST(L,1,NUMSYW,2,IRREP,LISTL)
C
C  FOR MBPT(4) COPY L TO T SINCE THEY ARE THE SAME
C    
      IF(ISAME) THEN
c YAU : old
c      CALL ICOPY(IINTFP*NUMSYT*DISSYT,L,1,T,1)
c YAU : new
       CALL DCOPY(NUMSYT*DISSYT,L,1,T,1)
c YAU : end
      ENDIF
C
C SPIN ADAPTED CODE FOR RHF
C
      IF(IUHF.EQ.0) THEN
       CALL SYMTRA(IRREP,POP1,POP2,DISSYW,L,T1)
       CALL SSCAL(NUMSYW*DISSYW,TWO,L,1)
       CALL SAXPY(NUMSYW*DISSYW,ONEM,T1,1,L,1)
      ENDIF
C
C  FOR CC GET T AMPLITUDES
C
      IF(.NOT.ISAME) THEN
       CALL GETLST(T,1,NUMSYT,1,IRREP,LISTT)
      ENDIF
C
      IF(IUHF.EQ.1.AND.ISPIN.EQ.1) THEN 
       CALL SYMTR1(IRREP,POP1,POP2,DISSYW,L,TMP,TMP(1+DISSYW),
     &             TMP(1+2*DISSYW))
       CALL SYMTR1(IRREP,POP1,POP2,DISSYT,T,TMP,TMP(1+DISSYT),
     &             TMP(1+2*DISSYT))
      ENDIF
C
      JOFF=1
      IOFF=1
      DO 90 IRREPI=1,NIRREP
C          
C        GET OCCUPATION NUMBER FOR JRREP     
C
       NOCCI=POP1(IRREPI)
C
C        DETERMINE KRREP WHOSE DIRECT PRODUCT WITH JRREP GIVES IRREP
C
       IRREPJ=DIRPRD(IRREP,IRREPI)
C
C        GET OCCUPATION NUMBER FOR KRREP
C
       NOCCJ=POP2(IRREPJ)
C
C        IF ZERO, NOTHING TO COMPUTE
C
       IF(MIN(NOCCI,NOCCJ).NE.0) THEN
C
        CALL XGEMM('T','N',NOCCI,NOCCI,DISSYW*NOCCJ,FACT,
     &             T(1,JOFF),NOCCJ*DISSYW,L(1,JOFF),      
     &             NOCCJ*DISSYW,ONE,G(IOFF),NOCCI)
       ENDIF
C
       JOFF=JOFF+NOCCJ*NOCCI
       IOFF=IOFF+NOCCI*NOCCI
90    CONTINUE
C
      RETURN
      END
