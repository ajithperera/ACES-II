      SUBROUTINE OST2AIAI2(ICORE,MAXCOR,IUHF,W,LAMBDA,WL)
C
C
C     THIS ROUTINE CALCULATE ACTIVE,INACTIVE-ACTIVE,INACTIVE T2 CONTRIBUTION
C
C     T2(Mi,Na)= W * (T2(IM,NA)+T1(N,I)*T1(A,M)) = W * TAU(IM,NA)   
C     T2(In,Am)= W * (T2(in,ma)+T1(m,i)*T1(a,n)) = W * TAU(in,ma)   
C     T2(Mi,Am)= W * (T2(In,Na)+T1(N,I)*T1(a,n)) = W * TAU(In,Na)   
C     T2(In,Na)= W * (T2(iM,mA)+T1(m,i)*T1(A,M)) = W * TAU(iM,mA)   
C
CEND
C
CPROGRAMED PS NOV/92
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,DISSYT,POP,VRT,DISSYT2
      CHARACTER*4 STRTAR,STRT2
      CHARACTER*2 STRDIS,STRNUM
      LOGICAL LAMBDA
C
      DIMENSION ICORE(MAXCOR)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &                DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
C
      INTEGER POPA,POPI,VRTA,VRTI
      COMMON /FSSYM/ POPA(8,2),POPI(8,2),VRTA(8,2),VRTI(8,2),
     &               NTAN(2),NTNA(2),NTAA(2),
     &               NTIN(2),NTNI(2),NTII(2),
     &               NTAI(2),NTIA(2),
     &               NF1AN(2),NF1AA(2),NF1IN(2),NF1II(2),NF1AI(2),
     &               NF2AN(2),NF2AA(2),NF2IN(2),NF2II(2),NF2AI(2)
C
      INTEGER FSDPDAN,FSDPDNA,FSDPDAA,FSDPDIN,FSDPDNI,FSDPDII,FSDPDAI,
     $   FSDPDIA
      COMMON /FSSYMPOP/ FSDPDAN(8,22),FSDPDNA(8,22),FSDPDAA(8,22),
     &                  FSDPDIN(8,22),FSDPDNI(8,22),FSDPDII(8,22),
     &                  FSDPDAI(8,22),FSDPDIA(8,22)
C
      COMMON /ACTIRR/ IRREPO(2),IRREPV(2)
C
      COMMON /FLAGS/ IFLAGS(100)
C
      CHARACTER*4 TARSTR(4),T2STR(4)
      DIMENSION LSTT2(4),LSTT1A(4),LSTT1B(4)
      DATA TARSTR /'AIAI','IAIA','IAAI','AIIA'/
      DATA T2STR  /'AIIA','AIIA','AIIA','IAAI'/
      DATA LSTT2 /1,2,3,3/
      DATA LSTT1A /1,2,1,2/
      DATA LSTT1B /1,2,2,1/
C
      DATA ONE,ONEM/1.D0,-1.D0/
C
      LISTT1=90
      IF(.NOT.LAMBDA) THEN
         LISTAR=63
      ELSE
         LISTAR=146
      ENDIF
      LISTT20=43
C
      DO 10 ICASE=1,4
         ISPINA=LSTT1A(ICASE)
         ISPINB=LSTT1B(ICASE)
         NSIZT1A=FSDPDAI(1,8+ISPINA)
         NSIZT1B=FSDPDIA(1,8+ISPINB)
C
         STRTAR=TARSTR(ICASE)
         STRT2=T2STR(ICASE)
         LISTT2=LISTT20+LSTT2(ICASE)
         STRDIS=STRTAR(1:2)
         STRNUM=STRTAR(3:4)
C     
C     I000:  T1A   I010:  T1B
C     
         I000=1
         I010=I000+NSIZT1A*IINTFP
         I020=I010+NSIZT1B*IINTFP
         CALL FSGETT1(ICORE(I000),ISPINA,LISTT1,'AI',8+ISPINA)
         CALL FSGETT1(ICORE(I010),ISPINB,LISTT1,'IA',8+ISPINB)
C     
         DO 100 IRREP=1,NIRREP
C
            IF(STRNUM.EQ.'AI') THEN
               NUMSYT=FSDPDAI(IRREP,ISYTYP(2,LISTAR))
            ELSEIF(STRNUM.EQ.'IA') THEN
               NUMSYT=FSDPDIA(IRREP,ISYTYP(2,LISTAR))
            ELSE
               WRITE(6,*)'@OST2AIAI2-E FATAL ERROR. ',STRNUM,
     $            ' FOR STRNUM NOT RECOGNIZED'
               CALL ERREX
            ENDIF
C
            IF(NUMSYT.EQ.0) GOTO 100
C     
            IF(STRDIS.EQ.'AI') THEN
               DISSYT=FSDPDAI(IRREP,ISYTYP(1,LISTAR))
            ELSEIF(STRDIS.EQ.'IA') THEN
               DISSYT=FSDPDIA(IRREP,ISYTYP(1,LISTAR))
            ELSE
               WRITE(6,*)'@OST2AIAI2-E FATAL ERROR. ',STRDIS,
     $            ' FOR STRDIS NOT RECOGNIZED'
               CALL ERREX
            ENDIF
            IF(DISSYT.EQ.0) GOTO 100
C
            IF(ICASE.GT.2) THEN
               IRREPI=DIRPRD(IRREPO(ICASE-2),IRREP)
               IRREP2=DIRPRD(IRREPO(5-ICASE),IRREPI)
               IF(ICASE.EQ.3) THEN
                  NUMSYT2=FSDPDIA(IRREP2,ISYTYP(2,LISTT2))
                  DISSYT2=FSDPDAI(IRREP2,ISYTYP(1,LISTT2))
               ELSE
                  NUMSYT2=FSDPDAI(IRREP2,ISYTYP(2,LISTT2))
                  DISSYT2=FSDPDIA(IRREP2,ISYTYP(1,LISTT2))
               ENDIF
               IF(NUMSYT2.NE.NUMSYT.OR.DISSYT2.NE.DISSYT) THEN
                  WRITE(6,*)'@OST2AIAI2-E, FATAL ERROR,',
     $               'NUMSYT2.NE.NUMSYT.OR.DISSYT2.NE.DISSYT',
     $               NUMSYT2,NUMSYT,DISSYT2,DISSYT
                  CALL ERREX
               ENDIF
            ELSE
               IRREP2=IRREP
            ENDIF
C     
C     I000:  T1A  I010:  T1B  I020: T2(TARGET)   I030: T2
C     
            I030=I020+NUMSYT*DISSYT*IINTFP
            I040=I030+NUMSYT*DISSYT*IINTFP
C     
            CALL FSGET(ICORE(I030),1,NUMSYT,1,IRREP2,LISTT2,STRT2)
            CALL FTAU(ICORE(I030),ICORE(I000),ICORE(I010),DISSYT,
     $         NUMSYT,POPI(1,ISPINA),POPA(1,ISPINB),
     $         VRTA(1,ISPINA),VRTI(1,ISPINB),IRREP2,3,ONE)
C     
            CALL FSGET(ICORE(I020),1,NUMSYT,1,IRREP,LISTAR,STRTAR)
            IF(.NOT.LAMBDA) THEN
               CALL SAXPY(NUMSYT*DISSYT,W,ICORE(I030),1,ICORE(I020),1)
               CALL FSPUT(ICORE(I020),1,NUMSYT,1,IRREP,LISTAR,STRTAR)
            ELSE
               WL=WL+W*SDOT(NUMSYT*DISSYT,ICORE(I030),1,ICORE(I020),1)
            ENDIF
C     
 100     CONTINUE
C
 10   CONTINUE
      RETURN
      END
