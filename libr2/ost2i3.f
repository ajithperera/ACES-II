      SUBROUTINE OST2I3(ICORE,MAXCOR,IRREPN1,IRREPM1,IRREPN2,IRREPM2,
     $   LIST1,LIST2,
     $   POPII,POPJ,POPC,POPD,STR1,STR2,LISTAR,FACT,FACT2,FTAU1,FTAU2,
     $   IEX)
C
C     THIS ROUTINE CALCULATE THE OUTER PRODUCT OF LIST1 AND LIST2:
C
C     T2(CI,DJ)=FACT*T2(M1C,N1I)*T2(N2D,M2J)
C
C     INPUT:
C             IRREPN1:        IRREP FOR N1
C             IRREPM1:        IRREP FOR M1
C             IRREPN2:        IRREP FOR N2
C             IRREPM2:        IRREP FOR M2
C             LIST1,LIST2:   LISTNUMBERS FOR T2'S
C             POPII,POPJ,
C             POPC,POPD:     POPULATION VECTORS FOR I,J,C AND D
C             STR1,STR2      'FS' STRINGS FOR T2'S
C             LISTAR:        LIST FOR TARGET
C             FACT:          FACTOR
C             FACT2:         FACTOR OF THE ORIGINAL CONTENT OF T2(CI,DJ)
C             FTAU1,FTAU2:   FACTOR FOR T1*T1 IN FIRST AND SECOND T2
C
C     CALLED BY OST2IIII3
C
CEND
C
CPROGRAMED PS NOV/92
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,DISSYT,DISSYT2,DISSYT1
      INTEGER POPII(8),POPJ(8),POPC(8),POPD(8)
      CHARACTER*4 STRING1,STRING2,STR1,STR2
C
      DIMENSION ICORE(MAXCOR)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &                DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
C
      INTEGER POPA,POPI,VRTA,VRTI
      COMMON /FSSYM/ POPA(8,2),POPI(8,2),VRTA(8,2),VRTI(8,2),
     &               NTAN(2),NTNA(2),NTAA(2),
     &               NTIN(2),NTNI(2),NTII(2),
     &               NTAI(2),NTIA(2),
     &               NF1AN(2),NF1AA(2),NF1IN(2),NF1II(2),NF1AI(2),
     &               NF2AN(2),NF2AA(2),NF2IN(2),NF2II(2),NF2AI(2)
C
      INTEGER FSDPDAN,FSDPDNA,FSDPDAA,FSDPDIN,FSDPDNI,FSDPDII,FSDPDAI,
     $   FSDPDIA
      COMMON /FSSYMPOP/ FSDPDAN(8,22),FSDPDNA(8,22),FSDPDAA(8,22),
     &                  FSDPDIN(8,22),FSDPDNI(8,22),FSDPDII(8,22),
     &                  FSDPDAI(8,22),FSDPDIA(8,22)
C
      DATA ONE,ONEM,ZILCH/1.D0,-1.D0,0.D0/
C
      IEX=0
C
C     READ IN T1 IF NECASSARY
C
      IF(FTAU1.NE.ZILCH) THEN
         CALL GETARG2(LIST1,STR1,I1,I2)
         I0T1=MAXCOR-NTIA(I1)*IINTFP+1
         I0T2=I0T1-NTAI(I2)*IINTFP
         MXCOR=I0T2-1
         CALL FSGETT1(ICORE(I0T1),I1,90,'IA',8+I1)
         CALL FSGETT1(ICORE(I0T2),I2,90,'AI',8+I2)
      ELSE IF(FTAU2.NE.ZILCH) THEN
         CALL GETARG2(LIST2,STR2,I1,I2)
         I0T1=MAXCOR-NTIA(I1)*IINTFP+1
         I0T2=I0T1-NTAI(I2)*IINTFP
         MXCOR=I0T2-1
         CALL FSGETT1(ICORE(I0T1),I1,90,'IA',8+I1)
         CALL FSGETT1(ICORE(I0T2),I2,90,'AI',8+I2)
      ENDIF
C      
      IRREP=DIRPRD(IRREPN2,IRREPM2)
      IF(IRREP.NE.DIRPRD(IRREPN1,IRREPM1)) THEN
         WRITE(6,*) '@OST2I3-E, FATAL ERROR, ',
     $      'IRREPN1xIRREPM1.NE.IRREPN2xIRREPM2',
     $      IRREPN1,IRREPM1,IRREPN2,IRREPM2
         CALL ERREX
      ENDIF
C
      NUMSYT=FSDPDII(IRREP,ISYTYP(2,LISTAR))
      DISSYT=FSDPDII(IRREP,ISYTYP(1,LISTAR))
      IF(NUMSYT*DISSYT.EQ.0) RETURN
C
C     I000:  T2(TAR)    I010:  T2(1)   I020:   T2(2)
C
      I000=1
      I010=I000+NUMSYT*DISSYT*IINTFP
      I020=I010+DISSYT*IINTFP
      I030=I020+NUMSYT*IINTFP
      CALL ZERO(ICORE(I010),DISSYT)
      CALL ZERO(ICORE(I020),NUMSYT)
C     
      IOFFT1=I010
      DO 200 IRREPI=1,NIRREP
         NPOPI=POPII(IRREPI)
         IRREPC=DIRPRD(IRREP,IRREPI)
         NPOPC=POPC(IRREPC)
         IF(NPOPI*NPOPC.EQ.0) GOTO 200
C     
         IRREP1=DIRPRD(IRREPI,IRREPN1)
         IF(IRREP1.NE.DIRPRD(IRREPC,IRREPM1)) GOTO 201
C     
         IF(STR1.EQ.'AIAI') THEN
            NUMSYT1=FSDPDAI(IRREP1,ISYTYP(2,LIST1))
            DISSYT1=FSDPDAI(IRREP1,ISYTYP(1,LIST1))
            STRING1='AIAI'
         ELSEIF(STR1.EQ.'AIIA') THEN
            NUMSYT1=FSDPDIA(IRREP1,ISYTYP(2,LIST1))
            DISSYT1=FSDPDAI(IRREP1,ISYTYP(1,LIST1))
            STRING1='AIIA'
         ELSE
            WRITE(6,*)'@OST2I3-E FATAL ERROR. ',STR1,
     $         ' FOR STR1 NOT RECOGNIZED'
            CALL ERREX
         ENDIF
C     
         CALL FSGET(ICORE(IOFFT1),1,NUMSYT1,1,IRREP1,LIST1,
     $      STRING1)
         IF(FTAU1.NE.ZILCH) THEN
         CALL FTAU(ICORE(IOFFT1),ICORE(I0T1),ICORE(I0T2),DISSYT1,
     $         NUMSYT1,POPA(1,I1),POPI(1,I2),VRTI(1,I1),VRTA(1,I2),
     $         IRREP1,3,FTAU1)
         ENDIF
 201     CONTINUE
         IOFFT1=IOFFT1+NPOPI*NPOPC*IINTFP
 200  CONTINUE
C     
      IOFFT2=I020
      DO 300 IRREPJ=1,NIRREP
         NPOPJ=POPJ(IRREPJ)
         IRREPD=DIRPRD(IRREP,IRREPJ)
         NPOPD=POPD(IRREPD)
         IF(NPOPJ*NPOPD.EQ.0) GOTO 300
C     
         IRREP2=DIRPRD(IRREPJ,IRREPM2)
         IF(IRREP2.NE.DIRPRD(IRREPD,IRREPN2)) GOTO 301
C     
         IF(STR2.EQ.'AIAI') THEN
            NUMSYT2=FSDPDAI(IRREP2,ISYTYP(2,LIST2))
            DISSYT2=FSDPDAI(IRREP2,ISYTYP(1,LIST2))
            STRING2='AIAI'
         ELSEIF(STR2.EQ.'IAIA') THEN
            NUMSYT2=FSDPDIA(IRREP2,ISYTYP(2,LIST2))
            DISSYT2=FSDPDIA(IRREP2,ISYTYP(1,LIST2))
            STRING2='IAIA'
         ELSEIF(STR2.EQ.'IAAI') THEN
            NUMSYT2=FSDPDAI(IRREP2,ISYTYP(2,LIST2))
            DISSYT2=FSDPDIA(IRREP2,ISYTYP(1,LIST2))
            STRING2='IAAI'
         ELSE
            WRITE(6,*)'@OST2I3-E FATAL ERROR. ',STR2,' FOR STR2',
     $         ' NOT RECOGNIZED'
            CALL ERREX
         ENDIF
C     
         CALL FSGET(ICORE(IOFFT2),1,NUMSYT2,1,IRREP2,LIST2,
     $      STRING2)
         IF(FTAU2.NE.ZILCH) THEN
         CALL FTAU(ICORE(IOFFT2),ICORE(I0T1),ICORE(I0T2),DISSYT2,
     $         NUMSYT2,POPA(1,I1),POPI(1,I2),VRTI(1,I1),VRTA(1,I2),
     $         IRREP2,3,FTAU2)
         ENDIF
 301     CONTINUE
         IOFFT2=IOFFT2+NPOPJ*NPOPD*IINTFP
 300  CONTINUE
C     
      IF(FACT2.NE.ZILCH) THEN
         CALL FSGET(ICORE(I000),1,NUMSYT,1,IRREP,LISTAR,'IIII')
      ENDIF
C     
      CALL XGEMM('N','N',DISSYT,NUMSYT,1,FACT,ICORE(I010),DISSYT,
     $   ICORE(I020),1,FACT2,ICORE(I000),DISSYT)
C     
      CALL FSPUT(ICORE(I000),1,NUMSYT,1,IRREP,LISTAR,'IIII')
C     
      IEX=1
C
      RETURN
C     
      END
