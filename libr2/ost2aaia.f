      SUBROUTINE OST2AAIA(ICORE,MAXCOR,IUHF,W,LAMBDA,WL)
C
C     THIS ROUTINE CALCULATE ACTIVE,ACTIVE-INACTIVE,ACTIVE T2 CONTRIBUTION
C
C     T2(Mn,Nc)= -W * T1(M,C)   and T2(Mn,Cm)= -W * T1(n,c)
C
CEND
C
CPROGRAMED PS NOV/92
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,DISSYT,POP,VRT
      CHARACTER*4 STRING
      LOGICAL LAMBDA
C
      DIMENSION ICORE(MAXCOR)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &                DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
C
      INTEGER POPA,POPI,VRTA,VRTI
      COMMON /FSSYM/ POPA(8,2),POPI(8,2),VRTA(8,2),VRTI(8,2),
     &               NTAN(2),NTNA(2),NTAA(2),
     &               NTIN(2),NTNI(2),NTII(2),
     &               NTAI(2),NTIA(2),
     &               NF1AN(2),NF1AA(2),NF1IN(2),NF1II(2),NF1AI(2),
     &               NF2AN(2),NF2AA(2),NF2IN(2),NF2II(2),NF2AI(2)
C
      INTEGER FSDPDAN,FSDPDNA,FSDPDAA,FSDPDIN,FSDPDNI,FSDPDII,FSDPDAI,
     $   FSDPDIA
      COMMON /FSSYMPOP/ FSDPDAN(8,22),FSDPDNA(8,22),FSDPDAA(8,22),
     &                  FSDPDIN(8,22),FSDPDNI(8,22),FSDPDII(8,22),
     &                  FSDPDAI(8,22),FSDPDIA(8,22)
C
      COMMON /FLAGS/ IFLAGS(100)
C
      DATA ONEM/-1.D0/
C
      LISTT1=90
      IF(.NOT.LAMBDA) THEN
         LISTT2=63
      ELSE
         LISTT2=146
      ENDIF
C
      DO 10 ISPIN=1,IUHF+1
         NSIZT1=FSDPDIA(1,8+ISPIN)
C
C     I000:  T1
C
         I000=1
         I010=I000+NSIZT1*IINTFP
         CALL FSGETT1(ICORE(I000),ISPIN,LISTT1,'IA',8+ISPIN)
         IOFFT1=I000
         DO 100 IRREPC=1,NIRREP
            NSYT1=VRTI(IRREPC,ISPIN)*POPA(IRREPC,ISPIN)
            IF(NSYT1.EQ.0) GOTO 100
            DO 200 IRREPN=1,NIRREP
               NPOPN=VRTA(IRREPN,ISPIN)
               IF(NPOPN.EQ.0) GOTO 200
               IRREP=DIRPRD(IRREPC,IRREPN)
C     
               NUMSYT=FSDPDAA(IRREP,ISYTYP(2,LISTT2))
               IF(ISPIN.EQ.1) THEN
                  DISSYT=FSDPDAI(IRREP,ISYTYP(1,LISTT2))
                  STRING='AIAA'
               ELSE
                  DISSYT=FSDPDIA(IRREP,ISYTYP(1,LISTT2))
                  STRING='IAAA'
               ENDIF
               IF(NUMSYT.NE.1.OR.DISSYT.NE.NSYT1) THEN
                  WRITE(6,*)'@OST2AAIA-E FATAL ERROR: DISSYT,',
     $               'NUMSYT,NSYT1',DISSYT,NUMSYT,NSYT1
                  CALL ERREX
               ENDIF
C     
C     I000:  T1  I010:  T2(TARGET)
               I020=I010+NUMSYT*DISSYT*IINTFP
C
               CALL FSGET(ICORE(I010),1,NUMSYT,1,IRREP,LISTT2,STRING)
               IF(.NOT.LAMBDA) THEN
                  CALL SAXPY(NSYT1,ONEM*W,ICORE(IOFFT1),1,ICORE(I010),1)
                  CALL FSPUT(ICORE(I010),1,NUMSYT,1,IRREP,LISTT2,STRING)
               ELSE
                  WL=WL+ONEM*W*SDOT(NUMSYT*DISSYT,ICORE(IOFFT1),1,
     $               ICORE(I010),1)
               ENDIF
 200        CONTINUE
            IOFFT1=IOFFT1+NSYT1*IINTFP
 100     CONTINUE
         IF(IOFFT1.NE.I010) THEN
            WRITE(6,*)'@OST2AAIA-E FATAL ERROR: IOFFT1.NE.I010',
     $         IOFFT1,I010
            CALL ERREX
         ENDIF
 10   CONTINUE
      RETURN
      END
