      SUBROUTINE W5AB2(ICORE,MAXCOR,IUHF,TERM1,TERM2,TERM3,TAU)
c     &                 IOFFLIST)
C
C THIS ROUTINE CALCULATES THREE OF THE FIVE CONTRACTIONS WHICH 
C  CONTRIBUTE TO THE W(AmEf) INTERMEDIATE WHICH IS USED IN CCSD
C  GRADIENTS AND CCSDT MODELS FOR AAAA AND BBBB SPIN CASES.  
C
C  FIRST CONTRACTION:
C           Z(AmEf) = SUM T1(m,g) * W (EfAg)
C  SECOND CONTRACTION:
C           Z(AmEf) =- T2(Ef,Nm) * F(AN)
C  THIRD CONTRACTION
C           Z(AmEf) = TAU(Ef,No) <Am|No>
C
C  THE REMAINING CONTRACTIONS ARE DONE BEFORE THIS ROUTINE IS
C   CALLED AND THE TARGET LIST CONTAINS THEM ON ENTRY. 
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH,ALPHA,BETA
      LOGICAL RHF,CCGRAD,TERM1,TERM2,TERM3,TAU
      DIMENSION ICORE(MAXCOR),IOFFT1(8,2)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /FLAGS/ IFLAGS(100)
C
      DATA ONE   /1.0D0/
      DATA ONEM  /-1.0D0/
      DATA ZILCH /0.0D0/
C
C  If this is an ROHF-MBPT energy calculation, then CCGRAD is false.
C  Otherwise true.
C
      CCGRAD=.TRUE.
      IF(IFLAGS(2).LE.4.AND.IFLAGS(11).EQ.2) CCGRAD=.FALSE.
C
      RHF=.TRUE.
      IF(IUHF.NE.0)RHF=.FALSE.
 
      iofflist = 0
 
      CALL GETT1(ICORE,MAXCOR,MXCOR,IUHF,IOFFT1)
      IBOT=1
      BETA=ONE
      LSTTAR=130+IOFFLIST
      DO 20 IRREPDO=1,NIRREP
       TARDSZ=IRPDPD(IRREPDO,ISYTYP(1,30))
       TARDIS=IRPDPD(IRREPDO,ISYTYP(2,30))
       TARSIZ=TARDSZ*TARDIS
       LISTW =233
       WDSZ=IRPDPD(IRREPDO,ISYTYP(1,LISTW))
       WDIS=IRPDPD(IRREPDO,ISYTYP(2,LISTW))
       CALL IZERO(ICORE,IINTFP*TARSIZ)
       I000=1
       I010=I000+IINTFP*TARSIZ
       IF(TERM2)THEN
C
C SECOND CONTRACTION: 
C                                
C           Z(AmEf) = SUM T1(m,g) * W (EfAg)
C
C THIS TERM IS SKIPPED IN TWO CASES : THE AO BASIS PPL ALGORITHM
C AND THE RHF ALGORITHM WHICH USES A<=B,CD STORAGE FOR THE ABCD
C INTEGRALS.
C
        IF(IFLAGS(93).EQ.2.OR.ISYTYP(1,233).EQ.5)GOTO 999
        CORLFT=MXCOR-I010
        CALL W5OAB2(ICORE(I010),CORLFT,IUHF,ICORE(IOFFT1(1,2)),
     &              ICORE(I000),IRREPDO,LISTW,2,WDSZ)
C
999     CONTINUE
        ENDIF
       I020=I010+IINTFP*TARDSZ*TARDIS
       IF(I020.LE.MAXCOR)THEN
        CALL GETLST(ICORE(I010),1,TARDIS,2,IRREPDO,LSTTAR)
        CALL SAXPY(TARSIZ,ONE,ICORE(I010),1,ICORE(I000),1)
       ELSE
        IOFF=I000
        I020=I010+IINTFP*TARDSZ
        DO 300 IDIS=1,TARDIS
         CALL GETLST(ICORE(I010),IDIS,1,1,IRREPDO,LSTTAR)
         CALL SAXPY (TARDSZ,ONE,ICORE(I010),1,ICORE(IOFF),1)
         IOFF=IOFF+TARDSZ*IINTFP
300     CONTINUE
       ENDIF
C
       CALL PUTLST(ICORE(I000),1,TARDIS,2,IRREPDO,LSTTAR)
C
20    CONTINUE
      RETURN
      END
