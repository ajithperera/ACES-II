      SUBROUTINE W5RING(ICORE,MAXCOR,IUHF,IOFFLIST)
C
C THIS ROUTINE CALCULATES THE CONTRIBUTIONS
C
C  W(Ab,Ci) <= - T(AM) W(Mb,Ci) + T(bm) * W(mA,Ci)
C
C USING A GENERAL OUT-OF-CORE ALGORITHM
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION ONE,ONEM,ZILCH
      LOGICAL TAU,RHF,CC2,HBAR_4LCCSD
      DIMENSION ICORE(MAXCOR),I0T(2)
      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NFEA(2),NFMI(2)
      COMMON/SYMINF/NSTART,NIRREP,IRREPY(255,2),DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON/SYMLOC/ISYMOFF(8,8,25)
      COMMON/FLAGS/IFLAGS(100)
      COMMON/FLAGS2/IFLAGS2(500)
C
      DATA ONE,ONEM,ZILCH /1.0D0,-1.0D0,0.0D0/
C
      RHF=IUHF.EQ.0
      CC2=IFLAGS(2) .EQ. 47
      HBAR_4LCCSD = (IFLAGS(2) .EQ. 6 .OR. IFLAGS2(117) .EQ. 7)
C
C DO ABAB SPIN CASE FIRST
C
C  W(Ab,Ci) <= - T(AM) W(Mb,Ci) + T(bm) * W(mA,Ci)
C
C
      LISTZ=130+IOFFLIST
C
C READ W(Mb,Ci) AS W(CM,bi) OFF LIST 118 AND THEN RESORT TO W(bM,Ci)
C READ W(mA,Ci) AS W(Cm,Ai) OFF LIST 125 AND THEN RESORT TO W(Am,Ci)
C
      ISIZ118=ISYMSZ(ISYTYP(1,118),ISYTYP(2,118))
      ISIZ125=ISYMSZ(ISYTYP(1,125),ISYTYP(2,125))
      I0T(1)=1
      CALL GETLST(ICORE(I0T(1)),1,1,1,1,90)
      IF(IUHF.EQ.0)THEN
       I0T(2)=I0T(1)
      ELSE
       I0T(2)=I0T(1)+IINTFP*NT(1)
       CALL GETLST(ICORE(I0T(2)),1,1,1,2,90)
      ENDIF
      I000=I0T(2)+IINTFP*NT(2)
      I010=I000+IINTFP*MAX(ISIZ118,ISIZ125)
      I020=I010+IINTFP*MAX(ISIZ118,ISIZ125)
      I030=I020+IINTFP*MAX(ISIZ118,ISIZ125)
      IF (CC2 .OR. HBAR_4LCCSD) THEN
         CALL GETALL(ICORE(I010),ISIZ118,1,18)
CSSS         call checksum("18", ICORE(I010), ISIZ118)
      ELSE
         CALL GETALL(ICORE(I010),ISIZ118,1,118)
      ENDIF 
      CALL SSTGEN(ICORE(I010),ICORE(I000),ISIZ118,VRT(1,1),POP(1,1),
     &            VRT(1,2),POP(1,2),ICORE(I030),1,'3214')
      IF (CC2 .OR. HBAR_4LCCSD) THEN
         CALL GETALL(ICORE(I020),ISIZ125,1,25)    
CSSS         call checksum("25", ICORE(I020), ISIZ125)
      ELSE
         CALL GETALL(ICORE(I020),ISIZ125,1,125)    
      ENDIF 
      CALL SSTGEN(ICORE(I020),ICORE(I010),ISIZ125,VRT(1,1),POP(1,2),
     &            VRT(1,1),POP(1,2),ICORE(I030),1,'3214')
C
C LOOP OVER CI AND FORM Z(Ab)=-T(AM)*W(bM;Ci)-W(Am,Ci)*T(bm) FOR EACH Ci
C
      IOFFW118=I000
      IOFFW125=I010
      DO 10 IRREP=1,NIRREP
       ITHRU=1
       DISSYZ=IRPDPD(IRREP,ISYTYP(1,LISTZ))
       I030=I020+IINTFP*DISSYZ
       I040=I030+IINTFP*DISSYZ
       DO 20 IRREPI=1,NIRREP
        IRREPC=DIRPRD(IRREPI,IRREP)
        NUMC=VRT(IRREPC,1)
        NUMI=POP(IRREPI,2)
        DO 30 I=1,NUMI
         DO 40 C=1,NUMC
          IOFFTA=I0T(1)
          IOFFTB=I0T(2)
          IOFFZ125=I020
          IOFFZ118=I030
          CALL GETLST(ICORE(I020),ITHRU,1,1,IRREP,LISTZ)
          DO 50 IRREPM=1,NIRREP
C
C LIST 118 CONTRACTION  -W(bM,Ci)*T(AM)
C
           IRREPB=DIRPRD(IRREPM,IRREP)
           IRREPA=IRREPM
           NUMM=POP(IRREPM,1)
           NUMB=VRT(IRREPB,2)
           NUMA=VRT(IRREPA,1)
           CALL XGEMM('N','T',NUMB,NUMA,NUMM,ONEM,ICORE(IOFFW118),NUMB,
     &                ICORE(IOFFTA),NUMA,ZILCH,ICORE(IOFFZ118),NUMB)
           IOFFW118=IOFFW118+IINTFP*NUMB*NUMM
           IOFFZ118=IOFFZ118+IINTFP*NUMA*NUMB
           IOFFTA=IOFFTA+IINTFP*NUMM*NUMA
C
C LIST 125 CONTRACTION  -W(Am,Ci)*T(mb)
C
           IRREPA=DIRPRD(IRREPM,IRREP)
           IRREPB=IRREPM
           NUMM=POP(IRREPM,2)
           NUMB=VRT(IRREPB,2)
           NUMA=VRT(IRREPA,1)
           CALL XGEMM('N','T',NUMA,NUMB,NUMM,ONEM,ICORE(IOFFW125),NUMA,
     &                ICORE(IOFFTB),NUMB,ONE,ICORE(IOFFZ125),NUMA)
C
           IOFFW125=IOFFW125+IINTFP*NUMA*NUMM
           IOFFZ125=IOFFZ125+IINTFP*NUMA*NUMB
           IOFFTB=IOFFTB+IINTFP*NUMM*NUMB
C
50        CONTINUE
          CALL SYMTRA(IRREP,VRT(1,2),VRT(1,1),1,ICORE(I030),
     &                ICORE(I040)) 
          CALL SAXPY (DISSYZ,ONE,ICORE(I040),1,ICORE(I020),1)
          CALL PUTLST(ICORE(I020),ITHRU,1,1,IRREP,LISTZ)
          ITHRU=ITHRU+1
40       CONTINUE
30      CONTINUE
20     CONTINUE
10    CONTINUE
C
      IF(RHF)RETURN
C
C DO BABA SPIN CASE 
C
C  W(Ab,Ic) <= - T(AM) W(Mb,cI) + T(bm) * W(mA,cI)
C
C
      LISTZ=129+IOFFLIST
C
C READ W(mA,cI) AS W(cm,AI) OFF LIST 117 AND THEN RESORT TO W(Am,cI)
C READ W(Mb,cI) AS W(cM,bI) OFF LIST 126 AND THEN RESORT TO W(bM,cI)
C
      ISIZ117=ISYMSZ(ISYTYP(1,117),ISYTYP(2,117))
      ISIZ126=ISYMSZ(ISYTYP(1,126),ISYTYP(2,126))
      I010=I000+IINTFP*MAX(ISIZ117,ISIZ126)
      I020=I010+IINTFP*MAX(ISIZ117,ISIZ126)
      I030=I020+IINTFP*MAX(ISIZ117,ISIZ126)
      IF (CC2 .OR. HBAR_4LCCSD) THEN
         CALL GETALL(ICORE(I010),ISIZ117,1,17)
CSSS	 call checksum("17", ICORE(I010), ISIZ117)
      ELSE
         CALL GETALL(ICORE(I010),ISIZ117,1,117)
      ENDIF
      CALL SSTGEN(ICORE(I010),ICORE(I000),ISIZ117,VRT(1,2),POP(1,2),
     &            VRT(1,1),POP(1,1),ICORE(I030),1,'3214')
      IF (CC2 .OR. HBAR_4LCCSD) THEN
         CALL GETALL(ICORE(I020),ISIZ126,1,26)    
CSSS	 call checksum("26", ICORE(I020), ISIZ126)
      ELSE
         CALL GETALL(ICORE(I020),ISIZ126,1,126)    
      ENDIF
      CALL SSTGEN(ICORE(I020),ICORE(I010),ISIZ126,VRT(1,2),POP(1,1),
     &            VRT(1,2),POP(1,1),ICORE(I030),1,'3214')
C
C LOOP OVER cI AND FORM Z(Ab)=-T(AM)*W(bM;cI)-W(Am,cI)*T(bm) FOR EACH cI
C
      IOFFW117=I000
      IOFFW126=I010
      DO 110 IRREP=1,NIRREP
       ITHRU=1
       DISSYZ=IRPDPD(IRREP,ISYTYP(1,LISTZ))
       I030=I020+IINTFP*DISSYZ
       I040=I030+IINTFP*DISSYZ
       DO 120 IRREPI=1,NIRREP
        IRREPC=DIRPRD(IRREPI,IRREP)
        NUMC=VRT(IRREPC,2)
        NUMI=POP(IRREPI,1)
        DO 130 I=1,NUMI
         DO 140 C=1,NUMC
          IOFFTA=I0T(1)
          IOFFTB=I0T(2)
          IOFFZ117=I020
          IOFFZ126=I030
C
C GET Ic DISTRIBUTION OFFSET
C
          IDISZ=ISYMOFF(IRREPC,IRREP,18)+I+(C-1)*NUMI-1
          CALL GETLST(ICORE(I020),IDISZ,1,1,IRREP,LISTZ)
          DO 150 IRREPM=1,NIRREP
C
C LIST 126 CONTRACTION  -W(bM,cI)*T(AM)
C
           IRREPB=DIRPRD(IRREPM,IRREP)
           IRREPA=IRREPM
           NUMM=POP(IRREPM,1)
           NUMB=VRT(IRREPB,2)
           NUMA=VRT(IRREPA,1)
           CALL XGEMM('N','T',NUMB,NUMA,NUMM,ONEM,ICORE(IOFFW126),NUMB,
     &                ICORE(IOFFTA),NUMA,ZILCH,ICORE(IOFFZ126),NUMB)
           IOFFW126=IOFFW126+IINTFP*NUMB*NUMM
           IOFFZ126=IOFFZ126+IINTFP*NUMA*NUMB
           IOFFTA=IOFFTA+IINTFP*NUMM*NUMA
C
C LIST 117 CONTRACTION  -W(Am,iC)*T(mb)
C
           IRREPA=DIRPRD(IRREPM,IRREP)
           IRREPB=IRREPM
           NUMM=POP(IRREPM,2)
           NUMB=VRT(IRREPB,2)
           NUMA=VRT(IRREPA,1)
           CALL XGEMM('N','T',NUMA,NUMB,NUMM,ONEM,ICORE(IOFFW117),NUMA,
     &                ICORE(IOFFTB),NUMB,ONE,ICORE(IOFFZ117),NUMA)
C
           IOFFW117=IOFFW117+IINTFP*NUMA*NUMM
           IOFFZ117=IOFFZ117+IINTFP*NUMA*NUMB
           IOFFTB=IOFFTB+IINTFP*NUMM*NUMB
C
150       CONTINUE
          CALL SYMTRA(IRREP,VRT(1,2),VRT(1,1),1,ICORE(I030),
     &                ICORE(I040)) 
          CALL SAXPY (DISSYZ,ONE,ICORE(I040),1,ICORE(I020),1)
          CALL PUTLST(ICORE(I020),IDISZ,1,1,IRREP,LISTZ)
          ITHRU=ITHRU+1
140      CONTINUE
130     CONTINUE
120    CONTINUE
110   CONTINUE
C
C DO AAAA AND BBBB SPIN CASES
C
C  W(A<B,CI) <= - T(AM) W(MB,CI) + ASSYM2
C
C
      DO 1000 ISPIN=1,2
       LISTZ=126+ISPIN+IOFFLIST
       LISTW=122+ISPIN
       IF (CC2 .OR. HBAR_4LCCSD)  LISTW = 22 + ISPIN
C
C READ W(MB,CI) AS W(CM,BI) OFF LIST AND RESORT TO W(BM,CI)
C
       ISIZW=ISYMSZ(ISYTYP(1,LISTW),ISYTYP(2,LISTW))
       I010=I000+IINTFP*ISIZW
       I020=I010+IINTFP*ISIZW
       CALL GETALL(ICORE(I010),ISIZW,1,LISTW)
CSSS       call checksum("122,123", ICORE(I010), ISIZW)
       CALL SSTGEN(ICORE(I010),ICORE(I000),ISIZW,
     &             VRT(1,ISPIN),POP(1,ISPIN),
     &             VRT(1,ISPIN),POP(1,ISPIN),
     &             ICORE(I020),1,'3214')
C
C LOOP OVER CI AND FORM Z(AB)=-T(AM)*W(BM;CI)
C
      IOFFW=I000
      DO 210 IRREP=1,NIRREP
       ITHRU=1
       DISSYZ=IRPDPD(IRREP,ISYTYP(1,LISTZ))
       I020=I010+IINTFP*DISSYZ
       I030=I020+IINTFP*DISSYZ
       DO 220 IRREPI=1,NIRREP
        IRREPC=DIRPRD(IRREPI,IRREP)
        NUMC=VRT(IRREPC,ISPIN)
        NUMI=POP(IRREPI,ISPIN)
        DO 230 I=1,NUMI
         DO 240 C=1,NUMC
          IOFFTA=I0T(ISPIN)
          IOFFZ=I020
          CALL GETLST(ICORE(I010),ITHRU,1,1,IRREP,LISTZ)
          DO 250 IRREPM=1,NIRREP
C
C LIST W CONTRACTION  -W(BM,CI)*T(AM)
C
           IRREPB=DIRPRD(IRREPM,IRREP)
           IRREPA=IRREPM
           NUMM=POP(IRREPM,ISPIN)
           NUMB=VRT(IRREPB,ISPIN)
           NUMA=VRT(IRREPA,ISPIN)
           CALL XGEMM('N','T',NUMB,NUMA,NUMM,ONEM,ICORE(IOFFW),NUMB,
     &                ICORE(IOFFTA),NUMA,ZILCH,ICORE(IOFFZ),NUMB)
           IOFFW=IOFFW+IINTFP*NUMB*NUMM
           IOFFZ=IOFFZ+IINTFP*NUMA*NUMB
           IOFFTA=IOFFTA+IINTFP*NUMM*NUMA
250       CONTINUE
          CALL ASSYM2(IRREP,VRT(1,ISPIN),1,ICORE(I020))
          CALL SAXPY (DISSYZ,ONE,ICORE(I020),1,ICORE(I010),1)
          CALL PUTLST(ICORE(I010),ITHRU,1,1,IRREP,LISTZ)
          ITHRU=ITHRU+1
240       CONTINUE
230      CONTINUE
220     CONTINUE
210    CONTINUE
C
1000  CONTINUE
C
      RETURN
      END
