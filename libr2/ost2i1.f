      SUBROUTINE OST2I1(ICORE,MAXCOR,IRREPN,IRREPN2,IRREPM2,IRREPM1,
     $   LIST1,ISPINT1,LIST2,
     $   POPII,POPJ,POPC,STR2,STRTAR,LISTAR,FACT,FACT2,FTAU2,IEX)
C
C     THIS ROUTINE CALCULATE THE OUTER PRODUCT OF LIST1 AND LIST2:
C
C     T2(CI,NJ)=FACT*T2(N2C,M2I)*T1(M1,J)
C
C     INPUT:
C             IRREPN:         IRREP FOR N
C             IRREPN2:        IRREP FOR N2
C             IRREPM2:        IRREP FOR M2
C             IRREPM1:        IRREP FOR M1
C             LIST1,LIST2:    LISTNUMBERS FOR T'S
C             ISPINT1:        SPIN FOR T1
C             POPII,POPJ,
C             POPC            POPULATION VECTORS FOR I,C AND D
C             STR2            'FS' STRINGS FOR T2
C             STRTAR          'FS' STRINGS FOR TARGET
C             LISTAR:         LIST FOR TARGET
C             FACT:           FACTOR
C             FACT2:          FACTOR OF THE ORIGINAL CONTENT OF T2(CI,DJ)
C             FTAU2:          FACTOR FOR T1*T1 IN T2
C     OUTPUT
C             IEX:            =1 IF SOMETHING WAS CALCULATED
C                             =0 OTHERWISE
C
C     CALLED BY OST2IIAI1
C
CEND
C
CPROGRAMED PS NOV/92
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,DISSYT,DISSYT2
      INTEGER POPII(8),POPJ(8),POPC(8)
      CHARACTER*4 STRING,STR2,STRTAR
C
      DIMENSION ICORE(MAXCOR)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &                DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
C
      INTEGER POPA,POPI,VRTA,VRTI
      COMMON /FSSYM/ POPA(8,2),POPI(8,2),VRTA(8,2),VRTI(8,2),
     &               NTAN(2),NTNA(2),NTAA(2),
     &               NTIN(2),NTNI(2),NTII(2),
     &               NTAI(2),NTIA(2),
     &               NF1AN(2),NF1AA(2),NF1IN(2),NF1II(2),NF1AI(2),
     &               NF2AN(2),NF2AA(2),NF2IN(2),NF2II(2),NF2AI(2)
C
      INTEGER FSDPDAN,FSDPDNA,FSDPDAA,FSDPDIN,FSDPDNI,FSDPDII,FSDPDAI,
     $   FSDPDIA
      COMMON /FSSYMPOP/ FSDPDAN(8,22),FSDPDNA(8,22),FSDPDAA(8,22),
     &                  FSDPDIN(8,22),FSDPDNI(8,22),FSDPDII(8,22),
     &                  FSDPDAI(8,22),FSDPDIA(8,22)
C
      DATA ONE,ONEM,ZILCH/1.D0,-1.D0,0.D0/
C
      IEX=0
C
C     READ IN T1 FOR TAU IF NECASSARY
C
      IF(FTAU2.NE.ZILCH) THEN
         CALL GETARG2(LIST2,STR2,I1,I2)
         I0T1=MAXCOR-NTIA(I1)*IINTFP+1
         I0T2=I0T1-NTAI(I2)*IINTFP
         MXCOR=I0T2-1
         CALL FSGETT1(ICORE(I0T1),I1,90,'IA',8+I1)
         CALL FSGETT1(ICORE(I0T2),I2,90,'AI',8+I2)
      ENDIF
C      
C     READ IN T1
C
      NSIZT1=FSDPDAI(1,8+ISPINT1)
      IF(NSIZT1.EQ.0) RETURN
C
C     I000: T1
C
      I000=1
      I010=I000+NSIZT1*IINTFP
      CALL FSGETT1(ICORE(I000),ISPINT1,LIST1,'AI',8+ISPINT1)
C
      IRREPJ=IRREPM1
      IRREP=DIRPRD(IRREPJ,IRREPN)
C
      IF(STRTAR.EQ.'IIAI') THEN
         NUMSYT=FSDPDAI(IRREP,ISYTYP(2,LISTAR))
         DISSYT=FSDPDII(IRREP,ISYTYP(1,LISTAR))
      ELSEIF(STRTAR.EQ.'AIII') THEN
         NUMSYT=FSDPDII(IRREP,ISYTYP(2,LISTAR))
         DISSYT=FSDPDAI(IRREP,ISYTYP(1,LISTAR))
      ELSE
         WRITE(6,*)'@OST2I1-E FATAL ERROR.',STRTAR,
     $         ' FOR STRTAR NOT RECOGNIZED'
         CALL ERREX
      ENDIF
      IF(NUMSYT*DISSYT.EQ.0) RETURN
C
C     I000:  T1    I010:  T2(TAR)   I020:   T2
C
      I020=I010+DISSYT*NUMSYT*IINTFP
      I030=I020+DISSYT*IINTFP
      CALL ZERO(ICORE(I020),DISSYT)
C
      IOFFT2=I020
      DO 100 IRREPI=1,NIRREP
         NPOPI=POPII(IRREPI)
         IRREPC=DIRPRD(IRREP,IRREPI)
         NPOPC=POPC(IRREPC)
         IF(NPOPI*NPOPC.EQ.0) GOTO 100
C
         IRREP2=DIRPRD(IRREPI,IRREPM2)
         IF(IRREP2.NE.DIRPRD(IRREPC,IRREPN2)) GOTO 101
C
         IF(STR2.EQ.'AIAI') THEN
            NUMSYT2=FSDPDAI(IRREP2,ISYTYP(2,LIST2))
            DISSYT2=FSDPDAI(IRREP2,ISYTYP(1,LIST2))
            STRING='AIAI'
         ELSEIF(STR2.EQ.'IAIA') THEN
            NUMSYT2=FSDPDIA(IRREP2,ISYTYP(2,LIST2))
            DISSYT2=FSDPDIA(IRREP2,ISYTYP(1,LIST2))
            STRING='IAIA'
         ELSEIF(STR2.EQ.'IAAI') THEN
            NUMSYT2=FSDPDAI(IRREP2,ISYTYP(2,LIST2))
            DISSYT2=FSDPDIA(IRREP2,ISYTYP(1,LIST2))
            STRING='IAAI'
         ELSEIF(STR2.EQ.'AIIA') THEN
            NUMSYT2=FSDPDIA(IRREP2,ISYTYP(2,LIST2))
            DISSYT2=FSDPDAI(IRREP2,ISYTYP(1,LIST2))
            STRING='AIIA'
         ELSE
            WRITE(6,*)'@OST2I1-E FATAL ERROR. ',STR2,
     $         ' FOR STR2 NOT RECOGNIZED'
            CALL ERREX
         ENDIF
C     
         CALL FSGET(ICORE(IOFFT2),1,NUMSYT2,1,IRREP2,LIST2,
     $               STRING)
         IF(FTAU2.NE.ZILCH) THEN
         CALL FTAU(ICORE(IOFFT2),ICORE(I0T1),ICORE(I0T2),DISSYT2,
     $         NUMSYT2,POPA(1,I1),POPI(1,I2),VRTI(1,I1),VRTA(1,I2),
     $         IRREP2,3,FTAU2)
         ENDIF
 101     CONTINUE
         IOFFT2=IOFFT2+NPOPI*NPOPC*IINTFP
 100     CONTINUE
C
         IF(FACT2.NE.ZILCH) THEN
            CALL FSGET(ICORE(I010),1,NUMSYT,1,IRREP,LISTAR,STRTAR)
         ENDIF
C
         IF(STRTAR.EQ.'IIAI') THEN
            IF(NUMSYT.NE.NSIZT1) THEN
               WRITE(6,*)'@OST2I1-E FATAL ERROR,',
     $            'NUMSYT.NE.NSIZT1',NUMSYT,NSIZT1
               CALL ERREX
            ENDIF
C
            CALL XGEMM('N','N',DISSYT,NUMSYT,1,FACT,ICORE(I020),DISSYT,
     $         ICORE(I000),1,FACT2,ICORE(I010),DISSYT)
         ELSE
            IF(DISSYT.NE.NSIZT1) THEN
               WRITE(6,*)'@OST2I1-E FATAL ERROR,',
     $            'DISSYT.NE.NSIZT1',DISSYT,NSIZT1
               CALL ERREX
            ENDIF
C
            CALL XGEMM('N','N',DISSYT,NUMSYT,1,FACT,ICORE(I000),DISSYT,
     $         ICORE(I020),1,FACT2,ICORE(I010),DISSYT)
         ENDIF
C
         CALL FSPUT(ICORE(I010),1,NUMSYT,1,IRREP,LISTAR,STRTAR)
C
         IEX=1
C
         RETURN
C
         END
