      SUBROUTINE PDCC_V1X2AB(W,T,Z,DISSYW,NUMSYW,DISSYT,NUMSYT,
     &                  LISTW,
     &                  LISTT,ITYPE,IRREP,T1A,T1B,TAU,POP1,POP2,
     &                  VRT1,VRT2,ELADAB,MODIF,IUHF)
C
C THIS ROUTINE FORMS THE AB LADDER TYPE CONTRACTION FOR MBPT(3) OR 
C  THE GENERALIZED LADDER CONTRACTIONS IN THE INTERMEDIATE-DRIVEN
C  COUPLED-CLUSTER SCHEME.
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL TAU,CIS,EOM, LTRP
      INTEGER DISSYT,DISSYW,DIRPRD,POP1,POP2,VRT1,VRT2
      CHARACTER*4 MODIF
      DIMENSION W(DISSYW,NUMSYW), T(DISSYT,NUMSYT)
      DIMENSION Z(DISSYT,NUMSYT)
      DIMENSION POP1(8),POP2(8),VRT1(8),VRT2(8),T1A(1),T1B(1)
      COMMON /FLAGS/ IFLAGS(100)
      COMMON /EXCITE/ CIS,EOM
      COMMON /LTRIP/ LTRP
      EQUIVALENCE(ICLLVL,IFLAGS(2))
      EQUIVALENCE(IDRLVL,IFLAGS(3))
      COMMON /MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &DIRPRD(8,8)
      COMMON/SYMPOP/IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
      DATA ONE,AZERO,TWO /1.0D0,0.D0,2.0D0/
c
#include "parcc.com"
C
      If (ISPAR) Then
         Call DZERO(T,NUMSYT*DISSYT)
      Else
C
C PICK UP THE RELEVANT T AND W PIECES.
C
         CALL GETLST(T,1,NUMSYT,1,IRREP,LISTT)
      ENDIF 
C
C FORM TAU AMPLITUDES IF REQUIRED 
C
      IF(TAU) THEN
       CALL FTAU(T,T1A,T1B,DISSYT,NUMSYT,POP1,POP2,
     &            VRT1,VRT2,IRREP,3,ONE)
      ENDIF
      CALL GETLST(W,1,NUMSYW,2,IRREP,LISTW)
C
C COMPUTE THE CONTRIBUTION.
C
      CALL XGEMM('N','T',DISSYT,DISSYW,NUMSYT,ONE,T,
     &           DISSYT,W,DISSYW,AZERO,Z,DISSYT)
C
C     If this is a CCSD(T) or QCISD(T) gradient calculation, read
C     current contents of list 116 and add to Z.
C
      IF(IDRLVL.GT.0.AND.(ICLLVL.EQ.21.OR.ICLLVL.EQ.22.OR.LTRP).OR.EOM)
     & THEN
       FX=ONE
       IF(EOM .or. LTRP)FX=TWO
       CALL GETLST(T,1,NUMSYT,1,IRREP,116)
       CALL SAXPY (DISSYT*NUMSYT,FX,T,1,Z,1)
       IF (LTRP) THEN
         CALL GETLST(T,1,NUMSYT,1,IRREP,116+59)
         CALL SAXPY(DISSYT*NUMSYT,ONE,T,1,Z,1)
       ENDIF
      ENDIF

      CALL GETLST(T,1,NUMSYT,1,IRREP,116)
      CALL DAXPY(DISSYT*NUMSYT, 1.0D0, T, 1, Z, 1)
      CALL PUTLST(Z,1,NUMSYT,1,IRREP,116)

      RETURN
      END
