      SUBROUTINE G2X2AB(T2,Z2,T1A,T1B,TAU,FMIAA,FMIBB,POP1,POP2,
     &                  VRT1,VRT2,DISSYT,DISSYZ,NUMSYT,NUMSYZ,
     &                  NFSIZA,NFSIZB,LISTT,LISTZ,
     &                  IRREP,IUHF,TMP)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL TAU
      INTEGER DISSYT,DISSYZ,DIRPRD,POP1,POP2,VRT1,VRT2
      DIMENSION T2(DISSYT,NUMSYT),Z2(DISSYZ,NUMSYZ),FMIAA(NFSIZA),
     &          FMIBB(NFSIZB),T1A(1),T1B(1)
      DIMENSION TMP(1),POP1(8),POP2(8),VRT1(8),VRT2(8)
C
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &                DIRPRD(8,8)
C
      DATA AZERO,ONE,ONEM /0.0D0,1.0D0,-1.0D0/
C
C   GET T2 AMPLITUDES
C
      CALL GETLST(T2,1,NUMSYT,2,IRREP,LISTT)
C
C IF TAU IS TRUE (CCSD) FORM TAU AMPLITUDES
C
      IF(TAU) THEN
       CALL FTAU(T2,T1A,T1B,DISSYT,NUMSYT,POP1,POP2,VRT1,VRT2,
     &           IRREP,3,ONE)
      ENDIF
C
      CALL ZERO(Z2,NUMSYZ*DISSYZ)
C
C   PERFORM MULTIPLICATION
C
      JOFF=1     
      IOFF=1
      DO 90 IRREPJ=1,NIRREP
C
       NOCCJ=POP2(IRREPJ)
C
       IRREPI=DIRPRD(IRREPJ,IRREP)
C
       NOCCI=POP1(IRREPI)
C
       IF(MIN(NOCCJ,NOCCI).NE.0) THEN
C
       CALL XGEMM('N','T',DISSYT*NOCCI,NOCCJ,NOCCJ,ONEM,T2(1,JOFF), 
     &            NOCCI*DISSYT,FMIBB(IOFF),NOCCJ,
     &            AZERO,Z2(1,JOFF),NOCCI*DISSYZ)
       ENDIF
C
       JOFF=JOFF+NOCCJ*NOCCI
       IOFF=IOFF+NOCCJ*NOCCJ
90    CONTINUE
C
      IF(IUHF.EQ.0) THEN
C
C   IN RHF THIS IS SIMPLY A TRANSPOSITION
C
       CALL GETLST(T2,1,NUMSYZ,1,IRREP,LISTZ)
       CALL VADD(T2,T2,Z2,NUMSYZ*DISSYZ,ONE)
C
       CALL SYMTR1(IRREP,POP1,POP2,DISSYZ,Z2,TMP,TMP(1+DISSYZ),
     &             TMP(1+2*DISSYZ))
       CALL SYMTR3(IRREP,VRT1,VRT2,DISSYZ,NUMSYZ,Z2,TMP,
     &             TMP(1+NUMSYZ),TMP(1+2*NUMSYZ))
       CALL VADD(Z2,Z2,T2,NUMSYZ*DISSYZ,ONE)
       CALL PUTLST(Z2,1,NUMSYZ,1,IRREP,LISTZ)
    
      ELSE
       CALL SYMTR1(IRREP,POP1,POP2,DISSYT,T2,TMP,TMP(1+DISSYT),
     &             TMP(1+2*DISSYT))
       CALL SYMTR1(IRREP,POP1,POP2,DISSYZ,Z2,TMP,TMP(1+DISSYZ),
     &             TMP(1+2*DISSYZ))
       JOFF=1     
       IOFF=1
       DO 190 IRREPI=1,NIRREP
C
        NOCCI=POP1(IRREPI)
C
        IRREPJ=DIRPRD(IRREPI,IRREP)
C
        NOCCJ=POP2(IRREPJ)
C
        IF(MIN(NOCCI,NOCCJ).NE.0)THEN
C
        CALL XGEMM('N','T',DISSYT*NOCCJ,NOCCI,NOCCI,ONEM,T2(1,JOFF), 
     &             NOCCJ*DISSYT,FMIAA(IOFF),NOCCI,
     &             ONE,Z2(1,JOFF),NOCCJ*DISSYZ)
        ENDIF
C
        JOFF=JOFF+NOCCJ*NOCCI
        IOFF=IOFF+NOCCI*NOCCI
190    CONTINUE
       CALL SYMTR1(IRREP,POP2,POP1,DISSYZ,Z2,TMP,TMP(1+DISSYZ),
     &             TMP(1+2*DISSYZ))
       CALL GETLST(T2,1,NUMSYZ,1,IRREP,LISTZ)
       CALL VADD(Z2,Z2,T2,NUMSYZ*DISSYZ,ONE)
       CALL PUTLST(Z2,1,NUMSYZ,1,IRREP,LISTZ)
       ENDIF
       RETURN
       END
