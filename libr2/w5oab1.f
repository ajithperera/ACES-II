      SUBROUTINE W5OAB1(ICORE,MAXCOR,IUHF,T1,TARGET,
     &                  IRREP,LISTW,ISPINT,WDSZ,DISTAR)
C
C THIS ROUTINE PERFORMS THE CONTRACTION
C                                         +
C             Z(Fe,Ma) = W(Fe,aG) * T(M,G)
C
C USING AN ALGORITHM WHICH DOES NOT ASSUME THAT W IS HELD IN
C CORE, BUT RATHER THAT N**3 ELEMENTS CAN FIT IN CORE, WHERE
C N IS THE NUMBER OF BASIS FUNCTIONS.  THIS ROUTINE PERFORMS 
C THE CONTRACTION FOR ONLY ONE IRREP SO IT MUST BE CALLED FROM
C INSIDE THE MAIN IRREP LOOP OF THE CORRESPONDING CONTRACTION IN
C W5AB1.
C
C THE TARGET ARRAY IS EMPTY WHEN PASSED IN BUT MUST BE RETURNED AS
C  Z(Fe,aM)
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION TARGET,T1,ONE
      DIMENSION ICORE(MAXCOR),T1(1),TARGET(1),IOFFT1(8)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      DATA ONE /1.0/
C
C COMPUTE T1 OFFSETS
C
      IOFF=1
      DO 5 IRREPT=1,NIRREP
       IOFFT1(IRREPT)=IOFF
       IOFF=IOFF+POP(IRREPT,ISPINT)*VRT(IRREPT,ISPINT)
5     CONTINUE
C
C THE W INTERMEDIATES ARE ON DISK IN THE MATRIX W(Fe,Ga)
C
C
C FIRST LOOP OVER IRREDUCIBLE REPRESENTATIONS OF a
C
      IOFFTAR=1
      IOFF=1
      DO 10 IRREPA=1,NIRREP
       IRREPG=DIRPRD(IRREPA,IRREP)
       IRREPM=IRREPG
C
C DETERMINE IF WE CAN GET A W(Fe,G) MATRIX IN CORE.  IF WE CAN'T
C THEN JOE USER IS OUT OF LUCK
C
       MATSIZ=WDSZ*VRT(IRREPG,ISPINT)
       IF(MATSIZ.GT.MAXCOR/IINTFP)THEN
        WRITE(6,1000)
1000    FORMAT(T3,'@W5OAB1-F, Sorry.  Not enough memory to perform',
     &            ' W(Fe,Ga)*T(G,M) contraction.')
        CALL INSMEM('W5OAB1',MATSIZ*IINTFP,MAXCOR)
       ENDIF
C
C GOOD.  WE CAN ACTUALLY DO THIS CALCULATION.  LOOP NOW OVER a
C  AND LOAD UP W(Fe,G) FOR EACH a.
C
       NUMA=VRT(IRREPA,3-ISPINT)
       NUMG=VRT(IRREPG,ISPINT)
       NUMM=POP(IRREPM,ISPINT)
       IOFFT=IOFFT1(IRREPM)
       DO 20 IA=1,NUMA 
        CALL GETLST(ICORE,IOFF,NUMG,1,IRREP,LISTW)
C
C NOW FORM MATRIX PRODUCT
C
C           Z(Fe,M) = W(Fe,G) * T(G,M)
C
C AND ACCUMULATE ON THE FLY INTO THE TARGET
C
        CALL XGEMM('N','N',WDSZ,NUMM,NUMG,ONE,ICORE,WDSZ,T1(IOFFT),
     &             NUMG,ONE,TARGET(IOFFTAR),WDSZ)
        IOFFTAR=IOFFTAR+NUMM*WDSZ
        IOFF=IOFF+NUMG
20     CONTINUE
10    CONTINUE
C
C CONVERT TARGET FROM Z(Fe,Ma) TO Z(Fe,aM)
C
      I000=1
      I010=I000+WDSZ*IINTFP
      I020=I010+WDSZ*IINTFP
      I030=I020+WDSZ*IINTFP
      CALL SYMTR1(IRREP,POP(1,1),VRT(1,2),WDSZ,TARGET,
     &            ICORE(I000),ICORE(I010),ICORE(I020))
      RETURN
      END
