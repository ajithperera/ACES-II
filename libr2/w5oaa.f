      SUBROUTINE W5OAA(ICORE,MAXCOR,T1,TARGET,
     &                  IRREP,LISTW,ISPIN,WDSZ)
C
C THIS ROUTINE PERFORMS THE CONTRACTION
C                                        
C             Z(E<F,AM) = W(E<F,AG) * T(G,M)
C
C USING AN ALGORITHM WHICH DOES NOT ASSUME THAT W IS HELD IN
C CORE, BUT RATHER THAT N**3/2 ELEMENTS CAN FIT IN CORE, WHERE
C N IS THE NUMBER OF BASIS FUNCTIONS.  THIS ROUTINE PERFORMS 
C THE CONTRACTION FOR ONLY ONE IRREP SO IT MUST BE CALLED FROM
C INSIDE THE MAIN IRREP LOOP OF THE CORRESPONDING CONTRACTION IN
C W5AA1.
C
C THE TARGET ARRAY IS PASSED IN AS Z(E<F,AM) SO NO FANCY TRICKS
C NEED TO BE USED TO REORDER IT.
C
CEND
      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION TARGET,T1,ONE
      DIMENSION ICORE(MAXCOR),T1(1),TARGET(1),IOFFT1(8)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      DATA ONE /1.0/
C
C COMPUTE T1 OFFSETS
C
      IOFF=1
      DO 5 IRREPT=1,NIRREP
       IOFFT1(IRREPT)=IOFF
       IOFF=IOFF+POP(IRREPT,ISPIN)*VRT(IRREPT,ISPIN)
5     CONTINUE
C
C THE W INTERMEDIATES ARE ON DISK IN THE MATRIX W(E<F,A<G)
C
C
C FIRST LOOP OVER IRREDUCIBLE REPRESENTATIONS OF G
C
      IOFFTAR=1
      DO 10 IRREPG=1,NIRREP
       IRREPA=DIRPRD(IRREPG,IRREP)
       IRREPM=IRREPG
C
       NUMA=VRT(IRREPA,ISPIN)
       NUMG=VRT(IRREPG,ISPIN)
       NUMM=POP(IRREPM,ISPIN)
       NUMEFA=WDSZ*NUMA
       IF(NUMEFA.EQ.0)GOTO 10
C
C DETERMINE HOW MANY W(E<FA) VECTORS CAN FIT IN CORE.
C
       NINCOR=MAXCOR/(NUMEFA*IINTFP)
       IF(NINCOR.EQ.0)THEN
        WRITE(6,1000)
1000    FORMAT(T3,'@W5OAA1-F, Sorry.  Not enough memory to perform',
     &            ' W(Ef,Ag)*T(g,m) contraction.')
        CALL INSMEM('W5OAA1',NUMEFA*IINTFP,MAXCOR)
       ENDIF
C
C GOOD.  WE CAN ACTUALLY DO THIS CALCULATION.  NOW LOAD UP
C SOME W(E<FA,G) AND GO TO WORK.  THIS IS A BIT TRICKY HERE BECAUSE
C WANT TO READ W AS AN EXPANDED W(E<F,AG) MATRIX.  THE I/O LOGIC IS
C HANDLED IN ROUTINE LDWAAAA.
C
       NLEFT=NUMG
       NFIRST=1
       IOFFT=IOFFT1(IRREPM)
1      NBATCH=MIN(NLEFT,NINCOR)
       NREAD =NBATCH*NUMA
       NLAST=NFIRST+NBATCH-1
       CALL LDWAAAA(ICORE,WDSZ,NUMEFA,IRREP,IRREPG,ISPIN,
     &              LISTW,NBATCH,NFIRST,NLAST)
       IF(MIN(NUMEFA,NUMM,NBATCH).NE.0)THEN
C
C FORM PRODUCT
C
C           Z(E<FA,G) = W(E<FA,G) * T(G,M)
C 
C FOR SEVERAL G AT A TIME AND ACCUMULATE
C
        CALL XGEMM('N','N',NUMEFA,NUMM,NBATCH,ONE,ICORE,NUMEFA,
     &             T1(IOFFT),NUMG,ONE,TARGET(IOFFTAR),NUMEFA)
       ENDIF
       NFIRST=NFIRST+NBATCH
       NLEFT=NLEFT-NBATCH
       IOFFT=IOFFT+NBATCH
       IF(NLEFT.NE.0)GOTO 1
       IOFFTAR=IOFFTAR+NUMEFA*NUMM
10    CONTINUE
      RETURN
      END
