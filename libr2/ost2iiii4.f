      SUBROUTINE OST2IIII4(ICORE,MAXCOR,IUHF,W,LAMBDA,WL)
C
C
C   THIS ROUTINE CALCULATE INACTIVE,INACTIVE-INACTIVE,INACTIVE T2 CONTRIBUTION
C
C     T2(Ij,Cd)= -W * TAU(iJ,mN)*TAU(nM,cD)
C
CEND
C
CPROGRAMED PS NOV/92
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,DISSYT,POP,VRT,DISSYT2,DISSYT1
      CHARACTER*4 STRING
      LOGICAL LAMBDA
C
      DIMENSION ICORE(MAXCOR)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &                DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
C
      INTEGER POPA,POPI,VRTA,VRTI
      COMMON /FSSYM/ POPA(8,2),POPI(8,2),VRTA(8,2),VRTI(8,2),
     &               NTAN(2),NTNA(2),NTAA(2),
     &               NTIN(2),NTNI(2),NTII(2),
     &               NTAI(2),NTIA(2),
     &               NF1AN(2),NF1AA(2),NF1IN(2),NF1II(2),NF1AI(2),
     &               NF2AN(2),NF2AA(2),NF2IN(2),NF2II(2),NF2AI(2)
C
      INTEGER FSDPDAN,FSDPDNA,FSDPDAA,FSDPDIN,FSDPDNI,FSDPDII,FSDPDAI,
     $   FSDPDIA
      COMMON /FSSYMPOP/ FSDPDAN(8,22),FSDPDNA(8,22),FSDPDAA(8,22),
     &                  FSDPDIN(8,22),FSDPDNI(8,22),FSDPDII(8,22),
     &                  FSDPDAI(8,22),FSDPDIA(8,22)
C
      COMMON /ACTIRR/ IRREPOA,IRREPOB,IRREPVA,IRREPVB
C
      COMMON /FLAGS/ IFLAGS(100)
C
      DATA ONE,ONEM,ZILCH/1.D0,-1.D0,0.D0/
C
C     GET T1 ELEMETS FOR TAU
C
C     I0TAIA: T1(AI,A)  ETC..
C
      I0TAIA=MAXCOR-NTAI(1)*IINTFP+1
      I0TAIB=I0TAIA-NTAI(2)*IINTFP
      I0TIAA=I0TAIB-NTIA(1)*IINTFP
      I0TIAB=I0TIAA-NTIA(2)*IINTFP
      MXCOR=I0TIAB-1
      CALL FSGETT1(ICORE(I0TAIA),1,90,'AI',9)
      CALL FSGETT1(ICORE(I0TAIB),2,90,'AI',10)
      CALL FSGETT1(ICORE(I0TIAA),1,90,'IA',9)
      CALL FSGETT1(ICORE(I0TIAB),2,90,'IA',10)
C
      IF(.NOT.LAMBDA) THEN
         LISTAR=63
      ELSE
         LISTAR=146
      ENDIF
      LISTT2=46
C
      IRREP=DIRPRD(IRREPVB,IRREPVA)
C
      NUMSYT=FSDPDII(IRREP,ISYTYP(2,LISTAR))
      DISSYT=FSDPDII(IRREP,ISYTYP(1,LISTAR))
      IF(NUMSYT*DISSYT.EQ.0) RETURN
C
      NUMSYT1=FSDPDII(IRREP,ISYTYP(2,LISTT2))
      DISSYT1=FSDPDAA(IRREP,ISYTYP(1,LISTT2))
      NUMSYT2=FSDPDAA(IRREP,ISYTYP(2,LISTT2))
      DISSYT2=FSDPDII(IRREP,ISYTYP(1,LISTT2))
C
      IF(NUMSYT1.NE.NUMSYT.OR.DISSYT2.NE.DISSYT) THEN
         WRITE(6,*)'@OST2IIII4-E FATAL ERROR: NUMSYT1,NUMSYT,DISSYT2,',
     $      'DISSYT: ',NUMSYT1,NUMSYT,DISSYT2,DISSYT
      ENDIF
C     
C     I000:  T2(TAR)    I010:  T2(1)   I020:  T2(2)
C
      I000=1
      I010=I000+NUMSYT*DISSYT*IINTFP
      I020=I010+NUMSYT1*DISSYT1*IINTFP
      I030=I020+NUMSYT2*DISSYT2*IINTFP
      IF(I030.GT.MXCOR) CALL INSMEM('OST2IIII4',I030,MXCOR)
C     
      CALL FSGET(ICORE(I000),1,NUMSYT1,1,IRREP,LISTT2,'AAII')
      CALL FTAU(ICORE(I000),ICORE(I0TAIA),ICORE(I0TAIB),DISSYT1,NUMSYT1,
     $   POPI(1,1),POPI(1,2),VRTA(1,1),VRTA(1,2),IRREP,3,ONE)
      CALL SYMTRA(IRREP,POPI(1,1),POPI(1,2),DISSYT1,ICORE(I000),
     $   ICORE(I010))
C     
      CALL FSGET(ICORE(I000),1,NUMSYT2,1,IRREP,LISTT2,'IIAA')
      CALL FTAU(ICORE(I000),ICORE(I0TIAA),ICORE(I0TIAB),DISSYT2,NUMSYT2,
     $   POPA(1,1),POPA(1,2),VRTI(1,1),VRTI(1,2),IRREP,3,ONE)
      CALL SYMTRA2(IRREP,VRTI(1,1),VRTI(1,2),DISSYT2,NUMSYT2,
     $   ICORE(I000),ICORE(I020))
C     
      IF(.NOT.LAMBDA) THEN
         CALL FSGET(ICORE(I000),1,NUMSYT,1,IRREP,LISTAR,'IIII')
         FACT=ONE
      ELSE
         FACT=ZILCH
      ENDIF
C
      CALL XGEMM('N','N',DISSYT,NUMSYT,1,ONEM*W,
     $   ICORE(I020),DISSYT,ICORE(I010),1,FACT,ICORE(I000),
     $   DISSYT)
C
      IF(.NOT.LAMBDA) THEN
         CALL FSPUT(ICORE(I000),1,NUMSYT,1,IRREP,LISTAR,'IIII')
      ELSE
         I020=I010+NUMSYT*DISSYT*IINTFP
         IF(I020.GT.MXCOR) CALL INSMEM('OST2IIII4',I020,MXCOR)
         CALL FSGET(ICORE(I010),1,NUMSYT,1,IRREP,LISTAR,'IIII')
         WL=WL+SDOT(NUMSYT*DISSYT,ICORE(I000),1,ICORE(I010),1)
      ENDIF
C     
      RETURN
      END
