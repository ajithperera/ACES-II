      SUBROUTINE OST2AIAI1(ICORE,MAXCOR,IUHF,W,LAMBDA,WL)
C
C
C     THIS ROUTINE CALCULATE ACTIVE,INACTIVE-ACTIVE,INACTIVE T2 CONTRIBUTION
C
C     T2(IM,AN)= -W * T2(iM,aN)
C     T2(in,am)= -W * T2(In,Am)
C
CEND
C
CPROGRAMED PS NOV/92
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,DISSYT,DISSYTAR,POP,VRT
      CHARACTER*4 STRING
      LOGICAL LAMBDA
C
      DIMENSION ICORE(MAXCOR)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &                DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
C
      INTEGER POPA,POPI,VRTA,VRTI
      COMMON /FSSYM/ POPA(8,2),POPI(8,2),VRTA(8,2),VRTI(8,2),
     &               NTAN(2),NTNA(2),NTAA(2),
     &               NTIN(2),NTNI(2),NTII(2),
     &               NTAI(2),NTIA(2),
     &               NF1AN(2),NF1AA(2),NF1IN(2),NF1II(2),NF1AI(2),
     &               NF2AN(2),NF2AA(2),NF2IN(2),NF2II(2),NF2AI(2)
C
      INTEGER FSDPDAN,FSDPDNA,FSDPDAA,FSDPDIN,FSDPDNI,FSDPDII,FSDPDAI,
     $   FSDPDIA
      COMMON /FSSYMPOP/ FSDPDAN(8,22),FSDPDNA(8,22),FSDPDAA(8,22),
     &                  FSDPDIN(8,22),FSDPDNI(8,22),FSDPDII(8,22),
     &                  FSDPDAI(8,22),FSDPDIA(8,22)
C
      COMMON /FLAGS/ IFLAGS(100)
C
      DATA ONE,ONEM/1.D0,-1.D0/
C
      IF(.NOT.LAMBDA) THEN
         LISTAR0=60
      ELSE
         LISTAR0=143
      ENDIF
      LISTT2=46
C
      DO 10 ISPIN=1,IUHF+1
         LISTAR=LISTAR0+ISPIN
C     
         DO 100 IRREP=1,NIRREP
            IF(ISPIN.EQ.1) THEN
               STRING='AIAI'
               NUMSYT=FSDPDAI(IRREP,ISYTYP(2,LISTT2))
               DISSYT=FSDPDAI(IRREP,ISYTYP(1,LISTT2))
            ELSE
               STRING='IAIA'
               NUMSYT=FSDPDIA(IRREP,ISYTYP(2,LISTT2))
               DISSYT=FSDPDIA(IRREP,ISYTYP(1,LISTT2))
            ENDIF
            NUMSYTAR=FSDPDIA(IRREP,ISYTYP(2,LISTAR))
            DISSYTAR=FSDPDIA(IRREP,ISYTYP(1,LISTAR))
C
            IF(NUMSYT.NE.NUMSYTAR.OR.DISSYT.NE.DISSYTAR) THEN
               WRITE(6,*)'@OST2AIAI1-E, FATAL ERROR, ',
     $            'NUMSYT.NE.NUMSYTAR.OR.DISSYT.NE.DISSYTAR',
     $            NUMSYT,NUMSYTAR,DISSYT,DISSYTAR
               CALL ERREX
            ENDIF
C
            IF(DISSYT*NUMSYT.EQ.0) GOTO 100
C     
C     I000: T2(TARGET)   I010: T2
C
            I000=1
            I010=I000+NUMSYT*DISSYT*IINTFP
            I020=I010+NUMSYT*DISSYT*IINTFP
C     
            CALL FSGET(ICORE(I010),1,NUMSYT,1,IRREP,LISTT2,STRING)
C     
            CALL FSGET(ICORE(I000),1,NUMSYT,1,IRREP,LISTAR,'IAIA')
            IF(.NOT.LAMBDA) THEN
               CALL SAXPY(NUMSYT*DISSYT,ONEM*W,ICORE(I010),1,
     $            ICORE(I000),1)
               CALL FSPUT(ICORE(I000),1,NUMSYT,1,IRREP,LISTAR,'IAIA')
            ELSE
               WL=WL+ONEM*W*SDOT(NUMSYT*DISSYT,ICORE(I000),1,
     $            ICORE(I010),1)
            ENDIF
C     
 100     CONTINUE
C
 10   CONTINUE
      RETURN
      END
