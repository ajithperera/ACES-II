      SUBROUTINE T3FT323V(D3T3,D3T3EXP,CORE,MAXCOR,IUHF,ISPIN1,ISPIN2,
     1                    LEN,LENEXP,IRPIJK,IJKVAL,IADT3,IADW)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER VRT,POP,DIRPRD,DISSIZ
      DIMENSION D3T3(LEN),D3T3EXP(LENEXP),CORE(1)
      DIMENSION IADT3(8),IADW(8),IADW2(8)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /SYM/    POP(8,2),VRT(8,2),NT1(2),NFMI(2),NFEA(2)
      COMMON /FLAGS/  IFLAGS(100)
      EQUIVALENCE(ICLLVL,IFLAGS( 2))
      EQUIVALENCE(IDRLVL,IFLAGS( 3))
      COMMON /FILES/  LUOUT,MOINTS
C
      COMMON /T3OFF/  IOFFVV(8,8,10),IOFFOO(8,8,10),IOFFVO(8,8,4)
      COMMON /T3IOOF/ IJKPOS(8,8,8,2),IJKLEN(36,8,4),IJKOFF(36,8,4),
     1                NCOMB(4)
C
      INDEX(I) = I*(I-1)/2
C
      I000  = 1
      I010  = I000 + MAX(NFEA(1),NFEA(2))
      I020  = I010 + LEN
      I030  = I020 + LENEXP
      I040  = I030 + LENEXP
      NEED  = I040 * IINTFP
      IF(NEED.GT.MAXCOR)THEN
      WRITE(LUOUT,1010) NEED,MAXCOR
 1010 FORMAT(' @T3FT323V-F, Insufficient memory. Need ',I10,' Got ',I10)
      CALL INSMEM('T3FT323V',NEED,MAXCOR)
      ENDIF
C
      CALL GETLIST(CORE(I010),IJKVAL,1,1,IRPIJK,ISPIN1 + 1 + 4)
C
C     F(c,e) * T(ABe,IJk)
C     F(C,E) * T(abE,ijK)
C
C     If this is a CCSDT calculation, we read the FEA intermediate.
C     Otherwise, we read the OCC-OCC block of the Fock matrix.
C
      IF(ICLLVL.EQ.18)THEN
      LISTF = 92
      LPART = ISPIN2
      IF(IUHF.EQ.0) LPART = 1
      ELSE
      LISTF = 92
      LPART = ISPIN2 + 2
      IF(IUHF.EQ.0) LPART = 1 + 2
      ENDIF
      CALL GETLST(CORE(I000),1,1,2,LPART,LISTF)
C     CALL RMDIAG(CORE(I000),VRT(1,ISPIN2),NIRREP)
C
      IOFF1 = I010
      IOFF2 = I000
      IOFF3 = 1
      DO   10 IRPC=1,NIRREP
      IRPE  = IRPC
      IRPAB = DIRPRD(IRPC,IRPIJK)
C
      IF(VRT(IRPC,ISPIN2).EQ.0.OR.IRPDPD(IRPAB,ISPIN1).EQ.0) GOTO 10
C
      DISSIZ = IRPDPD(IRPAB,ISPIN1)
      NDIS   = VRT(IRPC,ISPIN2)
      CALL XGEMM('N','N',DISSIZ,NDIS,NDIS,1.0D+00,
     1           CORE(IOFF1),DISSIZ,CORE(IOFF2),NDIS,1.0D+00,
     1           D3T3(IOFF3),DISSIZ)
C
      IOFF1 = IOFF1 + DISSIZ * NDIS
      IOFF2 = IOFF2 + NDIS   * NDIS
      IOFF3 = IOFF3 + DISSIZ * NDIS
   10 CONTINUE
C
C     F(A,E) * T(EBc,IJk) - F(B,E) * T(EAc,IJk)
C     F(a,e) * T(ebC,ijK) - F(b,e) * T(eaC,ijK)
C
      IF(ICLLVL.EQ.18)THEN
      LISTF = 92
      LPART = ISPIN1
      ELSE
      LISTF = 92
      LPART = ISPIN1 + 2
      ENDIF
      CALL GETLST(CORE(I000),1,1,2,LPART,LISTF)
C     CALL RMDIAG(CORE(I000),VRT(1,ISPIN1),NIRREP)
C
C     Expand T3.
C
      CALL ZERO(CORE(I020),LENEXP)
      CALL ZERO(CORE(I030),LENEXP)
      CALL SYMTRW2(CORE(I010),CORE(I020),CORE(I030),IADT3,IADW2,
     1             IRPIJK,ISPIN1,ISPIN2)
C
C     At CORE(I020) we have T3(B,c,E)/T3(b,C,e), labelled by IRPE. Use
C     transpose so we form D3T3EXP(A,B,c)/D3T3EXP(a,b,C).
C
      IOFF1 = I000
      IOFF2 = I020
      DO 20 IRPE=1,NIRREP
C
      IRPA  = IRPE
      IRPBC = DIRPRD(IRPE,IRPIJK)
C
      DISSIZ = VRT(IRPA,ISPIN1)
      NDIS   = IRPDPD(IRPBC,13)
      NSUM   = DISSIZ
C
      IF(DISSIZ.EQ.0.OR.NDIS.EQ.0) GOTO 20
C
      CALL XGEMM('T','T',DISSIZ,NDIS,NSUM,1.0D+00,
     1           CORE(IOFF1),DISSIZ,CORE(I020-1+IADW2(IRPE)),
     1           NDIS,1.0D+00,
C    1           CORE(IOFF1),DISSIZ,CORE(IOFF2),NDIS,1.0D+00,
     1           D3T3EXP(IADW(IRPBC)),VRT(IRPA,ISPIN1))
C
      IOFF1 = IOFF1 + DISSIZ * DISSIZ
      IOFF2 = IOFF2 + DISSIZ * NDIS
   20 CONTINUE
#ifdef _DEBUG_LVLM
      Write(*,*) "The T3*FVV contribution"
      call checksum("T3*FVV", D3T3exp, Lenexp)
#endif
      RETURN
      END
