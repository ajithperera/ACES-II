       SUBROUTINE DIPINT(NHKTA,NHKTB,KHKTA,KHKTB,LDIAG,ISTEPA,ISTEPB,
     *                  ONECEN,CORPX,CORPY,CORPZ,DIFDIP,WORK1)
C
C     tuh 1985
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C     -------------------------------------------------------
      PARAMETER (LUCMD = 5, LUPRI = 6)
#include "baslims.par"
#include "mxcent.par"
      PARAMETER (MXQN=8, MXAQN=MXQN*(MXQN+1)/2, MXAQNS=MXAQN*MXAQN
     *           *MXCONT*MXCONT)
      LOGICAL LDIAG, ONECEN, DIFDIP
      DIMENSION WORK1(1)
#include "lwork.par"
      PARAMETER (LWKRLM = LWORK3 - 8020)
      COMMON /CWORK3/ WK3LOW, SHGTF, AHGTF((MXCENT+1)*(2*MXQN+1)**3)
CSSS     &                RLMCOF(LWKRLM),WK3HGH
#include "odcs.com"
      COMMON /POINTER/IS0000, IS000X, IS000Y, IS000Z,
     *                IS00XX, IS00XY, IS00XZ, IS00YY,
     *                IS00YZ, IS00ZZ, IT0000, IT000X,
     *                IT000Y, IT000Z, IT00XX, IT00XY,
     *                IT00XZ, IT00YY, IT00YZ, IT00ZZ,
     *                ID0000, ID000X, ID000Y, ID000Z,
     *                ID00XX, ID00XY, ID00XZ, ID00YX,
     *                ID00YY, ID00YZ, ID00ZX, ID00ZY,
     *                ID00ZZ,
     *                IA0000, IA0X00, IA0Y00, IA0Z00,
     *                IAXX00, IAXY00, IAXZ00, IAYY00,
     *                IAYZ00, IAZZ00, IA000X, IA000Y,
     *                IA000Z, IA00XX, IA00XY, IA00XZ,
     *                IA00YY, IA00YZ, IA00ZZ, IA0X0X,
     *                IA0X0Y, IA0X0Z, IA0Y0X, IA0Y0Y,
     *                IA0Y0Z, IA0Z0X, IA0Z0Y, IA0Z0Z
      COMMON/SCRPOIN/ISCR1,ISCR2,ISCR3,ISCR4,ISCR5,ISCR6,ISCR7,ISCR8,
     *               ISCR9,ISCR10,ISCR11,ISCR12,ISCR13,ISCR14,ISCR15,
     *               ISCR16,ISCR17,ISCR18,ISCR19,ISCR20,
     *               JSCR(28)
      COMMON /LMNS/ LVALUA(MXAQN), MVALUA(MXAQN), NVALUA(MXAQN),
     *              LVALUB(MXAQN), MVALUB(MXAQN), NVALUB(MXAQN)
      COMMON /GENCON/ NRCA, NRCB, CONTA(MXCONT), CONTB(MXCONT)
      DATA THRSH /1.D-20/
C
C  FIRST FORM THE DIPOLE INTEGRALS OVER THE PRIMITIVES
C
      IF(.NOT.DIFDIP.OR.ONECEN) THEN
C
*VOCL LOOP,NOVREC
CDIR$ IVDEP
       DO 100 I = 1,KHKTA*KHKTB
       ICOMPA=(I-1)/KHKTB+1
       ICOMPB=I-(ICOMPA-1)*KHKTB
       LVALA = LVALUA(ICOMPA)
       MVALA = MVALUA(ICOMPA)
       NVALA = NVALUA(ICOMPA)
       ISTRAT = 1 + ISTEPA*LVALA
       ISTRAU = 1 + ISTEPA*MVALA
       ISTRAV = 1 + ISTEPA*NVALA
       LVALB = LVALUB(ICOMPB)
       MVALB = MVALUB(ICOMPB)
       NVALB = NVALUB(ICOMPB)
       ISTRET = ISTRAT + ISTEPB*LVALB
       ISTREU = ISTRAU + ISTEPB*MVALB
       ISTREV = ISTRAV + ISTEPB*NVALB
C
       SX0 = SHGTF*ODC00X(ISTRET)
       SY0 = SHGTF*ODC00Y(ISTREU)
       SZ0 = SHGTF*ODC00Z(ISTREV)
       DX0 = SHGTF*ODC00X(ISTRET + 1) + CORPX*SX0
       DY0 = SHGTF*ODC00Y(ISTREU + 1) + CORPY*SY0
       DZ0 = SHGTF*ODC00Z(ISTREV + 1) + CORPZ*SZ0
C
       WORK1(ISCR1+I) =  SX0*SY0*SZ0
       WORK1(ISCR2+I) =  DX0*SY0*SZ0
       WORK1(ISCR3+I) =  SX0*DY0*SZ0
       WORK1(ISCR4+I) =  SX0*SY0*DZ0
100   CONTINUE
C
C  NOW FORM THE CONTRACTIONS
C
      ISKIP=NRCA*NRCB
      IF(LDIAG) ISKIP= NRCA*(NRCA+1)/2
      IOFF=0
      DO 200 IRCA=1,NRCA
       MAXB=NRCB
       IF(LDIAG) MAXB=IRCA
       DO 200 IRCB=1,MAXB
        IOFF=IOFF+1
        CONT=CONTA(IRCA)*CONTB(IRCB) 
        IF(ABS(CONT).GT.THRSH) THEN
         CALL SAXPY(KHKTA*KHKTB*4,CONT,WORK1(ISCR1+1),1,
     *              WORK1(ID0000+IOFF),ISKIP)
        ENDIF
200   CONTINUE
C
      ELSE
C
*VOCL LOOP,NOVREC
CDIR$ IVDEP
       DO 1100 I = 1,KHKTA*KHKTB
       ICOMPA=(I-1)/KHKTB+1
       ICOMPB=I-(ICOMPA-1)*KHKTB
       LVALA = LVALUA(ICOMPA)
       MVALA = MVALUA(ICOMPA)
       NVALA = NVALUA(ICOMPA)
       ISTRAT = 1 + ISTEPA*LVALA
       ISTRAU = 1 + ISTEPA*MVALA
       ISTRAV = 1 + ISTEPA*NVALA
       LVALB = LVALUB(ICOMPB)
       MVALB = MVALUB(ICOMPB)
       NVALB = NVALUB(ICOMPB)
       ISTRET = ISTRAT + ISTEPB*LVALB
       ISTREU = ISTRAU + ISTEPB*MVALB
       ISTREV = ISTRAV + ISTEPB*NVALB
C
       SX0 = SHGTF*ODC00X(ISTRET)
       SY0 = SHGTF*ODC00Y(ISTREU)
       SZ0 = SHGTF*ODC00Z(ISTREV)
       DX0 = SHGTF*ODC00X(ISTRET + 1) + CORPX*SX0
       DY0 = SHGTF*ODC00Y(ISTREU + 1) + CORPY*SY0
       DZ0 = SHGTF*ODC00Z(ISTREV + 1) + CORPZ*SZ0
C
       WORK1(ISCR1+I) =  SX0*SY0*SZ0
       WORK1(ISCR2+I) =  DX0*SY0*SZ0
       WORK1(ISCR3+I) =  SX0*DY0*SZ0
       WORK1(ISCR4+I) =  SX0*SY0*DZ0
       SX1 = SHGTF*ODC10X(ISTRET)
       SY1 = SHGTF*ODC10Y(ISTREU)
       SZ1 = SHGTF*ODC10Z(ISTREV)
       DX1 = SHGTF*ODC10X(ISTRET + 1) + CORPX*SX1
       DY1 = SHGTF*ODC10Y(ISTREU + 1) + CORPY*SY1
       DZ1 = SHGTF*ODC10Z(ISTREV + 1) + CORPZ*SZ1
       WORK1(ISCR5+I) = DX1*SY0*SZ0
       WORK1(ISCR6+I) = SX1*DY0*SZ0
       WORK1(ISCR7+I) = SX1*SY0*DZ0
       WORK1(ISCR8+I) = DX0*SY1*SZ0
       WORK1(ISCR9+I) = SX0*DY1*SZ0
       WORK1(ISCR10+I) = SX0*SY1*DZ0
       WORK1(ISCR11+I) = DX0*SY0*SZ1
       WORK1(ISCR12+I) = SX0*DY0*SZ1
       WORK1(ISCR13+I) = SX0*SY0*DZ1
1100   CONTINUE
C
C  NOW FORM THE CONTRACTIONS
C
      ISKIP=NRCA*NRCB
      IF(LDIAG) ISKIP= NRCA*(NRCA+1)/2
      IOFF=0
      DO 1200 IRCA=1,NRCA
       MAXB=NRCB
       IF(LDIAG) MAXB=IRCA
       DO 1200 IRCB=1,MAXB
        IOFF=IOFF+1
        CONT=CONTA(IRCA)*CONTB(IRCB) 
        IF(ABS(CONT).GT.THRSH) THEN
         CALL SAXPY(KHKTA*KHKTB*4,CONT,WORK1(ISCR1+1),1,
     *              WORK1(ID0000+IOFF),ISKIP)
         CALL SAXPY(KHKTA*KHKTB*9,CONT,WORK1(ISCR5+1),1,
     *              WORK1(ID00XX+IOFF),ISKIP)
        ENDIF
1200   CONTINUE
      END IF
      RETURN
      END
