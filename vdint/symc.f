      SUBROUTINE SYMC(CFULL,CSYM,NBAST,NBAS,SCF,FOCK,ISPIN)
C
C  THIS SUBROUTINE RETURNS THE SYMMETRY PACKED LIST OF THE INPUT
C  MATRIX CFULL. IT IS USED IN THE INTEGRAL DERIVATIVE CODE TO
C  SYMMETYRY PACK THE EIGEN VECTOR MATRIX.
C
C
C  CFULL ..... INPUT MATRIX ( SIZE NBAST*NBAST)
C  CSYM ...... SYMMETRY PACKED OUTPUT MATRIX (SQUARE MATRICES WITHIN
C                                             EACH IRREP)
C  NBAST ..... TOTAL NUMBER OF BASIS FUNCTION
C  NBAS ...... NUMBER OF BASIS FUNCTION PER IRREP( ARRAY OF DIM 8)
C  SCF ....... FLAG WHICH TELLS IF THE EIGENVECTORS HAVE BEEN REORDERED
C              OR NOT.
C              SCF = .TRUE.   NO REORDERING REQUIRED HERE
C                  = .FALSE.  REORDERING IS REQUIRED
C  FOCK ...... FOCK = .TRUE. SYMMETRY PACK FOCK MATRIX
C                  = .FALSE. SYMMETRY PACK CMO
C  ISPIN ..... SPIN CASE (AS USUAL)
C
CEND
C
C   CODED OCT/90   JG
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      INTEGER DIRPRD,POP,VRT
      LOGICAL SCF,FOCK
      DIMENSION CFULL(NBAST,NBAST),CSYM(1),NBAS(8)
C
      COMMON/FLAGS/IFLAGS(100)
      COMMON/SYMINF/NSTART,NIRREP,IRREPA(255,2),DIRPRD(8,8)
      COMMON/SYM/POP(8,2),VRT(8,2),NT(2),NF1(2),NF2(2)
C
      IF((.NOT.SCF.OR.IFLAGS(3).EQ.2).AND.(.NOT.FOCK)) THEN
C
C  FOR CORRELATION METHODS REORDER C SO THAT WE HAVE ALL EIGENVECTORS
C  OF THE SAME IRREP TOGETHER
C 
       CALL REORDC(CFULL,CSYM,NBAST,POP(1,ISPIN),VRT(1,ISPIN),NBAS)
C
      ENDIF

      IOFFR=1
      IOFFC=0
      IOFFP=1
C
      DO 10 IRREP=1,NIRREP
       DO 20 IMO=1,NBAS(IRREP)
        IOFFC=IOFFC+1
        LENGTH=NBAS(IRREP)
        CALL SCOPY(LENGTH,CFULL(IOFFR,IOFFC),1,CSYM(IOFFP),1)
        IOFFP=IOFFP+LENGTH
20     CONTINUE
       IOFFR=IOFFR+NBAS(IRREP)
10    CONTINUE
C
C  ALL DONE,  RETURN
c
      RETURN
      END 
