      SUBROUTINE GIADER(IPRINT,NOINT,SEGMEN,WORK1,LWORK1)
C
C DRIVES THE EVALUATION OF 2E-GIAO INTEGRALS
C
CEND
C
C SEP/91 JG UNIVERSITY OF KARLSRUHE
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      PARAMETER (LUCMD = 5, LUPRI = 6)
      LOGICAL NOINT,SEGMEN
      LOGICAL         AEQB,   CEQD,   DIAGAB, DIAGCD, DIACAC,
     *                ONECEN, PQSYM,  DTEST,
     *                TPRIAB, TPRICD, TCONAB, TCONCD
      LOGICAL CROSS1, CROSS2
      LOGICAL TKTIME
      LOGICAL         DC101, DC1H1, DC1E1, DC2H1, DC2E1,
     *                DC102, DC1H2, DC1E2, DC2H2, DC2E2,
     *                DPATH1, DPATH2
C
      DIMENSION WORK1(LWORK1)
C
      COMMON /INTADR/ IWKAO, IWKSO, IWKHHS, IWK1HH, IWK1HC, IWKLST
      COMMON /INTINF/ THRESH,
     *                NHKTA,  NHKTB,  NHKTC,  NHKTD,
     *                MAXAB,  MAXCD,  JMAX0,
     *                KHKTA,  KHKTB,  KHKTC,  KHKTD,
     *                KHKTAB, KHKTCD, KHABCD,
     *                MHKTA,  MHKTB,  MHKTC,  MHKTD,
     *                MULA,   MULB,   MULC,   MULD,
     *                NORBA,  NORBB,  NORBC,  NORBD, NORBAB, NORBCD,
     *                NUCA,   NUCB,   NUCC,   NUCD,  NUCAB,  NUCCD,
     *                NSETA,  NSETB,  NSETC,  NSETD,
     *                ISTEPA, ISTEPB, ISTEPC, ISTEPD,
     *                NSTRA,  NSTRB,  NSTRC,  NSTRD,
     *                AEQB,   CEQD,
     *                DIAGAB, IAB0X,  IAB0Y,  IAB0Z,
     *                DIAGCD, ICD0X,  ICD0Y,  ICD0Z,
     *                DIACAC, ONECEN, PQSYM,  IPQ0X, IPQ0Y, IPQ0Z,
     *                TPRIAB, TPRICD, TCONAB, TCONCD,
     *                MAXDER, DTEST
      COMMON /CROSSD/ CROSS1, CROSS2
      COMMON /SUBDIR/ DC101, DC1H1, DC1E1, DC2H1, DC2E1,
     *                DC102, DC1H2, DC1E2, DC2H2, DC2E2,
     *                DPATH1, DPATH2, NTOTAL
      COMMON/SCD/SIGNCD
C
      DATA ONE,ONEM/1.D0,-1.D0/
C
C INITIALIZE NOINT
C
      NOINT = .FALSE.
C
C  SECTION ONE: HERMITIAN INTEGRALS 
C
      JMAX   = JMAX0 + MAXDER
      JMAXP  = JMAX + 1
      ISTEPT = NUCAB*NUCCD
      ISTEPU = ISTEPT*JMAXP
      ISTEPV = ISTEPU*JMAXP
      NRTUV  = ISTEPV*JMAXP
C
C  RJ(0,0,0) INTEGRALS
C
C     CORE MEMORY ALLOCATION
C
      IWKHHS = IWKAO + MAX(2*ISTEPV,NTOTAL)
      IWK1HH = IWKAO + NTOTAL
      IHHLST = IWK1HH + ISTEPT*(JMAX + 1)*(JMAX + 2)*(JMAX + 3)/6
      IWKMAX = MAX(IHHLST,IWKHHS + 2*NRTUV)
      IF (IWKMAX .GT. LWORK1) THEN
       WRITE (LUPRI, 1000) IWKMAX, LWORK1
       CALL ERREX
      END IF
C
      CALL R000(JMAX,NOINT,ISTEPT,ISTEPU,ISTEPV,NRTUV,NUCAB,NUCCD,
     *          THRESH,ONECEN,IPRINT) 
C
      IF (NOINT) RETURN
C
C  RJ(T,U,V) Integrals 
C
      CALL HERI(JMAX,ISTEPT,ISTEPU,ISTEPV,NRTUV,IPQ0X,IPQ0Y,IPQ0Z,
     *          IPRINT,WORK1,LWORK1)
C
C  SECTION TWO: CARTESIAN INTEGRALS
C
C  ZERO SPACE FOR FINAL CARTESIAN INTEGRALS
C
      CALL ZERO(WORK1(IWKAO + 1),NTOTAL)
C
C  EVALUATE INTEGRALS 
C
C  DIFFERENTIATION PATH 1
C
      IF (DPATH1) THEN
       IWKLST = IHHLST
       IPATH = 1
       SIGNCD=ONEM
       CALL G1DRIV(NHKTA,NHKTB,KHKTA,KHKTB,KHKTAB,MHKTA,MHKTB,
     &             NORBA,NORBB,NUCA,NUCB,NSETA,NSETB,
     &             MAXAB,MAXCD,NUCAB,NUCCD,NORBAB,JMAX,
     &             ISTEPA,ISTEPB,ISTEPT,ISTEPU,ISTEPV,
     &             TPRIAB,TCONAB,DIAGAB,PQSYM,IPQ0X,IPQ0Y,IPQ0Z,
     &             IAB0X,IAB0Y,IAB0Z,IPATH,MAXDER,DC101,DC1H1,DC1E1,
     &             CROSS1,SEGMEN,IPRINT,WORK1,LWORK1)
       CALL G2DRIV(NHKTC,NHKTD,KHKTC,KHKTD,KHKTAB,KHKTCD,KHABCD,
     &             MHKTC,MHKTD,NORBC,NORBD,NUCC,NUCD,NSETC,NSETD,
     &             NUCCD,NORBAB,NORBCD,ISTEPC,ISTEPD,
     &             TPRICD,TCONCD,DIAGCD,
     &             PQSYM,IPQ0X,IPQ0Y,IPQ0Z,ICD0X,ICD0Y,ICD0Z,
     &             IPATH,MAXDER,DC2H1,DC2E1,CROSS1,DTEST,SEGMEN,
     &             IPRINT,WORK1,LWORK1)
      END IF
C
C  DIFFERENTIATION PATH 2
C
      IF (DPATH2) THEN
       IWKLST = IHHLST
       IPATH = 2
       SIGNCD=ONE
       IF (DPATH1) CALL HERSWP(JMAX,NUCAB,NUCCD,IPRINT,WORK1,LWORK1)
       CALL G1DRIV(NHKTC,NHKTD,KHKTC,KHKTD,KHKTCD,MHKTC,MHKTD,
     *             NORBC,NORBD,NUCC,NUCD,NSETC,NSETD,
     *             MAXCD,MAXAB,NUCCD,NUCAB,NORBCD,JMAX,
     *             ISTEPC,ISTEPD,ISTEPT,ISTEPU,ISTEPV,
     *             TPRICD,TCONCD,DIAGCD,PQSYM,IPQ0X,IPQ0Y,IPQ0Z,
     *             ICD0X,ICD0Y,ICD0Z,IPATH,MAXDER,DC102,DC1H2,DC1E2,
     *             CROSS2,SEGMEN,IPRINT,WORK1,LWORK1)
       CALL G2DRIV(NHKTA,NHKTB,KHKTA,KHKTB,KHKTCD,KHKTAB,KHABCD,
     *             MHKTA,MHKTB,NORBA,NORBB,NUCA,NUCB,NSETA,NSETB,
     *             NUCAB,NORBCD,NORBAB,ISTEPA,ISTEPB,
     *             TPRIAB,TCONAB,DIAGAB,
     *             PQSYM,IPQ0X,IPQ0Y,IPQ0Z,IAB0X,IAB0Y,IAB0Z,
     *             IPATH,MAXDER,DC2H2,DC2E2,CROSS2,DTEST,SEGMEN,
     *             IPRINT,WORK1,LWORK1)
      END IF
      RETURN
 1000 FORMAT (//,1X,' WORK SPACE REQUIREMENT ',I9,' EXCEEDS ',
     *        ' CURRENT LIMIT ',I9,' OF WORK1.')
      END
