      SUBROUTINE GIINT4(NHKTA,NHKTB,KHKTA,KHKTB,ICENTA,LDIAG,
     &                  ISTEPA,ISTEPB,ISTEPU,ISTEPV,NAHGTF,
     &                  NATOMC,CORPX,CORPY,CORPZ,TWOG,EXPPI,
     &                  CHARGE,WORK1)
C
C THIS PROGRAM CALCULATES VARIOUS ONE-ELECTRON INTEGRALS
C WHICH ARE REQUIRED FOR THE EVALUATION OF MAGNETIC
C PROPERTIES USING GEERTSEN'S APPROACH TO CALCULATE
C THE DIAMAGNETIC CONTRIBUTION TO THE MAGNETABILIZITY
C TENSOR.
C
CEND
C
C  UNIVERSITY KARLSRUHE,    JULY/93  JG
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL LDIAG
      DIMENSION WORK1(1),CHARGE(1)
#include "lwork.par"
      PARAMETER (LWKRLM = LWORK3 - 8020)
#include "mxcent.par"
#include "baslims.par"
      PARAMETER (MXQN=8, MXAQN=MXQN*(MXQN+1)/2, MXAQNS=MXAQN*MXAQN
     &           *MXCONT*MXCONT)
      COMMON /CWORK3/ WK3LOW, SHGTF, AHGTF((MXCENT+1)*(2*MXQN+1)**3) 
CSSS                  RLMCOF(LWKRLM), WK3HGH
#include "odcs.com"
      COMMON /ADER/ ADER0 (MXAQNS)
      COMMON /POINTER/IS0000, IS000X, IS000Y, IS000Z,
     &                IS00XX, IS00XY, IS00XZ, IS00YY,
     &                IS00YZ, IS00ZZ, IT0000, IT000X, 
     &                IT000Y, IT000Z, IT00XX, IT00XY,
     &                IT00XZ, IT00YY, IT00YZ, IT00ZZ,
     &                IL0000, IL000X, IL000Y, IL000Z,
     &                IL00XX, IL00XY, IL00XZ, IL00YX,
     &                IL00YY, IL00YZ, IL00ZX, IL00ZY,
     &                IL00ZZ,
     &                IA0000, IA0X00, IA0Y00, IA0Z00,
     &                IAXX00, IAXY00, IAXZ00, IAYY00,
     &                IAYZ00, IAZZ00, IA000X, IA000Y,
     &                IA000Z, IA00XX, IA00XY, IA00XZ,
     &                IA00YY, IA00YZ, IA00ZZ, IA0X0X,
     &                IA0X0Y, IA0X0Z, IA0Y0X, IA0Y0Y,
     &                IA0Y0Z, IA0Z0X, IA0Z0Y, IA0Z0Z
      COMMON/SCRPOIN/ISCR1,ISCR2,ISCR3,ISCR4,ISCR5,ISCR6,ISCR7,
     &               ISCR8,ISCR9,ISCR10,ISCR11,ISCR12,ISCR13,ISCR14,
     &               ISCR15,ISCR16,ISCR17,ISCR18,ISCR19,ISCR20,
     &               JSCR1,JSCR2,JSCR3,JSCR4,JSCR5,JSCR6,
     &               JSCR7,JSCR8,JSCR9,JSCR10,JSCR11,JSCR12,JSCR13,
     &               JSCR14,JSCR15,JSCR16,JSCR17,JSCR18,JSCR19,
     &               JSCR20,JSCR21,JSCR22,JSCR23,JSCR24,JSCR25,JSCR26,
     &               JSCR27,JSCR28 
      COMMON /CENTC/ SIGNCX(MXCENT), SIGNCY(MXCENT), SIGNCZ(MXCENT),
     &               NCENTC(MXCENT), JSYMC(MXCENT),  JCENTC(MXCENT),
     &               ICXVEC(MXCENT), ICYVEC(MXCENT), ICZVEC(MXCENT)
      COMMON /LMNS/ LVALUA(MXAQN), MVALUA(MXAQN), NVALUA(MXAQN),
     &              LVALUB(MXAQN), MVALUB(MXAQN), NVALUB(MXAQN)
      COMMON/GORIGIN/GX,GY,GZ
      COMMON/GENCON/NRCA,NRCB,CONTA(MXCONT),CONTB(MXCONT)
C
      DATA THRSH /1.D-20/,ONEM/-1.D0/,HALFM/-0.5D0/,TWO/2.D0/
C
      DO 10 I=1,KHKTA*KHKTB
       ICOMPA=(I-1)/KHKTB+1
       ICOMPB=I-(ICOMPA-1)*KHKTB
       LVALA = LVALUA(ICOMPA)
       MVALA = MVALUA(ICOMPA)
       NVALA = NVALUA(ICOMPA)
       ISTRAT = 1 + ISTEPA*LVALA
       ISTRAU = 1 + ISTEPA*MVALA
       ISTRAV = 1 + ISTEPA*NVALA
       LVALB = LVALUB(ICOMPB)
       MVALB = MVALUB(ICOMPB)
       NVALB = NVALUB(ICOMPB)
       ISTRET = ISTRAT + ISTEPB*LVALB
       ISTREU = ISTRAU + ISTEPB*MVALB
       ISTREV = ISTRAV + ISTEPB*NVALB
C
C CALCULATE DERIVATIVES OF ONE-ELECTRON HAMILTON INTEGRALS
C WITH RESPECT TO AN EXTERNAL MAGNETIC FIELD
C
       SX0=SHGTF*ODC00X(ISTRET)
       SY0=SHGTF*ODC00Y(ISTREU)
       SZ0=SHGTF*ODC00Z(ISTREV)
       DX0=SHGTF*ODC00X(ISTRET+1)+(CORPX-GX)*SX0
       DY0=SHGTF*ODC00Y(ISTREU+1)+(CORPY-GY)*SY0
       DZ0=SHGTF*ODC00Z(ISTREV+1)+(CORPZ-GZ)*SZ0
       DX00=SHGTF*ODC00X(ISTRET+1)+CORPX*SX0
       DY00=SHGTF*ODC00Y(ISTREU+1)+CORPY*SY0
       DZ00=SHGTF*ODC00Z(ISTREV+1)+CORPZ*SZ0
       SX1=SHGTF*ODC10X(ISTRET)
       SY1=SHGTF*ODC10Y(ISTREU)
       SZ1=SHGTF*ODC10Z(ISTREV)
       DX1=SHGTF*ODC10X(ISTRET+1)+(CORPX-GX)*SX1
       DY1=SHGTF*ODC10Y(ISTREU+1)+(CORPY-GY)*SY1
       DZ1=SHGTF*ODC10Z(ISTREV+1)+(CORPZ-GZ)*SZ1
       QX0=TWO*SHGTF*ODC00X(ISTRET+2)+TWO*(CORPX-GX)*DX0
     &                               -((CORPX-GX)**2+HALFM*EXPPI)*SX0
       QY0=TWO*SHGTF*ODC00Y(ISTREU+2)+TWO*(CORPY-GY)*DY0
     &                               -((CORPY-GY)**2+HALFM*EXPPI)*SY0
       QZ0=TWO*SHGTF*ODC00Z(ISTREV+2)+TWO*(CORPZ-GZ)*DZ0
     &                               -((CORPZ-GZ)**2+HALFM*EXPPI)*SZ0
C
C ANGULAR MOMENTUM INTEGRALS
C
       WORK1(ISCR1+I)=SX0*(DY0*SZ1-SY1*DZ0)
       WORK1(ISCR2+I)=SY0*(DZ0*SX1-SZ1*DX0)
       WORK1(ISCR3+I)=SZ0*(DX0*SY1-SX1*DY0)
C
C MOMENTUM INTEGRALS
C
       WORK1(ISCR4+I)=-SX1*SY0*SZ0
       WORK1(ISCR5+I)=-SX0*SY1*SZ0
       WORK1(ISCR6+I)=-SX0*SY0*SZ1
C
C INTEGRALS (R-RG) x L(RG) + [(R-RG) X R ] X P
C
       WORK1(ISCR7+I)=SX1*(QY0*SZ0+SY0*QZ0)-DX0*(DY1*SZ0+SY0*DZ1)
     &               +DX0*GY*SY1*SZ0-DY1*SX0*GX*SZ0
     &               +DX0*GZ*SZ1*SY0-DZ1*SX0*GX*SY0
     &               +DX00*SY0*SZ0
       WORK1(ISCR8+I)=SY1*(QX0*SZ0+SX0*QZ0)-DY0*(DX1*SZ0+SX0*DZ1)
     &               +DY0*GZ*SZ1*SX0-DZ1*SY0*GY*SX0
     &               +DY0*GX*SX1*SZ0-DX1*SY0*GY*SZ0
     &               +DY00*SX0*SZ0
       WORK1(ISCR9+I)=SZ1*(QX0*SY0+SX0*QY0)-DZ0*(DX1*SY0+SX0*DY1)
     &               +DZ0*GX*SX1*SY0-DX1*SZ0*GZ*SY0
     &               +DZ0*GY*SY1*SX0-DY1*SZ0*GZ*SX0
     &               +DZ00*SX0*SY0
C
10    CONTINUE
C 
C NOW FORM CONTRACTIONS
C
      ISKIP=NRCA*NRCB
      IF(LDIAG) ISKIP=NRCA*(NRCA+1)/2
      IOFF=0
      DO 15 IRCA=1,NRCA
       MAXB=NRCB
       IF(LDIAG) MAXB=IRCA
       DO 15 IRCB=1,MAXB
        IOFF=IOFF+1
        CONT=CONTA(IRCA)*CONTB(IRCB)
        IF(ABS(CONT).GT.THRSH) THEN
         CALL SAXPY(KHKTA*KHKTB*9,CONT,WORK1(ISCR1+1),1,
     &              WORK1(IL000X+IOFF),ISKIP)
        ENDIF
15    CONTINUE
C
      MAXADD=2
      INT20=-NATOMC
      DO 100 I=1,KHKTA*KHKTB
       ICOMPA=(I-1)/KHKTB+1
       ICOMPB=I-(ICOMPA-1)*KHKTB
       LVALA = LVALUA(ICOMPA)
       MVALA = MVALUA(ICOMPA)
       NVALA = NVALUA(ICOMPA)
       ISTRAT = 1 + ISTEPA*LVALA
       ISTRAU = 1 + ISTEPA*MVALA
       ISTRAV = 1 + ISTEPA*NVALA
       LVALB = LVALUB(ICOMPB)
       MVALB = MVALUB(ICOMPB)
       NVALB = NVALUB(ICOMPB)
       ISTRET = ISTRAT + ISTEPB*LVALB
       ISTREU = ISTRAU + ISTEPB*MVALB
       ISTREV = ISTRAV + ISTEPB*NVALB
       MAXT = LVALA + LVALB+MAXADD
       MAXU = MVALA + MVALB+MAXADD
       MAXV = NVALA + NVALB+MAXADD
       INT20 = INT20 + NATOMC
       IADRAV = 1
       DO 200 IV = 0,MAXV
        IADREV=ISTREV+IV
        EV = ODC00Z(IADREV)
        FV = ODC10Z(IADREV)
        IF(IV.EQ.0) THEN
         RV=ODC00Z(IADREV+1)
        ELSE
         RV=(IV+1)*ODC00Z(IADREV+1)+TWOG*ODC00Z(IADREV-1)
        ENDIF
        RV=RV+(CORPZ-GZ)*ODC00Z(IADREV)
        IADRAU = IADRAV
        DO 300 IU = 0,MAXU
         IADREU=ISTREU+IU
         EU = ODC00Y(IADREU)
         FU = ODC10Y(IADREU)
         IF(IU.EQ.0) THEN
          RU=ODC00Y(IADREU+1)
         ELSE
          RU=(IU+1)*ODC00Y(IADREU+1)+TWOG*ODC00Y(IADREU-1)
         ENDIF  
         RU=RU+(CORPY-GY)*ODC00Y(IADREU)
         EE = EU*EV
         FE = FU*EV
         EF = EU*FV
         RE = RU*EV
         ER = EU*RV
         DO 400 IT = 0,MAXT
          IADRET=ISTRET+IT
          ET = ODC00X(IADRET)
          FT = ODC10X(IADRET)
          IF(IT.EQ.0) THEN
           RT=ODC00X(IADRET+1)
          ELSE
           RT=(IT+1)*ODC00X(IADRET+1)+TWOG*ODC00X(IADRET-1)
          ENDIF 
          RT=RT+(CORPX-GX)*ODC00X(IADRET)
C
          EEE = ET*EE
          FEE = FT*EE
          EFE = ET*FE
          EEF = ET*EF
          REE = RT*EE
          ERE = ET*RE
          EER = ET*ER
          
          IADR00 = IADRAU + IT
          IADR0T = IADR00 + 1
          IADR0U = IADR00 + ISTEPU
          IADR0V = IADR00 + ISTEPV
          IADD = - NAHGTF
C
C   LOOP OVER NUCLEI 
C
*VOCL LOOP,NOVREC
CDIR$ IVDEP
          DO 500 IATOM = 1,NATOMC
           IADD = IADD + NAHGTF
           FACT=ONEM/CHARGE(JCENTC(IATOM))
           INT2 = INT20 + IATOM
           AH00 = AHGTF(IADR00 + IADD)
           AH0T = AHGTF(IADR0T + IADD)
           AH0U = AHGTF(IADR0U + IADD)
           AH0V = AHGTF(IADR0V + IADD)
C
           WORK1(JSCR1+INT2)=WORK1(JSCR1+INT2)+FACT*
     &                       (AH0U*EEF-AH0V*EFE)
           WORK1(JSCR2+INT2)=WORK1(JSCR2+INT2)+FACT*
     &                       (AH0V*FEE-AH0T*EEF)
           WORK1(JSCR3+INT2)=WORK1(JSCR3+INT2)+FACT*
     &                       (AH0T*EFE-AH0U*FEE)
C
           WORK1(JSCR4+INT2)=WORK1(JSCR4+INT2)-FACT*
     &                      (AH0U*ERE+AH0V*EER)
           WORK1(JSCR5+INT2)=WORK1(JSCR5+INT2)+FACT*
     &                      AH0T*ERE
           WORK1(JSCR6+INT2)=WORK1(JSCR6+INT2)+FACT*
     &                      AH0T*EER
           WORK1(JSCR7+INT2)=WORK1(JSCR7+INT2)+FACT*
     &                      AH0U*REE
           WORK1(JSCR8+INT2)=WORK1(JSCR8+INT2)-FACT*
     &                      (AH0T*REE+AH0V*EER)
           WORK1(JSCR9+INT2)=WORK1(JSCR9+INT2)+FACT*
     &                      AH0U*EER
           WORK1(JSCR10+INT2)=WORK1(JSCR10+INT2)+FACT*
     &                      AH0V*REE
           WORK1(JSCR11+INT2)=WORK1(JSCR11+INT2)+FACT*
     &                      AH0V*ERE
           WORK1(JSCR12+INT2)=WORK1(JSCR12+INT2)-FACT*
     &                      (AH0U*ERE+AH0T*REE)
  500             CONTINUE
  400          CONTINUE
               IADRAU = IADRAU + ISTEPU
  300       CONTINUE
            IADRAV = IADRAV + ISTEPV
  200    CONTINUE
  100 CONTINUE
C
C  NOW FORM THE CONTRACTIONS
C
      ISKIP=NRCA*NRCB*NATOMC
      IF(LDIAG) ISKIP=NRCA*(NRCA+1)/2*NATOMC
      IOFF=0
      DO 700 IRCA=1,NRCA
       MAXB=NRCB
       IF(LDIAG) MAXB=IRCA
       DO 700 IRCB=1,MAXB
        CONT=CONTA(IRCA)*CONTB(IRCB)
        IF(ABS(CONT).GT.THRSH) THEN
         DO 710 IATOM=1,NATOMC
          CALL SAXPY(KHKTA*KHKTB*3,CONT,WORK1(JSCR1+IATOM),NATOMC,
     &               WORK1(IA000X+IOFF+IATOM),ISKIP)
          CALL SAXPY(KHKTA*KHKTB*9,CONT,WORK1(JSCR4+IATOM),NATOMC,
     &               WORK1(IA0X0X+IOFF+IATOM),ISKIP)
710      CONTINUE
        ENDIF
        IOFF=IOFF+NATOMC
700   CONTINUE

      RETURN
      END
