      SUBROUTINE DOOAA_MP2(T2AMPS,T2,L2,DOO,FACT,ISPIN,POP,VRT,
     &                     DISSYT,NUMSYT,NOCC2SQ,T2LEN,IRREP,
     &                     IUHF)

      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DOUBLE PRECISION L2 
      INTEGER DISSYT,DIRPRD,POP,VRT,POP1,VRT1
      INTEGER T2LEN

      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),DIRPRD(8,8)
      COMMON/SYM/POP1(8,2),VRT1(8,2),NTAA,NTBB,NF1(2),NF2(2)
C
      DIMENSION T2(DISSYT,NOCC2SQ),POP(8),VRT(8) 
      DIMENSION L2(DISSYT,NOCC2SQ)
      DIMENSION T2AMPS(T2LEN)
      DIMENSION DOO(NF1(1)+IUHF*NF1(2))
C
      DATA ONEM,ONE,HALF,TWO /-1.0D0,1.D0,0.5D0,2.0D0/
C
      CALL DCOPY(DISSYT*NUMSYT,T2AMPS,1,T2,1)
      CALL DCOPY(DISSYT*NUMSYT,T2AMPS,1,L2,1)

      CALL SYMEXP(IRREP,POP,DISSYT,T2)
      CALL SYMEXP(IRREP,POP,DISSYT,L2)
C
      JOFF=1
      IOFF=1

      DO IRREPJ=1,NIRREP
C          
C GET OCCUPATION NUMBER FOR JRREP     
C
       NOCCJ=POP(IRREPJ)
C
C DETERMINE IRREPI WHOSE DIRECT PRODUCT WITH JRREP GIVES IRREP
C
       IRREPI=DIRPRD(IRREP,IRREPJ)
C
C GET OCCUPATION NUMBER FOR IRREPI
C
       NOCCI=POP(IRREPI)
C
C IF NOCCI OR NOCCJ EQUAL ZERO, NOTHING TO COMPUTE
C
       IF(MIN(NOCCJ,NOCCI).NE.0) THEN
C
        CALL XGEMM('T','N',NOCCJ,NOCCJ,DISSYT*NOCCI,FACT,
     &              T2(1,JOFF),NOCCI*DISSYT,L2(1,JOFF),      
     &              NOCCI*DISSYT,ONE,DOO(IOFF),NOCCJ)
C
       ENDIF
C
       JOFF=JOFF+NOCCJ*NOCCI
       IOFF=IOFF+NOCCJ*NOCCJ

      ENDDO
C
      RETURN
      END
