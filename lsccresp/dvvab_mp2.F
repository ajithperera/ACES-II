      SUBROUTINE DVVAB_MP2(T2AMPS,T2,L2,DVV,FACT,ISPIN,POP1,POP2,
     &                     VRT1,VRT2,DISSYT,NUMSYT,T2LEN,IRREP,
     &                     TMP,IUHF)

      IMPLICIT DOUBLE PRECISION (A-H,O-Z)

      DOUBLE PRECISION L2
      INTEGER DISSYT,DIRPRD,POP1,POP2,VRT1,VRT2
      INTEGER T2LEN

      COMMON /MACHSP/IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),DIRPRD(8,8)
      COMMON/SYM/POP(8,2),VRT(8,2),NTAA,NTBB,NF1(2),NF2(2)

      DIMENSION T2(NUMSYT,DISSYT),L2(NUMSYT,DISSYT),TMP(1)
      DIMENSION T2AMPS(T2LEN)
      DIMENSION DVV(NF2(1)+IUHF*NF2(2))
      DIMENSION POP1(8),POP2(8),VRT1(8),VRT2(8)
C
      DATA ONE,ONEM,TWO/1.0D0,-1.D0,2.D0/
C
       CALL DCOPY(NUMSYT*DISSYT,T2AMPS,1,T2,1)
       CALL TRANSP(T2,L2,NUMSYT,DISSYT)
       CALL DCOPY(NUMSYT*DISSYT,L2,1,T2,1)
C
C SPIN ADAPTED CODE FOR RHF
C
       IF(IUHF.EQ.0) THEN
        CALL SPINAD3(IRREP,POP1,NUMSYT,DISSYT,T2,TMP,
     &               TMP(1+DISSYT))
       ENDIF
C
C  TRANSPOSE THE LAST TWO INDICES IN THE AA CASE
C
C  HOWEVER, IN RHF TAKE ADVANTAGE IF THE SPIN SYMMETRY
C
      IF(IUHF.EQ.1.AND.ISPIN.EQ.1) THEN
        CALL SYMTR1(IRREP,VRT1,VRT2,NUMSYT,T2,TMP,TMP(1+NUMSYT),
     &              TMP(1+2*NUMSYT))
        CALL SYMTR1(IRREP,VRT1,VRT2,NUMSYT,L2,TMP,TMP(1+NUMSYT),
     &              TMP(1+2*NUMSYT))
      ENDIF
C
C  PERFORM MULTIPLICATION
C
C  JOFF OFFSET IN THE VIRTUAL-VIRTUAL BLOCK OF T1 AND T2
C  IOFF OFFSET IN DVV
C
      IOFF=1
      JOFF=1
      DO 90 IRREPI=1,NIRREP
C          
       NVRTI=VRT1(IRREPI)
       IRREPJ=DIRPRD(IRREP,IRREPI)
       NVRTJ=VRT2(IRREPJ)

       IF(MIN(NVRTJ,NVRTI).NE.0) THEN
C
          CALL XGEMM('T','N',NVRTI,NVRTI,NUMSYT*NVRTJ,FACT,
     &               T2(1,JOFF),NVRTJ*NUMSYT,L2(1,JOFF),
     &               NVRTJ*NUMSYT,ONE,DVV(IOFF),NVRTI)
       ENDIF

       JOFF=JOFF+NVRTJ*NVRTI
       IOFF=IOFF+NVRTI*NVRTI
90    CONTINUE
C
      RETURN
      END
