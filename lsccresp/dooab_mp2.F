      SUBROUTINE DOOAB_MP2(T2AMPS,T2,L2,DOO,FACT,ISPIN,POP1,POP2,
     &                     VRT1,VRT2,DISSYT,NUMSYT,T2LEN,IRREP,
     &                     TMP,IUHF)

      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DOUBLE PRECISION L2
      INTEGER DISSYT,DIRPRD,POP1,POP2,VRT1,VRT2,POP,VRT
      INTEGER T2LEN

      COMMON/MACHSP/IINTLN,IFLTLN,IINTFP,ILONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),DIRPRD(8,8)
      COMMON/SYM/POP(8,2),VRT(8,2),NTAA,NTBB,NF1(2),NF2(2)
C
      DIMENSION T2(DISSYT,NUMSYT),TMP(1)
      DIMENSION L2(DISSYT,NUMSYT)
      DIMENSION T2AMPS(T2LEN)
      DIMENSION POP1(8),POP2(8),VRT1(8),VRT2(8) 
      DIMENSION DOO(NF1(1)+IUHF*NF1(2))
C
      DATA ONE /1.0D0/,HALF /0.5D0/,ONEM /-1.D0/, TWO /2.D0/
C
C SPIN ADAPTED CODE FOR RHF
C
      CALL DCOPY(DISSYT*NUMSYT,T2AMPS,1,T2,1)
      CALL DCOPY(DISSYT*NUMSYT,T2AMPS,1,L2,1)

      IF(IUHF.EQ.0) THEN
       CALL SPINAD1(IRREP,POP1,DISSYT,L2,TMP,TMP(1+DISSYT))
      ENDIF

C TRANSPOSE THE LAST TWO INDICES IN THE AA CASE
C HOWEVER, IN RHF TAKE ADVANTAGE IF THE SPIN SYMMETRY
C
      IF(IUHF.EQ.1.AND.ISPIN.EQ.1) THEN 
       CALL SYMTR1(IRREP,POP1,POP2,DISSYT,T2,TMP,TMP(1+DISSYT),
     &             TMP(1+2*DISSYT))
       CALL SYMTR1(IRREP,POP1,POP2,DISSYT,L2,TMP,TMP(1+DISSYT),
     &              TMP(1+2*DISSYT))
      ENDIF
C
      JOFF=1
      IOFF=1

      DO  IRREPI=1,NIRREP
C          
       NOCCI=POP1(IRREPI)
       IRREPJ=DIRPRD(IRREP,IRREPI)
       NOCCJ=POP2(IRREPJ)

       IF(MIN(NOCCI,NOCCJ).NE.0) THEN
C
        CALL XGEMM('T','N',NOCCI,NOCCI,DISSYT*NOCCJ,FACT,
     &             T2(1,JOFF),NOCCJ*DISSYT,L2(1,JOFF),      
     &             NOCCJ*DISSYT,ONE,DOO(IOFF),NOCCI)
C
       ENDIF

       JOFF=JOFF+NOCCJ*NOCCI
       IOFF=IOFF+NOCCI*NOCCI

      ENDDO
C
      RETURN
      END
