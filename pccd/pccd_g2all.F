      SUBROUTINE PCCD_G2ALL(T1,T2,G,MAXSIZE,FACT,ISPIN,DISSYT,NUMSYT,
     &                      LISTT1,LISTT2,LISTG,IRREP)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DISSYT,DISTMP,DISLEFT,DIRPRD,POP,VRT
      DIMENSION T1(DISSYT,NUMSYT),T2(DISSYT,NUMSYT),G(DISSYT,1)
      COMMON/SYM/POP(8,2),VRT(8,2),NTAA,NTBB,NF1AA,NF1BB,NF2AA,NF2BB
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),DIRPRD(8,8)
C
      DATA AZERO,ONE,ONEM,TWO /0.0D0,1.D0,-1.D0,2.D0/
C
C PICK UP FIRST THE APPROBIATE T AMPLITUDES
C
      CALL GETLST(T1,1,NUMSYT,2,IRREP,LISTT1)

      CALL GETLST(T2,1,NUMSYT,1,IRREP,LISTT2)
C
C CHECK IF WE CAN DO IT IN CORE OR NOT
C
      IF(DISSYT.NE.0) THEN
       MAXDIS=MAXSIZE/DISSYT
      ELSE
       MAXDIS=DISSYT
      ENDIF
C
      IF(MAXDIS.GE.DISSYT) THEN
C
C EVERYTHING FITS INTO CORE 
C
C PERFORM THE MULTIPLICATION 
C 
       FACT2=AZERO

       CALL XGEMM('N','T',DISSYT,DISSYT,NUMSYT,FACT,T1,DISSYT,
     &            T2,DISSYT,FACT2,G,DISSYT)
C
C     SAVE THE RESULT ON FILE
C
C SYMMETRIZE THE CALCULATED GAMMA INTERMEDIATE (NOT NECCESARY WHEN
C THE TWO LISTS ARE ACTUALLY THE SAME (ISAME = TRUE)
C
      CALL SYMMET2(G,DISSYT)
C
      CALL PUTLST(G,1,DISSYT,1,IRREP,LISTG)
C
      ELSE
C
C OUT CORE ALGORITHM
C
      DISLEFT=DISSYT
      IOFFSET=1
10    CONTINUE
      DISTMP=MIN(MAXDIS,DISLEFT)
      DISLEFT=DISLEFT-DISTMP
      FACT2=AZERO
C
      CALL XGEMM('N','T',DISSYT,DISTMP,NUMSYT,FACT,T1,DISSYT,
     &           T2(IOFFSET,1),DISSYT,FACT2,G,DISSYT)
C
C SAVE THE RESULT ON FILE
C
      CALL PUTLST(G,IOFFSET,DISTMP,1,IRREP,LISTG)
      IOFFSET=IOFFSET+DISTMP
      IF(DISLEFT.NE.0) GO TO 10
C
C SYMMETRIZE THE CALCULATED GAMMA INTERMEDIATE ( NOT NECCESSARY
C WHEN THE TWO LISTS ARE ACTUALLY THE SAME (ISAME = TRUE)
C
       LENGTH=2*DISSYT*NUMSYT+MAXSIZE
       CALL SYMMET3(IRREP,LISTG,DISSYT,T1,LENGTH)
C
      ENDIF
      RETURN
      END
