      SUBROUTINE PCCD_DWMBEJ(ICORE,MAXCOR,SPCASE,IUHF,C1,C2,C3,C4)

      IMPLICIT INTEGER (A-Z)
      DOUBLE PRECISION C1,C2,C3,C4,X,TWO,ONE,ONEM,HALF,ZILCH,TWOM
      CHARACTER*4 SPCASE,TAUTYP1,TAUTYP2
      DIMENSION ICORE(MAXCOR),IOFFT1(8,2)
      COMMON /INFO/ NOCA,NOCB,NVRTA,NVRTB
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &                DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),NTOT(18)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT1AA,NT1BB,NF1AA,NF1BB,NF2AA,
     &             NF2BB

      DATA ZILCH /0.0/
      DATA ONE /1.0/
      DATA HALF/0.5/
      DATA TWO /2.0/
      DATA TWOM /-2.0/
      DATA ONEM/-1.0/
      NNP1O2(I)=I*(I+1)/2
      X=1.0

      IF(C3.NE.0.0)CALL GETT1(ICORE,MAXCOR,MXCOR,IUHF,IOFFT1)

C DO SPIN CASES ABAB AND BABA:
C
C       W(MbEj)=<Mb|Ej>-(1/2)*SUM [T(jn,fb)<Mn|Ef>-T(jN,bF)<MN||EF>]
C
C       W(mBeJ)=<mB|eJ>-(1/2)*SUM [T(JN,FB)<mN|eF>-T(Jn,Bf)<mn||ef>]
C    
      IF(SPCASE.EQ.'ABAB'.OR.SPCASE.EQ.'BABA')THEN
       IF(IUHF.NE.0)THEN
       IF(SPCASE.EQ.'ABAB')THEN
        LSTINT=18
        LISTQ=56
        LISTQ1=57
        IF(IUHF.EQ.0)THEN
         LISTTA=34
        ELSE
         LISTTA=35
        ENDIF
        LISTTB=37
        LISTWA=18
        LISTWB=19
        TAUTYP1='BBBB'
       ELSEIF(SPCASE.EQ.'BABA')THEN 
        LSTINT=17
        LISTQ=57
        LISTQ1=56
        LISTTA=34
        LISTWA=17
        LISTTB=36
        LISTWB=20
        TAUTYP1='AAAA'
       ENDIF
       DO 200 IRREP=1,NIRREP 
        DSSYWA=IRPDPD(IRREP,ISYTYP(1,LISTWA))
        DSSYTA=IRPDPD(IRREP,ISYTYP(1,LISTTA))
        NMSYWA=IRPDPD(IRREP,ISYTYP(2,LISTWA))
        NMSYTA=IRPDPD(IRREP,ISYTYP(2,LISTTA))
        DSSYWB=IRPDPD(IRREP,ISYTYP(1,LISTWB))
        DSSYTB=IRPDPD(IRREP,ISYTYP(1,LISTTB))
        NMSYWB=IRPDPD(IRREP,ISYTYP(2,LISTWB))
        NMSYTB=IRPDPD(IRREP,ISYTYP(2,LISTTB))
        DISSYQ=IRPDPD(IRREP,ISYTYP(1,LISTQ))
        NUMSYQ=IRPDPD(IRREP,ISYTYP(2,LISTQ))
        I000=1
        I010=I000+IINTFP*DISSYQ*NUMSYQ
        I020=I010+IINTFP*NMSYWA*DSSYWA
        I030=I020+IINTFP*NMSYTA*DSSYTA
        CALL IZERO(ICORE(I000),DISSYQ*NUMSYQ*IINTFP)
        NSIZE=DISSYQ*NUMSYQ
        IF(C1.NE.0.0)THEN
         CALL GETLST(ICORE(I000),1,NUMSYQ,1,IRREP,LSTINT)
        ENDIF
        IF(C4.NE.0.0)THEN
         CALL GETLST(ICORE(I020),1,NMSYTA,1,IRREP,LISTTA)
         IF(C3.NE.0.0)THEN
          CALL GETARGS(LISTTA,I1,I2)
          CALL F2TAU(ICORE(I020),ICORE(IOFFT1(1,I1)),
     &                ICORE(IOFFT1(1,I2)),DSSYTA,NMSYTA,
     &                POP(1,I1),POP(1,I2),VRT(1,I1),VRT(1,I2),
     &                IRREP,TWO,TAUTYP1)
         ENDIF
         CALL CNTRCT(ICORE(I000),ICORE(I010),ICORE(I020),
     &               DISSYQ,NUMSYQ,DSSYTA,NMSYTA,DSSYWA,NMSYWA,
     &               IRREP,LISTWA,LISTWA,LISTTA,'WxT',X,C4,0) 
         I020=I010+IINTFP*NMSYWB*DSSYWB
         I030=I020+IINTFP*NMSYTB*DSSYTB
         I030=MAX(I030,I010+DISSYQ*NUMSYQ*IINTFP)
         CALL GETLST(ICORE(I020),1,NMSYTB,1,IRREP,LISTTB)
         CALL CNTRCT(ICORE(I000),ICORE(I010),ICORE(I020),
     &               DISSYQ,NUMSYQ,DSSYTB,NMSYTB,DSSYWB,NMSYWB,
     &               IRREP,LISTWB,LISTWB,LISTTB,'WxT',X,-C4,0)
        ENDIF
        CALL SUMSYM2(ICORE(I000),ICORE(I010),NSIZE,1,IRREP,LISTQ)
200    CONTINUE
C
C SPIN ADAPTED CODE FOR RHF "ABAB" SPIN CASE
C
       ELSE

       LSTINT=18
       LSTINTB=25
       LISTQ=56
       LISTTA=37
       LISTTB=39
       LISTWA=18
       LISTWB=21
       TAUTYP1='ABAB'
       DO 201 IRREP=1,NIRREP 
        DSSYWA=IRPDPD(IRREP,ISYTYP(1,LISTWA))
        DSSYTA=IRPDPD(IRREP,ISYTYP(1,LISTTA))
        NMSYWA=IRPDPD(IRREP,ISYTYP(2,LISTWA))
        NMSYTA=IRPDPD(IRREP,ISYTYP(2,LISTTA))
        DSSYWB=IRPDPD(IRREP,ISYTYP(1,LISTWB))
        DSSYTB=IRPDPD(IRREP,ISYTYP(1,LISTTB))
        NMSYWB=IRPDPD(IRREP,ISYTYP(2,LISTWB))
        NMSYTB=IRPDPD(IRREP,ISYTYP(2,LISTTB))
        DISSYQ=IRPDPD(IRREP,ISYTYP(1,LISTQ))
        NUMSYQ=IRPDPD(IRREP,ISYTYP(2,LISTQ))
        I000=1
        I010=I000+IINTFP*DISSYQ*NUMSYQ
        I020=I010+IINTFP*NMSYWA*DSSYWA
        I030=I020+IINTFP*NMSYTA*DSSYTA
        IF(C4.NE.0.0)THEN
C
C SPIN ADAPT THE T AMPLITUDES
C
         CALL GETLST(ICORE(I020),1,NMSYTA,1,IRREP,LISTTA)
         CALL GETLST(ICORE(I010),1,NMSYTA,1,IRREP,LISTTB)
         IF(C3.NE.0.0)THEN
          CALL GETARGS(LISTTA,I1,I2)
          CALL F2TAU(ICORE(I010),ICORE(IOFFT1(1,I1)),
     &                ICORE(IOFFT1(1,I2)),DSSYTA,NMSYTA,
     &                POP(1,I1),POP(1,I2),VRT(1,I1),VRT(1,I2),
     &                IRREP,TWO,TAUTYP1)
         ENDIF
         CALL SSCAL (NMSYTA*NMSYTA,TWO,ICORE(I020),1)
         CALL SAXPY (NMSYTA*NMSYTA,ONEM,ICORE(I010),1,ICORE(I020),1)
         CALL CNTRCT(ICORE(I000),ICORE(I010),ICORE(I020),
     &                DISSYQ,NUMSYQ,DSSYTA,NMSYTA,DSSYWA,NMSYWA,
     &                IRREP,LISTWA,LISTWB,LISTTA,'WxT',ZILCH,-C4,1)
        ENDIF
        NSIZE=DISSYQ*NUMSYQ
C
C NOW INCLUDE THE SPIN-ADAPTED INTEGRALS
C
        IF(C1.NE.0.0)THEN
         CALL GETLST(ICORE(I010),1,NUMSYQ,1,IRREP,LSTINT)
         CALL GETLST(ICORE(I020),1,NUMSYQ,1,IRREP,LSTINTB)
         CALL SSCAL (DISSYQ*DISSYQ,TWO,ICORE(I010),1)
         CALL SAXPY (DISSYQ*DISSYQ,ONEM,ICORE(I020),1,ICORE(I010),1) 
         IF(C4.NE.0.0)THEN
          CALL SAXPY (DISSYQ*DISSYQ,ONE,ICORE(I010),1,ICORE(I000),1)
         ELSE
          CALL DCOPY(DISSYQ*DISSYQ,ICORE(I010),1,ICORE(I000),1)
         ENDIF
        ENDIF
        CALL SUMSYM2(ICORE(I000),ICORE(I010),NSIZE,1,IRREP,LISTQ)
201    CONTINUE
       ENDIF
C
      ELSEIF(SPCASE.EQ.'ABBA'.OR.SPCASE.EQ.'BAAB')THEN
C
C DO SPIN CASES ABBA AND BAAB:
C
C       W(MbeJ) = -<Mb|Je>+(1/2)*SUM [T(Jn,Fb)<Mn|Fe>]
C
C       W(mBEj) = -<mB|jE>+(1/2)*SUM [T(jN,fB)<mN|fE>]
C
       IF(SPCASE.EQ.'ABBA')THEN
        LISTQ=58
        LSTINT=25
        LISTTA=38
        IF(IUHF.EQ.0)LISTTA=39
        LISTWA=21
        TAUTYP1='ABAB'
       ELSEIF(SPCASE.EQ.'BAAB')THEN
        LISTQ=59
        LSTINT=26
        LISTTA=39
        LISTWA=22
        TAUTYP1='BABA'
       ENDIF
       DO 300 IRREP=1,NIRREP 
        DSSYWA=IRPDPD(IRREP,ISYTYP(1,LISTWA))
        DSSYTA=IRPDPD(IRREP,ISYTYP(1,LISTTA))
        NMSYWA=IRPDPD(IRREP,ISYTYP(2,LISTWA))
        NMSYTA=IRPDPD(IRREP,ISYTYP(2,LISTTA))
        DISSYQ=IRPDPD(IRREP,ISYTYP(1,LISTQ))
        NUMSYQ=IRPDPD(IRREP,ISYTYP(2,LISTQ))
        I000=1
        I010=I000+IINTFP*DISSYQ*NUMSYQ
        I020=I010+IINTFP*NMSYWA*DSSYWA
        I030=I020+IINTFP*NMSYTA*DSSYTA
        I030=MAX(I030,I010+DISSYQ*NUMSYQ*IINTFP)
        CALL IZERO(ICORE(I000),DISSYQ*NUMSYQ*IINTFP)
        NSIZE=DISSYQ*NUMSYQ
C
        IF(C1.NE.0.0)THEN
         CALL GETLST(ICORE(I000),1,NUMSYQ,2,IRREP,LSTINT)
        ENDIF
        IF(C4.NE.0.0)THEN
         CALL GETLST(ICORE(I020),1,NMSYTA,1,IRREP,LISTTA)
         IF(C3.NE.0.0)THEN
          CALL GETARGS(LISTTA,I1,I2)
          CALL F2TAU(ICORE(I020),ICORE(IOFFT1(1,I1)),
     &                ICORE(IOFFT1(1,I2)),DSSYTA,NMSYTA,
     &                POP(1,I1),POP(1,I2),VRT(1,I1),VRT(1,I2),
     &                IRREP,TWO,TAUTYP1)
         ENDIF
         CALL CNTRCT(ICORE(I000),ICORE(I010),ICORE(I020),
     &               DISSYQ,NUMSYQ,DSSYTA,NMSYTA,DSSYWA,NMSYWA,
     &               IRREP,LISTWA,LISTWA,LISTTA,'WxT',X,C4,0)
        ENDIF
        CALL SUMSYM2(ICORE(I000),ICORE(I010),NSIZE,1,IRREP,LISTQ)
300    CONTINUE
      ENDIF
      RETURN
      END
