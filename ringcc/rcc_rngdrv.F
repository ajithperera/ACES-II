      SUBROUTINE RCC_RNGDRV(ICORE,MAXCOR,LISTW,LISTT,CALMOD,MAXSIZ,
     &                      INCREM,LSTSCR)
      IMPLICIT INTEGER (A-Z)
      LOGICAL SPINAD
      CHARACTER*3 CALMOD
      DIMENSION ICORE(MAXCOR)
      LOGICAL MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD,UCC,
     &            CC2,RCCD,DRCCD
      COMMON /FLAGS/ IFLAGS(100)
      COMMON/METH/MBPT2,MBPT3,M4DQ,M4SDQ,M4SDTQ,CCD,QCISD,CCSD,UCC,
     &            CC2,RCCD,DRCCD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPA(255),IRREPB(255),
     &DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
C Set the spinad to false for both rCCD and drCCD.

      SPINAD=.FALSE.

C LOOP OVER IRREPS.
C
       DO 100 IRREP=1,NIRREP
        DISSYW=IRPDPD(IRREP,ISYTYP(1,LISTW))
        DISSYT=IRPDPD(IRREP,ISYTYP(1,LISTT))
        NUMSYW=IRPDPD(IRREP,ISYTYP(2,LISTW))
        NUMSYT=IRPDPD(IRREP,ISYTYP(2,LISTT))
        DISSYQ=DISSYT
        NUMSYQ=NUMSYW
        if (NUMSYQ*DISSYQ.LT.0) call trap_intovf('RNGDRV',1)
        MAXSIZ=MAX(NUMSYQ*DISSYQ,MAXSIZ)
        I000=1
        I010=I000+IINTFP*NUMSYQ*DISSYQ
        I020=I010+IINTFP*NUMSYT*DISSYT
        I030=I020+IINTFP*DISSYW*NUMSYW
C
C CAN WE DO IT IN CORE?
C
        if (I030.LT.0) call trap_intovf('RNGDRV',2)
        IF(I030.LT.MAXCOR)THEN
C
C FORM THE SYMMETRY PACKED Q VECTOR.
C
         CALL RCC_RNGPRD(ICORE(I020),ICORE(I010),ICORE(I000),
     &                   ICORE(I030),MAXCOR-I030, 
     &                   DISSYW,NUMSYW,DISSYT,NUMSYT,DISSYQ,NUMSYQ,
     &                   LISTW,LISTT,IRREP,CALMOD,SPINAD)
        ELSE
         CALL INSMEM('RNGDRV',I090,MAXCOR)
        ENDIF
        IF(CALMOD.EQ.'WxT')THEN
         CALL TRANSP(ICORE(I000),ICORE(I010),NUMSYQ,DISSYQ)
         CALL DCOPY(NUMSYQ*DISSYQ,ICORE(I010),1,ICORE(I000),1)
         ITMP=NUMSYQ
         NUMSYQ=DISSYQ
         DISSYQ=ITMP
        ENDIF
C
C AUGMENT Q(ABAB) AND DUMP IT TO DISK.
C
        IF(INCREM.EQ.1)THEN
         CALL GETLST(ICORE(I020),1,NUMSYQ,1,IRREP,LSTSCR)
         CALL VADD(ICORE(I020),ICORE(I020),ICORE(I000),
     &             DISSYQ*NUMSYQ,1.D0)
         CALL MPMT(ICORE(I020),NUMSYQ)
         CALL PUTLST(ICORE(I020),1,NUMSYQ,1,IRREP,LSTSCR)
         CALL DCOPY(DISSYQ*NUMSYQ,ICORE(I020),1,ICORE(I000),1)
        ELSE
         CALL MPMT(ICORE(I000),NUMSYQ)
         CALL PUTLST(ICORE(I000),1,NUMSYQ,1,IRREP,LSTSCR)
        ENDIF
100    CONTINUE
       RETURN
       END
