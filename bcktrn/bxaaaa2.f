      SUBROUTINE BXAAAA2(EVEC,BUF,BUF1,BUFTMP,LENBUF,LENBUF1,
     &                   LENTMP,IUHF,SCR,MAXSIZE,INCORE,ALASKA)
C
C THIS ROUTINE TRANSFORMS ELEMENTS OF THE MO TWO-PARTICLE DENSITY
C  MATRIX TO THE ATOMIC ORBITAL BASIS.  THIS ROUTINE DEALS SPECIFICALLY
C  WITH THOSE ELEMENTS OF SYMMETRY TYPE AAAA.
C
C    G(P<Q,R<S) -> G(XX,R<S) -> G(X<X,R<S) -> G(X<X,XX) -> G(X<X,X<X)
C
C  (THE SYMBOL "<" IS UNDERSTOOD TO BE "LESS THAN OR EQUAL TO" 
C   THROUGHOUT THIS ROUTINE)
C
C * FOR DROPPED CORE OR SPHERICAL HARMONIC BASIS SETS, THE MO GAMMA
C   ELEMENTS ARE STORED ON LISTS WHICH ARE SIZED FOR THE AO GAMMAS,
C   HENCE SOMEWHAT UNCONVENTIONAL I/O.
C
C EVEC IS THE SYMMETRY PACKED EIGENVECTOR MATRIX AS PREPARED BY SYPKEV.
C
C REQUIRED  SPIN CASES:
C             PQRS
C             ----
C             AAAA (UHF ONLY)
C             BBBB (UHF ONLY, DONE TOGETHER WITH AABB)
C             AABB (RHF AND UHF)
C
C MEMORY REQUIREMENTS:
C       
C       BUF   = MAX[SQUARE CANON AO LIST(IRR)] OVER ALL IRREPS.  (RHF AND UHF)
C               NSZTRI (OUT-OF-CORE)
C       BUF1  = NSZTRI                                           (UHF ONLY)
C       BUFTMP= 3*NAO*NAO                                        (RHF AND UHF)
C
CEND
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,AOPOP
      LOGICAL INCORE,SPHHRM,MOEQAO,ALASKA
      DIMENSION BUF(LENBUF),BUF1(LENBUF1),BUFTMP(LENTMP),EVEC(1)
      DIMENSION SCR(MAXSIZE)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /AOPOPS/ AOPOP(8),MOPOP(8),NAO,NAOSPH,NMO
      COMMON /AOOFS2/ INDAO(8,2)
      COMMON /BASTYP/ SPHHRM,MOEQAO
C
      DATA ONE   /1.0D0/
      DATA ZILCH /0.0D0/
      DATA FOURTH/0.25D0/
      DATA FOUR/4.D0/
C
      NNP1O2(I)=(I*(I+1))/2
C
      CALL ZERO(BUF,LENBUF)
C
C  LOOP OVER THE DIFFERENT TYPES
C
      DO 1000 ITYPE=1,IUHF+1
C
C SET SPINS FOR RIGHT AND LEFT SIDE OF GAMMA
C
       IF(ITYPE.EQ.1) THEN
C
C   AABB AND BBBB SPIN CASES
C
        LIST=1
        ISL=1
        ISR=2
C
       ELSE
C
C  AAAA SPIN CASE
C
        LIST=6
        ISL=1
        ISR=1
C
       ENDIF
C
C LOOP OVER IRREPS AND DO THE TRANSFORMATION.
C
       DO 100 IRREPDO=1,NIRREP
        NSIZAO=AOPOP(IRREPDO)
        NSIZMO=MOPOP(IRREPDO)
        NSZTRIAO=NNP1O2(NSIZAO)
        NSZFULAO=NSIZAO*NSIZAO
        NSZTRIMO=NNP1O2(NSIZMO)
        NSZFULMO=NSIZMO*NSIZMO
        IF(INCORE)THEN
         IF(MOEQAO .OR. ALASKA)THEN
          CALL GETLST(BUF,1,NSZTRIMO,1,LIST,IRREPDO)
         ELSE
          IOFF=1
          DO 5 I=1,NSZTRIMO
           CALL GETLST(BUF(IOFF),I,1,1,LIST,IRREPDO)
           IOFF=IOFF+NSZTRIAO
5         CONTINUE
         ENDIF
        ENDIF
        IOFF  =1
        IOFFB =1+NSIZAO*NSIZAO
        IOFFB2=IOFFB+NSIZAO*NSIZAO
        DO 110 INDXRS=1,NSZTRIMO
        IF(.NOT.INCORE)CALL GETLST(BUF,INDXRS,1,1,LIST,IRREPDO)
C
C FIRST HALF TRANSFORM THE AMPLITUDES TO G(XX,R<S).  HALF TRANSFORMED
C  GAMMAS ARE WRITTEN BACK OVER BUF.
C                                             +
C            G(XX,R<S) = C(XP) * G(PQ,R<S) * C (XQ)
C
         IOFFE =INDAO(IRREPDO,ISL)
         CALL EXPND2(BUF(IOFF),BUFTMP(IOFFB),NSIZMO)
         CALL SYTRAA(BUFTMP(IOFFB),EVEC(IOFFE),EVEC(IOFFE),
     &               NSIZAO,NSIZMO,NSIZMO,BUFTMP(IOFFB2),BUFTMP,0)
         CALL SQUEZ2(BUFTMP,BUF(IOFF),NSIZAO)
         IF(IUHF.EQ.1.AND.ITYPE.EQ.1) THEN
          LIST2=6
          IOFFE=INDAO(IRREPDO,ISR)
          CALL GETLST(BUF1,INDXRS,1,1,LIST2,IRREPDO+50)
          CALL EXPND2(BUF1,BUFTMP(IOFFB),NSIZMO)
          CALL SYTRAA(BUFTMP(IOFFB),EVEC(IOFFE),EVEC(IOFFE),
     &                NSIZAO,NSIZMO,NSIZMO,BUFTMP(IOFFB2),BUFTMP,0)
          CALL SQUEZ3(BUFTMP,BUF(IOFF),NSIZAO)
         ENDIF
         IF(INCORE)THEN
          IOFF=IOFF+NSZTRIAO
         ELSE
          CALL PUTLST(BUF(IOFF),INDXRS,1,1,LIST,IRREPDO)
         ENDIF
110     CONTINUE
C
C IF THIS IS NOT USING THE IN-CORE ALGORITHM, DO THE SNAZZY 
C  OUT-OF-CORE IN-PLACE TRANSPOSITION NOW. 
C
C The older incarnations of trnlst was limited to cases 
C where number of AOs were equal to number of MOs. This 
C created severe limtations on the large calculations with
C drop core and/or spherical options. The new trnlst.f is
C an extension of the orginal. See trnlst.f for implementation
C notes, 11/2001 Ajith Perera. 
C
        IF(.NOT.INCORE) THEN
          CALL TRNLST(LIST,IRREPDO,NSZTRIAO,NSZTRIMO, SCR,
     &                MAXSIZE)
        ENDIF
C
C NOW DO SECOND HALF TRANSFORMATION, COPYING HALF-TRANSFORMED GAMMAS 
C  FROM BUF (IN-CORE) OR READING THEM FROM DISK (OUT-OF-CORE).
C                                             +
C            G(X<X,XX) = C(XR) * G(X<X,RS) * C (XS)
C
C
        IF(ITYPE.EQ.1) THEN
C
C  FIRST PASS THROUGH THE BACK TRANSFORMATION, DO NOT ACCUMULATE
C
         IOFF =1
         IOFFE =INDAO(IRREPDO,ISR)
         DO 120 INDXPQ=1,NSZTRIAO
          IF(INCORE)THEN
           CALL SCOPY(NSZTRIMO,BUF(IOFF),NSZTRIAO,BUFTMP(IOFFB2),1)
          ELSE
           CALL GETLST(BUFTMP(IOFFB2),INDXPQ,1,1,LIST,IRREPDO) 
          ENDIF
          CALL EXPND2(BUFTMP(IOFFB2),BUFTMP(IOFFB),NSIZMO)
          CALL SYTRAA(BUFTMP(IOFFB),EVEC(IOFFE),EVEC(IOFFE),
     &                NSIZAO,NSIZMO,NSIZMO,BUFTMP(IOFFB2),BUFTMP,0)
          CALL SQUEZ2(BUFTMP,BUFTMP(IOFFB),NSIZAO)
          CALL SSCAL(NSZTRIAO,FOURTH,BUFTMP(IOFFB),1)
          CALL PUTLST(BUFTMP(IOFFB),IOFF,1,1,1,IRREPDO)
          IOFF=IOFF+1
120      CONTINUE
C
        ELSE 
C
C  SECOND PASS THROUGH THE BACK TRANSFORMATION, ACCUMULATE 
C
         IOFF =1
         IOFFE =INDAO(IRREPDO,ISR)
         DO 121 INDXPQ=1,NSZTRIAO
          IF(INCORE)THEN
           CALL SCOPY(NSZTRIMO,BUF(IOFF),NSZTRIAO,BUFTMP(IOFFB2),1)
          ELSE
           CALL GETLST(BUFTMP(IOFFB2),INDXPQ,1,1,LIST,IRREPDO)
          ENDIF
          CALL EXPND2(BUFTMP(IOFFB2),BUFTMP(IOFFB),NSIZMO)
          CALL SYTRAA(BUFTMP(IOFFB),EVEC(IOFFE),EVEC(IOFFE),
     &                NSIZAO,NSIZMO,NSIZMO,BUFTMP(IOFFB2),BUFTMP,0)
          CALL SSCAL(NSIZAO*NSIZAO,FOURTH,BUFTMP,1)
          CALL GETLST(BUFTMP(IOFFB),IOFF,1,1,1,IRREPDO)
          CALL SQUEZ3(BUFTMP,BUFTMP(IOFFB),NSIZAO)
          CALL PUTLST(BUFTMP(IOFFB),IOFF,1,1,1,IRREPDO)
          IOFF=IOFF+1
121      CONTINUE
C
        ENDIF
C
100    CONTINUE
C
1000  CONTINUE
C
C FOR UHF WE HAVE TO SYMMETRIZE THE GAMMA LISTS
C
      IF(IUHF.EQ.1) THEN
C
       DO 300 IRREP=1,NIRREP
        NSIZE=NNP1O2(AOPOP(IRREP))
        IF (NSIZE.GT.0) CALL SYMMET3(1,IRREP,NSIZE,SCR,MAXSIZE)
300    CONTINUE
      ENDIF
      RETURN
      END 
