      SUBROUTINE BXABCD2(EVEC,BUF,BUF1,BUFTMP,LENBUF,LENBF1,LENTMP,
     &                   IUHF,ALASKA)
C
C THIS ROUTINE TRANSFORMS ELEMENTS OF THE MO TWO-PARTICLE DENSITY
C  MATRIX TO THE ATOMIC ORBITAL BASIS.  THIS ROUTINE DEALS SPECIFICALLY
C  WITH THOSE ELEMENTS OF SYMMETRY TYPE AABB.
C
C              G(PQ,RS) -> G(XX,RS) -> G(XX,XX)
C
C
C EVEC IS THE SYMMETRY PACKED EIGENVECTOR MATRIX AS PREPARED BY SYPKEV.
C
C SPIN CASES:
C             PQRS
C             ----
C             AAAA (ISPIN=1)
C             BBBB (ISPIN=2)
C             AABB (ISPIN=3)
C
C MEMORY REQUIREMENTS:
C       
C       BUF   = MAX[NAO(IRRA)*NAO(IRRB)*NAO(IRRC)*NAO(IRRD)] FOR 
C                ALL POSSIBLE SYMMETRY ALLOWED COMBINATIONS.
C       BUFTMP= MAX[3*NAO(IRRA)*NAO(IRRA)] FOR ALL IRRA.
C
CEND
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER DIRPRD,AOPOP,MOPOP
      LOGICAL SPHHRM,MOEQAO,ALASKA
      DIMENSION BUF1(LENBF1)
      DIMENSION BUF(LENBUF),BUFTMP(LENTMP),EVEC(1),IDID(8)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /AOPOPS/ AOPOP(8),MOPOP(8),NAO,NAOSPH,NMO
      COMMON /AOOFS2/ INDAO(8,2)
      COMMON /BASTYP/ SPHHRM,MOEQAO
      DATA ONE   /1.0/
      DATA ZILCH /0.0/
      DATA FOURTH/0.25/
C
C  LISTG : LIST ON WHICH THE TRANFORMED GAMMAS ARE SAVED
C
       LISTG=4
C
C LOOP OVER DIFFERENT TYPES
C
      DO 1000 ITYPE=1,IUHF+1
C
       IF(ITYPE.EQ.1) THEN
C
C AABB AND BBBB SPIN CASES 
C
C  LISTW : FIRST LIST PROCESSED
C  LISTW2 : SECOND LIST PROCESSED
C
        LISTW=4
        LOFFW=0
        LISTW2=9
        LOFFW2=50
        ISL=1
        ISR=2
        FACT=ONE
        IF(IUHF.EQ.0) FACT=FOURTH
C
       ELSE
C
        LISTW=4
        LOFFW=50
        LISTW2=9
        LOFFW2=0
        ISL=2
        ISR=1
        FACT=FOURTH
C
       ENDIF 
C
C LOOP OVER ALLOWED IRREP COMBINATIONS AND DO THE TRANSFORMATION.
C
      ITHRU=0
      DO 100 IRREPDO=2,NIRREP
       DO 101 IRREPI=1,NIRREP
        IRREPXIA=DIRPRD(IRREPI,IRREPDO)
        IF(IRREPXIA.LT.IRREPI)GOTO 101
        IBOT=MAX(IRREPI,IRREPXIA)+1
        CALL IZERO(IDID,8)
        DO 102 ITMP=IBOT,NIRREP
         IRREPXIC=DIRPRD(ITMP,IRREPDO)
         ITMP2=MIN(IRREPXIC,ITMP)
         IRREPXIC=MAX(ITMP,IRREPXIC)
         IRREPXIB=ITMP2
         IF(MAX(IDID(IRREPXIC),IDID(IRREPXIB)).NE.0)GOTO 102
         IDID(IRREPXIC)=1
         IDID(IRREPXIB)=1
         ITHRU=ITHRU+1
         NMOXIC=MOPOP(IRREPXIC)
         NMOXIB=MOPOP(IRREPXIB)
         NMOXIA=MOPOP(IRREPXIA)
         NMOXI =MOPOP(IRREPI)
         NAOXIC=AOPOP(IRREPXIC)
         NAOXIB=AOPOP(IRREPXIB)
         NAOXIA=AOPOP(IRREPXIA)
         NAOXI =AOPOP(IRREPI)
         NSZLFTA=NAOXIC*NAOXIB
         NSZRHTA=NAOXIA*NAOXI
         NSZLFTM=NMOXIC*NMOXIB
         NSZRHTM=NMOXIA*NMOXI
         IOFFB =1+NSZLFTA
         IOFFB2=IOFFB+NSZLFTA
         IOFFXIC=INDAO(IRREPXIC,ISL)
         IOFFXIB=INDAO(IRREPXIB,ISL)
         IOFFXIA=INDAO(IRREPXIA,ISR)
         IOFFXI =INDAO(IRREPI ,ISR)
         IF(MOEQAO .OR. ALASKA)THEN
          CALL GETLST(BUF,1,NSZRHTM,1,LISTW,ITHRU+LOFFW)
         ELSE
          IOFF=1
          DO 103 I=1,NSZRHTM
           CALL GETLST(BUF(IOFF),I,1,1,LISTW,ITHRU+LOFFW)
           IOFF=IOFF+NSZLFTA
103       CONTINUE
         ENDIF 
         IF(ITYPE.EQ.1) CALL ACES_LIST_XPOSE(LISTG,ITHRU,.FALSE.) 
         IOFF  =1
         DO 110 INDXRS=1,NSZRHTM
C
C FIRST HALF TRANSFORM THE AMPLITUDES TO G(XX,R<S).  HALF TRANSFORMED
C  GAMMAS ARE WRITTEN BACK OVER BUF.
C                                           +
C            G(XX,RS) = C(XP) * G(PQ,RS) * C (XQ)
C
          CALL SCOPY(NSZLFTM,BUF(IOFF),1,BUFTMP(IOFFB),1)
          CALL SYTRAB1(BUFTMP(IOFFB),EVEC(IOFFXIC),EVEC(IOFFXIB),
     &                 NAOXIC,NAOXIB,NMOXIC,NMOXIB,BUFTMP(IOFFB2),
     &                 BUF(IOFF),0)
          IOFF=IOFF+NSZLFTA
110      CONTINUE
C
C FOR UHF WE HAVE TO NOW THE AAAA OR BBBB STUFF
C
         IF(IUHF.EQ.1) THEN 
         IOFF  =1
         IOFFB =1+NSZLFTA
         IOFFXIC=INDAO(IRREPXIC,ISR)
         IOFFXIB=INDAO(IRREPXIB,ISR)
         DO 111 INDXRS=1,NSZRHTM
C
C FIRST HALF TRANSFORM THE AMPLITUDES TO G(XX,R<S).  HALF TRANSFORMED
C  GAMMAS ARE WRITTEN BACK OVER BUF.
C                                           +
C            G(XX,RS) = C(XP) * G(PQ,RS) * C (XQ)
C
          CALL GETLST(BUFTMP(IOFFB),INDXRS,1,1,LISTW2,ITHRU+LOFFW2)
          CALL SYTRAB1(BUFTMP(IOFFB),EVEC(IOFFXIC),EVEC(IOFFXIB),
     &                 NAOXIC,NAOXIB,NMOXIC,NMOXIB,BUFTMP(IOFFB2),
     &                 BUF(IOFF),1)
          IOFF=IOFF+NSZLFTA
111      CONTINUE
         ENDIF
C
C NOW DO SECOND HALF TRANSFORMATION, COPYING HALF-TRANSFORMED GAMMAS 
C  FROM BUF.
C                                           +
C            G(XX,XX) = C(XR) * G(XX,RS) * C (XS)
C
C
         IF(ITYPE.EQ.1) THEN
         IOFF  =1
         IOFFB =1+NSZRHTA
         IOFFB2=IOFFB+NSZRHTA
         DO 120 INDXPQ=1,NSZLFTA
          CALL SCOPY(NSZRHTM,BUF(IOFF),NSZLFTA,BUFTMP(IOFFB),1)
          CALL SYTRAB1(BUFTMP(IOFFB),EVEC(IOFFXIA),EVEC(IOFFXI),
     &                 NAOXIA,NAOXI,NMOXIA,NMOXI,BUFTMP(IOFFB2),
     &                 BUFTMP,0)
          CALL SSCAL(NSZRHTA,FACT,BUFTMP,1)
          CALL PUTLST(BUFTMP,IOFF,1,1,LISTG,ITHRU)
          IOFF=IOFF+1
120      CONTINUE
         ELSE
         IOFF  =1
         IOFFB =1+NSZRHTA
         IOFFB2=IOFFB+NSZRHTA
         DO 121 INDXPQ=1,NSZLFTA
          CALL SCOPY(NSZRHTM,BUF(IOFF),NSZLFTA,BUFTMP(IOFFB),1)
          CALL GETLST(BUFTMP,IOFF,1,1,LISTG,ITHRU)
          CALL SYTRAB1(BUFTMP(IOFFB),EVEC(IOFFXIA),EVEC(IOFFXI),
     &                 NAOXIA,NAOXI,NMOXIA,NMOXI,BUFTMP(IOFFB2),
     &                 BUFTMP,1)
          CALL SSCAL(NSZRHTA,FACT,BUFTMP,1)
          CALL PUTLST(BUFTMP,IOFF,1,1,LISTG,ITHRU)
          IOFF=IOFF+1
121      CONTINUE
         ENDIF
102     CONTINUE
101    CONTINUE
100   CONTINUE
C
1000  CONTINUE
      RETURN
      END 
