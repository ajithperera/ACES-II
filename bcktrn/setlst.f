      SUBROUTINE SETLST
C
C CREATES LISTS FOR AO GAMMA FILE.
C
      IMPLICIT INTEGER (A-Z)
      DIMENSION IDID(8)
      COMMON /SYM/ POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /AOPOPS/ AOPOP(8),MOPOP(8),NAO,NAOSPH,NMO
      COMMON /SZAOGM/ AOGMSZ(2,4,100)
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      NNP1O2(I)=(I*(I+1))/2
C
      IONE=1
C
C INITIALIZE NEW MOINTS FILE.
C
C CREATE AAAA LISTS.
C
      MAXDSZ=0
      CALL IZERO(AOGMSZ,800)
c      IJUNK=1
      call aces_io_remove(50,'MOINTS')
      IJUNK=0
      DO 10 IRREP=1,NIRREP
       NUMDIS=NNP1O2(AOPOP(IRREP))
       DISSIZ=NUMDIS
       CALL UPDMOI(NUMDIS,DISSIZ,1,IRREP,IJUNK,0)
       MAXDSZ=MAX(MAXDSZ,DISSIZ)
       AOGMSZ(1,1,IRREP)=DISSIZ
       AOGMSZ(2,1,IRREP)=NUMDIS
       IJUNK=0
10    CONTINUE
C
C CREATE AABB LISTS.
C
      ITHRU=0
      DO 20 IRREPA=2,NIRREP
       DO 21 IRREPB=1,IRREPA-1
        ITHRU=ITHRU+1
        DISSIZ=NNP1O2(AOPOP(IRREPA))
        NUMDIS=NNP1O2(AOPOP(IRREPB))
        CALL UPDMOI(NUMDIS,DISSIZ,2,ITHRU,0,0)
        MAXDSZ=MAX(MAXDSZ,DISSIZ)
        AOGMSZ(1,2,ITHRU)=DISSIZ
        AOGMSZ(2,2,ITHRU)=NUMDIS
21     CONTINUE
20    CONTINUE
C
C CREATE ABAB LISTS.
C
      ITHRU=0
      DO 30 IRREP=2,NIRREP
       DO 31 IRREPA=1,NIRREP
        IRREPB=DIRPRD(IRREPA,IRREP)
        IF(IRREPB.LT.IRREPA)GOTO 31
        ITHRU=ITHRU+1
        NUMDIS=AOPOP(IRREPA)*AOPOP(IRREPB)
        DISSIZ=NUMDIS
        CALL UPDMOI(NUMDIS,DISSIZ,3,ITHRU,0,0)
        MAXDSZ=MAX(MAXDSZ,DISSIZ)
        AOGMSZ(1,3,ITHRU)=DISSIZ
        AOGMSZ(2,3,ITHRU)=NUMDIS
31     CONTINUE
30    CONTINUE
C
C CREATE ABCD LISTS.
C
      ITHRU=0
      DO 40 IRREP=2,NIRREP
       DO 41 IRREPD=1,NIRREP
        IRREPC=DIRPRD(IRREPD,IRREP)
        IF(IRREPC.LT.IRREPD)GOTO 41
        IBOT=MAX(IRREPC,IRREPD)+1
        CALL IZERO(IDID,8)
        DO 42 ITMP=IBOT,NIRREP 
         IRREPA=DIRPRD(ITMP,IRREP)
         IRREPB=MIN(IRREPA,ITMP)      
         IRREPA=MAX(IRREPA,ITMP)
         IF(MAX(IDID(IRREPA),IDID(IRREPB)).NE.0)GOTO 42
         IDID(IRREPA)=1
         IDID(IRREPB)=1
         ITHRU=ITHRU+1
         DISSIZ=AOPOP(IRREPC)*AOPOP(IRREPD)
         NUMDIS=AOPOP(IRREPA)*AOPOP(IRREPB)
         CALL UPDMOI(NUMDIS,DISSIZ,4,ITHRU,0,0)
         MAXDSZ=MAX(MAXDSZ,DISSIZ)
         AOGMSZ(1,4,ITHRU)=DISSIZ
         AOGMSZ(2,4,ITHRU)=NUMDIS
42      CONTINUE
41     CONTINUE
40    CONTINUE
C
      CALL PUTREC(20,'JOBARC','MAXAODSZ',IONE,MAXDSZ)
C
      RETURN
      END
