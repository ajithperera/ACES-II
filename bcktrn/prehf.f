

      SUBROUTINE PREHF(ICORE,MAXCOR,IUHF)
C
C THIS ROUTINE SETS UP THE REQUIRED INFORMATION FOR AN MBPT(2)
C BACKTRANSFORMATION OF THE T2 VECTOR FOR RHF AND UHF REFERENCE
C FUNCTIONS.
C
CEND
      IMPLICIT INTEGER (A-Z)
      CHARACTER*4 SPTYPE(2)
      DOUBLE PRECISION HALF
C
      DIMENSION ICORE(MAXCOR)
C
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /SYMPOP/ IRPDPD(8,22),ISYTYP(2,500),ID(18)
      COMMON /SYM/    POP(8,2),VRT(8,2),NT(2),NFMI(2),NFEA(2)
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWD
C
      DATA HALF/0.5D0/
      DATA SPTYPE /'AAAA','BBBB'/
C
C INITIALIZE LIST ON GAMLAM FILE WHICH WILL HOLD THE ABAB 
C T2 AMPLITUDES ( SPIN ADAPTED IN THE RHF CASE)

c      IMODE=1
      call aces_io_remove(51,'GAMLAM')
      IMODE=0
      CALL INIPCK(1,ISYTYP(1,18),ISYTYP(2,18),118,IMODE,0,1)
C
C FOR UHF WE HAVE ALSO TO INITIALIZE THE AAAA AND BBBB
C LIST
C
      IF(IUHF.EQ.1) THEN
       CALL INIPCK(1,ISYTYP(1,19),ISYTYP(2,19),119,IMODE,0,1)
       CALL INIPCK(1,ISYTYP(1,20),ISYTYP(2,20),120,IMODE,0,1)
      ENDIF
C
C READ T2 VECTOR (ABAB SPIN CASE) INTO CORE,
C FORM SPIN ADAPTED T2 VECTOR FOR RHF
C
C          [T2 = 2 * T2(AB,IJ) - T2(BA,IJ)]  (RHF ONLY)
C
C AND ACCUMULATE UNTIL THE FULL T2 IS IN CORE.
C  
      ISTART=1
      NSIZE=0
      DO 10 IRREP=1,NIRREP
       NUMDIS=IRPDPD(IRREP,ISYTYP(2,46))
       DISSIZ=IRPDPD(IRREP,ISYTYP(1,46))
       I000=ISTART
       I010=I000+NUMDIS*DISSIZ*IINTFP
       I020=I010+NUMDIS*IINTFP
       I030=I020+NUMDIS*IINTFP
       CALL GETLST(ICORE(I000),1,NUMDIS,1,IRREP,46)
       IF(IUHF.EQ.0) THEN
        CALL SPINAD3(IRREP,VRT(1,1),DISSIZ,NUMDIS,ICORE(I000),
     &               ICORE(I010),ICORE(I020))
       ENDIF
       NSIZE=NSIZE+NUMDIS*DISSIZ
       ISTART=I000+NUMDIS*DISSIZ*IINTFP
10    CONTINUE
C
C NOW DO AN REORDER TO AIBJ AND KEEP IN CORE.
C
      I000=1
      I010=I000+NSIZE*IINTFP
      I020=I010+NSIZE*IINTFP
      CALL SST002(ICORE(I000),ICORE(I010),NSIZE,NSIZE,ICORE(I020),
     &            'AABB')
      CALL PUTALL(ICORE(I010),NSIZE,1,118)
C
C  FOR RHF WE ARE NOW DONE, HOWEVER FOR UHF SOME MORE WORK HAS TO BE DONE.
C
      IF(IUHF.NE.0) THEN
C
C  LOOP OVER BOTH SPIN CASES
C
       DO 200 ISPIN=1,2
C
        LISTIN=43+ISPIN
        LISTOUT=18+ISPIN
        NSIZIN=ISYMSZ(ISYTYP(1,LISTIN),ISYTYP(2,LISTIN))
        NSIZOUT=ISYMSZ(ISYTYP(1,LISTOUT),ISYTYP(2,LISTOUT))
        I000=1
        I010=I000+NSIZOUT*IINTFP
        I020=I010+NSIZOUT*IINTFP
        CALL GETALL(ICORE(I000),NSIZIN,1,LISTIN)
        CALL SST003(ICORE(I000),ICORE(I010),NSIZIN,NSIZOUT,ICORE(I020),
     &              SPTYPE(ISPIN),'AIBJ')
        CALL SSCAL(NSIZOUT,HALF,ICORE(I010),1)
        CALL PUTALL(ICORE(I010),NSIZOUT,1,LISTOUT+100)
C
200    CONTINUE
C
      ENDIF
C
      CALL ACES_IO_REMOVE(50,'MOINTS')
      RETURN
      END
