      SUBROUTINE SYTRAB2(GAB,GBA,CVA,CVB,COA,COB,NAOA,NAOB1,NAOB,
     &                   NVA,NVB,NOA,NOB,BUF,RESULT,IACC)
C
C THIS ROUTINE PERFORMS THE SYMMETRIZED TRANSFORMATION
C
C                            +                              +        +
C Q(XX) = C(Xa,Aa) Z(Aa,Ib) C (Xb,Ib) + [C(Xb,Ab) Z(Ab,Ia) C (Xa,Ia)]
C
C                                                          +
C       = C(Xa,Aa) Z(Aa,Ib) C (Xb,Ib) + C(Xa,Ia) Z(Ia,Ab) C (Xb,Ab)
C
C USED IN TRANSFORMING THE T2 VECTOR TO THE ATOMIC ORBITAL BASIS
C
C THE RESULT IS RETURNED IN VECTOR RESULT, AND BUF IS A SCRATCH
C  VECTOR OF LENGTH NAOA*NAOB.
C
CEND
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION CVA(NAOA,NVA),CVB(NAOB,NVB),COA(NAOA,NOA),COB(NAOB,NOB)
      DIMENSION GAB(NVA,NOB),GBA(NVB,NOA),BUF(1),RESULT(NAOA*NAOB1)
C
      DATA ZILCH /0.0D0/
      DATA ONE   /1.0D0/
C
      IF(IACC.EQ.0)CALL ZERO(RESULT,NAOA*NAOB1)
C
      IF(MIN(NVA,NOB).NE.0)THEN
       CALL XGEMM('N','N',NAOA,NOB,NVA,ONE,CVA,NAOA,GAB,NVA,
     &            ZILCH,BUF,NAOA)
       CALL XGEMM('N','T',NAOA,NAOB1,NOB,ONE,BUF,NAOA,COB,NAOB,
     &            ONE,RESULT,NAOA)
      ENDIF
C
      IF(MIN(NVB,NOA).NE.0)THEN
       CALL XGEMM('N','T',NAOA,NVB,NOA,ONE,COA,NAOA,GBA,NVB,
     &            ZILCH,BUF,NAOA)
       CALL XGEMM('N','T',NAOA,NAOB1,NVB,ONE,BUF,NAOA,CVB,NAOB,
     &           ONE,RESULT,NAOA)
      ENDIF
C
      RETURN
      END
