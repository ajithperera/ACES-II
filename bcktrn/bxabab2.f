      SUBROUTINE BXABAB2(EVEC,BUF,BUF1,BUFTMP,LENBUF,LENBF1,LENTMP,
     &                   IUHF,ALASKA)
C
C THIS ROUTINE TRANSFORMS ELEMENTS OF THE MO TWO-PARTICLE DENSITY
C  MATRIX TO THE ATOMIC ORBITAL BASIS.  THIS ROUTINE DEALS SPECIFICALLY
C  WITH THOSE ELEMENTS OF SYMMETRY TYPE AABB.
C
C              G(PQ,RS) -> G(XX,RS) -> G(XX,XX)
C
C
C EVEC IS THE SYMMETRY PACKED EIGENVECTOR MATRIX AS PREPARED BY SYPKEV.
C
C REQUIRED SPIN CASES:
C             PQRS
C             ----
C             AAAA (UHF ONLY)
C             BBBB (UHF ONLY)
C             AABB (RHF AND UHF
C             BBAA (UHF ONLY)
C
C MEMORY REQUIREMENTS:
C       
C       BUF   = MAX[NAO(IRRA)*NAO(IRRB)*NAO(IRRA)*NAO(IRRB)] FOR 
C                ALL POSSIBLE COMBINATIONS.
C       BUF1  = 
C       BUFTMP= MAX[3*NAO(IRRA)*NAO(IRRA)] FOR ALL IRRA.
C
CEND
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL SPHHRM,MOEQAO,ALASKA
      INTEGER DIRPRD,AOPOP,MOPOP
      DIMENSION BUF(LENBUF),BUFTMP(LENTMP),EVEC(1),BUF1(LENBF1)
C
      COMMON /MACHSP/ IINTLN,IFLTLN,IINTFP,IALONE,IBITWDX 
      COMMON /SYMINF/ NSTART,NIRREP,IRREPS(255,2),DIRPRD(8,8)
      COMMON /AOPOPS/ AOPOP(8),MOPOP(8),NAO,NAOSPH,NMO
      COMMON /AOOFS2/ INDAO(8,2)
      COMMON /BASTYP/ SPHHRM,MOEQAO
C
      DATA ONE   /1.0D0/
      DATA ZILCH /0.0D0/
      DATA FOURTH/0.25D0/
C
      CALL ZERO(BUF,LENBUF)
C
C   LISTG : LIST ON WHICH THE TRANSFORMED GAMMAS ARE SAVED
C
       LISTG=3
C
C  LOOP OVER ALL SPIN CASES
C
      DO 1000 ITYPE=1,IUHF+1
C
       IF(ITYPE.EQ.1) THEN
C 
C AABB AND BBBB SPIN CASES
C
C  LISTW : FIRST LIST PROCESSED
C  LISTW2 : SECOND LIST PROCESSED
C
        LISTW=3
        LOFFW=0
        LISTW2=8
        LOFFW2=50
        ISL=1
        ISR=2 
        FACT=ONE
        IF(IUHF.EQ.0) FACT=FOURTH 
C
       ELSE
C 
        LISTW=3
        LOFFW=50
        LISTW2=8
        LOFFW2=0
        ISL=2
        ISR=1
        FACT=FOURTH
C
      ENDIF
C
C LOOP OVER IRREP PAIRS AND DO THE TRANSFORMATION.
C
      ITHRU=0
      DO 100 IRREPDO=2,NIRREP
       DO 101 IRREPI=1,NIRREP
        IRREPXI=DIRPRD(IRREPI,IRREPDO)
        IF(IRREPXI.LT.IRREPI)GOTO 101
        ITHRU=ITHRU+1 
        NAOXIA=AOPOP(IRREPXI)
        NAOIA =AOPOP(IRREPI) 
        NAOXIM=MOPOP(IRREPXI)
        NAOIM =MOPOP(IRREPI) 
        NSIZEA=NAOXIA*NAOIA
        NSIZEM=NAOXIM*NAOIM
        IF(MOEQAO .OR. ALASKA)THEN
         CALL GETLST(BUF,1,NSIZEM,1,LISTW,ITHRU+LOFFW)
        ELSE
         IOFF=1
         DO 102 I=1,NSIZEM
          CALL GETLST(BUF(IOFF),I,1,1,LISTW,ITHRU+LOFFW)
          IOFF=IOFF+NSIZEA
102      CONTINUE
        ENDIF
        IOFF  =1
        IOFFB =1+NSIZEA
        IOFFB2=IOFFB+NSIZEA
        IOFFXI=INDAO(IRREPXI,ISL)
        IOFFI =INDAO(IRREPI,ISL)
        DO 110 INDXRS=1,NSIZEM
C
C FIRST HALF TRANSFORM THE AMPLITUDES TO G(XX,R<S).  HALF TRANSFORMED
C  GAMMAS ARE WRITTEN BACK OVER BUF.
C                                           +
C            G(XX,RS) = C(XP) * G(PQ,RS) * C (XQ)
C
         CALL SCOPY(NSIZEM,BUF(IOFF),1,BUFTMP(IOFFB),1)
         CALL SYTRAB1(BUFTMP(IOFFB),EVEC(IOFFXI),EVEC(IOFFI),
     &               NAOXIA,NAOIA,NAOXIM,NAOIM,BUFTMP(IOFFB2),
     &               BUF(IOFF),0)
         IOFF=IOFF+NSIZEA
110     CONTINUE
C
C  FOR UHF DO HERE THE BBBB AND AAAA STUFF
C
        IF(IUHF.EQ.1) THEN
        IOFF  =1
        IOFFB =1+NSIZEA
        IOFFB2=IOFFB+NSIZEA
        IOFFXI=INDAO(IRREPXI,ISR)
        IOFFI =INDAO(IRREPI,ISR)
        DO 111 INDXRS=1,NSIZEM
C
C FIRST HALF TRANSFORM THE AMPLITUDES TO G(XX,R<S).  HALF TRANSFORMED
C  GAMMAS ARE WRITTEN BACK OVER BUF.
C                                           +
C            G(XX,RS) = C(XP) * G(PQ,RS) * C (XQ)
C
         CALL GETLST(BUFTMP(IOFFB),INDXRS,1,1,LISTW2,ITHRU+LOFFW2)
         CALL SYTRAB1(BUFTMP(IOFFB),EVEC(IOFFXI),EVEC(IOFFI),
     &               NAOXIA,NAOIA,NAOXIM,NAOIM,BUFTMP(IOFFB2),
     &               BUF(IOFF),1)
         IOFF=IOFF+NSIZEA
111     CONTINUE
        ENDIF
C
C NOW DO SECOND HALF TRANSFORMATION, COPYING HALF-TRANSFORMED GAMMAS 
C  FROM BUF.
C                                           +
C            G(XX,XX) = C(XR) * G(XX,RS) * C (XS)
C
C
        IOFF =1
        IOFFXI=INDAO(IRREPXI,ISR)
        IOFFI =INDAO(IRREPI,ISR)
        IF(ITYPE.EQ.1) THEN
         DO 120 INDXPQ=1,NSIZEA
          CALL SCOPY(NSIZEM,BUF(IOFF),NSIZEA,BUFTMP(IOFFB),1)
          CALL SYTRAB1(BUFTMP(IOFFB),EVEC(IOFFXI),EVEC(IOFFI),
     &                 NAOXIA,NAOIA,NAOXIM,NAOIM,BUFTMP(IOFFB2),
     &                 BUFTMP,0)
          CALL SSCAL(NAOXIA*NAOIA,FACT,BUFTMP,1)
          CALL PUTLST(BUFTMP,IOFF,1,1,LISTG,ITHRU)
          IOFF=IOFF+1
120      CONTINUE
        ELSE
         DO 121 INDXPQ=1,NSIZEA
          CALL SCOPY(NSIZEM,BUF(IOFF),NSIZEA,BUFTMP(IOFFB),1)
          CALL GETLST(BUFTMP,IOFF,1,1,LISTG,ITHRU)
          CALL SYTRAB1(BUFTMP(IOFFB),EVEC(IOFFXI),EVEC(IOFFI),
     &                NAOXIA,NAOIA,NAOXIM,NAOIM,BUFTMP(IOFFB2),
     &                BUFTMP,1)
          CALL SSCAL(NAOXIA*NAOIA,FACT,BUFTMP,1)
          CALL PUTLST(BUFTMP,IOFF,1,1,LISTG,ITHRU)
          IOFF=IOFF+1
121      CONTINUE
        ENDIF
101    CONTINUE
100   CONTINUE
C
C END OF LOOP OVER TYPES
C
1000  CONTINUE
      RETURN
      END 
